MODULE InvoiceDebt;

REQUIRE Invoice, Debt;

NAMESPACE Invoicing;

// extend
EXTEND CLASS Invoice : OutgoingDebt;
number (Invoice i) += seriesNumber(i);
date (Invoice i) += date(i);

partner (Invoice i) += customer(i);
company (Invoice i) += company(i);

amount (Invoice i) += totalAmount(i);

// pay
pay 'Разнести' (IncomingDebt d, Invoice i) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        paid(d, i) <- left(d, i);
        APPLY;
    }
} CHANGEMOUSE 'DBLCLK';

EXTEND FORM invoice
    PROPERTIES(i) READONLY paid  

    OBJECTS d = IncomingDebt
    PROPERTIES(d) READONLY date, number, type
    PROPERTIES paid(d, i)
    FILTERS paid(d, i)
    
    OBJECTS dd = IncomingDebt
    PROPERTIES(dd) READONLY date, number, type, left
    PROPERTIES pay(dd, i) TOOLBAR
    FILTERS canBePaid(dd, i)   
;

DESIGN invoice {
    total {
        MOVE PROPERTY(paid(i));
    }
    details {
        NEW debts {
            caption = 'Разнесение оплат';
            MOVE BOX(d) { caption = 'Разнесенные'; }            
            MOVE BOX(dd) { caption = 'Доступные'; }          
        }
    }
}

EXTEND FORM invoices   
    PROPERTIES(i) READONLY paid  
;