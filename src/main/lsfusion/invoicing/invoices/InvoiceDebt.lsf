MODULE InvoiceDebt;

REQUIRE InvoiceDone, InvoiceCanceled, Debt;

NAMESPACE Invoicing;

// extend
EXTEND CLASS Invoice : OutgoingDebt;
active (Invoice i) += ready(i) AND NOT canceled(i);
number (Invoice i) += number(i);
dateTime (Invoice i) += dateTime(i);
dueDateTime (Invoice i) += dueDateTime(i);

partner (Invoice i) += customer(i);
company (Invoice i) += company(i);

amount (Invoice i) += totalAmount(i);

// pay
pay 'Разнести' (IncomingDebt d, Invoice i) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        paid(d, i) <- left(d, i);
        APPLY;
    }
} CHANGEMOUSE 'DBLCLK';

EXTEND FORM invoice
    PROPERTIES(i) READONLY paid  

    OBJECTS d = IncomingDebt
    PROPERTIES(d) READONLY dateTime, number, type
    PROPERTIES paid(d, i)
    FILTERS paid(d, i)
    
    OBJECTS dd = IncomingDebt
    PROPERTIES(dd) READONLY dateTime, number, type, left
    PROPERTIES pay(dd, i) TOOLBAR
    FILTERS canBePaid(dd, i)   
;

DESIGN invoice {
    total {
        MOVE PROPERTY(paid(i));
    }
    details {
        NEW debts {
            caption = 'Разнесение оплат';
            MOVE BOX(d) { caption = 'Разнесенные'; }            
            MOVE BOX(dd) { caption = 'Доступные'; }          
        }
    }
}

EXTEND FORM invoices   
    PROPERTIES(i) READONLY paid  
;

// auto set done
WHEN SET(totalAmount(Invoice i) = paid(i)) DO done(i) <- TRUE;
WHEN DROPPED(totalAmount(Invoice i) = paid(i)) DO done(i) <- NULL;