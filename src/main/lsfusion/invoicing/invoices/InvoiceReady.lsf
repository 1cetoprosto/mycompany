MODULE InvoiceReady;

REQUIRE Invoice;

NAMESPACE Invoicing;

EXTEND CLASS InvoiceStatus {
    ready 'К оплате'
}
ready 'К оплате' = DATA BOOLEAN (Invoice);

status(Invoice i) += WHEN ready(i) THEN InvoiceStatus.ready;
colorStatus(Invoice i) += WHEN status(i) = InvoiceStatus.ready THEN RGB(252,247,149);

markAsToDo 'К оплате' (Invoice i) {
    APPLY; 
        IF canceled() THEN RETURN;
        
        NEWSESSION {
            ready(i) <- TRUE;
            APPLY;
        }
}

EXTEND FORM invoice
    PROPERTIES(i) markAsToDo SHOWIF status(i) = InvoiceStatus.draft, ready
;

EXTEND FORM invoices    
    EXTEND FILTERGROUP status
        FILTER 'К оплате' status(i) = InvoiceStatus.ready 'F8'    
;

DESIGN invoice {
    primaryActions {
        MOVE PROPERTY(markAsToDo(i)) { fontStyle = 'bold'; }
    }                
    status {
        MOVE PROPERTY(ready(i));                               
    }    
}