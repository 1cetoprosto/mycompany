MODULE InvoiceOutstandingCredit;

REQUIRE Invoice, Utils;

NAMESPACE Invoicing;

CLASS ABSTRACT InvoiceOutstandingCredit 'Закрытие задолженности';

date 'Дата' = ABSTRACT DATETIME (InvoiceOutstandingCredit);
amount 'Сумма' = ABSTRACT NUMERIC[14,2] (InvoiceOutstandingCredit);
amountLeft 'Остаток' = ABSTRACT NUMERIC[14,2] (InvoiceOutstandingCredit);

partner = ABSTRACT Partner (InvoiceOutstandingCredit);
namePartner 'Партнер' (InvoiceOutstandingCredit c) = name(partner(c));

canBeMatched = ABSTRACT BOOLEAN (InvoiceOutstandingCredit, Invoice);

type 'Тип' = ABSTRACT ISTRING[30](InvoiceOutstandingCredit);

number 'Номер' = ABSTRACT STRING[28] (InvoiceOutstandingCredit);

paid 'Оплачено' = DATA NUMERIC[14,2](InvoiceOutstandingCredit, Invoice);
paid 'Оплачено' (Invoice i) = (GROUP SUM paid(InvoiceOutstandingCredit c, i)) (+) (GROUP SUM paid(i, Invoice ii));
toPay 'Остаток' (Invoice i) = totalAmount(i) (-) paid(i);

invoiced 'Оплачено' (InvoiceOutstandingCredit c) = GROUP SUM paid(c, Invoice i);

EXTEND FORM invoices   
    PROPERTIES(i) READONLY paid  
;

EXTEND FORM invoice
    PROPERTIES (ii) paid
;

addPayment 'Привязать' (InvoiceOutstandingCredit p, Invoice i) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        paid(p, i) <- min(Invoicing.amountLeft[InvoiceOutstandingCredit](p), toPay(i));
        APPLY;
    }
}

EXTEND FORM invoice
    OBJECTS p = InvoiceOutstandingCredit
    PROPERTIES(p) READONLY date, number, type
    PROPERTIES READONLY paid(p, i)
    FILTERS paid(p, i)
    
    OBJECTS pp = InvoiceOutstandingCredit
    PROPERTIES(pp) READONLY date, number, type, Invoicing.amountLeft[InvoiceOutstandingCredit]
    PROPERTIES addPayment(pp, i) DRAW pp TOOLBAR
    FILTERS canBeMatched(pp, i)   
;

DESIGN invoice {
    pane {
        NEW outstandingCredit BEFORE total {
            caption = 'Платежи';
            fill =1;
            type = CONTAINERV; 
            MOVE BOX (p);
            MOVE BOX (pp);
        }
    }
}