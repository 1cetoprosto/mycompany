MODULE InvoiceOutstandingCredit;

REQUIRE Invoice, Utils;

NAMESPACE Invoicing;

CLASS ABSTRACT InvoiceOutstandingCredit 'Закрытие задолженности';

number 'Номер' = ABSTRACT STRING[28] (InvoiceOutstandingCredit);
date 'Дата' = ABSTRACT DATETIME (InvoiceOutstandingCredit);

customer = ABSTRACT Partner (InvoiceOutstandingCredit);
nameCustomer 'Покупатель' (InvoiceOutstandingCredit c) = name(customer(c));

// type
CLASS InvoiceOutstandingCreditType 'Тип закрытия задолженности';
name 'Имя' (InvoiceOutstandingCreditType t) = staticCaption(t) IF t IS InvoiceOutstandingCreditType CHARWIDTH 10;

type = ABSTRACT InvoiceOutstandingCreditType (InvoiceOutstandingCredit);
nameType 'Тип' (InvoiceOutstandingCredit c) = name(type(c));

// sum
amount 'Сумма' = ABSTRACT NUMERIC[14,2] (InvoiceOutstandingCredit);
amountLeft 'Остаток' = ABSTRACT NUMERIC[14,2] (InvoiceOutstandingCredit);

canBeMatched = ABSTRACT BOOLEAN (InvoiceOutstandingCredit, Invoice);

paid 'Оплачено' = DATA NUMERIC[14,2] (InvoiceOutstandingCredit, Invoice);
paid 'Оплачено' (Invoice i) = (GROUP SUM paid(InvoiceOutstandingCredit c, i)) (+) (GROUP SUM paid(i, Invoice ii));
toPay 'Остаток' (Invoice i) = totalAmount(i) (-) paid(i);

invoiced 'Оплачено' (InvoiceOutstandingCredit c) = GROUP SUM paid(c, Invoice i);

EXTEND FORM invoices   
    PROPERTIES(i) READONLY paid  
;

EXTEND FORM invoice
    PROPERTIES(ii) paid
;

toPay (InvoiceOutstandingCredit p, Invoice i) = min(Invoicing.amountLeft[InvoiceOutstandingCredit](p), toPay(i));

setPaid 'Привязать' (InvoiceOutstandingCredit p, Invoice i) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        paid(p, i) <- toPay(p, i);
        APPLY;
    }
} CHANGEMOUSE 'DBLCLK';

EXTEND FORM invoice
    OBJECTS p = InvoiceOutstandingCredit
    PROPERTIES(p) READONLY date, number, nameType
    PROPERTIES paid(p, i)
    FILTERS paid(p, i)
    
    OBJECTS pp = InvoiceOutstandingCredit
    PROPERTIES(pp) READONLY date, number, nameType, Invoicing.amountLeft[InvoiceOutstandingCredit]
    PROPERTIES setPaid(pp, i) DRAW pp TOOLBAR
    FILTERS canBeMatched(pp, i)   
;

DESIGN invoice {
    details {
        NEW outstandingCredit {
            caption = 'Разнесение оплат';     
            MOVE BOX(p) { caption = 'Разнесенные'; }            
            MOVE BOX(pp) { caption = 'Доступные'; }          
        }
    }
}
