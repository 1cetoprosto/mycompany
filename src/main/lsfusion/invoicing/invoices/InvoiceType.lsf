MODULE InvoiceType;

REQUIRE Numerator, InvoicingSettings;

NAMESPACE Invoicing;

CLASS InvoiceType 'Тип реализации';
name 'Название' = DATA ISTRING[50] (InvoiceType) NONULL;

numerator 'Нумератор' = DATA Numerator (InvoiceType);
nameNumerator 'Нумератор' (InvoiceType o) = name(numerator(o));

taxIncluded 'Цена включает налоги' = DATA BOOLEAN (InvoiceType);

FORM invoiceType 'Тип реализации'
    OBJECTS o = InvoiceType PANEL 
    PROPERTIES(o) name, nameNumerator, taxIncluded
     
    EDIT InvoiceType OBJECT o;
;

FORM invoiceTypes 'Типы реализации'
    OBJECTS o = InvoiceType
    PROPERTIES(o) READONLY name
    
    LIST InvoiceType OBJECT o;
;

EXTEND FORM options
    OBJECTS it = InvoiceType
    PROPERTIES(it) READONLY name
    PROPERTIES(it) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(it);
    }
}

// default type
countInvoiceTypes 'Кол-во типов' () = GROUP SUM 1 IF r IS InvoiceType; 
firstInvoiceType = GROUP LAST InvoiceType r ORDER DESC r WHERE r IS InvoiceType;

defaultInvoiceType = firstInvoiceType() IF countInvoiceTypes() = 1;