MODULE Invoice;

REQUIRE InvoicingSettings, TaxItem, BankAccount, Time, MetaTax, ItemSale, PartnerSale, Company, 
        BarCode, MetaNumerator, Doc;

NAMESPACE Invoicing;

CLASS Invoice 'Расходная накладная';

// type
CLASS InvoiceType 'Тип документа' {
    invoice 'Реализация'
}
name 'Имя' (InvoiceType b) = staticCaption(b) IF b IS InvoiceType CHARWIDTH 20;

type 'Тип' = ABSTRACT CASE InvoiceType (Invoice);
type (Invoice b) += WHEN b IS Invoice THEN InvoiceType.invoice;
nameType 'Тип' (Invoice b) = name(type(b));

colorType 'Цвет' = ABSTRACT CASE COLOR (Invoice);

// date
date 'Дата' = DATA DATETIME (Invoice) NONULL IN id;
WHEN LOCAL SET (Invoice b IS Invoice) DO date(b) <- currentDateTime();

@defineSeriesNumber(invoice, 'Расходная накладная', 'РН');


// customer
customer 'Покупатель' = DATA Partner (Invoice) NONULL;
nameCustomer 'Покупатель' (Invoice b) = name(customer(b));
CONSTRAINT customer(Invoice b) AND NOT isCustomer(customer(b)) 
                CHECKED BY customer
                MESSAGE 'Контрагент должен быть покупателем';

// company
company 'Компания' = DATA Company (Invoice) NONULL;
nameCompany 'Компания' (Invoice b) = name(company(b));
WHEN LOCAL SET(Invoice b IS Invoice) AND NOT CHANGED(company(b)) DO company(b) <- defaultCompany(); 

// payment
dueDate 'Оплатить до' = DATA DATETIME (Invoice);

// partner account
customerAccount 'Счет покупателя' = DATA Account (Invoice);
nameCustomerAccount 'Счет покупателя' (Invoice b) = name(customerAccount(b));
WHEN LOCAL CHANGED (customer(Invoice b)) DO customerAccount(b) <- defaultAccount(customer(b));

CONSTRAINT customerAccount(Invoice b) AND NOT customer(b) = holder(customerAccount(b))
    CHECKED BY customerAccount
    MESSAGE 'Счет должен принадлежать выбранному контрагенту';

// company account
companyAccount 'Счет компании' = DATA Account (Invoice);
nameCompanyAccount 'Счет компании' (Invoice b) = name(companyAccount(b));
WHEN LOCAL CHANGED(company(Invoice b)) DO companyAccount(b) <- defaultAccount(company(b));

CONSTRAINT companyAccount(Invoice b) AND NOT company(b) = holder(companyAccount(b))
    CHECKED BY companyAccount
    MESSAGE 'Счет должен принадлежать выбранной компании';

customerReference 'Код покупателя' = DATA STRING[30] (Invoice);
sourceDocument 'Исходный документ' = DATA STRING[30] (Invoice);
notes 'Примечания' = DATA ISTRING[50] (Invoice);

// lines
CLASS InvoiceLine 'Строки документа';

invoice 'Расходная накладная' = DATA Invoice (InvoiceLine) NONULL DELETE;

index 'Индекс' = PARTITION SUM 1 ORDER InvoiceLine l BY invoice(l) IN id MATERIALIZED;

item 'Номенклатура' = DATA Item (InvoiceLine) NONULL;
nameItem 'Номенклатура' (InvoiceLine l) = name(item(l)) IN id;
idBarCodeItem 'Штрих код' (InvoiceLine l) = idBarCode(item(l));
idItem 'Код' (InvoiceLine l) = id(item(l));

CONSTRAINT item(InvoiceLine l) AND NOT canBeSold(item(l))
            CHECKED BY item 
            MESSAGE 'Наменклатура не предназначена для продажи';

unitMeasure 'Ед.изм.' = DATA ItemMeasure (InvoiceLine);
nameUnitMeasure 'Ед.изм.' (InvoiceLine l) = name(unitMeasure(l));

description 'Описание' = DATA STRING (InvoiceLine); 

quantity 'Кол-во' = DATA NUMERIC[16,3] (InvoiceLine) NONULL;
unitPrice 'Цена за штуку' = DATA NUMERIC[10,2] (InvoiceLine) NONULL;

WHEN LOCAL SETCHANGED(item(InvoiceLine l)) AND NOT CHANGED(unitPrice(l)) DO unitPrice(l) <- salesPrice(item(l));

// taxes
@defineTaxCalc(invoice, i, sales);

// readonly
readonly = ABSTRACT CASE BOOLEAN (Invoice);
readonly = ABSTRACT CASE BOOLEAN (InvoiceLine);

// Line properties
date 'Дата накладной' (InvoiceLine l) = date(invoice(l));
nameCustomer 'Покупатель' (InvoiceLine l) = nameCustomer(invoice(l));
nameCompany 'Компания' (InvoiceLine l) = nameCompany(invoice(l));

FORM invoice 'Расходная накладная'
    OBJECTS  i = Invoice PANEL 
    PROPERTIES(i) READONLYIF readonly(i) 
                             nameType BACKGROUND colorType(i), date, series, number, nameCustomer, nameCompany, sourceDocument,
                             dueDate, nameCustomerAccount, nameCompanyAccount,
                             customerReference, notes
        
    OBJECTS l = InvoiceLine
    PROPERTIES(l) READONLYIF readonly(l) index, nameItem, description, nameUnitMeasure, idBarCodeItem, idItem, quantity, 
                        unitPrice, taxes ON CHANGE changeTax(l), NEW, DELETE 
    FILTERS invoice(l) = i

    EDIT Invoice OBJECT i
;

DESIGN invoice {
    OBJECTS {
        NEW header {
            type = CONTAINERH;
            alignment = STRETCH;
            NEW headerLeft {
                MOVE PROPERTY(nameType(i));
                MOVE PROPERTY(date(i));
                NEW seriesNumber { 
                    type = CONTAINERH; 
                    MOVE PROPERTY(series(i)); 
                    MOVE PROPERTY(number(i));
                }
            }
            NEW headerRight {
                MOVE PROPERTY(nameCustomer(i))  { notNull = TRUE; }
                MOVE PROPERTY(nameCompany(i)) { notNull = TRUE; }           
                MOVE PROPERTY(sourceDocument(i));           
            }
            NEW relatedDoc {
                fill = 1;
                type = TABBED;
            }
        }
        NEW details {
            fill = 5;
            type = TABBED;
            NEW lines {
                caption = 'Строки';
                MOVE BOX(l) { 
                    fill = 3;
                    PROPERTY(nameItem(l)) { notNull = TRUE; } 
                }
                NEW linesFooter {                     
                    type = CONTAINERH;
                    fill = 1;
                }
            }
            NEW otherInformation {
                caption = 'Прочая информация';
                type = COLUMNS;
                columns = 2;
                NEW payment {
                    caption = 'Оплата';
                    MOVE PROPERTY(dueDate(i)); 
                    MOVE PROPERTY(nameCompanyAccount(i));
                    MOVE PROPERTY(nameCustomerAccount(i));              
                }           
                NEW invoiceInformat {
                    caption = 'Прочие';
                    MOVE PROPERTY(customerReference(i)); 
                    MOVE PROPERTY(notes(i));
                }
            }
        }
    }
}

// copy
copyInvoice 'Копировать' (Invoice i)  { 
    NEWSESSION {
        NEW ni = Invoice {
            customer(ni) <- customer(i);
            notes(ni) <- notes(i);
            FOR invoice(InvoiceLine l) = i INLINE NEW nl = InvoiceLine DO {
                invoice(nl) <- ni;
                item(nl) <- item(l);
                quantity(nl) <- quantity(l);
                unitPrice(nl) <- unitPrice(l);
            }
            SHOW invoice OBJECTS i = ni DOCKED;
        }
    }
}
EXTEND FORM invoice PROPERTIES(i) copyInvoice;
 
addInvoice 'Накладная' () {
    NEWSESSION {
        NEW b = Invoice {
            SHOW invoice OBJECTS i = b;  
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

FORM invoices 'Расходные накладные'
    OBJECTS i = Invoice
    PROPERTIES(i) READONLY nameType BACKGROUND colorType(i), date, dueDate, nameCustomer, notes         
    PROPERTIES(i) NEWSESSION EDIT
    PROPERTIES(i) NEWSESSION READONLYIF readonly(i) DELETE
    PROPERTIES addInvoice() DRAW i TOOLBAR
           
    LIST Invoice OBJECT i;

NAVIGATOR {
    operations {
        NEW invoices FIRST;
    }
}

@defineTaxForm(invoice, i);
@defineBarCodeAdd(invoice, i, item);

@defineStatus(invoice, 'счета', i);
@defineDocHistory(invoice, i, item, quantity);

nameStatus 'Статус' (InvoiceLine l) = nameStatus(invoice(l));
readonly (InvoiceLine l) += WHEN NOT status(invoice(l)) = InvoiceStatus.draft THEN TRUE;

@defineDocSearch(invoice, item, quantity, i, canBeSold);

DESIGN invoice {
    secondaryActions {
        MOVE PROPERTY(copyInvoice(i)); 
    }
}
