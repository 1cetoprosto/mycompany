MODULE InvoiceDone;

REQUIRE InvoiceReady, Time, InvoiceOutstandingCredit;

NAMESPACE Invoicing;

done 'Оплачено' = DATA BOOLEAN (Invoice);

EXTEND CLASS InvoiceStatus {
    done 'Оплачено'
}

WHEN SET(totalAmount(Invoice i) = paid(i)) DO {
    done(i) <- TRUE;
}

EXTEND FORM invoice
    PROPERTIES(i) done READONLY 
;

EXTEND FORM invoices 
    EXTEND FILTERGROUP status
        FILTER 'Оплачено' status(i) = InvoiceStatus.done 'F7'
;

status(Invoice b) += WHEN done(b) THEN InvoiceStatus.done;

DESIGN invoice {                               
    status {
        MOVE PROPERTY(done(i));                              
    }                 
}

EXTEND FORM item
    OBJECTS il = InvoiceLine
    PROPERTIES(il) READONLY nameStatus, date, namePartner, nameCompany
    PROPERTIES(il) READONLY nameItem, quantity, unitPrice
    FILTERS item(il) = i
;

DESIGN item {
    tabs {
        NEW invoices {
            caption = 'Реализация покупателям';
            MOVE BOX(il);
        }
    }
}

EXTEND FORM partners
    OBJECTS i = Invoice
    PROPERTIES(i) READONLY nameStatus, nameType, date, dueDate, namePartner, notes
    PROPERTIES(i) READONLY untaxedAmount, totalTax, totalAmount, paid
    FILTERS partner(i) = p
;

DESIGN partners {
    operations {
        NEW invoices {
            caption = 'Реализация товаров и услуг';
            MOVE BOX(i);
        }
    }
}
