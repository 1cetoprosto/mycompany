MODULE InvoiceDone;

REQUIRE InvoiceReady, Time, InvoiceDebt;

NAMESPACE Invoicing;

done 'Оплачено' = DATA BOOLEAN (Invoice);

EXTEND CLASS InvoiceStatus {
    done 'Оплачено'
}

WHEN SET(totalAmount(Invoice i) = paid(i)) DO done(i) <- TRUE;
WHEN DROPPED(totalAmount(Invoice i) = paid(i)) DO done(i) <- NULL;

EXTEND FORM invoice
    PROPERTIES(i) done 
;

EXTEND FORM invoices 
    EXTEND FILTERGROUP status
        FILTER 'Оплачено' status(i) = InvoiceStatus.done 'F7'
;

status(Invoice b) += WHEN done(b) THEN InvoiceStatus.done;

DESIGN invoice {                               
    status {
        MOVE PROPERTY(done(i));                              
    }                 
}

EXTEND FORM item
    OBJECTS il = InvoiceLine
    PROPERTIES(il) READONLY nameStatus, date, nameCustomer, nameCompany
    PROPERTIES(il) READONLY nameItem, quantity, unitPrice
    FILTERS item(il) = i
;

DESIGN item {
    tabs {
        NEW invoices {
            caption = 'Реализация покупателям';
            MOVE BOX(il);
        }
    }
}

EXTEND FORM partner
    OBJECTS i = Invoice
    PROPERTIES(i) READONLY nameStatus, date, dueDate, nameCustomer, note
    PROPERTIES(i) READONLY untaxedAmount, totalTax, totalAmount, paid
    FILTERS customer(i) = p
;

DESIGN partner {
    tabs {
        NEW invoices {
            caption = 'Расходная накладная';
            MOVE BOX(i);
        }
    }
}
