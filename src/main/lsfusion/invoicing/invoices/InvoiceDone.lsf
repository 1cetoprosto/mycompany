MODULE InvoiceDone;

REQUIRE Invoicing, InvoiceReady, Time;

NAMESPACE Invoicing;

done 'Оплачено' = DATA BOOLEAN (Invoice);

EXTEND CLASS InvoiceStatus {
    done 'Оплачено'
}

registerPayment 'Оформить платеж' (Invoice i){
    APPLY; 
    IF canceled() THEN RETURN;
    IF done(i) THEN RETURN;
    
    NEWSESSION {
        NEW p = Payment {
            date(p) <- currentDateTime();
            amount(p) <- totalAmount(i) (-) paid(i);
            fromAccount(p) <- partnerAccount(i);
            DIALOG payment OBJECTS p = p DO {
                IF (totalAmount(i) > amount(p) (+) paid(i)) THEN {
                    ASK 'Оплачена не вся сумма. Закрыть накладную ?' DO {
                        done(i) <- TRUE;
                        paid(p, i) <- amount(p);
                    } ELSE {
                        paid(p, i) <- amount(p);
                    }
                } ELSE {
                    done(i) <- TRUE; 
                    paid(p, i) <- amount(p);
                }
                APPLY; 
            }
        } 
    }
}

EXTEND FORM invoice
    PROPERTIES(i) registerPayment SHOWIF status(i) = InvoiceStatus.ready, done READONLY 
;

EXTEND FORM invoices 
    EXTEND FILTERGROUP status
        FILTER 'Отгружен' status(i) = InvoiceStatus.done 'F7'
;

status(Invoice b) += WHEN done(b) THEN InvoiceStatus.done;

DESIGN invoice {                               
    statusActions {                  
        MOVE PROPERTY(registerPayment(i));
    }
    status {
        MOVE PROPERTY(done(i));                              
    }                 
}
