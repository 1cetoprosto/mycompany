MODULE InvoiceDone;

REQUIRE Invoicing, InvoiceReady, Time;

NAMESPACE Invoicing;

done 'Оплачено' = DATA BOOLEAN (Invoice);

EXTEND CLASS InvoiceStatus {
    done 'Оплачено'
}

registerPayment 'Оформить платеж' (Invoice i){
    APPLY; 
    IF canceled() THEN RETURN;
    IF done(i) THEN RETURN;
    
    NEWSESSION {
        NEW p = Payment {
            invoice(p) <- i;
            date(p) <- currentDateTime();
            amount(p) <- totalAmount(i);
            fromAccount(p) <- partnerAccount(i);
            SHOW payment OBJECTS p = p;
            IF (amount(p) < totalAmount(i) (-) paid(i) OR NOT amount(p)) THEN {
                ASK 'Оплачена не вся сумма. Закрыть счет ?' DO {}
                ELSE {
                    paid(p) <- TRUE;
                    APPLY;
                    RETURN;
                }
            }   
        } 
        done(i) <- TRUE;
        APPLY;
    }
}

WHEN SETCHANGED(done(invoice(Payment p))) DO {
    paid(p) <- TRUE;
}

EXTEND FORM invoice
    PROPERTIES(i) registerPayment SHOWIF ready(i), done READONLY 
;

EXTEND FORM invoices 
    EXTEND FILTERGROUP status
        FILTER 'Отгружен' status(i) = InvoiceStatus.done 'F7'
;

status(Invoice b) += WHEN done(b) THEN InvoiceStatus.done;

DESIGN invoice {                               
    statusActions {                  
        MOVE PROPERTY(registerPayment(i));
    }
    status {
        MOVE PROPERTY(done(i));                              
    }                 
}
