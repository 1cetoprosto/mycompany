MODULE InvoiceDone;

REQUIRE Invoicing, InvoiceReady;

NAMESPACE Invoicing;

done 'Оплачено' = DATA BOOLEAN (Invoice);

EXTEND CLASS InvoiceStatus {
    done 'Оплачено'
}

registerPayment 'Оформить платеж' (Invoice i){
    APPLY; 
    IF canceled() THEN RETURN;
    IF done(i) THEN RETURN;
    
    NEWSESSION {
        NEW p = Payment {
            invoice(p) <- i;
            amount(p) <- totalAmount(invoice(p));
            SHOW payment OBJECTS p = p; 
            IF amountPaid(p) < amount(p) THEN ASK 'Оплачена не вся сумма. Принять?' DO {} ELSE RETURN; 
        }
        done(i) <- TRUE;
        APPLY;
    }
}

WHEN SETCHANGED(done(invoice(Payment p))) DO {
    paid(p) <- TRUE;
}

EXTEND FORM invoice
    PROPERTIES(i) registerPayment SHOWIF ready(i), done READONLY 
;

EXTEND FORM invoices 
    EXTEND FILTERGROUP status
        FILTER 'Отгружен' status(i) = InvoiceStatus.done 'F7'
;

status(Invoice b) += WHEN done(b) THEN InvoiceStatus.done;

DESIGN invoice {                               
    statusActions {                  
        MOVE PROPERTY(registerPayment(i));
    }
    status {
        MOVE PROPERTY(done(i));                              
    }                 
}
