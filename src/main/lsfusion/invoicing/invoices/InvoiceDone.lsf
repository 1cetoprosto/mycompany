MODULE InvoiceDone;

REQUIRE Invoicing, InvoiceReady, Time, InvoiceOutstandingCredit;

NAMESPACE Invoicing;

done 'Оплачено' = DATA BOOLEAN (Invoice);

EXTEND CLASS InvoiceStatus {
    done 'Оплачено'
}

WHEN SET(totalAmount(Invoice i) = paid(i)) DO {
    done(i) <- TRUE;
}

EXTEND FORM invoice
    PROPERTIES(i) done READONLY 
;

EXTEND FORM invoices 
    EXTEND FILTERGROUP status
        FILTER 'Оплачено' status(i) = InvoiceStatus.done 'F7'
;

status(Invoice b) += WHEN done(b) THEN InvoiceStatus.done;

DESIGN invoice {                               
    status {
        MOVE PROPERTY(done(i));                              
    }                 
}

EXTEND FORM items
    OBJECTS il = InvoiceLine
    PROPERTIES(il) READONLY nameStatus, date, namePartner, nameCompany
    PROPERTIES(il) READONLY nameItem, quantity, unitPrice
    FILTERS item(il) = i
;

DESIGN items {
    operations {
        NEW invoices {
            caption = 'Расходные накладные';
            MOVE BOX(il);
        }
    }
}

EXTEND FORM partners
    OBJECTS il = InvoiceLine
    PROPERTIES(il) READONLY nameStatus, date, namePartner, nameCompany
    PROPERTIES(il) READONLY nameItem, quantity, unitPrice
    FILTERS partner(il) = p
;

DESIGN partners {
    operations {
        NEW invoices {
            caption = 'Расходные накладные';
            MOVE BOX(il);
        }
    }
}
