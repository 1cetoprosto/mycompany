MODULE InvoiceDone;

REQUIRE Invoicing, InvoiceReady, Time, InvoiceOutstandingCredit;

NAMESPACE Invoicing;

done 'Оплачено' = DATA BOOLEAN (Invoice);

EXTEND CLASS InvoiceStatus {
    done 'Оплачено'
}

WHEN SET(totalAmount(Invoice i) = paid(i)) DO {
    done(i) <- TRUE;
}

EXTEND FORM invoice
    PROPERTIES(i) done READONLY 
;

EXTEND FORM invoices 
    EXTEND FILTERGROUP status
        FILTER 'Оплачено' status(i) = InvoiceStatus.done 'F7'
;

status(Invoice b) += WHEN done(b) THEN InvoiceStatus.done;

DESIGN invoice {                               
    status {
        MOVE PROPERTY(done(i));                              
    }                 
}

EXTEND FORM items
    OBJECTS invoice = (in = Invoice, il = InvoiceLine)
    PROPERTIES(in) nameStatus, date, namePartner, nameCompany
    PROPERTIES(il) nameItem, quantity, unitPrice
    FILTERS invoice(il) = in, item(il) = i
;

DESIGN items {
    operations {
        NEW invoices {
            caption = 'Расходные накладные';
            MOVE BOX(invoice);
        }
    }
}

EXTEND FORM partners
    OBJECTS invoice = (in = Invoice, il = InvoiceLine)
    PROPERTIES(in) nameStatus, date, namePartner, nameCompany
    PROPERTIES(il) nameItem, quantity, unitPrice
    FILTERS invoice(il) = in, partner(in) = p
;

DESIGN partners {
    operations {
        NEW invoices {
            caption = 'Расходные накладные';
            MOVE BOX(invoice);
        }
    }
}
