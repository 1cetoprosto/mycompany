MODULE CreditNote;

REQUIRE InvoiceDone, Bill;

NAMESPACE Invoicing;

// types
isCreditNote 'Возврат' = DATA BOOLEAN (BillType);
EXTEND FORM billType PROPERTIES(o) isCreditNote;

isCreditNote (Bill r) = isCreditNote(type(r));

billType = DATA BillType (InvoiceType);
nameBillType 'Тип приобретения для возврата' (InvoiceType t) = name(billType(t));
EXTEND FORM invoiceType PROPERTIES(o) nameBillType;

// lines
@defineDocLineRelation(invoice, bill, 'Реализации', 'Возвраты', i, b);

numberInvoiceLine 'Реализация' (BillLine l) = number(invoiceLine(l)); 

CONSTRAINT invoiceLine(BillLine l) AND NOT 
                (customer(invoiceLine(l)) = vendor(l) AND 
                 item(invoiceLine(l)) = item(l))
    CHECKED BY invoiceLine
    MESSAGE 'Контрагент и номенклатура реализации должны совпадать с контрагентом и товаром возврата';

// events
WHEN LOCAL SETCHANGED(invoiceLine(BillLine l)) DO {
    price(l) <- resultPrice(invoiceLine(l));
    in(l, Tax t) <- in(invoiceLine(l), t);
}

EXTEND FORM bill
    PROPERTIES(l) numberInvoiceLine SHOWIF isCreditNote(b)
;

// add
addCreditNote 'Вернуть' (Invoice i) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION { 
        NEW b = Bill {
            type(b) <- billType(type(i));
            
            dateTime(b) <- currentDateTime();
            
            vendor(b) <- customer(i);
            vendorAccount(b) <- customerAccount(i);
            
            company(b) <- company(i);
            companyAccount(b) <- companyAccount(i);
            
            note(b) <- note(i);
            
            FOR invoice(InvoiceLine l) = i INLINE NEW bl = BillLine DO {
                invoiceLine(bl) <- l; 
                bill(bl) <- b;

                item(bl) <- item(l);

                quantity(bl) <- quantity(l);
            }
            SHOW bill OBJECTS b = b DOCKED;
        }
        
    }
}

EXTEND FORM invoice
    PROPERTIES(i) addCreditNote SHOWIF ready(i)
;

DESIGN invoice {
    secondaryActions {
        MOVE PROPERTY(addCreditNote(i));
    }               
}