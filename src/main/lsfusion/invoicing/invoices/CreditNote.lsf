MODULE CreditNote;

REQUIRE InvoiceDone, Bill;

NAMESPACE Invoicing;

creditNoteLine = DATA InvoiceLine (BillLine);
numberCreditNoteLine 'Приходная накладная' (BillLine l) = number(creditNoteLine(l)); 

CONSTRAINT creditNoteLine(BillLine l) AND NOT 
                (customer(creditNoteLine(l)) = vendor(l) AND 
                 item(creditNoteLine(l)) = item(l))
    CHECKED BY creditNoteLine
    MESSAGE 'Контрагент и номенклатура расходной накладной должны совпадать с контрагентом и товаром возврата';

in (Invoice i, Bill b) = GROUP SUM 1 IF invoice(creditNoteLine(BillLine l)) = i AND bill(l) = b MATERIALIZED; 

countInvoices 'Кол-во расходных накладных' (Bill b) = GROUP SUM 1 IF in(Invoice i, b) MATERIALIZED;
descriptionInvoices 'Расходные накладные' (Bill b) = GROUP CONCAT description(Invoice i) IF in(i, b), ','; 

// source
sourceDocument (Bill b) += WHEN countInvoices(b) THEN descriptionInvoices(b);
openSourceDocument (Bill b) + { FOR in(Invoice i, b) DO edit(i); }

// events
WHEN LOCAL SETCHANGED(creditNoteLine(BillLine l)) DO {
    price(l) <- price(creditNoteLine(l));
    in(l, Tax t) <- in(creditNoteLine(l), t);
}

isCreditNote 'Возврат' (Bill b) = DATA BOOLEAN (Bill);

EXTEND FORM bill
    PROPERTIES(b) isCreditNote
    
    PROPERTIES(l) numberCreditNoteLine SHOWIF isCreditNote(b)
;

DESIGN bill {
    headerRight {
        MOVE PROPERTY(isCreditNote(b));
    }
}

// add
addCreditNote 'Создать возврат' (Invoice i) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION { 
        NEW b = Bill {
            isCreditNote(b) <- TRUE;
            
            dateTime(b) <- currentDateTime();
            
            vendor(b) <- customer(i);
            vendorAccount(b) <- customerAccount(i);
            
            company(b) <- company(i);
            companyAccount(b) <- companyAccount(i);
            
            note(b) <- note(i);
            
            FOR invoice(InvoiceLine l) = i INLINE NEW bl = BillLine DO {
                creditNoteLine(bl) <- l; 
                bill(bl) <- b;

                item(bl) <- item(l);

                quantity(bl) <- quantity(l);
            }
            SHOW bill OBJECTS b = b DOCKED;
        }
        
    }
}

EXTEND FORM invoice
    PROPERTIES(i) addCreditNote SHOWIF ready(i)
;

DESIGN invoice {
    secondaryActions {
        MOVE PROPERTY(addCreditNote(i));
    }               
}

EXTEND FORM bills
    PROPERTIES READONLY isCreditNote(b)
;