MODULE CreditNote;

REQUIRE InvoiceDone, Bill;

NAMESPACE Invoicing;

creditNote = DATA Invoice (Bill);
creditNoteLine = DATA InvoiceLine (BillLine);

// source
sourceDocument (Bill b) += WHEN creditNote(b) THEN description(creditNote(b));
openSourceDocument (Bill b) + { IF creditNote(b) THEN edit(creditNote(b)); }

isCreditNote 'Возврат' (Bill b) = TRUE IF creditNote(b);

// add
addCreditNote 'Создать возврат' (Invoice i) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION { 
        NEW b = Bill {
            creditNote(b) <- i;
            
            dateTime(b) <- currentDateTime();
            
            vendor(b) <- customer(i);
            vendorAccount(b) <- customerAccount(i);
            
            company(b) <- company(i);
            companyAccount(b) <- companyAccount(i);
            
            note(b) <- note(i);
            
            FOR invoice(InvoiceLine l) = i INLINE NEW bl = BillLine DO {
                creditNoteLine(bl) <- l; 
                bill(bl) <- b;

                item(bl) <- item(l);

                quantity(bl) <- quantity(l);
                price(bl) <- price(l);
                FOR in(l, Tax t) DO {
                    in(bl, t) <- in(l,t);
                }
            }
            SHOW bill OBJECTS b = b DOCKED;
        }
        
    }
}

EXTEND FORM invoice
    PROPERTIES(i) addCreditNote SHOWIF ready(i)
;

DESIGN invoice {
    secondaryActions {
        MOVE PROPERTY(addCreditNote(i));
    }               
}

EXTEND FORM bills
    PROPERTIES READONLY isCreditNote(b)
;