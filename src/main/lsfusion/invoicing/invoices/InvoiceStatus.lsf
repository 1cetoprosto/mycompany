MODULE InvoiceStatus;

REQUIRE Invoicing, Invoice;

NAMESPACE Invoicing;

CLASS InvoiceStatus 'Статус счета' {
    draft 'Черновик'
}

name 'Имя' (InvoiceStatus o) = staticCaption(o) IF o IS InvoiceStatus CHARWIDTH 15;

status 'Статус' = ABSTRACT CASE InvoiceStatus (Invoice);
nameStatus 'Статус' (Invoice i) = name(status(i));
colorStatus 'Цвет' = ABSTRACT CASE COLOR (Invoice);

readonly (InvoiceLine l) += WHEN NOT status(invoice(l)) = InvoiceStatus.draft THEN TRUE;
readonly (Invoice i) += WHEN NOT status(i) = InvoiceStatus.draft THEN TRUE;

EXTEND FORM invoice
    PROPERTIES(i) nameStatus BACKGROUND colorStatus(i)
;

EXTEND FORM invoices    
    FILTERGROUP status
        FILTER 'Черновик' status(i) = InvoiceStatus.draft 'F10'
;

EXTEND FORM invoices
    PROPERTIES(i) nameStatus BEFORE date(i) BACKGROUND colorStatus(i)
;

status(Invoice b) += WHEN b IS Invoice THEN InvoiceStatus.draft;
colorStatus(Invoice b) += WHEN status(b) == InvoiceStatus.draft THEN RGB(187,242,210);

copyInvoice 'Копировать' (Invoice i)  { 
    NEWSESSION {
        NEW ni = Invoice {
            partner(ni) <- partner(i);
            notes(ni) <- notes(i);
            FOR invoice(InvoiceLine l) = i INLINE NEW nl = InvoiceLine DO {
                invoice(nl) <- ni;
                item(nl) <- item(l);
                quantity(nl) <- quantity(l);
                unitPrice(nl) <- unitPrice(l);
                FOR in(l, Tax t) DO {
                    in(nl,t) <- in(l,t);
                }
            }
            SHOW invoice OBJECTS i = ni DOCKED;
        }
    }
}

EXTEND FORM invoice
    PROPERTIES(i) copyInvoice
;

DESIGN invoice {
    OBJECTS {        
        NEW statusPane FIRST {
            caption = 'Статусы';
            type = CONTAINERH;
            alignment = STRETCH;                          
            NEW statusActions {
                type = CONTAINERH;                    
                flex = 1; 
                NEW primary {
                    type = CONTAINERH;
                }
                NEW  secondary {
                    type = CONTAINERH;
                    MOVE PROPERTY (copyInvoice(i));  
                }            
            }
            NEW status {
                type = CONTAINERH;                
                MOVE PROPERTY(nameStatus(i)); 
            }
        }                
    }
}
