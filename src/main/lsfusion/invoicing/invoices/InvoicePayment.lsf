MODULE InvoicePayment;

REQUIRE InvoiceDone, InvoiceDebt, IncomingPaymentDebt;

NAMESPACE Invoicing;

registerPayment 'Зарегистрировать платеж' (Invoice i) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        NEW p = IncomingPayment {
            partner(p) <- customer(i);
            partnerAccount(p) <- customerAccount(i);
            
            company(p) <- company(i);
            companyAccount(p) <- companyAccount(i);
            
            amount(p) <- left(i);
            DIALOG incomingPayment OBJECTS p = p DOCKED DO {
                paid(p, i) <- left(p, i);
               IF (totalAmount(i) > amount(p) (+) paid(i)) THEN {
                   ASK 'Оплачена не вся сумма. Закрыть документ?' DO {
                       done(i) <- TRUE;
                   }
               }
               APPLY; 
            }
       } 
   }
}

EXTEND FORM invoice
    PROPERTIES(i) registerPayment SHOWIF status(i) = InvoiceStatus.ready 
;

DESIGN invoice {
    primaryActions {
        MOVE PROPERTY(registerPayment(i)) { fontStyle = 'bold'; }
    }
}