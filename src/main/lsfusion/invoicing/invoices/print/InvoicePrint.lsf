MODULE InvoicePrint;

REQUIRE InvoiceDone;

NAMESPACE Invoicing;

partnerAddress 'Адрес' (Invoice i) = concatAddress(customer(i),'\n'); 
partnerPhone 'Телефон' (Invoice i) = phone(customer(i));
companyAddress 'Адрес' (Invoice i) = concatAddress(company(i),'\n');
companyPhone 'Телефон' (Invoice i) = phone(company(i));
companyEmail 'Электронная почта' (Invoice i) = email(company(i));
companyWebsite 'Веб сайт' (Invoice i) = website(company(i));

FORM printInvoice 'Расходная накладная'
    OBJECTS i = Invoice PANEL 
    PROPERTIES(i) date, dueDate, nameCustomer, nameStatus, number, partnerAddress, partnerPhone
    PROPERTIES(i) nameCompany, companyAddress, companyPhone, companyEmail, companyWebsite
    PROPERTIES(i) untaxedAmount, totalAmount, paid
    
    OBJECTS l = InvoiceLine
    PROPERTIES(l) nameItem, quantity, unitPrice, taxes, subtotal
    FILTERS invoice(l) = i
    
    OBJECTS t = Tax
    PROPERTIES name(t), taxAmount(t, i)
    FILTERS countLine(i, t)
;

print(Invoice i) {
    PRINT printInvoice OBJECTS i = i; 
}

EXTEND FORM invoice
    PROPERTIES(i) print
;

DESIGN invoice {
    secondaryActions {
        MOVE PROPERTY(print(i)) { caption = 'Печатать'; charWidth = 10;}
    }
}
