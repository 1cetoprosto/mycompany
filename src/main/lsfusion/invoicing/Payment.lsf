MODULE Payment;

REQUIRE Invoicing, Account, Company, Time, InvoicingSettings;

NAMESPACE Invoicing;

CLASS Payment 'Платеж';

date 'Дата' = DATA DATETIME (Payment) NONULL;
date(Payment p) <- currentDateTime() WHEN SET(p IS Payment);

toAccount 'Счет (куда)' = DATA Account (Payment) NONULL;
toAccountNumber 'Номер счета (куда)' (Payment p) = number(toAccount(p));
nameToAccountHolder 'Держатель счета (куда)' (Payment p) = nameAccountHolder(toAccount(p));

fromAccount 'Счет (откуда)' = DATA Account (Payment) NONULL;
fromAccountNumber 'Номер счета (откуда)' (Payment p) = number(fromAccount(p));
nameFromAccountHolder 'Держатель счета (откуда)' (Payment p) = nameAccountHolder(fromAccount(p));

amount 'Сумма' = DATA NUMERIC[14,2] (Payment);

inAmount 'Доход' (Account a) = GROUP SUM amount(Payment p) IF toAccount(p) = a;
outAmount 'Расход' (Account a) = GROUP SUM amount(Payment p) IF fromAccount(p) = a;

balance 'Баланс' (Account a) = inAmount(a) (-) outAmount(a);

series 'Серия' = DATA STRING[2] (Payment);
number 'Номер' = DATA STRING[28] (Payment) NONULL;

WHEN SETCHANGED (Payment p IS Payment) DO {
    number(p) <- curStringValue(numerator());
    series(p) <- series(numerator());
    incrementValueSession(numerator());   
}

seriesNumber 'Серия/Номер' (Payment p) = CONCAT '/', series(p), number(p); 

CONSTRAINT toAccount(Payment p) AND fromAccount(p) AND 
                NOT (holder(toAccount(p)) IS Company OR holder(fromAccount(p)) IS Company)
                CHECKED BY toAccount, fromAccount
                MESSAGE 'Один из счетов должен принадлежать компании';

FORM payment 'Платеж'
    OBJECTS p = Payment PANEL 
    PROPERTIES(p) date, fromAccountNumber, nameFromAccountHolder, toAccountNumber, nameToAccountHolder, 
                amount
    
    EDIT Payment OBJECT p
;

FORM payments 'Платежи'
    OBJECTS p = Payment
    PROPERTIES(p) READONLY date, fromAccountNumber, nameFromAccountHolder, toAccountNumber, nameToAccountHolder, 
                amount 
    PROPERTIES (p) NEWSESSION NEW, EDIT, DELETE 
    
    LIST Payment OBJECT p
;

NAVIGATOR {
    invoicing {
        operations {
            NEW payments;
        }
    }
}
