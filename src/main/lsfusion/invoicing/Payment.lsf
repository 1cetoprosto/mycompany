MODULE Payment;

REQUIRE Invoicing, Account, AccLedger;

NAMESPACE Invoicing;

CLASS Payment 'Платеж';

date 'Дата' = DATA DATETIME (Payment);
paid 'Оплачено' = DATA BOOLEAN (Payment) CHARWIDTH 10;

toAccount 'Счет (куда)' = DATA Account (Payment);
toAccountNumber 'Номер счета (куда)' (Payment p) = accountNumber(toAccount(p));
nameToAccountHolder 'Держатель счета (куда)' (Payment p) = nameAccountHolder(toAccount(p));

fromAccount 'Счет (откуда)' = DATA Account (Payment);
fromAccountNumber 'Номер счета (откуда)' (Payment p) = accountNumber(fromAccount(p));
nameFromAccountHolder 'Держатель счета (откуда)' (Payment p) = nameAccountHolder(fromAccount(p));

amount 'Сумма' = DATA NUMERIC[14,2] (Payment);

amountPaid 'Оплачено' = DATA NUMERIC[14,2] (Payment);

FORM payment 'Платеж'
    OBJECTS p = Payment PANEL 
    PROPERTIES(p) date, fromAccountNumber, nameFromAccountHolder, toAccountNumber, nameToAccountHolder, 
                amount READONLY, amountPaid 
    
    EDIT Payment OBJECT p
;

FORM payments 'Платежи'
    OBJECTS p = Payment
    PROPERTIES(p) READONLY paid, date, fromAccountNumber, nameFromAccountHolder, toAccountNumber, nameToAccountHolder, 
                amount, amountPaid
    PROPERTIES (p) NEWSESSION NEW, EDIT, DELETE 
    
    LIST Payment OBJECT p
;

NAVIGATOR {
    invoicing {
        NEW payments;
    }
}

EXTEND CLASS Payment : AccLedger;

done(Payment p) += paid(p);
date(Payment p) += date(p);

toAccount(Payment p) += toAccount(p);
fromAccount(Payment p) += fromAccount(p);
amount(Payment p) += amount(p);

