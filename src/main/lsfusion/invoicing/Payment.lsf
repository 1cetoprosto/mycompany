MODULE Payment;

REQUIRE Invoicing, Account;

NAMESPACE Invoicing;

CLASS Payment 'Платеж';

date 'Дата' = DATA DATETIME (Payment);
paid 'Оплачено' = DATA BOOLEAN (Payment) CHARWIDTH 10;

toAccount 'Счет (куда)' = DATA Account (Payment);
toAccountNumber 'Номер счета (куда)' (Payment p) = accountNumber(toAccount(p));
nameToAccountHolder 'Держатель счета (куда)' (Payment p) = nameAccountHolder(toAccount(p));

fromAccount 'Счет (откуда)' = DATA Account (Payment);
fromAccountNumber 'Номер счета (откуда)' (Payment p) = accountNumber(fromAccount(p));
nameFromAccountHolder 'Держатель счета (откуда)' (Payment p) = nameAccountHolder(fromAccount(p));

amount 'Сумма' = DATA NUMERIC[14,2] (Payment);

inAmount 'Доход' (Account a) = GROUP SUM amount(Payment p) IF paid(p) AND toAccount(p) = a;
outAmount 'Расход' (Account a) = GROUP SUM amount(Payment p) IF paid(p) AND fromAccount(p) = a;

balance 'Баланс' (Account a) = inAmount(a) (-) outAmount(a);

FORM payment 'Платеж'
    OBJECTS p = Payment PANEL 
    PROPERTIES(p) date, fromAccountNumber, nameFromAccountHolder, toAccountNumber, nameToAccountHolder, 
                amount
    
    EDIT Payment OBJECT p
;

FORM payments 'Платежи'
    OBJECTS p = Payment
    PROPERTIES(p) READONLY paid, date, fromAccountNumber, nameFromAccountHolder, toAccountNumber, nameToAccountHolder, 
                amount 
    PROPERTIES (p) NEWSESSION NEW, EDIT, DELETE 
    
    LIST Payment OBJECT p
;

NAVIGATOR {
    invoicing {
        operations {
            NEW payments;
        }
    }
}
