MODULE OutgoingPaymentDebt;

REQUIRE OutgoingPayment, Debt;

NAMESPACE Invoicing;

// extend
EXTEND CLASS OutgoingPayment : OutgoingDebt;
number (OutgoingPayment p) += seriesNumber(p);
date (OutgoingPayment p) += date(p);

partner (OutgoingPayment p) += partner(p);
company (OutgoingPayment p) += company(p);

amount (OutgoingPayment p) += amount(p);

// pay
pay 'Разнести' (IncomingDebt d, OutgoingPayment p) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        paid(d, p) <- left(d, p);
        APPLY;
    }
} CHANGEMOUSE 'DBLCLK';

EXTEND FORM outgoingPayment
    OBJECTS d = IncomingDebt
    PROPERTIES(d) READONLY date, number, type
    PROPERTIES paid(d, p)
    FILTERS paid(d, p)
    
    OBJECTS dd = IncomingDebt
    PROPERTIES(dd) READONLY date, number, type, left
    PROPERTIES pay(dd, p) TOOLBAR
    FILTERS canBePaid(dd, p)   
;

DESIGN outgoingPayment {
    details {
        NEW debts {
            caption = 'Разнесение оплат';
            MOVE BOX(d) { caption = 'Разнесенные'; }            
            MOVE BOX(dd) { caption = 'Доступные'; }          
        }
    }
}

// auto set paid
WHEN (SETCHANGED(reference(OutgoingPayment p)) OR SETCHANGED(partner(p))) AND 
     isISubstring(reference(p), number(IncomingDebt d)) AND 
     canBePaid(d, p) DO
    paid(d, p) <- left(d, p); 