MODULE Payment;

REQUIRE InvoicingSettings, BankAccount, Company, Time, MetaNumerator;

NAMESPACE Invoicing;

CLASS Payment 'Платеж';

date 'Дата' = DATA DATETIME (Payment) NONULL;
date(Payment p) <- currentDateTime() WHEN SET(p IS Payment);

toAccount 'Счет получателя' = DATA Account (Payment);
toAccountName 'Счет получателя' (Payment p) = IF toAccount(p) IS BankAccount 
    THEN nameAccount(toAccount(p)) ELSE number(toAccount(p));

toPartner 'Получатель' = DATA Partner (Payment) NONULL;
nameToPartner 'Получатель' (Payment p) = name(toPartner(p));

WHEN LOCAL CHANGED(toPartner(Payment p)) DO {
    toAccount(p) <- defaultAccount(toPartner(p));
}

fromAccount 'Счет плательщика' = DATA Account (Payment);
fromAccountName 'Счет плательщика' (Payment p) = IF fromAccount(p) IS BankAccount 
    THEN nameAccount(fromAccount(p)) ELSE number(fromAccount(p));

fromPartner 'Плательщик' = DATA Partner (Payment) NONULL;
nameFromPartner 'Плательщик' (Payment p) = name(fromPartner(p));

WHEN LOCAL CHANGED(fromPartner(Payment p)) DO {
    fromAccount(p) <- defaultAccount(fromPartner(p));
}

amount 'Сумма' = DATA NUMERIC[14,2] (Payment) NONULL;

inAmount 'Доход' (Account a) = GROUP SUM amount(Payment p) IF toAccount(p) = a;
outAmount 'Расход' (Account a) = GROUP SUM amount(Payment p) IF fromAccount(p) = a;

balance 'Баланс' (Account a) = inAmount(a) (-) outAmount(a);

@defineSeriesNumber(payment, 'Платежи', 'ПЛ');

isIncoming 'Входящий' (Payment p) = toPartner(p) IS Company;
isOutgoing 'Входящий' (Payment p) = fromPartner(p) IS Company;

notes 'Примечания' = DATA ISTRING[50] (Payment);
reference 'Ссылка' = DATA ISTRING[50] (Payment);

CONSTRAINT toAccount(Payment p) AND fromAccount(p) AND 
                NOT (holder(toAccount(p)) IS Company OR holder(fromAccount(p)) IS Company)
                CHECKED BY toAccount, fromAccount
                MESSAGE 'Один из счетов должен принадлежать компании';
                
CONSTRAINT toPartner(Payment p) AND fromPartner(p) AND 
                NOT (toPartner(p) IS Company OR fromPartner(p) IS Company)
                CHECKED BY toPartner, fromPartner
                MESSAGE 'Один из держателей счетов должен быть компанией';
    
CONSTRAINT (toPartner(Payment p) AND toAccount(p)) AND NOT (holder(toAccount(p)) = toPartner(p))
                CHECKED BY  toAccount
                MESSAGE  'Счет и держатель счета не совпадают';
                
CONSTRAINT (fromPartner(Payment p) AND fromAccount(p)) AND NOT (holder(fromAccount(p)) = fromPartner(p))
                CHECKED BY fromAccount
                MESSAGE  'Счет и держатель счета не совпадают';

FORM payment 'Платеж'
    OBJECTS p = Payment PANEL 
    PROPERTIES(p) date,  amount, reference, fromAccountName, nameFromPartner, 
                         Invoicing.toAccountName[Payment], nameToPartner, series, number, notes
               
    
    EDIT Payment OBJECT p
;

FORM payments 'Платежи'
    OBJECTS p = Payment
    PROPERTIES(p) READONLY date, fromAccountName, nameFromPartner, Invoicing.toAccountName[Payment], nameToPartner, 
                amount, reference, notes
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE 
    
    LIST Payment OBJECT p
;

NAVIGATOR {
    invoicing {
        operations {
            NEW payments;
        }
    }
}

DESIGN payment {
    OBJECTS {
        NEW header {
            NEW options {
                caption = 'Данные';
                type = CONTAINERH;
                fill = 1;
                MOVE PROPERTY(date(p)); 
                MOVE PROPERTY(amount(p));
                MOVE PROPERTY(series(p));
                MOVE PROPERTY(number(p));
                MOVE PROPERTY(reference(p));    
            }
            NEW namesAndAccounts {
                type = CONTAINERH;
                fill = 1;
                NEW from {
                    type = CONTAINERH;
                    caption = 'Плательщик';
                    MOVE PROPERTY(nameFromPartner(p)) { notNull = TRUE; }
                    MOVE PROPERTY(fromAccountName(p));
                }
                NEW to { 
                    type = CONTAINERH;
                    caption = 'Получатель';
                    MOVE PROPERTY(nameToPartner(p)) { notNull = TRUE; }
                    MOVE PROPERTY(toAccountName(p));
                }
            } 
            NEW additions {
                caption = 'Дополнительно';
                type = CONTAINERH;
                fill = 1;
                MOVE PROPERTY(notes(p));
            }     
        }
        NEW details {
            type = TABBED;
            fill = 1;
            NEW invoices {
                type = SPLITV;
                fill = 1;
                caption = 'Реализация товаров и услуг';
            }
            NEW bills {
                type = SPLITV;
                fill = 1;
                caption = 'Приобретение товаров и услуг';
            }
        }
    }
}
