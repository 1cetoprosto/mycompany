MODULE Payment;

REQUIRE Invoicing, Account, Company, Time, InvoicingSettings;

NAMESPACE Invoicing;

CLASS Payment 'Платеж';

date 'Дата' = DATA DATETIME (Payment) NONULL;
date(Payment p) <- currentDateTime() WHEN SET(p IS Payment);

toAccount 'Счет (куда)' = DATA Account (Payment) NONULL;
toAccountNumber 'Номер счета (куда)' (Payment p) = number(toAccount(p));

toAccountHolder 'Держатель счета (куда)' = DATA Partner (Payment) NONULL;
nameToAccountHolder 'Держатель счета (куда)' (Payment p) = name(toAccountHolder(p));

WHEN LOCAL CHANGED (toAccountHolder(Payment p)) DO {
    toAccount(p) <- defaultAccount(toAccountHolder(p));
}

fromAccount 'Счет (откуда)' = DATA Account (Payment) NONULL;
fromAccountNumber 'Номер счета (откуда)' (Payment p) = number(fromAccount(p));

fromAccountHolder 'Держатель счета (откуда)' = DATA Partner (Payment) NONULL;
nameFromAccountHolder 'Держатель счета (откуда)' (Payment p) = name(fromAccountHolder(p));

WHEN LOCAL CHANGED (fromAccountHolder(Payment p)) DO {
    fromAccount(p) <- defaultAccount(fromAccountHolder(p));
}

amount 'Сумма' = DATA NUMERIC[14,2] (Payment);

inAmount 'Доход' (Account a) = GROUP SUM amount(Payment p) IF toAccount(p) = a;
outAmount 'Расход' (Account a) = GROUP SUM amount(Payment p) IF fromAccount(p) = a;

balance 'Баланс' (Account a) = inAmount(a) (-) outAmount(a);

series 'Серия' = DATA STRING[2] (Payment);
number 'Номер' = DATA STRING[28] (Payment) NONULL;

WHEN SETCHANGED (Payment p IS Payment  AND numeratorPayment() AND NOT number(p)) DO {
    number(p) <- curStringValue(numeratorPayment());
    series(p) <- series(numeratorPayment());
    incrementValueSession(numeratorPayment());   
}

seriesNumber 'Серия/Номер' (Payment p) = CONCAT '/', series(p), number(p); 

CONSTRAINT toAccount(Payment p) AND fromAccount(p) AND 
                NOT (holder(toAccount(p)) IS Company OR holder(fromAccount(p)) IS Company)
                CHECKED BY toAccount, fromAccount
                MESSAGE 'Один из счетов должен принадлежать компании';
                
CONSTRAINT toAccountHolder(Payment p) AND fromAccountHolder(p) AND 
                NOT (toAccountHolder(p) IS Company OR fromAccountHolder(p) IS Company)
                CHECKED BY toAccountHolder, fromAccountHolder
                MESSAGE 'Один из держателей счетов должен быть компанией';
    
CONSTRAINT (toAccountHolder(Payment p) AND toAccount(p)) AND NOT (holder(toAccount(p)) = toAccountHolder(p))
                CHECKED BY  toAccount
                MESSAGE  'Счет и держатель счета не совпадают';
                
CONSTRAINT (fromAccountHolder(Payment p) AND fromAccount(p)) AND NOT (holder(fromAccount(p)) = fromAccountHolder(p))
                CHECKED BY fromAccount
                MESSAGE  'Счет и держатель счета не совпадают';

FORM payment 'Платеж'
    OBJECTS p = Payment PANEL 
    PROPERTIES(p) date,  amount, series, number, fromAccountNumber, nameFromAccountHolder, 
                         toAccountNumber, nameToAccountHolder 
               
    
    EDIT Payment OBJECT p
;

FORM payments 'Платежи'
    OBJECTS p = Payment
    PROPERTIES(p) READONLY date, fromAccountNumber, nameFromAccountHolder, toAccountNumber, nameToAccountHolder, 
                amount 
    PROPERTIES (p) NEWSESSION NEW, EDIT, DELETE 
    
    LIST Payment OBJECT p
;

NAVIGATOR {
    invoicing {
        operations {
            NEW payments;
        }
    }
}
