MODULE Bill;

REQUIRE Invoicing, TaxItem, Account, Time, MetaTax, InvoicingSettings;

NAMESPACE Invoicing;

CLASS Bill 'Приходная накладная';

date 'Дата накладной' = DATA DATETIME (Bill) NONULL IN id;

WHEN LOCAL SET (Bill b IS Bill) DO {
    date(b) <- currentDateTime();
}

dueDate 'Дата к оплате' = DATA DATETIME (Bill);

partner 'Партнер' = DATA Partner (Bill) NONULL;
namePartner 'Партнер' (Bill b) = name(partner(b));

partnerAccount 'Счет партнера'  = DATA Account(Bill);

WHEN LOCAL CHANGED (partner(Bill b)) DO {
    partnerAccount(b) <- defaultAccount(partner(b));
}

partnerAccountNumber 'Счет партнера' (Bill b) = number(partnerAccount(b));

CONSTRAINT partnerAccount(Bill b) AND NOT partner(b) = holder(partnerAccount(b))
    CHECKED BY partnerAccount
    MESSAGE 'Счет должен принадлежать выбранному партнеру';
    
referenceNUmber 'Код поставщика' = DATA STRING[30] (Bill) NONULL;

notes 'Примечания' = DATA ISTRING[50] (Bill);

series 'Серия' = DATA STRING[2] (Bill);
number 'Номер' = DATA STRING[28] (Bill) NONULL;

WHEN SETCHANGED (Bill b IS Bill  AND numeratorBill() AND NOT number(b)) DO {
    number(b) <- curStringValue(numeratorBill());
    series(b) <- series(numeratorBill());
    incrementValueSession(numeratorBill());   
}

seriesNumber 'Серия/Номер' (Bill b) = CONCAT '/', series(b), number(b); 

CLASS BillLine 'Строка накладной';

bill 'Приходная накладная' = DATA Bill (BillLine) NONULL DELETE;

item 'Номенклатура' = DATA Item (BillLine);
itemName 'Номенклатура' (BillLine l) = name(item(l)) IN id;

quantity 'Кол-во' = DATA NUMERIC[16,3] (BillLine) NONULL;
unitPrice 'Цена за штуку' = DATA NUMERIC[10,2] (BillLine) NONULL;

readonly = ABSTRACT CASE BOOLEAN (BillLine);
readonly = ABSTRACT CASE BOOLEAN (Bill);

// налоги
@defineTaxCalc(bill, b);

FORM bill 'Приходная накладная'
    OBJECTS  b = Bill PANEL 
    PROPERTIES(b) READONLYIF readonly(b) date, dueDate, namePartner, partnerAccountNumber, 
                                series, number, referenceNUmber, notes
        
    OBJECTS l = BillLine
    PROPERTIES(l) READONLYIF readonly(l) itemName, quantity, unitPrice, taxes ON CHANGE changeTax(l), NEW, DELETE 
    FILTERS bill(l) = b

    EDIT Bill OBJECT b;
;
    
DESIGN bill {
    OBJECTS {
        NEW header{
            MOVE BOX (b);
            PROPERTY (namePartner(b)) {notNull = TRUE;}
        }
        NEW details{
            fill = 1;
            type = TABBED;
            NEW box {
                caption = 'Строка накладной';
                fill = 1;
                MOVE BOX (l);
                NEW pane {
                    type = CONTAINERH;
                    fill = 1;
                }
            }
        }
    }
}

FORM bills 'Приходная накладная'
    OBJECTS b = Bill
    PROPERTIES(b) READONLY date, dueDate, namePartner, notes     
    PROPERTIES(b) NEWSESSION EDIT
    PROPERTIES(b) NEWSESSION READONLYIF readonly(b) DELETE
           
    LIST Bill OBJECT b;
;
    
NAVIGATOR {
    invoicing {
        operations {
            NEW bills;
        }
    }
}

@defineTaxForm(bill, b);