MODULE Bill;

REQUIRE InvoicingSettings, TaxItem, BankAccount, Time, MetaTax, ItemPurchase, PartnerPurchase, Company,
            BarCode, MetaNumerator, Doc;

NAMESPACE Invoicing;

CLASS Bill 'Приходная накладная';

// type
CLASS BillType 'Тип документа' {
    bill 'Поступление'
}
name 'Имя' (BillType b) = staticCaption(b) IF b IS BillType CHARWIDTH 20;

type 'Тип' = ABSTRACT CASE BillType (Bill);
type (Bill b) += WHEN b IS Bill THEN BillType.bill;
nameType 'Тип' (Bill b) = name(type(b));

colorType 'Цвет' = ABSTRACT CASE COLOR (Bill);

// date
date 'Дата' = DATA DATETIME (Bill) NONULL IN id;
WHEN LOCAL SET (Bill b IS Bill) DO date(b) <- currentDateTime();

@defineSeriesNumber(bill, 'Приходная накладная', 'ПН');

// vendor
vendor 'Поставщик' = DATA Partner (Bill) NONULL;
nameVendor 'Контрагент' (Bill b) = name(vendor(b));
CONSTRAINT vendor(Bill b) AND NOT isVendor(vendor(b)) 
                CHECKED BY vendor
                MESSAGE 'Контрагент должен быть поставщиком';

// company
company 'Компания' = DATA Company (Bill) NONULL;
nameCompany 'Компания' (Bill b) = name(company(b));
WHEN LOCAL SET(Bill b IS Bill) AND NOT CHANGED(company(b)) DO company(b) <- defaultCompany(); 

// payment
dueDate 'Оплатить до' = DATA DATETIME (Bill);

// partner account
vendorAccount 'Счет поставщика' = DATA Account (Bill);
nameVendorAccount 'Счет поставщика' (Bill b) = IF vendorAccount(b) IS BankAccount
    THEN nameAccount(vendorAccount(b)) ELSE number(vendorAccount(b));
WHEN LOCAL CHANGED (vendor(Bill b)) DO vendorAccount(b) <- defaultAccount(vendor(b));

CONSTRAINT vendorAccount(Bill b) AND NOT vendor(b) = holder(vendorAccount(b))
    CHECKED BY vendorAccount
    MESSAGE 'Счет должен принадлежать выбранному контрагенту';

// company account
companyAccount 'Счет компании' = DATA Account (Bill);
nameCompanyAccount 'Счет компании' (Bill b) = IF companyAccount(b) IS BankAccount 
    THEN nameAccount(companyAccount(b)) ELSE number(companyAccount(b));
WHEN LOCAL CHANGED(company(Bill b)) DO companyAccount(b) <- defaultAccount(company(b));

CONSTRAINT companyAccount(Bill b) AND NOT company(b) = holder(companyAccount(b))
    CHECKED BY companyAccount
    MESSAGE 'Счет должен принадлежать выбранной компании';

vendorReference 'Код поставщика' = DATA STRING[30] (Bill);
sourceDocument 'Исходный документ' = DATA STRING[30] (Bill);
notes 'Примечания' = DATA ISTRING[50] (Bill);

// lines
CLASS BillLine 'Строки документа';

bill 'Приходная накладная' = DATA Bill (BillLine) NONULL DELETE;

index 'Индекс' = PARTITION SUM 1 ORDER BillLine l BY bill(l) IN id MATERIALIZED;

item 'Номенклатура' = DATA Item (BillLine) NONULL;
nameItem 'Номенклатура' (BillLine l) = name(item(l)) IN id;
idBarCodeItem 'Штрих код' (BillLine l) = idBarCode(item(l));
idItem 'Код' (BillLine l) = id(item(l));

CONSTRAINT item(BillLine l) AND NOT canBePurchased(item(l))
            CHECKED BY item 
            MESSAGE 'Наменклатура не предназначена для закупки';

unitMeasure 'Ед.изм.' = DATA ItemMeasure (BillLine);
nameUnitMeasure 'Ед.изм.' (BillLine l) = name(unitMeasure(l));

description 'Описание' = DATA STRING (BillLine); 

quantity 'Кол-во' = DATA NUMERIC[16,3] (BillLine) NONULL;
unitPrice 'Цена за штуку' = DATA NUMERIC[10,2] (BillLine) NONULL;

WHEN LOCAL SETCHANGED(item(BillLine l)) AND NOT CHANGED(unitPrice(l)) DO unitPrice(l) <- cost(item(l));

// taxes
@defineTaxCalc(bill, b, purchase);

// readonly
readonly = ABSTRACT CASE BOOLEAN (Bill);
readonly = ABSTRACT CASE BOOLEAN (BillLine);

// Line properties
date 'Дата накладной' (BillLine l) = date(bill(l));
partner 'Партнер' (BillLine l) = vendor(bill(l));
namePartner 'Партнер' (BillLine l) = nameVendor(bill(l));
nameCompany 'Компания' (BillLine l) = nameCompany(bill(l));

FORM bill 'Приходная накладная'
    OBJECTS  b = Bill PANEL 
    PROPERTIES(b) READONLYIF readonly(b) 
                             nameType BACKGROUND colorType(b), date, series, number, nameVendor, nameCompany, sourceDocument,
                             dueDate, nameVendorAccount, nameCompanyAccount,
                             vendorReference, notes
        
    OBJECTS l = BillLine
    PROPERTIES(l) READONLYIF readonly(l) index, nameItem, description, nameUnitMeasure, idBarCodeItem, idItem, quantity, 
                        unitPrice, taxes ON CHANGE changeTax(l), NEW, DELETE 
    FILTERS bill(l) = b

    EDIT Bill OBJECT b
;

DESIGN bill {
    OBJECTS {
        NEW header {
            alignment = STRETCH;    
            type = CONTAINERH;
            NEW headerLeft {
                MOVE PROPERTY(nameType(b));
                MOVE PROPERTY(date(b));
                NEW seriesNumber { 
                    type = CONTAINERH; 
                    MOVE PROPERTY(series(b)); 
                    MOVE PROPERTY(number(b));
                }
            }
            NEW headerRight {
                MOVE PROPERTY(nameVendor(b))  { notNull = TRUE; }
                MOVE PROPERTY(nameCompany(b)) { notNull = TRUE; }           
                MOVE PROPERTY(sourceDocument(b));           
            }
            NEW relatedDoc {
                fill = 1;
                type = TABBED;
            }
        }
        NEW details {
            fill = 5;
            type = TABBED;
            NEW lines {
                caption = 'Строки';
                MOVE BOX(l) { 
                    fill = 3;
                    PROPERTY(nameItem(l)) { notNull = TRUE; } 
                }
                NEW linesFooter {                     
                    type = CONTAINERH;
                    fill = 1;
                }
            }
            NEW otherInformation {
                caption = 'Прочая информация';
                type = COLUMNS;
                columns = 2;
                NEW payment {
                    caption = 'Оплата';
                    MOVE PROPERTY(dueDate(b)); 
                    MOVE PROPERTY(nameCompanyAccount(b));
                    MOVE PROPERTY(nameVendorAccount(b));              
                }           
                NEW billInformat {
                    caption = 'Прочие';
                    MOVE PROPERTY(vendorReference(b)); 
                    MOVE PROPERTY(notes(b));
                }
            }
        }
    }
}

// copy
copyBill 'Копировать' (Bill b)  { 
    NEWSESSION {
        NEW nb = Bill {
            vendor(nb) <- vendor(b);
            notes(nb) <- notes(b);
            FOR bill(BillLine l) = b INLINE NEW nl = BillLine DO {
                bill(nl) <- nb;
                item(nl) <- item(l);
                quantity(nl) <- quantity(l);
                unitPrice(nl) <- unitPrice(l);
            }
            SHOW bill OBJECTS b = nb DOCKED;
        }
    }
}
EXTEND FORM bill PROPERTIES(b) copyBill;
 
addBill 'Накладная' () {
    NEWSESSION {
        NEW b = Bill {
            SHOW bill OBJECTS b = b;  
        }
    }
}IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

FORM bills 'Приходные накладные'
    OBJECTS b = Bill
    PROPERTIES(b) READONLY nameType BACKGROUND colorType(b), date, dueDate, nameVendor, notes         
    PROPERTIES(b) NEWSESSION EDIT
    PROPERTIES(b) NEWSESSION READONLYIF readonly(b) DELETE
    PROPERTIES addBill() DRAW b TOOLBAR
           
    LIST Bill OBJECT b;

NAVIGATOR {
    invoicing {
        operations {
            NEW bills FIRST;
        }
    }
}

@defineTaxForm(bill, b);
@defineBarCodeAdd(bill, b, item);

@defineStatus(bill, 'счета', b);
@defineDocHistory(bill, b, item, quantity);

nameStatus 'Статус' (BillLine l) = nameStatus(bill(l));
readonly (BillLine l) += WHEN NOT status(bill(l)) = BillStatus.draft THEN TRUE;

@defineDocSearch(bill, item, quantity, b, canBePurchased);

DESIGN bill {
    secondaryActions {
        MOVE PROPERTY(copyBill(b)); 
    }
}