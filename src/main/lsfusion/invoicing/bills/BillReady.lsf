MODULE BillReady;

REQUIRE Bill;

NAMESPACE Invoicing;

EXTEND CLASS BillStatus {
    ready 'К оплате'
}

ready 'К оплате' = DATA BOOLEAN (Bill);

markAsToDo 'К оплате' (Bill b) {
    APPLY; 
        IF canceled() THEN RETURN;
        
        NEWSESSION {
            ready(b) <- TRUE;
            APPLY;
        }
}
countLines (Bill b) = GROUP SUM 1 IF bill(BillLine l) = b;

EXTEND FORM bill
    PROPERTIES(b) markAsToDo SHOWIF status(b) = BillStatus.draft AND countLines(b), ready
;

EXTEND FORM bills    
    EXTEND FILTERGROUP status
        FILTER 'К отгрузке' status(b) = BillStatus.ready 'F8'    
;

status(Bill b) += WHEN ready(b) THEN BillStatus.ready;
colorStatus(Bill b) += WHEN status(b) == BillStatus.ready THEN RGB(252,247,149);

DESIGN bill {
    primaryActions {
        MOVE PROPERTY(markAsToDo(b)) { fontStyle = 'bold'; }
    }                
    status {
        MOVE PROPERTY(ready(b));                               
    }    
}
