MODULE BillDone;

REQUIRE Invoicing, BillReady, BillOutstandingCredit, Time;

NAMESPACE Invoicing;

done 'Оплачено' = DATA BOOLEAN (Bill);

EXTEND CLASS BillStatus {
    done 'Оплачено'
}

WHEN SET(totalAmount(Bill b) = paid(b)) DO {
    done(b) <- TRUE;
}

EXTEND FORM bill
    PROPERTIES(b) done READONLY 
;

EXTEND FORM bills 
    EXTEND FILTERGROUP status
        FILTER 'Оплачено' status(b) = BillStatus.done 'F7'
;

status(Bill b) += WHEN done(b) THEN BillStatus.done;

DESIGN bill {                               
    status {
        MOVE PROPERTY(done(b));                              
    }                 
}

EXTEND FORM items
    OBJECTS bill = (b = Bill, bl = BillLine)
    PROPERTIES(b) nameStatus, date, namePartner, nameCompany
    PROPERTIES(bl) nameItem, quantity, unitPrice
    FILTERS bill(bl) = b, item(bl) = i
;

DESIGN items {
    operations {
        NEW bills {
            caption = 'Приходные накладные';
            MOVE BOX(bill);
        }
    }
}

EXTEND FORM partners
    OBJECTS bill = (b = Bill, bl = BillLine)
    PROPERTIES(b) nameStatus, date, namePartner, nameCompany
    PROPERTIES(bl) nameItem, quantity, unitPrice
    FILTERS bill(bl) = b, partner(b) = p
;

DESIGN partners {
    operations {
        NEW bills {
            caption = 'Приходные накладные';
            MOVE BOX(bill);
        }
    }
}
            