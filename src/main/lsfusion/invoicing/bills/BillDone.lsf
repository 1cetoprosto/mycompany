MODULE BillDone;

REQUIRE BillReady, Time;

NAMESPACE Invoicing;

done 'Оплачено' = DATA BOOLEAN (Bill);

EXTEND CLASS BillStatus {
    done 'Оплачено'
}

EXTEND FORM bill
    PROPERTIES(b) done 
;

EXTEND FORM bills 
    EXTEND FILTERGROUP status
        FILTER 'Оплачено' status(b) = BillStatus.done 'F7'
;

status(Bill b) += WHEN done(b) THEN BillStatus.done;

DESIGN bill {                               
    status {
        MOVE PROPERTY(done(b));                              
    }                 
}

EXTEND FORM item
    OBJECTS bl = BillLine
    PROPERTIES(bl) READONLY nameStatus, date, nameVendor, nameCompany
    PROPERTIES(bl) READONLY nameItem, quantity, unitPrice
    FILTERS item(bl) = i
;

DESIGN item {
    tabs {
        NEW bills {
            caption = 'Приобретение у поставщиков';
            MOVE BOX(bl);
        }
    }
}

EXTEND FORM partner
    OBJECTS b = Bill
    PROPERTIES(b) READONLY nameStatus, date, dueDate, nameVendor, note
    PROPERTIES(b) READONLY untaxedAmount, totalTax, totalAmount
    FILTERS vendor(b) = p
;

DESIGN partner {
    tabs {
        NEW bills {
            caption = 'Приходная накладная';
            MOVE BOX(b);
        }
    }
}
            