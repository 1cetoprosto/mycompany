MODULE BillDone;

REQUIRE Invoicing, BillReady, Time;

NAMESPACE Invoicing;

done 'Оплачено' = DATA BOOLEAN (Bill);

EXTEND CLASS BillStatus {
    done 'Оплачено'
}

registerPayment 'Оформить платеж' (Bill b){
    APPLY; 
    IF canceled() THEN RETURN;
    IF done(b) THEN RETURN;
    
    NEWSESSION {
        NEW p = Payment {
            bill(p) <- b;
            date(p) <- currentDateTime();
            amount(p) <- totalAmount(b);
            fromAccount(p) <- partnerAccount(b);
            DIALOG payment OBJECTS p = p DO {
                IF (totalAmount(b) > paid(b)) THEN {
                    ASK 'Оплачена не вся сумма. Закрыть счет ?' DO {
                        done(b) <- TRUE;
                    }
                } ELSE done(b) <- TRUE; 
                APPLY;
            }   
        } 
    }
}

EXTEND FORM bill
    PROPERTIES(b) registerPayment SHOWIF ready(b), done READONLY 
;

EXTEND FORM bills 
    EXTEND FILTERGROUP status
        FILTER 'Отгружен' status(b) = BillStatus.done 'F7'
;

status(Bill b) += WHEN done(b) THEN BillStatus.done;

DESIGN bill {                               
    statusActions {                  
        MOVE PROPERTY(registerPayment(b));
    }
    status {
        MOVE PROPERTY(done(b));                              
    }                 
}
            