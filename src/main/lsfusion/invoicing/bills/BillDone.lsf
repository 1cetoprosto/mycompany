MODULE BillDone;

REQUIRE BillReady, BillOutstandingCredit, Time;

NAMESPACE Invoicing;

done 'Оплачено' = DATA BOOLEAN (Bill);

EXTEND CLASS BillStatus {
    done 'Оплачено'
}

WHEN SET(totalAmount(Bill b) = paid(b)) DO {
    done(b) <- TRUE;
}

EXTEND FORM bill
    PROPERTIES(b) done READONLY 
;

EXTEND FORM bills 
    EXTEND FILTERGROUP status
        FILTER 'Оплачено' status(b) = BillStatus.done 'F7'
;

status(Bill b) += WHEN done(b) THEN BillStatus.done;

DESIGN bill {                               
    status {
        MOVE PROPERTY(done(b));                              
    }                 
}

EXTEND FORM item
    OBJECTS bl = BillLine
    PROPERTIES(bl) READONLY nameStatus, date, namePartner, nameCompany
    PROPERTIES(bl) READONLY nameItem, quantity, unitPrice
    FILTERS item(bl) = i
;

DESIGN item {
    tabs {
        NEW bills {
            caption = 'Приобретение у поставщиков';
            MOVE BOX(bl);
        }
    }
}

EXTEND FORM partner
    OBJECTS bl = Bill
    PROPERTIES(bl) READONLY nameStatus, nameType, date, dueDate, nameVendor, notes
    PROPERTIES(bl) READONLY untaxedAmount, totalTax, totalAmount, paid
    FILTERS vendor(bl) = p
;

DESIGN partner {
    tabs {
        NEW bills {
            caption = 'Приходная накладная';
            MOVE BOX(bl);
        }
    }
}
            