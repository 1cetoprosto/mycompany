MODULE BillDone;

REQUIRE Invoicing, BillReady, Time;

NAMESPACE Invoicing;

done 'Оплачено' = DATA BOOLEAN (Bill);

EXTEND CLASS BillStatus {
    done 'Оплачено'
}

registerPayment 'Оформить платеж' (Bill b){
    APPLY; 
    IF canceled() THEN RETURN;
    IF done(b) THEN RETURN;
    IF totalAmount(b) = paid(b) THEN {
        done(b) <- TRUE;
        APPLY;
        RETURN;
    }
    
   NEWSESSION {
       NEW p = Payment {
           date(p) <- currentDateTime();
           amount(p) <- totalAmount(b) (-) paid(b);
           fromAccount(p) <- partnerAccount(b);
           DIALOG payment OBJECTS p = p DO {
               IF (totalAmount(b) > amount(p) (+) paid(b)) THEN {
                   ASK 'Оплачена не вся сумма. Закрыть накладную ?' DO {
                       done(b) <- TRUE;
                       paid(p, b) <- amount(p);
                   } ELSE {
                       paid(p, b) <- amount(p);
                   }
               } ELSE {
                   done(b) <- TRUE; 
                   paid(p, b) <- amount(p);
               }
               APPLY; 
           }
       } 
   }
}

EXTEND FORM bill
    PROPERTIES(b) registerPayment SHOWIF status(b) = BillStatus.ready, done READONLY 
;

EXTEND FORM bills 
    EXTEND FILTERGROUP status
        FILTER 'Отгружен' status(b) = BillStatus.done 'F7'
;

status(Bill b) += WHEN done(b) THEN BillStatus.done;

DESIGN bill {                               
    statusActions {                  
        MOVE PROPERTY(registerPayment(b));
    }
    status {
        MOVE PROPERTY(done(b));                              
    }                 
}
            