MODULE BillDone;

REQUIRE Invoicing, BillReady;

NAMESPACE Invoicing;

done 'Оплачено' = DATA BOOLEAN (Bill);

EXTEND CLASS BillStatus {
    done 'Оплачено'
}

registerPayment 'Оформить платеж' (Bill b){
    APPLY; 
    IF canceled() THEN RETURN;
    IF done(b) THEN RETURN;
    
    NEWSESSION {
        NEW p = Payment {
            bill(p) <- b;
            SHOW payment OBJECTS p = p;
            IF (amount(p) < totalAmount(b) (-) paid(b) OR NOT amount(p)) THEN {
                ASK 'Оплачена не вся сумма. Закрыть счет ?' DO {}
                    ELSE {
                        paid(p) <- TRUE;
                        APPLY;
                        RETURN;
                    }
             }   
        } 
        done(b) <- TRUE;
        APPLY;
    }
}

WHEN SETCHANGED(done(bill(Payment p))) DO {
    paid(p) <- TRUE;
}

EXTEND FORM bill
    PROPERTIES(b) registerPayment SHOWIF ready(b), done READONLY 
;

EXTEND FORM bills 
    EXTEND FILTERGROUP status
        FILTER 'Отгружен' status(b) = BillStatus.done 'F7'
;

status(Bill b) += WHEN done(b) THEN BillStatus.done;

DESIGN bill {                               
    statusActions {                  
        MOVE PROPERTY(registerPayment(b));
    }
    status {
        MOVE PROPERTY(done(b));                              
    }                 
}
            