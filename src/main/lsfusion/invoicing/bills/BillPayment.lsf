MODULE BillPayment;

REQUIRE BillOutstandingCredit, BillDone, Payment;

NAMESPACE Invoicing;

EXTEND CLASS Payment : BillOutstandingCredit;
amount(Payment p) += amount(p);
account(Payment p) += fromAccount(p);
date(Payment p) += date(p);
number(Payment p) += number(p);
type(Payment p) += 'Платеж' IF p IS Payment;

isRefund 'Возврат' = ABSTRACT BOOLEAN (BillOutstandingCredit);

registerPayment 'Оформить платеж' (Bill b){
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        NEW p = Payment {
            date(p) <- currentDateTime();
            amount(p) <- totalAmount(b) (-) paid(b);
            IF isRefund(b) THEN fromAccount(p) <- partnerAccount(b);
               ELSE toAccount(p) <- partnerAccount(b);
            DIALOG payment OBJECTS p = p DO {
               IF (totalAmount(b) > amount(p) (+) paid(b)) THEN {
                   ASK 'Оплачена не вся сумма. Закрыть накладную ?' DO {
                       done(b) <- TRUE;
                   }
               }
               paid(p, b) <- amount(p);
               APPLY; 
            }
       } 
   }
}

EXTEND FORM bill
    PROPERTIES(b) registerPayment SHOWIF status(b) = BillStatus.ready 
;

DESIGN bill {
    statusActions{
        primary {
            MOVE PROPERTY(registerPayment(b));
        }
    }
}
