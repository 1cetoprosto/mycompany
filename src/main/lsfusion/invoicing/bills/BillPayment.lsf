MODULE BillPayment;

REQUIRE BillDone, BillDebt, OutgoingPaymentDebt;

NAMESPACE Invoicing;

registerPayment 'Зарегистрировать платеж' (Bill b) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        NEW p = OutgoingPayment {
            partner(p) <- vendor(b);
            partnerAccount(p) <- vendorAccount(b);
            
            company(p) <- company(b);
            companyAccount(p) <- companyAccount(b);
            
            amount(p) <- left(b);
            DIALOG outgoingPayment OBJECTS p = p DOCKED DO {
               paid(b, p) <- left(b, p);
               IF (left(b) > 0) THEN {
                   ASK 'Оплачена не вся сумма. Закрыть документ?' DO {
                       done(b) <- TRUE;
                   }
               }
               APPLY; 
            }
       } 
   }
}

EXTEND FORM bill
    PROPERTIES(b) registerPayment SHOWIF status(b) = BillStatus.ready 
;

DESIGN bill {
    primaryActions {
        MOVE PROPERTY(registerPayment(b)) { fontStyle = 'bold'; }
    }
}