MODULE BillPrint;

REQUIRE BillDone;

NAMESPACE Invoicing;

partnerAddress 'Адрес' (Bill b) = concatAddress(partner(b),'\n'); 
partnerPhone 'Телефон' (Bill b) = phone(partner(b));
companyAddress 'Адрес' (Bill b) = concatAddress(company(b),'\n');
companyPhone 'Телефон' (Bill b) = phone(company(b));
companyEmail 'Электронная почта' (Bill b) = email(company(b));
companyWebsite 'Веб сайт' (Bill b) = website(company(b));


FORM printBill 'Приобретение товаров и услуг'
    OBJECTS b = Bill PANEL 
    PROPERTIES(b) date, dueDate, namePartner, nameStatus, number, partnerAddress, partnerPhone
    PROPERTIES(b) nameCompany, companyAddress, companyPhone, companyEmail, companyWebsite
    PROPERTIES(b) untaxedAmount, totalAmount, paid
    
    OBJECTS l = BillLine
    PROPERTIES(l) nameItem, quantity, unitPrice, taxes, subtotal
    FILTERS bill(l) = b
    
    OBJECTS t = Tax
    PROPERTIES name(t), taxAmount(t, b)
    FILTERS countLine(b, t)
;

print(Bill b) {
    PRINT printBill OBJECTS b = b; 
}

EXTEND FORM bill
    PROPERTIES(b) print
;

DESIGN bill {
    statusActions {
        secondary {
            MOVE PROPERTY(print(b)) {caption = 'Печатать'; charWidth=10;}
        }
    }
}
