MODULE BillType;

REQUIRE Numerator, InvoicingSettings;

NAMESPACE Invoicing;

CLASS BillType 'Тип приобретения';
name 'Название' = DATA ISTRING[50] (BillType) NONULL;

numerator 'Нумератор' = DATA Numerator (BillType);
nameNumerator 'Нумератор' (BillType o) = name(numerator(o));

taxIncluded 'Цена включает налоги' = DATA BOOLEAN (BillType);

FORM billType 'Тип приобретения'
    OBJECTS o = BillType PANEL 
    PROPERTIES(o) name, nameNumerator, taxIncluded
     
    EDIT BillType OBJECT o;
;

FORM billTypes 'Типы приобретения'
    OBJECTS o = BillType
    PROPERTIES(o) READONLY name
    
    LIST BillType OBJECT o;
;

EXTEND FORM options
    OBJECTS bt = BillType
    PROPERTIES(bt) READONLY name
    PROPERTIES(bt) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(bt);
    }
}

// default type
countBillTypes 'Кол-во типов' () = GROUP SUM 1 IF r IS BillType; 
firstBillType = GROUP LAST BillType r ORDER DESC r WHERE r IS BillType;

defaultBillType = firstBillType() IF countBillTypes() = 1;