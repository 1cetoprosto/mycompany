MODULE Refund;

REQUIRE BillDone, Invoice;

NAMESPACE Invoicing;

refund = DATA Bill (Invoice);
refundLine = DATA BillLine (InvoiceLine);

isRefund 'Возврат' (Invoice i) = TRUE IF refund(i);

// add
addRefund 'Создать возврат' (Bill b) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION { 
        NEW i = Invoice {
            refund(i) <- b;
            
            dateTime(i) <- currentDateTime();
            
            customer(i) <- vendor(b);
            customerAccount(i) <- vendorAccount(b);
            
            company(i) <- company(b);
            companyAccount(i) <- companyAccount(b);
            
            sourceDocument(i) <- number(b);
            note(i) <- note(b);
            
            FOR bill(BillLine l) = b INLINE NEW il = InvoiceLine DO {
                refundLine(il) <- l; 
                invoice(il) <- i;

                item(il) <- item(l);

                quantity(il) <- quantity(l);
                price(il) <- price(l);
                FOR in(l, Tax t) DO {
                    in(il, t) <- in(l,t);
                }
            }
            SHOW invoice OBJECTS i = i DOCKED;
        }
        
    }
}

EXTEND FORM bill
    PROPERTIES(b) addRefund SHOWIF ready(b)
;

DESIGN bill {
    secondaryActions {
        MOVE PROPERTY(addRefund(b));
    }               
}

EXTEND FORM invoices
    PROPERTIES READONLY isRefund(i)
;