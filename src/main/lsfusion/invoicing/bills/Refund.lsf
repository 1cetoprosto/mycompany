MODULE Refund;

REQUIRE BillPayment;

NAMESPACE Invoicing;

CLASS Refund 'Акт возврата' : Bill;
isRefund 'Акт возврата' (Bill b) = b IS Refund;

EXTEND CLASS Refund : BillOutstandingCredit;
amount(Refund c) += totalAmount(c);
partner (Refund c) += partner[Bill](c);
date(Refund c) += date[Bill](c);
number(Refund c) += number[Bill](c);

CLASS BillType 'Тип накладной' {
    bill 'Накладная',
    refund 'Возврат'
}

name 'Имя' (BillType o) = staticCaption(o) IF o IS BillType CHARWIDTH 15;

type 'Тип' (Bill b)=  IF(isRefund(b)) THEN ISTRING[30](name(BillType.refund))
                        ELSE ISTRING[30](name(BillType.bill));

type(Refund c) += type[Bill](c);
isRefund(Refund r) += isRefund[Bill](r);

EXTEND FORM bill
    PROPERTIES(b) type
;

refund 'Создать акт возврата' (Bill b) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION { 
        NEW ni = Refund {
            date[Bill](ni) <- currentDateTime();
            partner[Bill](ni) <- partner(b);
            notes(ni) <- notes(b);
            FOR bill(BillLine l) = b INLINE NEW nl = BillLine DO {
                bill(nl) <- ni;
                item(nl) <- item(l);
                quantity(nl) <- quantity(l);
                unitPrice(nl) <- unitPrice(l);
                FOR in(l, Tax t) DO {
                    in(nl,t) <- in(l,t);
                }
            }
            SHOW bill OBJECTS b = ni DOCKED;
        }
        
    }
}

EXTEND FORM bill
    PROPERTIES(b) refund SHOWIF status(b) = BillStatus.ready AND NOT isRefund(b)
;

DESIGN bill {
    statusActions {
        primary {
            MOVE PROPERTY(refund(b));
        }               
    }   
}

EXTEND FORM bill
    OBJECTS bi = Bill
    PROPERTIES(bi) READONLY date, number, type
    PROPERTIES READONLY paid(b, bi)
    FILTERS paid(b, bi), NOT b = bi
    
    OBJECTS bil = Bill
    PROPERTIES(bil) READONLY date, number, type, toPay
    PROPERTIES addBillPayment(b, bil) DRAW bil TOOLBAR
    FILTERS partner(bil) = partner(b), toPay(bil) > 0, NOT b = bil,
            status(bil) = BillStatus.ready
;

DESIGN bill {
    statusActions {
        primary {
            MOVE PROPERTY(refund(b));
        }                
    }
    
    pane {
        NEW outstandingCreditPane BEFORE total{
            type = TABBED;
            fill = 1;
            MOVE outstandingCredit;
            NEW creditInvoices {
                caption = 'Накладные';
                showIf = b IS Refund;
                MOVE BOX(bi);
                MOVE BOX(bil);
            }
        }
    }
}

addRefund 'Возврат' () {
    NEWSESSION {
        NEW r = Refund {
            SHOW bill OBJECTS b = r;  
        }
    }
} IMAGE 'add.png';

EXTEND FORM bills
    PROPERTIES addRefund() DRAW b TOOLBAR
;
