MODULE Refund;

REQUIRE BillDone, Invoice;

NAMESPACE Invoicing;

@defineDocLineRelation(bill, invoice, 'Приходные накладные', 'Возвраты', b, i);

numberBillLine 'Расходная накладная' (InvoiceLine l) = number(billLine(l)); 

CONSTRAINT billLine(InvoiceLine l) AND NOT 
                (vendor(billLine(l)) = customer(l) AND 
                 item(billLine(l)) = item(l))
    CHECKED BY billLine
    MESSAGE 'Контрагент и номенклатура расходной накладной должны совпадать с контрагентом и товаром возврата';

// events
WHEN LOCAL SETCHANGED(billLine(InvoiceLine l)) DO {
    price(l) <- price(billLine(l));
    in(l, Tax t) <- in(billLine(l), t);
}

isRefund 'Возврат' (Invoice i) = DATA BOOLEAN (Invoice);

EXTEND FORM invoice
    PROPERTIES(i) isRefund
    
    PROPERTIES(l) numberBillLine SHOWIF isRefund(i)
;

DESIGN invoice {
    headerRight {
        MOVE PROPERTY(isRefund(i));
    }
}

// add
addRefund 'Создать возврат' (Bill b) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION { 
        NEW i = Invoice {
            isRefund(i) <- TRUE;
            
            dateTime(i) <- currentDateTime();
            
            customer(i) <- vendor(b);
            customerAccount(i) <- vendorAccount(b);
            
            company(i) <- company(b);
            companyAccount(i) <- companyAccount(b);
            
            note(i) <- note(b);
            
            FOR bill(BillLine l) = b INLINE NEW bl = InvoiceLine DO {
                billLine(bl) <- l; 
                invoice(bl) <- i;

                item(bl) <- item(l);

                quantity(bl) <- quantity(l);
            }
            SHOW invoice OBJECTS i = i DOCKED;
        }
        
    }
}

EXTEND FORM bill
    PROPERTIES(b) addRefund SHOWIF ready(b)
;

DESIGN bill {
    secondaryActions {
        MOVE PROPERTY(addRefund(b));
    }               
}

EXTEND FORM invoices
    PROPERTIES READONLY isRefund(i)
;