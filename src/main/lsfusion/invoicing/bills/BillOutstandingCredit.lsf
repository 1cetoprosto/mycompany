MODULE BillOutstandingCredit;

REQUIRE Bill, Utils;

NAMESPACE Invoicing;

CLASS ABSTRACT BillOutstandingCredit 'Закрытие задолженности';

date 'Дата' = ABSTRACT DATETIME (BillOutstandingCredit);
amount = ABSTRACT NUMERIC[14,2] (BillOutstandingCredit);
amountLeft 'Остаток' = ABSTRACT NUMERIC[14,2] (BillOutstandingCredit);

partner = ABSTRACT Partner (BillOutstandingCredit);
namePartner 'Партнер' (BillOutstandingCredit c) = name(partner(c));

canBeMatched = ABSTRACT BOOLEAN (BillOutstandingCredit, Bill);

type 'Тип' = ABSTRACT ISTRING[30] (BillOutstandingCredit);

number 'Номер' = ABSTRACT STRING[28] (BillOutstandingCredit);

paid 'Оплачено' = DATA NUMERIC[14,2] (BillOutstandingCredit, Bill);
paid 'Оплачено' (Bill b) = (GROUP SUM paid(BillOutstandingCredit c, b)) (+) (GROUP SUM paid(b, Bill bb));
toPay 'Остаток' (Bill b) = totalAmount(b) (-) paid(b);

billed 'Оплачено' (BillOutstandingCredit c) = GROUP SUM paid(c, Bill i);

EXTEND FORM bills   
    PROPERTIES(b) READONLY paid  
;

EXTEND FORM bill
    PROPERTIES(bb) paid
;

addBillPayment 'Привязать' (BillOutstandingCredit p, Bill b) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        paid(p, b) <- min(Invoicing.amountLeft[BillOutstandingCredit](p), toPay(b));
        APPLY;
    }
}

EXTEND FORM bill
    OBJECTS p = BillOutstandingCredit
    PROPERTIES(p) READONLY date, number, type
    PROPERTIES READONLY paid(p, b)
    FILTERS paid(p, b)
    
    OBJECTS pp = BillOutstandingCredit
    PROPERTIES(pp) READONLY date, number, type, Invoicing.amountLeft[BillOutstandingCredit]
    PROPERTIES addBillPayment(pp, b) DRAW pp TOOLBAR
    FILTERS canBeMatched(pp, b)   
;

DESIGN bill {
    pane {
        NEW outstandingCredit BEFORE total {
            caption = 'Платежи';
            fill =1;
            type = CONTAINERV; 
            MOVE BOX(p) {           
                GRID(p) { headerHeight = 25; } 
            }
            REMOVE TOOLBARBOX(p);
            MOVE BOX(pp) {
                GRID(pp) { headerHeight = 25; }             
            }
            REMOVE TOOLBARBOX(pp);
        }
    }
}