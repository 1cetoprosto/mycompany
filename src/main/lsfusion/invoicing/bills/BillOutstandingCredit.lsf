MODULE BillOutstandingCredit;

REQUIRE Bill, Utils;

NAMESPACE Invoicing;

CLASS ABSTRACT BillOutstandingCredit 'Закрытие задолженности';

number 'Номер' = ABSTRACT STRING[28] (BillOutstandingCredit);
date 'Дата' = ABSTRACT DATETIME (BillOutstandingCredit);

vendor = ABSTRACT Partner (BillOutstandingCredit);
nameVendor 'Поставщик' (BillOutstandingCredit c) = name(vendor(c));

// type
CLASS BillOutstandingCreditType 'Тип закрытия задолженности';
name 'Имя' (BillOutstandingCreditType t) = staticCaption(t) IF t IS BillOutstandingCreditType CHARWIDTH 10;

type = ABSTRACT BillOutstandingCreditType (BillOutstandingCredit);
nameType (BillOutstandingCredit c) = name(type(c));

// sum
amount = ABSTRACT NUMERIC[14,2] (BillOutstandingCredit);
amountLeft 'Остаток' = ABSTRACT NUMERIC[14,2] (BillOutstandingCredit);

canBeMatched = ABSTRACT BOOLEAN (BillOutstandingCredit, Bill);

paid 'Оплачено' = DATA NUMERIC[14,2] (BillOutstandingCredit, Bill);
paid 'Оплачено' (Bill b) = (GROUP SUM paid(BillOutstandingCredit c, b)) (+) (GROUP SUM paid(b, Bill bb));
toPay 'Остаток' (Bill b) = totalAmount(b) (-) paid(b);

billed 'Оплачено' (BillOutstandingCredit c) = GROUP SUM paid(c, Bill i);

EXTEND FORM bills   
    PROPERTIES(b) READONLY paid  
;

EXTEND FORM bill
    PROPERTIES(bb) paid
;

addBillPayment 'Привязать' (BillOutstandingCredit p, Bill b) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        paid(p, b) <- min(Invoicing.amountLeft[BillOutstandingCredit](p), toPay(b));
        APPLY;
    }
}

EXTEND FORM bill
    OBJECTS p = BillOutstandingCredit
    PROPERTIES(p) READONLY date, number, nameType
    PROPERTIES READONLY paid(p, b)
    FILTERS paid(p, b)
    
    OBJECTS pp = BillOutstandingCredit
    PROPERTIES(pp) READONLY date, number, nameType, Invoicing.amountLeft[BillOutstandingCredit]
    PROPERTIES addBillPayment(pp, b) DRAW pp TOOLBAR
    FILTERS canBeMatched(pp, b)   
;

DESIGN bill {
    details {
        NEW outstandingCredit {
            caption = 'Разнесение оплат';
            MOVE BOX(p);           
            MOVE BOX(pp);
        }
    }
}