MODULE BillOutstandingCredit;

REQUIRE Bill;

NAMESPACE Invoicing;

CLASS ABSTRACT BillOutstandingCredit 'Закрытие задолженности';

date 'Дата' = ABSTRACT DATETIME (BillOutstandingCredit);
amount = ABSTRACT NUMERIC[14,2] (BillOutstandingCredit);

partner = ABSTRACT Partner (BillOutstandingCredit);
namePartner 'Партнер' (BillOutstandingCredit c) = name(partner(c));

type 'Тип' = ABSTRACT ISTRING[30](BillOutstandingCredit);

number 'Номер' = ABSTRACT STRING[28] (BillOutstandingCredit);

paid 'Оплачено' = DATA NUMERIC[14,2](BillOutstandingCredit, Bill);
paid 'Оплачено' (Bill b) = (GROUP SUM paid(BillOutstandingCredit c, b)) (+) (GROUP SUM paid(b, Bill bb));
toPay 'Остаток' (Bill b) = totalAmount(b) (-) paid(b);

billed 'Оплачено' (BillOutstandingCredit c) = GROUP SUM paid(c, Bill i);
toBill 'Остаток' (BillOutstandingCredit c) = amount(c) (-) billed(c);

EXTEND FORM bills   
    PROPERTIES(b) READONLY paid  
;

EXTEND FORM bill
    PROPERTIES (bb) paid
;

addBillPayment 'Привязать' (BillOutstandingCredit p, Bill b) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        IF toBill(p) >= totalAmount(b) (-) paid(b) THEN paid(p, b) <- totalAmount(b) (-) paid(b);
        ELSE paid(p, b) <- toBill(p);
        APPLY;
    }
}

EXTEND FORM bill
    OBJECTS p = BillOutstandingCredit
    PROPERTIES(p) READONLY date, number, type
    PROPERTIES READONLY paid(p, b)
    FILTERS paid(p, b)
    
    OBJECTS pp = BillOutstandingCredit
    PROPERTIES(pp) READONLY date, number, type, toBill
    PROPERTIES addBillPayment(pp, b) DRAW pp TOOLBAR
    FILTERS partner(pp) = partner(b), toBill(pp) > 0, NOT pp = b
    
;

DESIGN bill {
    pane {
        NEW outstandingCredit BEFORE total {
            caption = 'Платежи';
            fill =1;
            type = CONTAINERV; 
            MOVE BOX (p);
            MOVE BOX (pp);
        }
    }
}