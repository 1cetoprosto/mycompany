MODULE BillPaymentTerms;

REQUIRE Partner, PaymentTerms, PartnerPurchase, Bill;

NAMESPACE Invoicing;

vendorPaymentTerms 'Условия оплаты' = DATA PaymentTerm (Partner);
nameVendorPaymentTerms 'Условия оплаты'(Partner p) = name(vendorPaymentTerms(p));

paymentTerms 'Условия оплаты' = DATA PaymentTerm (Bill); 
namePaymentTerms 'Условия оплаты' (Bill b) = name(paymentTerms(b));

WHEN LOCAL SETCHANGED(paymentTerms(Bill b)) AND date(b) DO {
    dueDate(b) <- sumDay(date(b), days(paymentTerms(b)));
}

//если не указаны уловия оплаты для поставщиков ставим дефолтный
WHEN SET (Partner p IS Partner) DO {
    IF isVendor(p) AND NOT vendorPaymentTerms(p) THEN vendorPaymentTerms(p) <- GROUP LAST (PaymentTerm pt) IF defaultForPurchase(pt) ORDER pt;
} 

EXTEND FORM partner 
    PROPERTIES(p) nameVendorPaymentTerms
;

DESIGN partner {
    vendor {
        vendorOptions{
            MOVE PROPERTY(nameVendorPaymentTerms(p));
        } 
    }
}

EXTEND FORM bill
    PROPERTIES(b) namePaymentTerms
;

DESIGN bill {
    payment {
        MOVE PROPERTY(namePaymentTerms(b)) FIRST;
    }
}
