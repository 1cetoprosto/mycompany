MODULE Account;

REQUIRE Invoicing, Partner;

NAMESPACE Invoicing;

CLASS ABSTRACT Account 'Счет';

accountNumber 'Номер счета' = DATA ISTRING[30] (Account) IN id;

accountHolder 'Держатель счета' = DATA Partner (Account);
nameAccountHolder 'Держатель счета' (Account a) = name(accountHolder(a)) IN id;

notes 'Примечания' = DATA ISTRING[50] (Account);

FORM account 'Счет'
    OBJECTS a = Account PANEL 
    PROPERTIES(a) accountNumber, nameAccountHolder, notes
    
    EDIT Account OBJECT a
;

FORM accounts 'Счета'
    OBJECTS a = Account
    PROPERTIES(a) READONLY  accountNumber, nameAccountHolder, notes
    PROPERTIES(a) NEWSESSION EDIT, DELETE
    
    LIST Account OBJECT a

;

NAVIGATOR {
    invoicing {
        settings{
            NEW accounts;        
        }
    }
}

newAccount 'Счет' (Partner p) {
    NESTEDSESSION {
        NEW a = Account {
            accountHolder(a) <- p;
            SHOW account OBJECTS a = a;  
        }
    }
} IMAGE 'add.png';

EXTEND FORM partner
    OBJECTS a = Account
    PROPERTIES(a) READONLY accountNumber, notes
    PROPERTIES (a) NESTEDSESSION EDIT, DELETE 
    PROPERTIES newAccount(p) DRAW a TOOLBAR
        
    FILTERS accountHolder(a) = p
;
