MODULE Account;

REQUIRE Invoicing, Partner;

NAMESPACE Invoicing;

CLASS ABSTRACT Account 'Счет';

number 'Номер счета' = DATA ISTRING[30] (Account) IN id;

holder 'Держатель счета' = DATA Partner (Account);
nameAccountHolder 'Держатель счета' (Account a) = name(holder(a)) IN id;

dataDefaultAccount = DATA Account (Partner);
calcDefaultAccount (Partner p) = GROUP LAST Account a ORDER DESC a WHERE holder(a) =p;

defaultAccount (Partner p) = OVERRIDE dataDefaultAccount(p), calcDefaultAccount(p);
default 'По умолчанию' (Account a) = defaultAccount(holder(a)) = a CHARWIDTH 10;

notes 'Примечания' = DATA ISTRING[50] (Account);

changeDefault (Account a) {
    INPUT b = BOOLEAN DO {
        IF b THEN
            defaultAccount(Partner p) <- a WHERE p = holder(a);
    }
}

FORM account 'Счет'
    OBJECTS a = Account PANEL 
    PROPERTIES(a) number, nameAccountHolder, notes, default ON CHANGE changeDefault(a)
    
    EDIT Account OBJECT a
;

FORM accounts 'Счета'
    OBJECTS a = Account
    PROPERTIES(a) READONLY  number, nameAccountHolder, notes, default
    PROPERTIES(a) NEWSESSION EDIT, DELETE
    
    LIST Account OBJECT a

;

NAVIGATOR {
    invoicing {
        settings{
            NEW accounts;        
        }
    }
}

newAccount 'Счет' (Partner p) {
    NESTEDSESSION {
        NEW a = Account {
            holder(a) <- p;
            SHOW account OBJECTS a = a;  
        }
    }
} IMAGE 'add.png';

EXTEND FORM partner
    OBJECTS a = Account
    PROPERTIES(a) READONLY number, notes, default
    PROPERTIES (a) NESTEDSESSION EDIT, DELETE 
    PROPERTIES newAccount(p) DRAW a TOOLBAR
        
    FILTERS holder(a) = p
;
