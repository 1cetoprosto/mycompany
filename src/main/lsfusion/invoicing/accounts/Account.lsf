MODULE Account;

REQUIRE InvoicingSettings, Partner;

NAMESPACE Invoicing  ;

CLASS ABSTRACT Account 'Счет';

number 'Номер счета' = DATA ISTRING[30] (Account) IN id NONULL;

holder 'Держатель счета' = DATA Partner (Account) NONULL;
nameAccountHolder 'Держатель счета' (Account a) = name(holder(a)) IN id;

dataDefaultAccount = DATA Account (Partner);
calcDefaultAccount (Partner p) = GROUP LAST Account a ORDER DESC a WHERE holder(a) = p;

defaultAccount (Partner p) = OVERRIDE dataDefaultAccount(p), calcDefaultAccount(p);
default 'По умолчанию' (Account a) = defaultAccount(holder(a)) = a CHARWIDTH 10;

notes 'Примечания' = DATA ISTRING[50] (Account);

CLASS AccountType 'Тип счета';

name 'Имя' (AccountType o) = staticCaption(o) IF o IS AccountType CHARWIDTH 15;

type  = ABSTRACT CASE AccountType (Account);
nameType 'Тип счета' (Account a) = name(type(a));

changeDefault (Account a) {
    INPUT b = BOOLEAN DO {
        IF b THEN
            defaultAccount(Partner p) <- a WHERE p = holder(a);
    }
}

FORM account 'Счет'
    OBJECTS a = Account PANEL 
    PROPERTIES(a) number, nameAccountHolder, notes, nameType, default ON CHANGE changeDefault(a)
    
    EDIT Account OBJECT a
;

FORM accounts 'Счета'
    OBJECTS a = Account
    PROPERTIES(a) READONLY  number, nameAccountHolder, notes, nameType, default
    PROPERTIES(a) NEWSESSION EDIT, DELETE
    
    LIST Account OBJECT a

;

NAVIGATOR {
    invoicing {
        Invoicing.settings {
            NEW accounts;        
        }
    }
}

newAccount 'Счет' (Partner p) {
    NESTEDSESSION {
        NEW a = Account {
            holder(a) <- p;
            SHOW account OBJECTS a = a;  
        }
    }
} IMAGE 'add.png';

EXTEND FORM partner
    OBJECTS a = Account
    PROPERTIES(a) READONLY number, nameType, notes, default
    PROPERTIES(a) NESTEDSESSION EDIT, DELETE 
//    PROPERTIES newAccount(p) DRAW a TOOLBAR
        
    FILTERS holder(a) = p
;

DESIGN partner {
    tabs {
        NEW accounts FIRST {
            caption = 'Счета';
            MOVE BOX(a);
        }
    }
}
