MODULE BankAccount;

REQUIRE Invoicing, Account, Bank;

NAMESPACE Invoicing;

CLASS BankAccount 'Банковский счет' : Account;

bank 'Банк' = DATA Bank (BankAccount);
nameBank 'Банк' (BankAccount b) = name(bank(b));
routingNumber 'Код банка' (BankAccount b) = routingNumber(bank(b));

EXTEND CLASS AccountType {
     bank 'Банковский счет'
}

type(Account a) += WHEN a IS BankAccount THEN AccountType.bank;

EXTEND FORM account
    PROPERTIES(a) nameBank, routingNumber READONLY 
;

newBankAccount 'Банковский счет' () {
    NEWSESSION {
        NEW b = BankAccount {
            SHOW account OBJECTS a = b;  
        }
    }
} IMAGE 'add.png';

newBankAccount 'Банковский счет' (Partner p) {
    NESTEDSESSION {
        NEW b = BankAccount {
            holder(b) <- p;
            SHOW account OBJECTS a = b;  
        }
    }
} IMAGE 'add.png';

EXTEND FORM accounts
    PROPERTIES newBankAccount() DRAW a TOOLBAR
    
    LIST BankAccount OBJECT a
;

EXTEND FORM partner
    PROPERTIES newBankAccount(p) DRAW a TOOLBAR
;