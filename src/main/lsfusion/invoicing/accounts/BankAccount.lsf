MODULE BankAccount;

REQUIRE Account, Bank, Utils;

NAMESPACE Invoicing;

CLASS BankAccount 'Банковский счет' : Account;

bank 'Банк' = DATA Bank (BankAccount);
nameBank 'Банк' (BankAccount b) = name(bank(b));
routingNumber 'Код банка' (BankAccount b) = routingNumber(bank(b));

nameAccount 'Наименование' (BankAccount b) = 
    ISTRING[35](CONCAT ' ', substr(number(b),length(number(b)) - 4, 4), nameBank(b));

EXTEND CLASS AccountType {
     bank 'Банковский счет'
}

type(Account a) += WHEN a IS BankAccount THEN AccountType.bank;

EXTEND FORM account
    PROPERTIES(a) nameBank, routingNumber READONLY, nameAccount
;

newBankAccount 'Банковский счет' () {
    NEWSESSION {
        NEW b = BankAccount {
            SHOW account OBJECTS a = b;  
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

newBankAccount 'Банковский счет' (Partner p) {
    NESTEDSESSION {
        NEW b = BankAccount {
            holder(b) <- p;
            SHOW account OBJECTS a = b;  
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

EXTEND FORM accounts
    PROPERTIES newBankAccount() DRAW a TOOLBAR
    
    LIST BankAccount OBJECT a
;

EXTEND FORM partner
    PROPERTIES newBankAccount(p) DRAW a TOOLBAR
;