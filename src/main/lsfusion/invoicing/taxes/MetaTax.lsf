MODULE MetaTax;

META defineTaxCalc(doc, let, type)
    subtotal 'Сумма до налогов'  = DATA NUMERIC[14,2] (###doc##Line);
    
    WHEN LOCAL SETCHANGED(quantity(###doc##Line l)) OR SETCHANGED(unitPrice(l)) DO {
        subtotal(l) <- NUMERIC[14,2](quantity(l) * unitPrice(l));
    }
    
    in 'Вкл' = DATA BOOLEAN (###doc##Line, Tax);
    WHEN LOCAL CHANGED(item(###doc##Line l)) DO
        in(l, Tax t) <- ##type##In(item(l), t);
        
    untaxedAmount 'Сумма до налогов' (###doc let) = GROUP SUM subtotal(###doc##Line l) IF doc(l) = let;
    
    taxAmount 'Сумма налога (строка)' (Tax t, ###doc##Line l) = DATA NUMERIC[14,2] (Tax, ###doc##Line);
    
    WHEN LOCAL CHANGED(subtotal(###doc##Line l) IF in(l, Tax t)) DO
        taxAmount (t, l) <- NUMERIC[14,2] (value(t) / 100 * subtotal(l));
    
    CONSTRAINT (DROPPED(Tax t IS Tax) AND PREV(taxAmount(t, ###doc##Line l))) 
                                                            MESSAGE 'Нельзя удалять налог, участвующий в рассчетах';
    
    taxAmount 'Сумма налога (общая)' (Tax t, ###doc let) = 
        GROUP SUM taxAmount(t, ###doc##Line l) IF doc(l) = let AND in(l, t);
    
    totalTax 'Налог' (###doc let) = GROUP SUM taxAmount(Tax t, let);
    
    totalAmount 'Итоговая сумма' (###doc let) = NUMERIC[14,2](untaxedAmount(let) (+) totalTax(let));
    
    countLine (###doc let, Tax t) = GROUP SUM 1 IF in( ###doc##Line l, t) AND doc(l) = let;
    
    taxes 'Налоги' ( ###doc##Line l) = GROUP CONCAT name(Tax t) IF in(l, t), ', ' ORDER t CHARWIDTH 8;
    
    taxLine 'Сумма налогов' ( ###doc##Line l) = GROUP SUM taxAmount(Tax t, l) IF in(l, t);
    
    changeTax ( ###doc##Line l) {
        setTax (Tax t) <- in(l, t);
        DIALOG changeTax DO {
            in(l, Tax t) <- setTax(t);
        } 
    } 
END

META defineTaxForm(doc, let)
    EXTEND FORM doc  
        PROPERTIES(l) READONLYIF readonly(l) subtotal
            
        OBJECTS t = Tax
        PROPERTIES READONLY name(t), taxAmount(t, let), taxAmount(t, l)
        FILTERS countLine(let, t)
                
        OBJECTS let##let = ###doc PANEL 
        PROPERTIES(let##let) untaxedAmount, totalTax, totalAmount
        FILTERS let##let = let    
    ;
        
    DESIGN doc {
        pane {
            NEW taxes {
                fill = 1;
                MOVE BOX(t);
            }
            NEW total {
                MOVE BOX(let##let); 
                GROUP(,let##let) { type = CONTAINERV; }      
            }
        }
    }
        
    EXTEND FORM doc##s 
        PROPERTIES(let) READONLY untaxedAmount, totalTax, totalAmount     
    ;
END

