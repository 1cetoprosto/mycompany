MODULE MetaTax;

META defineTaxCalc(doc, let, rus)
    in 'Вкл' = DATA BOOLEAN (###doc##Line, Tax);
    WHEN LOCAL CHANGED(item(###doc##Line l)) DO
        in(l, Tax t) <- in(item(l), t);
        
    untaxedAmount 'Сумма до налогов' (###doc let) = GROUP SUM untaxedAmount(###doc##Line l) IF doc(l) = let;
    
    taxAmount 'Сумма налога (строка)' (Tax t, ###doc##Line l) = DATA NUMERIC[14,2] (Tax, ###doc##Line);
    
    WHEN LOCAL CHANGED (untaxedAmount(###doc##Line l) IF in(l, Tax t)) DO
        taxAmount (t, l) <- NUMERIC[14,2] (value(t) / 100 * untaxedAmount(l));
    
    CONSTRAINT (DROPPED (Tax t IS Tax) AND PREV (taxAmount(t, ###doc##Line l))) 
                                                            MESSAGE 'Нельзя удалять налог, участвующий в рассчетах';
    
    taxAmount 'Сумма налога (общая)' (Tax t, ###doc let) = 
        GROUP SUM taxAmount(t, ###doc##Line l) IF doc(l) = let AND in(l, t);
    
    totalTax 'Налог' (###doc let) = GROUP SUM taxAmount(Tax t, let);
    
    totalAmount 'Итоговая сумма' (###doc let) = NUMERIC[14,2](untaxedAmount(let) (+) totalTax(let));
    
    countLine (###doc let, Tax t) = GROUP SUM 1 IF in( ###doc##Line l, t) AND doc(l) = let;
    
    taxes 'Налоги' ( ###doc##Line l) = GROUP CONCAT name(Tax t) IF in(l, t), ', ' ORDER t CHARWIDTH 8;
    
    changeTax ( ###doc##Line l) {
        setTax (Tax t) <- in(l, t);
        DIALOG changeTax DO {
            in(l, Tax t) <- setTax(t);
        } 
    } 
    
    FORM doc rus
    OBJECTS  let = ###doc PANEL 
    PROPERTIES(let) date, dueDate, namePartner, partnerAccountNumber, notes
        
    OBJECTS l = ###doc##Line
    PROPERTIES(l) READONLYIF readonly(l) itemName, quantity, unitPrice, taxes ON CHANGE changeTax(l), NEW, DELETE 
    FILTERS doc(l) = let
        
    OBJECTS t = Tax
    PROPERTIES READONLY name(t), taxAmount(t, let), taxAmount(t, l)
    FILTERS countLine(let, t)
            
    OBJECTS let##let = ###doc PANEL 
    PROPERTIES(let##let) untaxedAmount, totalTax, totalAmount
    FILTERS let##let = let
        
    EDIT ###doc OBJECT let;
    ;
    
    DESIGN doc {
        OBJECTS {
            NEW header{
                MOVE BOX (let);
                PROPERTY (namePartner(let)) {notNull = TRUE;}
            }
            NEW details{
                fill = 1;
                type = TABBED;
                NEW box {
                    caption = 'Строка накладной';
                    fill = 1;
                    MOVE BOX (l);
                    NEW pane {
                        type = SPLITH;
                        fill = 1;
                        NEW pane1{
                            MOVE BOX (t);
                        }
                        NEW pane2 {
                            MOVE BOX (let##let); 
                            GROUP(,let##let) {type = CONTAINERV;}      
                        }
                    }
                }
            }
        }
    }
    
    FORM doc##s rus
    OBJECTS let = ###doc
    PROPERTIES(let) READONLY date, dueDate, namePartner, notes, untaxedAmount, totalTax, totalAmount     
    PROPERTIES(let) NEWSESSION NEW, EDIT
    PROPERTIES(let) NEWSESSION READONLYIF readonly(let) DELETE
           
    LIST ###doc OBJECT let;
    ;
    
    NAVIGATOR {
        invoicing {
            operations {
                NEW doc##s;
            }
        }
    }
    
    copy###doc 'Копировать' (###doc let)  { 
        NEWSESSION {
            NEW n##let = ###doc {
                partner(n##let) <- partner(let);
                notes(n##let) <- notes(let);
                FOR doc(###doc##Line l) = let INLINE NEW nl = ###doc##Line DO {
                    doc(nl) <- n##let;
                    item(nl) <- item(l);
                    quantity(nl) <- quantity(l);
                }
                SHOW doc OBJECTS let = n##let DOCKED;
            }
        }
    }
END

