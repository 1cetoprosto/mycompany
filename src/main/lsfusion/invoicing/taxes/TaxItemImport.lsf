MODULE TaxItemImport;

REQUIRE TaxItem;

NAMESPACE Invoicing;

taxItemImportSales 'Импорт налогов на реализацию' () {
    LOCAL id = STRING[50] (INTEGER);
    LOCAL group = STRING[50] (INTEGER);
    LOCAL value = NUMERIC[5,2] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS FROM f TO id = A, group = B, value = C;
            
            FOR [GROUP SUM 1 BY id(INTEGER i)](STRING id) AND id(Item item) = id DO {
                FOR imported(INTEGER i) AND id(i) = id DO {
                    salesIn(item, GROUP LAST Tax t IF nameTaxGroup(t) = group(i) AND value(t) = value(i)) <- TRUE;
                }
            } 
            
            APPLY;
            MESSAGE 'Импорт завершен';
        }
    }
}

taxItemImportPurchase 'Импорт налогов на закупку' () {
    LOCAL id = STRING[50] (INTEGER);
    LOCAL group = STRING[50] (INTEGER);
    LOCAL value = NUMERIC[5,2] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS FROM f TO id = A, group = B, value = C;
            
            FOR [GROUP SUM 1 BY id(INTEGER i)](STRING id) AND id(Item item) = id DO {
                FOR imported(INTEGER i) AND id(i) = id DO {
                    purchaseIn(item, GROUP LAST Tax t IF nameTaxGroup(t) = group(i) AND value(t) = value(i)) <- TRUE;
                }
            } 
            
            APPLY;
            MESSAGE 'Импорт завершен';
        }
    }
}


taxItemExportSales 'Экспорт налогов на реализацию' () {
    NEWSESSION {
        LOCAL f = EXCELFILE ();
        EXPORT XLSX FROM id(Item i), name(TaxGroup g), value(Tax t) WHERE salesIn(i, t) AND taxGroup(t) = g TO f;   
        open(f());
    }
}

taxItemExportPurchase 'Экспорт налогов на закупку' () {
    NEWSESSION {
        LOCAL f = EXCELFILE ();
        EXPORT XLSX FROM id(Item i), name(TaxGroup g), value(Tax t) WHERE purchaseIn(i, t) AND taxGroup(t) = g TO f;   
        open(f());
    }
}

EXTEND FORM MasterData.options 
    PROPERTIES taxItemImportSales(), taxItemExportSales(), taxItemImportPurchase(), taxItemExportPurchase()
;

DESIGN MasterData.options {
    import {
        NEW tax {
            type = CONTAINERH;
            caption = 'Налоги';
            NEW importTax {
                MOVE PROPERTY(taxItemImportSales());
                MOVE PROPERTY(taxItemImportPurchase());
            }
            NEW exportTax {
                MOVE PROPERTY(taxItemExportSales());
                MOVE PROPERTY(taxItemExportPurchase());
            }
        }
    }
}