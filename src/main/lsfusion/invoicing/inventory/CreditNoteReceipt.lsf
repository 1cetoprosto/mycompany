MODULE CreditNoteReceipt;

REQUIRE InvoiceShipment;

NAMESPACE Invoicing;

typeReceiptFromInvoice = DATA ReceiptType ();
nameTypeReceiptFromInvoice 'Тип прихода на основе расходной накладной (возврат)' = name(typeReceiptFromInvoice());
EXTEND FORM options PROPERTIES nameTypeReceiptFromInvoice();

// receipt
creditNote = DATA CreditNote (Receipt);
countReceipts 'Кол-во поступлений' (CreditNote r) = GROUP SUM 1 IF creditNote(Receipt s) = r;

creditNoteLine = DATA InvoiceLine (ReceiptLine);

creditNoteReceiptCreated = DATA Receipt ();
newReceipt (CreditNote r) {
    NEW s = Receipt {
        creditNote(s) <- r;
         
        partner(s) <- customer[Invoice](r);
        type(s) <- typeReceiptFromInvoice();
        location(s) <- location(r);
        sourceDocument(s) <- seriesNumber(r);
        
        immediateTransfer(s) <- TRUE;   
        done(s) <- TRUE;           
            
        FOR invoice(InvoiceLine l) = r INLINE NEW sl = ReceiptLine DO {            
            receipt(sl) <- s;
            product(sl) <- item(l);
            done(sl) <- quantity(l);
            creditNoteLine(sl) <- l;
        }
        
        creditNoteReceiptCreated() <- s;
    } 
}

createReceipt 'Создать поступление' (CreditNote r) {
    APPLY;
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        newReceipt(r);
        SHOW receipt OBJECTS r = creditNoteReceiptCreated();
    }
}

EXTEND FORM invoice
    PROPERTIES(i) createReceipt SHOWIF ready(i) AND isCreditNote(i) AND typeReceiptFromInvoice() AND NOT countReceipts(i) 

    OBJECTS r = Receipt
    PROPERTIES(r) SHOWIF isCreditNote(i) READONLY seriesNumber, scheduledDate, executionDate, nameStatus
    PROPERTIES(r) SHOWIF isCreditNote(i) EDIT GRID   
    FILTERS creditNote(r) = i
;

DESIGN invoice {
    primaryActions {
        MOVE PROPERTY(createReceipt(i)) { fontStyle = 'bold'; }
    }
    relatedDoc {
        MOVE BOX(r);
        REMOVE TOOLBARSYSTEM(r);
    }
}

// auto create
autoCreateReceiptFromInvoice 'Автоматически создавать отгрузку на основе расходной накладной (возврат)' = DATA BOOLEAN ();
EXTEND FORM options PROPERTIES autoCreateReceiptFromInvoice() SHOWIF typeReceiptFromInvoice();

WHEN SET(ready(CreditNote r)) AND autoCreateReceiptFromInvoice() AND isCreditNote(r) DO newReceipt(r);
WHEN SET(canceled(creditNote(Receipt s))) AND autoCreateReceiptFromInvoice() DO canceled(s) <- TRUE; 
WHEN CHANGED(quantity(creditNoteLine(ReceiptLine l))) AND autoCreateReceiptFromInvoice() DO done(l) <- quantity(creditNoteLine(l));
WHEN CHANGED(item(creditNoteLine(ReceiptLine l))) AND autoCreateReceiptFromInvoice() AND item(creditNoteLine(l)) IS Product DO product(l) <- item(creditNoteLine(l));
