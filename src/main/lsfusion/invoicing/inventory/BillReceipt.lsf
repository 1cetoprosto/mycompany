MODULE BillReceipt;

REQUIRE BillCanceled, BillReady, ReceiptCanceled,
        CreditNote, ShipmentReceipt;

NAMESPACE Invoicing;

typeReceiptFromBill = DATA ReceiptType ();
nameTypeReceiptFromBill 'Тип поступления на основе приходной накладной' = name(typeReceiptFromBill());

EXTEND FORM options PROPERTIES nameTypeReceiptFromBill();

// location
location 'Место хранения' = DATA Location (Bill);
nameLocation 'Место хранения' (Bill b) = name(location(b));

EXTEND FORM bill 
    PROPERTIES(b) nameLocation SHOWIF typeReceiptFromBill()
;
DESIGN bill {
    headerLeft {
        MOVE PROPERTY(nameLocation(b));
    }
}

// lines
billLine = DATA BillLine (ReceiptLine);

in (Bill i, Receipt s) = GROUP SUM 1 IF bill(billLine(ReceiptLine l)) = i AND receipt(l) = s MATERIALIZED; 

countBills 'Кол-во накладных' (Receipt s) = GROUP SUM 1 IF in(Bill i, s) MATERIALIZED;
descriptionBills 'Накладные' (Receipt s) = GROUP CONCAT description(Bill i) IF in(i, s), ','; 

countReceiptLines 'Кол-во строк отгрузки' (Bill b) = GROUP SUM 1 IF bill(billLine(ReceiptLine l)) = b;

// source
sourceDocument (Receipt s) += WHEN countBills(s) THEN descriptionBills(s);
openSourceDocument (Receipt s) + { FOR in(Bill i, s) DO edit(i); }

billReceiptCreated = DATA LOCAL Receipt ();
newReceipt (Bill b) {
    NEW r = Receipt { 
        isReturn(r) <- isCreditNote(b);

        vendor(r) <- vendor(b);
        type(r) <- typeReceiptFromBill();
        location(r) <- location(b);

        immediate(r) <- TRUE;
        done(r) <- TRUE;    
    
        FOR bill(BillLine l) = b AND item(l) IS Product INLINE NEW rl = ReceiptLine DO {            
            receipt(rl) <- r;
            product(rl) <- item(l);
            done(rl) <- quantity(l);
            billLine(rl) <- l;
        }
        
        billReceiptCreated() <- r;
    }
}

createReceipt 'Создать поступление' (Bill b) {
    APPLY;
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        newReceipt(b);
        SHOW receipt OBJECTS r = billReceiptCreated();
    }
} 

EXTEND FORM bill
    PROPERTIES(b) createReceipt SHOWIF ready(b) AND typeReceiptFromBill() AND NOT countReceiptLines(b) 

    OBJECTS r = Receipt
    PROPERTIES(r) READONLY nameStatus BACKGROUND colorStatus(r), number, scheduledDateTime, executionDateTime
    PROPERTIES(r) NEWSESSION EDIT GRID   
    FILTERS in(b, r)
;

DESIGN bill {
    primaryActions {
        MOVE PROPERTY(createReceipt(b)) { fontStyle = 'bold'; }
    }
    relatedDoc {
        MOVE BOX(r);
        REMOVE TOOLBARSYSTEM(r);
    }
}

// auto create
autoCreateReceiptFromBill 'Автоматически создавать поступление на основе приходной накладной' = DATA BOOLEAN ();
EXTEND FORM options PROPERTIES autoCreateReceiptFromBill() SHOWIF typeReceiptFromBill();

WHEN SET(ready(Bill b)) AND autoCreateReceiptFromBill() DO newReceipt(b);
WHEN SET(canceled(Bill b)) AND in(b, Receipt r) AND countBills(r) = 1 AND autoCreateReceiptFromBill() DO canceled(r) <- TRUE; 
WHEN CHANGED(quantity(billLine(ReceiptLine l))) AND autoCreateReceiptFromBill() DO done(l) <- quantity(billLine(l));
WHEN CHANGED(item(billLine(ReceiptLine l))) AND autoCreateReceiptFromBill() AND item(billLine(l)) IS Product DO product(l) <- item(billLine(l));