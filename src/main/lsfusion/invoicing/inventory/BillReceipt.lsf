MODULE BillReceipt;

REQUIRE BillCanceled, Refund, ReceiptCanceled, ShipmentCanceled;

NAMESPACE Invoicing;

typeReceiptFromBill = DATA ReceiptType ();
nameTypeReceiptFromBill 'Тип поступления на основе приходной накладной' = name(typeReceiptFromBill());

EXTEND FORM options PROPERTIES nameTypeReceiptFromBill();

// location
location 'Место хранения' = DATA Location (Bill);
nameLocation 'Место хранения' (Bill b) = name(location(b));

EXTEND FORM bill 
    PROPERTIES(b) nameLocation SHOWIF typeReceiptFromBill()
;
DESIGN bill {
    headerLeft {
        MOVE PROPERTY(nameLocation(b));
    }
}

// receipt
bill = DATA Bill (Receipt);
countReceipts 'Кол-во поступлений' (Bill b) = GROUP SUM 1 IF bill(Receipt r) = b;

billLine = DATA BillLine (ReceiptLine);

billReceiptCreated = DATA LOCAL Receipt ();
newReceipt (Bill b) {
    NEW r = Receipt { 
        bill(r) <- b; 

        partner(r) <- vendor(b);
        type(r) <- typeReceiptFromBill();
        location(r) <- location(b);
        sourceDocument(r) <- seriesNumber(b);

        immediateTransfer(r) <- TRUE;
        done(r) <- TRUE;    
    
        FOR bill(BillLine l) = b AND item(l) IS Product INLINE NEW rl = ReceiptLine DO {            
            receipt(rl) <- r;
            product(rl) <- item(l);
            done(rl) <- quantity(l);
            billLine(rl) <- l;
        }
        
        billReceiptCreated() <- r;
    }
}

createReceipt 'Создать поступление' (Bill b) {
    APPLY;
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        newReceipt(b);
        SHOW receipt OBJECTS r = billReceiptCreated();
    }
} 

EXTEND FORM bill
    PROPERTIES(b) createReceipt SHOWIF ready(b) AND NOT isRefund(b) AND typeReceiptFromBill() AND NOT countReceipts(b) 

    OBJECTS r = Receipt
    PROPERTIES(r) SHOWIF NOT isRefund(b) READONLY seriesNumber, scheduledDate, executionDate, nameStatus
    PROPERTIES(r) SHOWIF NOT isRefund(b) EDIT GRID   
    FILTERS bill(r) = b
;

DESIGN bill {
    primaryActions {
        MOVE PROPERTY(createReceipt(b)) { fontStyle = 'bold'; }
    }
    relatedDoc  {
        MOVE BOX(r);
        REMOVE TOOLBARSYSTEM(r);
    }
}

// auto create
autoCreateReceiptFromBill 'Автоматически создавать поступление на основе приходной накладной' = DATA BOOLEAN ();
EXTEND FORM options PROPERTIES autoCreateReceiptFromBill() SHOWIF typeReceiptFromBill();

WHEN SET(ready(Bill b)) AND autoCreateReceiptFromBill() AND NOT isRefund(b) DO newReceipt(b);
WHEN SET(canceled(bill(Receipt r))) AND autoCreateReceiptFromBill() DO canceled(r) <- TRUE; 
WHEN CHANGED(quantity(billLine(ReceiptLine l)))  AND autoCreateReceiptFromBill() DO done(l) <- quantity(billLine(l));
WHEN CHANGED(item(billLine(ReceiptLine l))) AND autoCreateReceiptFromBill() AND item(billLine(l)) IS Product DO product(l) <- item(billLine(l));