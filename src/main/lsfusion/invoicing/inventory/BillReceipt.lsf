MODULE BillReceipt;

REQUIRE BillCanceled, BillReady, ReceiptCanceled,
        CreditNote, ShipmentReceipt;

NAMESPACE Invoicing;

receiptType = DATA ReceiptType (BillType);
nameReceiptType 'Тип поступления на основе приобретения' (BillType t) = name(receiptType(t));

EXTEND FORM billType PROPERTIES nameReceiptType(o);

// location
location 'Место хранения' = DATA Location (Bill);
nameLocation 'Место хранения' (Bill b) = name(location(b));

EXTEND FORM bill 
    PROPERTIES(b) nameLocation SHOWIF receiptType(type(b))
;
DESIGN bill {
    headerLeft {
        MOVE PROPERTY(nameLocation(b));
    }
}

// lines
@defineDocLineRelation(bill, receipt, 'Приобретения', 'Поступления', b, r);

billReceiptCreated = DATA LOCAL Receipt ();
newReceipt (Bill b) {
    NEW r = Receipt { 
        vendor(r) <- vendor(b);
        type(r) <- receiptType(type(b));
        location(r) <- location(b);

        immediate(r) <- TRUE;
        done(r) <- TRUE;    
    
        FOR bill(BillLine l) = b AND item(l) IS Product INLINE NEW rl = ReceiptLine DO {            
            receipt(rl) <- r;
            product(rl) <- item(l);
            done(rl) <- quantity(l);
            billLine(rl) <- l;
        }
        
        billReceiptCreated() <- r;
    }
}

createReceipt 'Создать поступление' (Bill b) {
    APPLY;
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        newReceipt(b);
        SHOW receipt OBJECTS r = billReceiptCreated() DOCKED;
    }
} 

EXTEND FORM bill
    PROPERTIES(b) createReceipt SHOWIF ready(b) AND receiptType(type(b)) AND NOT countReceiptLine(b) 

    OBJECTS r = Receipt
    PROPERTIES(r) READONLY nameStatus BACKGROUND colorStatus(r), number, scheduledDateTime, executionDateTime
    PROPERTIES(r) NEWSESSION EDIT GRID   
    FILTERS in(b, r)
;

DESIGN bill {
    primaryActions {
        MOVE PROPERTY(createReceipt(b)) { fontStyle = 'bold'; }
    }
    relatedDoc {
        MOVE BOX(r);
        REMOVE TOOLBARSYSTEM(r);
    }
}

// auto create
autoCreateReceipt 'Автоматически создавать поступление на основе приобретения' = DATA BOOLEAN (BillType);
EXTEND FORM billType PROPERTIES autoCreateReceipt(o) SHOWIF receiptType(o);

WHEN SET(ready(Bill b)) AND autoCreateReceipt(type(b)) DO 
    newReceipt(b);
WHEN SET(canceled(Bill b)) AND in(b, Receipt r) AND countBill(r) = 1 AND autoCreateReceipt(type(b)) DO 
    canceled(r) <- TRUE; 
WHEN CHANGED(quantity(billLine(ReceiptLine l))) AND autoCreateReceipt(type(bill(billLine(l)))) DO 
    done(l) <- quantity(billLine(l));
WHEN CHANGED(item(billLine(ReceiptLine l))) AND autoCreateReceipt(type(bill(billLine(l)))) DO 
    product(l) <- item(billLine(l));