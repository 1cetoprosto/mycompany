MODULE Doc;

META defineDocSearch (doc, it, fun, let)
    quantity 'Кол-во' (###doc i, ###it p) = 
        GROUP SUM fun(###doc##Line l) BY doc(l), it(l);
    last###doc##Line 'Последняя строка' (###doc i, ###it p) = 
        GROUP LAST ###doc##Line l ORDER l BY doc(l), it(l);
        
        changeQuantity 'Изменить кол-во' (###doc i, ###it p) {
            INPUT q = NUMERIC[14,3] DO {
                IF last###doc##Line(i, p) THEN {
                    IF q THEN
                        fun(###doc##Line l) <- q IF l = last###doc##Line(i, p)
                            WHERE doc(l) = i AND it(l) = p;
                    ELSE
                        DELETE ###doc##Line l WHERE doc(l) = i AND it(l) == p;
                }ELSE
                    IF q THEN
                        NEW l = ###doc##Line{
                            doc(l) <- i;
                            it(l) <- p;
                            fun(l) <- q;
                        }
            }
        }
    
    EXTEND FORM doc
        TREE categories c = Category PARENT parent
        PROPERTIES READONLY name(c)
        ORDER name(c)
        
        OBJECTS itm = ###it
        PROPERTIES(itm) READONLY name, barCode
        PROPERTIES(let, itm) quantity ON CHANGE changeQuantity(let, itm)
        FILTERS level(category(itm), c)
    ;
    
    DESIGN doc {
        details {
            NEW search {
                caption = 'Подбор';
                type = SPLITH;
                fill = 1;
                MOVE BOX(TREE categories);
                MOVE BOX(itm) { 
                    fill = 2;
                }
            }
        }
    }
END
