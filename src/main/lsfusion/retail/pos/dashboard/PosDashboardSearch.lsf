MODULE PosDashboardSearch;

REQUIRE PosDashboard, 
        SalesInvoicePricelist;

NAMESPACE Retail;

filterName 'Наименование' = DATA LOCAL ISTRING (); 
filterPrice 'Цена' = DATA LOCAL NUMERIC[14,2] ();

EXTEND FORM posDashboard
    OBJECTS it = Item
    PROPERTIES(it) READONLY name, idBarCode
    PROPERTIES(i, it) quantity ON CHANGE changeQuantity(i, it)
    PROPERTIES READONLY listPrice(i, it)
    ORDER name(it)

    FILTERS canBeSold(it),
            listPrice(i, it),
            active(it)

    PROPERTIES() filterName, filterPrice
    FILTERS isISubstring(name(it), filterName()) OR NOT filterName(),
            listPrice(i, it) = filterPrice() OR NOT filterPrice()
;

DESIGN posDashboard {
    tabPane {
        NEW search {
            caption = 'Поиск';
            NEW filters {
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(filterName()) { changeKey = 'F6'; }                
                MOVE PROPERTY(filterPrice()) { changeKey = 'F7'; }                
            }
            NEW searchItems {
                fill = 1;
                REMOVE BOX(it);
                MOVE GRID(it);
                MOVE TOOLBARBOX(it);
                MOVE USERFILTER(it);
            }
        }
    }
}

// barcode add
processBarcode (Invoice i, STRING[20] b) + {
    FOR Item it = itemBarCode(b) DO {
        setQuantity(i, it, quantity(i, it) (+) 1);
        SEEK posDashboard.l = lastInvoiceLine(i, it);
        consumedBarcode() <- TRUE;
    }
}