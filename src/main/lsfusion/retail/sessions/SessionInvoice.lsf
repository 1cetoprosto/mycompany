MODULE SessionInvoice;

REQUIRE Session, SalesInvoiceDiscount;

NAMESPACE Retail;

invoiceType = DATA InvoiceType (Pos);
nameInvoiceType 'Тип реализации' (Pos p) = name(invoiceType(p));

EXTEND FORM pos
    PROPERTIES(p) nameInvoiceType
;

session = DATA Session (Invoice);
numberSession 'Смена' (Invoice i) = number(session(i));

totalAmount 'Итого' (Session s) = GROUP SUM totalAmount(Invoice i) IF session(i) = s; 

WHEN LOCAL SET(session(Invoice i)) AND NOT CHANGED(type(i)) DO
    type(i) <- invoiceType(pos(session(i)));
    
WHEN LOCAL SET(session(Invoice i)) AND NOT CHANGED(company(i)) DO
    company(i) <- company(pos(session(i)));

EXTEND FORM session
    OBJECTS i = Invoice
    PROPERTIES(i) READONLY dateTime, number, nameCustomer, 'Итого' = fullAmount, 'Скидка' = discountAmount, 'К оплате' = totalAmount
    PROPERTIES(i) NEWSESSION EDIT
    FILTERS session(i) = s
;

EXTEND FORM invoice
    PROPERTIES(i) READONLY numberSession SHOWIF session(i)
;

DESIGN invoice {
    otherInformation {
        NEW session {
            caption = 'Смена';
            MOVE PROPERTY(numberSession(i));
        }
    }
}