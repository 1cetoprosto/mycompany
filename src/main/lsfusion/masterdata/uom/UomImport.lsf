MODULE UomImport;

REQUIRE Uom;

NAMESPACE MasterData;

uomExport 'Экспорт единиц измерения' () {
    NEWSESSION {
        IF NOT (GROUP SUM 1 IF Uom i AND i IS Uom) THEN {
            NEW i = Uom {
                id(i) <- '1';
                name(i) <- 'Штука';
            }
        }
    
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM 'Код' = id(Uom i), 'Наименование' = name(i) TO f;   
        open(f());
    }
}

uomImport 'Импорт единиц измерения' () {
    LOCAL id = STRING[50] (INTEGER);
    LOCAL code = ISTRING[10] (INTEGER);
    LOCAL name = ISTRING[50] (INTEGER);
    LOCAL group = ISTRING[50] (INTEGER);
    LOCAL base = ISTRING[50] (INTEGER);
    LOCAL coefficient = DOUBLE (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO id = A, code = B, name = C, group = D, base = E, coefficient = F;
            
            FOR imported(INTEGER i) DO NEW im = Uom {
                id(im) <- id(i);
                name(im) <- name(i);
            }
            
            APPLY;
            MESSAGE 'Импорт завершен';
        }
    }    
}

EXTEND FORM MasterData.options 
    PROPERTIES uomExport(), uomImport()
;

DESIGN MasterData.options {
    import {
        NEW uom {
            type = CONTAINERH;
            caption = 'Единицы измерения';
            MOVE PROPERTY(uomImport());
            MOVE PROPERTY(uomExport());
        }
    }
}