MODULE Partner;

REQUIRE MasterDataSettings, Utils;

NAMESPACE MasterData;

CLASS ABSTRACT Partner 'Контрагент';

META addressesCalc (subject,let)
    address 'Адрес' = DATA ISTRING[50] (###subject);
    city 'Город' = DATA ISTRING[30] (###subject);
    state 'Область' = DATA ISTRING[30] (###subject);
    zip'Почтовый индекс' = DATA ISTRING[10] (###subject);
END

META addressesForm (subject,let)
    EXTEND FORM ##subject 
       PROPERTIES(##let) address, city, state, zip
    ;   
    EXTEND FORM ##subject##s 
       PROPERTIES(##let) fullAddress
    ;   
END

id 'Код' = DATA STRING[50] (Partner) IN id CHARWIDTH 8;
partner (STRING[50] id) = GROUP AGGR Partner p BY id(p);

WHEN SETCHANGED(Partner p IS Partner  AND numeratorPartner() AND NOT id(p)) DO {
    id(p) <- CONCAT '', series(numeratorPartner()), curStringValue(numeratorPartner());
    incrementValueSession(numeratorPartner());   
}

name 'Наименование' = ABSTRACT ISTRING[92] (Partner) IN id;

phone 'Телефон' = DATA ISTRING[30] (Partner);
email 'Электронная почта' = DATA ISTRING[30] (Partner);

@addressesCalc(partner,p);

concatAddress (Partner p, STRING sep) = (OVERRIDE address(p)+sep,'') + (OVERRIDE city(p)+sep,'') + (OVERRIDE state(p)+sep,'') + (OVERRIDE zip(p),'');

fullAddress 'Адрес' (Partner p) = concatAddress(p,', '); 

archived 'Неактивный' = DATA BOOLEAN (Partner) CHARWIDTH 10;
active 'Активный' (Partner p) = NOT archived(p) CHARWIDTH 10;

showName = ABSTRACT CASE BOOLEAN (Partner);

CLASS PartnerType 'Тип партнера';

name 'Имя' (PartnerType o) = staticCaption(o) IF o IS PartnerType CHARWIDTH 15;

type  = ABSTRACT CASE PartnerType (Partner);
nameType 'Тип партнера' (Partner p) = name(type(p));

FORM partner 'Контрагент'
    OBJECTS p = Partner PANEL 
    PROPERTIES(p) name SHOWIF showName(p), id, nameType, phone, email, archived
    
    EDIT Partner OBJECT p
;

FORM partners 'Контрагенты'
    OBJECTS p = Partner
    PROPERTIES(p) READONLY name, id, nameType, phone, email 
    PROPERTIES(p) NEWSESSION EDIT, DELETE 
    FILTERGROUP active
        FILTER 'Активный' active(p) 'F10' DEFAULT
        FILTER 'Неактивный' archived(p) 'F9'
    
    LIST Partner OBJECT p
;

DESIGN partners {
    OBJECTS {
        NEW header {
            fill = 1;
            MOVE BOX(p);
        }
        NEW operations {
            type = TABBED;
            fill = 1;
        }
    }
}

@addressesForm(partner,p);

NAVIGATOR {
    masterData {
        NEW partners;
    }
}

DESIGN partner {
    OBJECTS {
        NEW header {
            type = CONTAINERV;
            MOVE PROPERTY(id(p)); 
            MOVE PROPERTY(nameType(p));
            MOVE PROPERTY(name(p));         
            NEW pane {
                marginTop=10;
                marginBottom=10;
                type = CONTAINERH;
                alignment = STRETCH;
                NEW box1 {
                    MOVE PROPERTY(address(p));
                    MOVE PROPERTY(city(p));
                    MOVE PROPERTY(state(p));
                    MOVE PROPERTY(zip(p));
                }
                NEW box2 {
                    marginLeft=20;
                    MOVE PROPERTY(phone(p));
                    MOVE PROPERTY(email(p));
                }
                NEW box3 {
                    marginLeft=20;
                    type = CONTAINERV;
                    MOVE PROPERTY(archived(p));                   
                }
            }    
        }
        NEW tabs {
            type = TABBED;
            fill = 1;
        }
    }
}