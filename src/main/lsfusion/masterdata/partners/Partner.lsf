MODULE Partner;

REQUIRE MasterData;

NAMESPACE MasterData;

CLASS ABSTRACT Partner 'Контрагент';

name 'Наименование' = ABSTRACT ISTRING[92] (Partner) IN id;
address 'Адрес' = DATA ISTRING[50] (Partner);
city 'Город' = DATA ISTRING[30](Partner);
state 'Область' = DATA ISTRING[30](Partner);
zip'Почтовый индекс' = DATA INTEGER (Partner);
phone 'Телефон' = DATA ISTRING[30](Partner);
email 'Электронная почта' = DATA ISTRING[30](Partner);

archived 'Неактивный' = DATA BOOLEAN (Partner) CHARWIDTH 10;
active 'Активный' (Partner p) = NOT archived(p) CHARWIDTH 10;

showName = ABSTRACT CASE BOOLEAN (Partner);

CLASS PartnerType 'Тип партнера';

name 'Имя' (PartnerType o) = staticCaption(o) IF o IS PartnerType CHARWIDTH 15;

type  = ABSTRACT CASE PartnerType (Partner);
nameType 'Тип партнера' (Partner p) = name(type(p));

FORM partner 'Контрагент'
    OBJECTS p = Partner PANEL 
    PROPERTIES(p) nameType, name SHOWIF showName(p), address, city, state, zip, phone, email, archived
    
    EDIT Partner OBJECT p
;

FORM partners 'Контрагенты'
    OBJECTS p = Partner
    PROPERTIES(p) READONLY nameType, name, address, city, state, zip, phone, email
    PROPERTIES(p) NEWSESSION EDIT, DELETE 
    FILTERGROUP active
        FILTER 'Активный' active(p) 'F10' DEFAULT
        FILTER 'Неактивный' archived(p) 'F9'
    
    LIST Partner OBJECT p
;

NAVIGATOR {
    masterData {
        NEW partners;
    }
}

DESIGN partner {
    OBJECTS {
        NEW header {
            type = CONTAINERH;
            MOVE PROPERTY (nameType(p));
            MOVE PROPERTY (name(p)); 
        }
        NEW pane {
            type = CONTAINERH;
            alignment = STRETCH;
            NEW box1 {
                MOVE PROPERTY (address(p));
                MOVE PROPERTY (city(p));
                MOVE PROPERTY (state(p));
                MOVE PROPERTY (zip(p));
            }
            NEW box2 {
                MOVE PROPERTY (phone(p));
                MOVE PROPERTY (email(p));
            }
        }
        NEW box {
            MOVE PROPERTY (archived(p));        
        }

    }
}