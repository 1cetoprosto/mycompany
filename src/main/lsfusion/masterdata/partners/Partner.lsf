MODULE Partner;

REQUIRE MasterData;

NAMESPACE MasterData;

CLASS ABSTRACT Partner 'Контрагент';

META addressesCalc (subject,let)
    address 'Адрес' = DATA ISTRING[50] (###subject);
    city 'Город' = DATA ISTRING[30](###subject);
    state 'Область' = DATA ISTRING[30](###subject);
    zip'Почтовый индекс' = DATA ISTRING[10] (###subject);
END

META addressesForm (subject,let)
    EXTEND FORM ##subject 
       PROPERTIES(##let) address, city, state, zip
    ;   
    EXTEND FORM ##subject##s 
       PROPERTIES(##let) fullAddress
    ;   
END

name 'Наименование' = ABSTRACT ISTRING[92] (Partner) IN id;

phone 'Телефон' = DATA ISTRING[30](Partner);
email 'Электронная почта' = DATA ISTRING[30](Partner);

@addressesCalc(partner,p);
fullAddress (Partner p) = CONCAT ' \n', address(p), city(p), state(p), zip(p);

archived 'Неактивный' = DATA BOOLEAN (Partner) CHARWIDTH 10;
active 'Активный' (Partner p) = NOT archived(p) CHARWIDTH 10;

showName = ABSTRACT CASE BOOLEAN (Partner);

CLASS PartnerType 'Тип партнера';

name 'Имя' (PartnerType o) = staticCaption(o) IF o IS PartnerType CHARWIDTH 15;

type  = ABSTRACT CASE PartnerType (Partner);
nameType 'Тип партнера' (Partner p) = name(type(p));

FORM partner 'Контрагент'
    OBJECTS p = Partner PANEL 
    PROPERTIES(p) nameType, name SHOWIF showName(p), phone, email, archived
    
    EDIT Partner OBJECT p
;

FORM partners 'Контрагенты'
    OBJECTS p = Partner
    PROPERTIES(p) READONLY nameType, name, phone, email
    PROPERTIES(p) NEWSESSION EDIT, DELETE 
    FILTERGROUP active
        FILTER 'Активный' active(p) 'F10' DEFAULT
        FILTER 'Неактивный' archived(p) 'F9'
    
    LIST Partner OBJECT p
;

@addressesForm(partner,p);

NAVIGATOR {
    masterData {
        NEW partners;
    }
}

DESIGN partner {
    OBJECTS {
        NEW header {
            type = CONTAINERH;
            MOVE PROPERTY (nameType(p));
            MOVE PROPERTY (name(p)); 
        }
        NEW pane {
            type = CONTAINERH;
            alignment = STRETCH;
            NEW box1 {
                MOVE PROPERTY (address(p));
                MOVE PROPERTY (city(p));
                MOVE PROPERTY (state(p));
                MOVE PROPERTY (zip(p));
            }
            NEW box2 {
                MOVE PROPERTY (phone(p));
                MOVE PROPERTY (email(p));
            }
        }
        NEW box {
            MOVE PROPERTY (archived(p));        
        }

    }
}