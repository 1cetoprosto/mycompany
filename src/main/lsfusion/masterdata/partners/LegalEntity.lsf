MODULE LegalEntity;

REQUIRE Partner, MasterData;

NAMESPACE MasterData;

CLASS LegalEntity 'Организация';

name 'Название' = DATA ISTRING[30] (LegalEntity);
website 'Веб сайт' = DATA ISTRING[30](LegalEntity);

// Наследуем организацию от партнера
EXTEND CLASS LegalEntity : Partner;

name (LegalEntity l) += name(l);

EXTEND CLASS PartnerType {
    legalEntity 'Организация'
}

type (Partner p) += WHEN p IS LegalEntity THEN PartnerType.legalEntity;

EXTEND FORM partner
    PROPERTIES(p) website
;

DESIGN partner {
    box2 {
        MOVE PROPERTY(website(p));
    }
}

newLegalEntity 'Организация' () {
    NEWSESSION {
        NEW l = LegalEntity {
            
            SHOW partner OBJECTS p = l;  
        }
    }
} IMAGE 'add.png';

EXTEND FORM partners
    PROPERTIES newLegalEntity() DRAW p TOOLBAR
    
    LIST LegalEntity OBJECT p
;