MODULE LegalEntity;

REQUIRE Partner, MasterData;

NAMESPACE MasterData;

CLASS LegalEntity 'Организация';

name 'Название' = DATA ISTRING[30] (LegalEntity);
address 'Адрес' = DATA ISTRING[50] (LegalEntity);
city 'Город' = DATA ISTRING[30](LegalEntity);
state 'Область' = DATA ISTRING[30](LegalEntity);
zip 'Почтовый индекс' = DATA INTEGER (LegalEntity);
phone 'Телефон' = DATA ISTRING[30](LegalEntity);
email 'Электронная почта' = DATA ISTRING[30](LegalEntity);
website 'Веб сайт' = DATA ISTRING[30](LegalEntity);

archived 'Неактивный' = DATA BOOLEAN (LegalEntity) CHARWIDTH 10;
active 'Активный' (LegalEntity i) = NOT archived(i) CHARWIDTH 10;

FORM legalEntity 'Организация'
    OBJECTS le = LegalEntity PANEL
    PROPERTIES(le) name, address, city, state, zip, phone, email, website, active, archived
    
    EDIT LegalEntity OBJECT le
;

DESIGN legalEntity {
    OBJECTS {
        NEW header {
            alignment = STRETCH;
            MOVE PROPERTY(name(le));
        }
        NEW pane {
            type = CONTAINERH;
            //flex = 1;
            alignment = STRETCH;
            
            NEW leftPane {
                fill = 1;
                MOVE PROPERTY(address(le));
                MOVE PROPERTY(city(le));
                MOVE PROPERTY(state(le));
                MOVE PROPERTY(zip(le));
            }
            NEW rightPane {
                fill = 1;
                MOVE PROPERTY(phone(le));
                MOVE PROPERTY(email(le));
                MOVE PROPERTY (website(le));
            }
        }
        
        NEW details {
            type = TABBED;
            fill = 1;
        }
    }
}


FORM legalEntities 'Организации'
    OBJECTS le = LegalEntity
    PROPERTIES(le) READONLY name, address, city, state, zip, phone, email, website, active, archived
    PROPERTIES(le) NEWSESSION NEW, EDIT, DELETE
    
    LIST LegalEntity OBJECT le
;

NAVIGATOR {
    masterData {
        NEW legalEntities;
    }
}

// Наследуем организацию от партнера

EXTEND CLASS LegalEntity : Partner;

name (LegalEntity l) += name(l);
address (LegalEntity l) += CONCAT ' ', address(l), city(l), state(l), zip(l);