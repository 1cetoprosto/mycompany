MODULE BarCode;

REQUIRE MasterData, Item;

NAMESPACE MasterData;

CLASS BarCode 'Штрих код';

item 'Номенклатура' = DATA Item (BarCode) NONULL DELETE;
idItem 'Код номенклатуры' (BarCode b) = id(item(b));
nameItem 'Номенклатура' (BarCode b) = name(item(b));
id 'Штрих код' = DATA STRING[20] (BarCode);
barcode (STRING[50] id) = GROUP AGGR BarCode b BY id(b);

dataDefaultBarCode = DATA BarCode (Item);
calcDefaultBarCode (Item i) = GROUP LAST BarCode b ORDER DESC b WHERE item(b) = i;

defaultBarCode (Item i) = OVERRIDE dataDefaultBarCode(i), calcDefaultBarCode(i);
default 'По умолчанию' (BarCode b) = defaultBarCode(item(b)) = b CHARWIDTH 10;


barCode (Item i) = defaultBarCode(i);

idBarCode 'Штрих код' (Item i) = id(barCode(i));

changeDefault (BarCode b) {
    INPUT c = BOOLEAN DO {
        IF c THEN
            defaultBarCode(Item i) <- b WHERE i = item(b);
    }
}

setBarCode(Item i) {
    INPUT id = STRING[20] DO {
        IF NOT barCode(i) THEN NEW b = BarCode {
            item(b) <- i;
        }
        id(BarCode b) <- id WHERE b = barCode(i); 
    }
}

EXTEND FORM item
    PROPERTIES(i) idBarCode AFTER nameCategory(i) ON CHANGE setBarCode(i)
    
    OBJECTS b = BarCode
    PROPERTIES(b) nameItem, id, default ON CHANGE changeDefault(b)
    PROPERTIES(b) NEW, DELETE
    FILTERS item(b) = i
;

DESIGN item {
    column1 {
        MOVE PROPERTY(idBarCode(i));
    }
    tabs {
        NEW barcodes {
            caption = 'Штрих коды';
            MOVE BOX(b);        
        }
    }
}

EXTEND FORM items
    PROPERTIES(i) READONLYIF isReadonly() idBarCode AFTER name(i)
;

META defineBarCodeAdd(doc, let, itm)
    ##docBarCode 'Штрих код' = DATA ISTRING[20] (###doc) CHANGEKEY 'F4';
    
    addProduct 'Добавить продукт' (###doc ##let) {
        INPUT q = ISTRING[20] DO {
            IF (GROUP LAST item(BarCode bar) ORDER name(item(bar)) WHERE (id(bar) = q AND item(bar) IS ###itm)) THEN {
                NEW l = ###doc##Line {
                    ##doc(l) <- ##let;
                    ##itm(l) <- GROUP LAST item(BarCode bar) ORDER name(item(bar)) WHERE (id(bar) = q AND item(bar) IS ###itm);
                }
            } ELSE MESSAGE 'Штрих код не найден';
        }
    };
    
    EXTEND FORM ##doc
        PROPERTIES(##let) READONLYIF readonly(l) ##docBarCode DRAW l TOOLBAR ON CHANGE addProduct(##let)
    ;
END

