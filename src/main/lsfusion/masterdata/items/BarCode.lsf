MODULE BarCode;

REQUIRE MasterData, Item;

NAMESPACE MasterData;

CLASS BarCode 'Штрих код';

item 'Номенклатура' = DATA Item (BarCode);
nameItem 'Номенклатура' (BarCode b) = name(item(b));
id 'Штрих код' = DATA STRING[20] (BarCode);

barCode (Item i) = GROUP LAST BarCode b ORDER DESC b WHERE item(b) = i;
id 'Штрих код' (Item i) = id(barCode(i));

setBarCode(Item i) {
    INPUT id = STRING[20] DO {
        IF NOT barCode(i) THEN NEW b = BarCode {
            item(b) <- i;
        }
        id(BarCode b) <- id WHERE b = barCode(i); 
    }
}

WHEN SETCHANGED (id(BarCode b)) DO {
    FOR id(b) = id(BarCode bb) AND item(b) = item(bb) AND NOT b = bb DO {DELETE (bb);}
}

EXTEND FORM item
    PROPERTIES(i) id AFTER nameCategory(i) ON CHANGE setBarCode(i)
    
    OBJECTS b = BarCode
    PROPERTIES(b) nameItem, id
    FILTERS item(b) = i
;

DESIGN item {
    box {
        NEW box2 {
            fill = 1;
            caption = 'Штрих коды';
            MOVE BOX (b);        
        }

    }
}

EXTEND FORM items
    PROPERTIES(i) READONLYIF isReadonly() id BEFORE canonicalNameCategory(i)
;


