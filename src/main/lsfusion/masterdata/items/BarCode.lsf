MODULE BarCode;

REQUIRE MasterData, Item;

NAMESPACE MasterData;

CLASS BarCode 'Штрих код';

item 'Номенклатура' = DATA Item (BarCode);
nameItem 'Номенклатура' (BarCode b) = name(item(b));
id 'Штрих код' = DATA STRING[20] (BarCode);

barCode (Item i) = GROUP LAST BarCode b ORDER DESC b WHERE item(b) = i;
idBarCode 'Штрих код' (Item i) = id(barCode(i));

setBarCode(Item i) {
    INPUT id = STRING[20] DO {
        IF NOT barCode(i) THEN NEW b = BarCode {
            item(b) <- i;
        }
        id(BarCode b) <- id WHERE b = barCode(i); 
    }
}

EXTEND FORM item
    PROPERTIES(i) idBarCode AFTER nameCategory(i) ON CHANGE setBarCode(i)
    
    OBJECTS b = BarCode
    PROPERTIES(b) nameItem, id
    FILTERS item(b) = i
;

DESIGN item {
    pane {
        NEW barcodes {
            caption = 'Штрих коды';
            MOVE BOX (b);        
        }

    }
}

EXTEND FORM items
    PROPERTIES(i) READONLYIF isReadonly() idBarCode BEFORE canonicalNameCategory(i)
;


