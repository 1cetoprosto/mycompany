MODULE ItemVariant;

REQUIRE ItemAttribute, BarCode, Product, ServiceP;

NAMESPACE MasterData;

variantOf 'Основная номенклатура' = DATA Item (Item);
nameVariantOf 'Основная номенклатура' (Item i) = name(variantOf(i));

copy(Item n, Item o) + {
     variantOf(n) <- variantOf(o);
}

readonly (Item i) += TRUE IF variantOf(i);
readonly(Item i, ItemAttribute a) += TRUE IF attribute(variantOf(i), a);

WHEN LOCAL CHANGED(attribute(variantOf(Item i), ItemAttribute a)) DO {
     attribute(i, a) <- attribute(variantOf(i), a);
}

WHEN LOCAL CHANGED(dataName(variantOf(Item i))) DO {
    dataName(i) <- dataName(variantOf(i));
}

WHEN LOCAL CHANGED(category(variantOf(Item i))) DO {
    category(i) <- category(variantOf(i));
}

WHEN LOCAL CHANGED(unitMeasure(variantOf(Item i))) DO {
    unitMeasure(i) <- unitMeasure(variantOf(i));
}

WHEN LOCAL CHANGED(archived(variantOf(Item i))) DO {
    archived(i) <- archived(variantOf(i));
}

EXTEND FORM item
    PROPERTIES(i) nameVariantOf
;

addVariant 'Разновидность' (Item ii) {
    NEWSESSION {
        IF ii IS Product THEN NEW p = Product {
                variantOf(p) <- ii;
                SHOW item OBJECTS i = p;
            }
        ELSE NEW s = Service {
            variantOf(s) <- ii;
            SHOW item OBJECTS i = s;
        }
    }
}IMAGE 'add.png';

EXTEND FORM items 
    OBJECTS ii = Item
    PROPERTIES(ii) name, idBarCode, id, nameCategory, nameUnitMeasure
    PROPERTIES addVariant(i) DRAW ii TOOLBAR 
    FILTERS variantOf(ii) = i
    
    FILTERGROUP variants
        FILTER 'Основные' NOT variantOf(i) 'F10'
        FILTER 'Разновидности' variantOf(i) 'F9'
;


DESIGN item {
    header {
        MOVE PROPERTY(nameVariantOf(i)) AFTER PROPERTY(name(i));
    }
}

DESIGN items {
    operations {
        NEW variants {
            caption = 'Разновидности';
            MOVE BOX(ii);
        }
    }
}

