MODULE ItemVariant;

REQUIRE ItemAttribute, BarCode;

NAMESPACE MasterData;

variantOf 'Основная номенклатура' = DATA Item (Item);
nameVariantOf 'Основная номенклатура' (Item i) = name(variantOf(i));

readonly (Item i) += IF variantOf(i) THEN TRUE;

WHEN LOCAL CHANGED(attribute(variantOf(Item i), ItemAttribute a)) DO {
     attribute(i, a) <- attribute(variantOf(i), a);
}

WHEN LOCAL CHANGED(dataName(variantOf(Item i))) DO {
    dataName(i) <- dataName(variantOf(i));
}

WHEN LOCAL CHANGED(category(variantOf(Item i))) DO {
    category(i) <- category(variantOf(i));
}

WHEN LOCAL CHANGED(unitMeasure(variantOf(Item i))) DO {
    unitMeasure(i) <- unitMeasure(variantOf(i));
}

WHEN LOCAL CHANGED(archived(variantOf(Item i))) DO {
    archived(i) <- archived(variantOf(i));
}


EXTEND FORM item
    PROPERTIES(i) nameVariantOf
;

addVariant 'Вариант' (Item ii) {
    NEWSESSION {
        NEW i = Item {
            variantOf(i) <- ii;
            SHOW item OBJECTS i = i;
        }
    }
}IMAGE 'add.png';

EXTEND FORM items 
    OBJECTS ii = Item
    PROPERTIES(ii) name, idBarCode, id, nameCategory, nameUnitMeasure
    PROPERTIES addVariant(i) DRAW ii TOOLBAR 
    FILTERS variantOf(ii) = i
    
    FILTERGROUP variants
        FILTER 'Основные' NOT variantOf(i) 'F10'
        FILTER 'Варианты' variantOf(i) 'F9'
;


DESIGN item {
    header {
        MOVE PROPERTY(nameVariantOf(i)) AFTER PROPERTY(name(i));
    }
}

DESIGN items {
    operations {
        NEW variants {
            caption = 'Варианты';
            MOVE BOX(ii);
        }
    }
}

