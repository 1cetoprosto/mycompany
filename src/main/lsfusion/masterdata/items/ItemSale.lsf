MODULE ItemSale;

REQUIRE MasterData, Item;

NAMESPACE MasterData;

canBeSold 'Предназначен для продажи' = DATA BOOLEAN (Item);

WHEN LOCAL SET(Item i IS Item) DO {
    canBeSold(i) <- TRUE;
}

salesPrice 'Цена продажи' = DATA NUMERIC[10,2] (Item) CHARWIDTH 15 NOFLEX;

copy(Item n, Item o) + {
    canBeSold(n) <- canBeSold(o);
    salesPrice(n) <- salesPrice(o);
}

dataMarkup 'Надбавка по товару, %' = DATA NUMERIC[10,2] (Item);
markup 'Надбавка, %' (Item i) = OVERRIDE dataMarkup(i), parentMarkup(category(i));

EXTEND FORM item 
    PROPERTIES(i) canBeSold, salesPrice, dataMarkup
;

DESIGN item {
    column3 {
        MOVE PROPERTY(canBeSold(i));       
    }
    tabs {
        NEW sale {
            caption = 'Продажа';
            showIf = canBeSold(i);
            MOVE PROPERTY(salesPrice(i)) { pattern = '#,##0.00'; }
            MOVE PROPERTY(dataMarkup(i)) { alignment = START; }
        }
    }
}

EXTEND FORM items
    PROPERTIES(i) READONLYIF isReadonly() canBeSold
;

DESIGN items {
    PROPERTY(canBeSold(i)) { caption = 'Продажа'; };
}