MODULE BarCodeImport;

REQUIRE BarCode, Import;

NAMESPACE MasterData;

barcodeExport 'Экспорт штрих кодов' () {
    IF NOT (GROUP SUM 1 IF BarCode b AND b IS BarCode) THEN {
        NEW b = BarCode {
            id(b) <- 'Test';
            id(b) <- 'Test';
        }
    }
    LOCAL f = EXCELFILE ();
    EXPORT XLSX HEADER FROM 'Код номенклатуры' = idItem(BarCode b), 'Штрих код' = id(b) TO f;        
    open(f());
}

barcodeImport 'Импорт штрих кодов' () {
    LOCAL id = STRING[20] (INTEGER);
    LOCAL item = ISTRING[50] (INTEGER);

    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO item = A, id = B;
            FOR imported(INTEGER i) DO NEW b = BarCode {
                id(b) <- id(i);
                item(b) <- item(item(i));
            }
            APPLY;
            MESSAGE 'Импорт завершен';
        }
    }
}

EXTEND FORM import 
    PROPERTIES barcodeExport(), barcodeImport()
;

DESIGN import {
    OBJECTS {
        NEW barcode {
            type = CONTAINERH;
            caption = 'Штрих коды';
            MOVE PROPERTY(barcodeImport());
            MOVE PROPERTY(barcodeExport());
        }
    }
}

