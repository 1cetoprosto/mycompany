MODULE BarCodeImport;

REQUIRE BarCode, Import;

NAMESPACE MasterData;

barcodeExport 'Экспорт штрих кодов' () {
    NEWSESSION {
        IF NOT (GROUP SUM 1 IF BarCode b AND b IS BarCode) THEN {
            NEW b = BarCode {
                id(b) <- '123';
                item(b) <- GROUP LAST Item i IF i IS Item;
            }
        }
    }
    LOCAL f = EXCELFILE ();
    EXPORT XLSX HEADER FROM 'Код номенклатуры' = idItem(BarCode b), 'Штрих код' = id(b) TO f;        
    open(f());
}

barcodeImport 'Импорт штрих кодов' () {
    LOCAL id = STRING[20] (INTEGER);
    LOCAL idItem = ISTRING[50] (INTEGER);

    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO idItem = A, id = B;
            
            FOR [GROUP SUM 1 BY idItem(INTEGER i)](num) AND NOT item[STRING](num) DO {
                MESSAGE 'Отсутствуют необходимые объекты. Импорт отменен';
                RETURN;
            }
            
            FOR imported(INTEGER i) DO NEW b = BarCode {
                id(b) <- id(i);
                item(b) <- item(idItem(i));
            }
            APPLY;
            MESSAGE 'Импорт завершен';
        }
    }
}

EXTEND FORM import 
    PROPERTIES barcodeExport(), barcodeImport()
;

DESIGN import {
    OBJECTS {
        NEW barcode {
            type = CONTAINERH;
            caption = 'Штрих коды';
            MOVE PROPERTY(barcodeImport());
            MOVE PROPERTY(barcodeExport());
        }
    }
}

