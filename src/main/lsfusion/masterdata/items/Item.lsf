MODULE Item;

REQUIRE MasterData, Category, Tax, ItemMeasure;

NAMESPACE MasterData;

CLASS ABSTRACT Item 'Номенклатура';
id 'Код' = DATA STRING[50] (Item) IN id;
item (STRING[50] id) = GROUP AGGR Item i BY id(i);

name 'Наименование' = DATA ISTRING[50] (Item) IN id;

category 'Категория' = DATA Category (Item) NONULL INDEXED;
idCategory 'Код категории' (Item i) = id(category(i));
nameCategory 'Категория' (Item i) = name(category(i));
canonicalNameCategory 'Категория (полная)' (Item i) = canonicalName(category(i));
unitMeasure 'Единицы учетные' = DATA ItemMeasure (Item);
idUnitMeasure 'Код учетной единицы' (Item i) = id(unitMeasure(i));
nameUnitMeasure 'Единицы учетные' (Item i) = name(unitMeasure(i));
WHEN LOCAL SET(Item i IS Item) DO {
    unitMeasure(i) <- GROUP LAST ItemMeasure im IF default(im);
}

archived 'Неактивный' = DATA BOOLEAN (Item) CHARWIDTH 10;
active 'Активный' (Item i) = NOT archived(i) CHARWIDTH 10;

level1 'Группа 1' (Item i) = nameCategory1(category(i));
level2 'Группа 2' (Item i) = nameCategory2(category(i));
level3 'Группа 3' (Item i) = nameCategory3(category(i));
level4 'Группа 4' (Item i) = nameCategory4(category(i));

FORM item 'Номенклатура'
    OBJECTS i = Item PANEL
    PROPERTIES(i) id, name, nameCategory, nameUnitMeasure, archived
      
    EDIT Item OBJECT i
;

DESIGN item {
    OBJECTS {        
        NEW header{                          
            MOVE PROPERTY(name(i)) {fill=1;};                  
            NEW pane {
                marginTop=10;
                marginBottom=10;
                MOVE PROPERTY(id(i));
                type = CONTAINERH;
                NEW mainProp {
                    type = CONTAINERV;
                    MOVE PROPERTY(nameCategory(i));
                }        
                NEW extProp {
                    marginLeft=20;
                    MOVE PROPERTY(nameUnitMeasure(i));
                    type = CONTAINERV;
                }        
                NEW optProp {
                    marginLeft=20;
                    type = CONTAINERV;
                    MOVE PROPERTY(archived(i));
                }        
            }    
        }
        NEW tabs {
            type = TABBED;
            fill = 1;
        }
    }  
}


FORM items 'Номенклатуры'
    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    PROPERTIES newCategory(c) DRAW c TOOLBAR
    PROPERTIES(c) NEWSESSION EDIT, DELETE
    ORDER name(c)

    OBJECTS i = Item
    PROPERTIES(i) READONLYIF isReadonly() id, name, nameCategory, canonicalNameCategory, nameUnitMeasure
    PROPERTIES(i) SHOWIF isReadonly() NEWSESSION EDIT, DELETE 
    FILTERS level(category(i), c)
    FILTERGROUP active
        FILTER 'Активный' active(i) 'F10' DEFAULT 
        FILTER 'Неактивный' archived(i) 'F9'
   
    LIST Item OBJECT i
;

@extendFormEditable(items);

DESIGN items {
    OBJECTS {
        NEW pane {
            type = SPLITH;
            fill = 1;
            MOVE BOX(TREE categories);                
            MOVE BOX(i) { 
                fill = 2; 
                GRID(i) { defaultComponent = TRUE; }
            }
            
            
        }
    }
}

NAVIGATOR {
    masterData {
        NEW items;
    }
}
