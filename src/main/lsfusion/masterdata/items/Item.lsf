MODULE Item;

REQUIRE MasterData, Category, Tax, ItemMeasure;

NAMESPACE MasterData;

CLASS ABSTRACT Item 'Номенклатура';
name 'Имя' = DATA ISTRING[50] (Item) IN id;

category 'Категория' = DATA Category (Item) NONULL INDEXED;
nameCategory 'Категория' (Item i) = name(category(i));
canonicalNameCategory 'Категория (полная)' (Item i) = canonicalName(category(i));
unitMeasure 'Единицы учетные' = DATA ItemMeasure (Item);
nameUnitMeasure 'Единицы учетные' (Item i) = name(unitMeasure(i));
WHEN LOCAL SET(Item i IS Item) DO {
    unitMeasure(i) <- GROUP LAST ItemMeasure im IF default(im);
}

archived 'Неактивный' = DATA BOOLEAN (Item) CHARWIDTH 10;
active 'Активный' (Item i) = NOT archived(i) CHARWIDTH 10;

FORM item 'Номенклатура'
    OBJECTS i = Item PANEL
    PROPERTIES(i) name, nameCategory, nameUnitMeasure, archived
      
    EDIT Item OBJECT i
;

DESIGN item {
    OBJECTS {
        NEW header {
            MOVE BOX (i);
            type = CONTAINERH;
            NEW mainProp {
                type = CONTAINERV;
                MOVE PROPERTY (name(i));
                MOVE PROPERTY (nameCategory(i));
                MOVE PROPERTY (nameUnitMeasure(i));
            }        
            NEW extProp {
                type = CONTAINERV;
            }        
            NEW optProp {
                type = CONTAINERV;
                MOVE PROPERTY (archived(i));
            }        
            
        }
        NEW pane {
            type = TABBED;
            fill = 1;
        }
    }  
}

FORM items 'Номенклатура'
    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    PROPERTIES newCategory(c) DRAW c TOOLBAR
    PROPERTIES(c) NEWSESSION EDIT, DELETE
    ORDER name(c)

    OBJECTS i = Item
    PROPERTIES(i) READONLYIF isReadonly() name, nameCategory, canonicalNameCategory, nameUnitMeasure
    PROPERTIES(i) SHOWIF isReadonly() NEWSESSION EDIT, DELETE 
    FILTERS level(category(i), c)
    FILTERGROUP active
        FILTER 'Активный' active(i) 'F10' DEFAULT 
        FILTER 'Неактивный' archived(i) 'F9'
   
    LIST Item OBJECT i
;

@extendFormEditable(items);

DESIGN items {
    OBJECTS {
        NEW pane {
            type = SPLITH;
            fill = 1;
            MOVE BOX(TREE categories);                
            MOVE BOX(i) { 
                fill = 2; 
                GRID(i) { defaultComponent = TRUE; }
            }
            
            
        }
    }
}

NAVIGATOR {
    masterData {
        NEW items;
    }
}
