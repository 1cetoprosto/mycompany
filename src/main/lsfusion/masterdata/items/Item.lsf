MODULE Item;

REQUIRE MasterData, Category, Tax;

NAMESPACE MasterData;

CLASS ABSTRACT Item 'Номенклатура';
name 'Имя' = DATA ISTRING[50] (Item) IN id;

category 'Категория' = DATA Category (Item) NONULL INDEXED;
nameCategory 'Категория' (Item i) = name(category(i));
canonicalNameCategory 'Категория (полная)' (Item i) = canonicalName(category(i));

barCode 'Штрих код' = DATA INTEGER (Item);

active 'Активный' = DATA BOOLEAN (Item) CHARWIDTH 10;
archived 'Неактивный' = DATA BOOLEAN (Item) CHARWIDTH 10;

WHEN LOCAL SETCHANGED(active(Item i)) DO archived(i) <- NULL;
WHEN LOCAL SETCHANGED(archived(Item i)) DO active(i) <- NULL;

FORM item 'Номенклатура'
    OBJECTS i = Item PANEL
    PROPERTIES(i) name, nameCategory, barCode, active, archived
      
    EDIT Item OBJECT i
;

DESIGN item {
    OBJECTS {
        NEW box{
        type = CONTAINERH;
            MOVE PROPERTY(active(i));
            MOVE PROPERTY(archived(i));
        }
        MOVE BOX (i);
    }
    
}

FORM items 'Номенклатура'
    TREE categories c = Category PARENT parent
    PROPERTIES READONLY name(c)
    PROPERTIES newCategory(c) DRAW c TOOLBAR
    PROPERTIES(c) NEWSESSION EDIT, DELETE
    ORDER name(c)

    OBJECTS i = Item
    PROPERTIES(i) READONLYIF isReadonly() name, nameCategory, barCode, canonicalNameCategory,
                    active, archived
    PROPERTIES(i) SHOWIF isReadonly() NEWSESSION EDIT, DELETE 
    FILTERS level(category(i), c)
    FILTERGROUP active
        FILTER 'Активные товары' active(i) 'F10' DEFAULT 
        FILTER 'Неактивные товары' archived(i) 'F9'
   
    LIST Item OBJECT i
;

@extendFormEditable(items);

DESIGN items {
    OBJECTS {
        NEW pane {
            type = SPLITH;
            fill = 1;
            MOVE BOX(TREE categories);
            MOVE BOX(i) { 
                fill = 2; 
                GRID(i) { defaultComponent = TRUE; }
            }
        }
    }
}

NAVIGATOR {
    masterData {
        NEW items;
    }
}
