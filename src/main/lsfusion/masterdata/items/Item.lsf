MODULE Item;

REQUIRE MasterDataSettings, Category, Tax, ItemMeasure;

NAMESPACE MasterData;

CLASS ABSTRACT Item 'Номенклатура';
id 'Код' = DATA STRING[50] (Item) IN id CHARWIDTH 8;
item (STRING[50] id) = GROUP AGGR Item i BY id(i);

WHEN SETCHANGED(Item i IS Item  AND numeratorItem() AND NOT id(i)) DO {
    id(i) <- CONCAT '', series(numeratorItem()), curStringValue(numeratorItem());
    incrementValueSession(numeratorItem());   
}

name 'Наименование' = DATA ISTRING[50] (Item) IN id;

category 'Категория' = DATA Category (Item) NONULL INDEXED;
idCategory 'Код категории' (Item i) = id(category(i));
nameCategory 'Категория' (Item i) = name(category(i));
canonicalNameCategory 'Категория (полная)' (Item i) = canonicalName(category(i));

unitMeasure 'Единица измерения' = DATA ItemMeasure (Item);
idUnitMeasure 'Код единицы измерения' (Item i) = id(unitMeasure(i));
nameUnitMeasure 'Единица измерения' (Item i) = name(unitMeasure(i));
WHEN LOCAL SET(Item i IS Item) DO unitMeasure(i) <- GROUP MAX ItemMeasure im IF default(im);

cost 'Плановая себестоимость' = DATA NUMERIC[10,2] (Item) CHARWIDTH 15 NOFLEX;

archived 'Неактивный' = DATA BOOLEAN (Item) CHARWIDTH 10;
active 'Активный' (Item i) = NOT archived(i) CHARWIDTH 10;

level1 'Группа 1' (Item i) = nameCategory1(category(i));
level2 'Группа 2' (Item i) = nameCategory2(category(i));
level3 'Группа 3' (Item i) = nameCategory3(category(i));
level4 'Группа 4' (Item i) = nameCategory4(category(i));

FORM item 'Номенклатура'
    OBJECTS i = Item PANEL
    PROPERTIES(i) name, id, nameCategory, nameUnitMeasure, archived, cost
      
    EDIT Item OBJECT i
;

DESIGN item {
    OBJECTS {        
        NEW header {                          
            MOVE PROPERTY(name(i)) { fill = 1; };                  
            NEW pane {
                type = CONTAINERH;
                NEW column1 {
                    MOVE PROPERTY(nameCategory(i));
                    MOVE PROPERTY(id(i));
                }        
                NEW column2 {
                    MOVE PROPERTY(nameUnitMeasure(i));
                    MOVE PROPERTY(cost(i)) { pattern = '#,##0.00'; };
                }        
                NEW column3 {
                    MOVE PROPERTY(archived(i));
                }
            }    
        }
        NEW tabs {
            type = TABBED;
            fill = 1;
        }
    }  
}

FORM items 'Номенклатуры'
    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    PROPERTIES newCategory(c) DRAW c TOOLBAR
    PROPERTIES(c) NEWSESSION EDIT, DELETE
    ORDER name(c)

    OBJECTS i = Item
    PROPERTIES(i) READONLYIF isReadonly() name, id, nameCategory, nameUnitMeasure
    PROPERTIES(i) SHOWIF isReadonly() NEWSESSION EDIT, DELETE 
    FILTERS level(category(i), c)
    FILTERGROUP active
        FILTER 'Активный' active(i) 'F10' DEFAULT 
        FILTER 'Неактивный' archived(i) 'F9'
   
    LIST Item OBJECT i
;

@extendFormEditable(items);

DESIGN items {
    OBJECTS {
        NEW pane {
            type = SPLITH;
            fill = 1;
            MOVE BOX(TREE categories);                
            NEW details {
                fill = 2; 
                MOVE BOX(i) { 
                    GRID(i) { defaultComponent = TRUE; }
                }
                NEW operations {
                    type = TABBED;
                    fill = 1;
                }
            }

        }
    }
}

NAVIGATOR {
    masterData {
        NEW items;
    }
}
