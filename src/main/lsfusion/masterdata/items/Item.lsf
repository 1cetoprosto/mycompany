MODULE Item;

REQUIRE MasterData, Category, Tax;

NAMESPACE MasterData;

CLASS ABSTRACT Item 'Номенклатура';
name 'Имя' = DATA ISTRING[50] (Item) IN id;

category 'Категория' = DATA Category (Item) NONULL INDEXED;
nameCategory 'Категория' (Item i) = name(category(i));
canonicalNameCategory 'Категория (полная)' (Item i) = canonicalName(category(i));

archived 'Неактивный' = DATA BOOLEAN (Item) CHARWIDTH 10;
active 'Активный' (Item i) = NOT archived(i) CHARWIDTH 10;

FORM item 'Номенклатура'
    OBJECTS i = Item PANEL
    PROPERTIES(i) name, nameCategory, active, archived
      
    EDIT Item OBJECT i
;

DESIGN item {
    OBJECTS {
        NEW head {
        type = CONTAINERH;
            MOVE PROPERTY(active(i));
            MOVE PROPERTY(archived(i));
        }
        NEW header {
            MOVE BOX (i);
        }
        NEW box {}
    }  
}

FORM items 'Номенклатура'
    TREE categories c = Category PARENT parent
    PROPERTIES READONLY name(c)
    PROPERTIES newCategory(c) DRAW c TOOLBAR
    PROPERTIES(c) NEWSESSION EDIT, DELETE
    ORDER name(c)

    OBJECTS i = Item
    PROPERTIES(i) READONLYIF isReadonly() name, nameCategory, canonicalNameCategory,
                    active, archived
    PROPERTIES(i) SHOWIF isReadonly() NEWSESSION EDIT, DELETE 
    FILTERS level(category(i), c)
    FILTERGROUP active
        FILTER 'Активный' active(i) 'F10' DEFAULT 
   
    LIST Item OBJECT i
;

@extendFormEditable(items);

DESIGN items {
    OBJECTS {
        NEW pane {
            type = SPLITH;
            fill = 1;
            MOVE BOX(TREE categories);                
            MOVE BOX(i) { 
                fill = 2; 
                GRID(i) { defaultComponent = TRUE; }
            }
            
            
        }
    }
}

NAVIGATOR {
    masterData {
        NEW items;
    }
}
