MODULE Product;

REQUIRE MasterData, Item;

NAMESPACE MasterData;

CLASS Product 'Товар' : Item;
weight 'Вес единицы, кг' = DATA NUMERIC[11,3] (Product);
volume 'Объем единицы, м3 ' = DATA NUMERIC[11,3] (Product);
customerLeadTime 'Плановое время поставки, дней' = DATA INTEGER (Product);
descriptionForShipment 'Доп. информация для отгрузки' = DATA STRING (Product);   
descriptionForReceipt 'Доп. информация для прихода' = DATA STRING (Product);   
descriptionForTransfer 'Доп. информация для внутреннего перемещения' = DATA STRING (Product);   

copyProduct 'Копировать' (Product p) {
    NEWSESSION {
        NEW np = Product {
            weight(np) <- weight(p);
            volume(np) <- volume(p);
            customerLeadTime(np) <- customerLeadTime(p);
            descriptionForShipment(np) <- descriptionForShipment(p);
            descriptionForReceipt(np) <- descriptionForReceipt(p);
            descriptionForTransfer(np) <- descriptionForTransfer(p);
            copy(np, p);
            SHOW item OBJECTS i = np;
        }
    }
}

EXTEND FORM item
    PROPERTIES(i) weight, volume, customerLeadTime, descriptionForShipment, descriptionForReceipt, descriptionForTransfer
;

DESIGN item{
    tabs {
        NEW product {
            caption = 'Склад';
            NEW box1 {
                MOVE PROPERTY(weight(i)) { charWidth = 5; };
                MOVE PROPERTY(volume(i)) { charWidth = 5; };
                MOVE PROPERTY(customerLeadTime(i)) { charWidth = 5; };
            }  
            MOVE PROPERTY(descriptionForShipment(i));  
            MOVE PROPERTY(descriptionForReceipt(i));  
            MOVE PROPERTY(descriptionForTransfer(i));  
        }
    }
}

newProduct 'Товар' (Category c) {
    NEWSESSION {
        NEW p = Product {
            category(p) <- c;
            SHOW item OBJECTS i = p;  
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

EXTEND FORM items
    PROPERTIES newProduct(c) DRAW i TOOLBAR, copyProduct(i) DRAW i TOOLBAR 
    
    LIST Product OBJECT i
;