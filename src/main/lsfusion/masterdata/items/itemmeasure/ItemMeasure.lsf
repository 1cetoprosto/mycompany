MODULE ItemMeasure;

REQUIRE MasterDataSettings, MetaNumerator;

NAMESPACE MasterData;

CLASS GroupMeasure 'Группа единиц измерения';
id 'Код' = DATA STRING[50] (GroupMeasure) IN id;
groupMeasure (STRING[50] id) = GROUP AGGR GroupMeasure g BY id(g);

name 'Наименование' = DATA STRING[50] (GroupMeasure) IN base NONULL;

CLASS ItemMeasure 'Единицы измерения';

@defineID(itemMeasure, i, 'единиц измерения', 'IM');

code 'Код наименования' = DATA STRING[10] (ItemMeasure);
name 'Наименование' = DATA STRING[50] (ItemMeasure) CHARWIDTH 8 NONULL;
group 'Группа' = DATA GroupMeasure (ItemMeasure);
idGroup 'Код группы' (ItemMeasure i) = id(group(i));
groupName 'Группа' (ItemMeasure i) = name(group(i));
baseMeasure 'Базовая единица' = DATA ItemMeasure (ItemMeasure);
idBaseMeasure 'Код базовой единицы' (ItemMeasure i) = id(baseMeasure(i));
baseMeasureName 'Базовая единица' (ItemMeasure i) = name(baseMeasure(i)); 
coefficient 'Коэффициент к базовой' = DATA DOUBLE (ItemMeasure);
default 'По умолчанию' = DATA BOOLEAN (ItemMeasure);

FORM groupMeasures 'Группы единиц измерения'
    OBJECTS g = GroupMeasure
    PROPERTIES(g) id, name
    PROPERTIES(g) NEW, DELETE   
;

FORM itemMeasure 'Единицы измерения'
    OBJECTS i = ItemMeasure PANEL 
    PROPERTIES(i) name, id, code, groupName, baseMeasureName, coefficient, default
    
    EDIT ItemMeasure OBJECT i
;

FORM itemMeasures 'Единицы измерения'
    OBJECTS i = ItemMeasure
    PROPERTIES(i) READONLY name, id, code, groupName, baseMeasureName, coefficient, default
    PROPERTIES(i) NEWSESSION NEW, EDIT, DELETE
    
    LIST ItemMeasure OBJECT i
;


NAVIGATOR {
    masterData {
        NEW itemMeasures;
    }
}



