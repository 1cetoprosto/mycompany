MODULE ItemPurchase;

REQUIRE MasterData, Item, TaxItem, ItemMeasure;

NAMESPACE MasterData;

canBePurchased 'Предназначен для закупки' = DATA BOOLEAN (Item);

cost 'Себестоимость' = DATA NUMERIC[10,2] (Item) CHARWIDTH 15 NOFLEX;
unitMeasurePurchase 'Единицы в приходе (по умолчанию)' = DATA ItemMeasure (Item);
nameUnitMeasurePurchase 'Единицы в приходе (по умолчанию)' (Item i) = name(unitMeasure(i));

WHEN LOCAL SET(Item i IS Item) DO { 
    canBePurchased(i) <- TRUE;
    unitMeasurePurchase(i) <- GROUP LAST ItemMeasure im IF default(im);
}

inPurchase 'Вкл' = DATA BOOLEAN (Item, Tax);
taxesPurchase 'Налоги поставщиков' (Item i) = GROUP CONCAT name(Tax t) IF inPurchase(i, t), ', ' ORDER t CHARWIDTH 8; 
changeTaxPurchase (Item i) {
    setTax (Tax t) <- inPurchase(i, t);
    DIALOG changeTax DO {
        inPurchase(i, Tax t) <- setTax(t);
    } 
} 

EXTEND FORM item 
    PROPERTIES(i) canBePurchased, cost, nameUnitMeasurePurchase, taxesPurchase ON CHANGE changeTaxPurchase(i)
;

DESIGN item {
    pane {
        NEW purchase {
            caption = 'Закупка';
            NEW purchaseOptions{
                MOVE PROPERTY (canBePurchased(i));
                MOVE PROPERTY (cost(i));
                MOVE PROPERTY (nameUnitMeasurePurchase(i));
                MOVE PROPERTY (taxesPurchase(i)); //{showIf=canBePurchased(i);};
            }
        }
    }
}
