MODULE PurchaseOrderCancel;

REQUIRE PurchaseOrder, PurchaseOrderStatus;

NAMESPACE Purchase;

EXTEND CLASS OrderStatus{
    orderCanceled 'Заказ отменен'
}

canceledDateTime 'Отменен' = DATA DATETIME (Order);
canceled 'Отменен' = DATA BOOLEAN (Order);

cancelOrder 'Отменить' (Order o){
    canceledDateTime(o) <- currentDateTime();
    canceled(o) <- TRUE;    
    MESSAGE 'Заказ отменен';    
}

status(Order o) += WHEN canceled(o) THEN OrderStatus.orderCanceled;
colorStatus(Order o) += WHEN status(o) == OrderStatus.orderCanceled THEN RGB(252,247,149);

EXTEND FORM order
     PROPERTIES (o) cancelOrder
;

DESIGN order {
    statusPane {
        statusActions {
            MOVE PROPERTY (cancelOrder(o));
        }
    }
}

EXTEND FORM orders
    PROPERTIES (o) READONLY SHOWIF showActionsTime() canceledDateTime BEFORE seriesNumber(o) ;
;