MODULE PurchaseOrder;

REQUIRE Time, MetaNumerator, Individual, Company,
        MetaTax, TaxItem, BarCode,
        PurchaseSettings, PartnerPurchase, ItemSale,
        Incoterms, PaymentTerms, Doc, PurchasePricelist,
        Location;  

NAMESPACE Purchase;

CLASS Order 'Заказ';

@defineStatus(order, 'заказа');

dateTime 'Создан'  = DATA DATETIME (Order);
WHEN LOCAL SET(Order o IS Order) AND NOT CHANGED(dateTime(o)) DO { dateTime(o) <- currentDateTime();}

@defineNumber(order, 'Заказы на закупку', 'ЗК');

vendor 'Поставщик' = DATA Partner (Order) NONULL;
nameVendor 'Поставщик' (Order o) = name(vendor(o));

CONSTRAINT vendor(Order o) AND NOT isVendor(vendor(o))
                CHECKED BY vendor[Order]
                MESSAGE 'Контрагент должен быть поставщиком';

// Invoicing information
paymentTerms 'Условие оплаты' = DATA PaymentTerm (Order);
namePaymentTerms 'Условие оплаты' (Order o)= name(paymentTerms(o));

// Shipping information
scheduledDateTime 'Срок поставки' = DATA DATETIME (Order);   
WHEN LOCAL SET(Order o IS Order) AND NOT CHANGED(scheduledDateTime(o)) DO scheduledDateTime(o) <- currentDateTime();     

location = DATA Location (Order);
nameLocation 'Место доставки' (Order o) = name(location(o));

incoterm 'Условие поставки' = DATA Incoterm (Order);
nameIncoterm 'Условие поставки' (Order o)= name(incoterm(o));

// Purchase information
representative 'Наш представитель' = DATA Individual (Order);
nameRepresentative 'Наш представитель' (Order o)= name(representative(o));

CONSTRAINT representative(Order o) AND NOT type(legalEntity(representative(o))) = PartnerType.company 
    CHECKED BY representative MESSAGE 'Должен быть наш работник';

vendorReference 'Входящий номер поставщика' = DATA STRING[30] (Order);

// Lines
CLASS OrderLine 'Спецификация';
order =  DATA Order (OrderLine) NONULL  DELETE;

index 'Индекс' = PARTITION SUM 1 ORDER OrderLine l BY order(l) IN id MATERIALIZED;

item = DATA Item (OrderLine) NONULL;
nameItem 'Номенклатура' (OrderLine l) = name(item(l));
idBarCodeItem 'Штрихкод' (OrderLine l) = idBarCode(item(l));
idItem 'Код' (OrderLine l) = id(item(l));

CONSTRAINT item(OrderLine l) AND NOT canBePurchased(item(l))
            CHECKED BY item[OrderLine] 
            MESSAGE 'Номенклатура не предназначена для закупки';

description 'Описание' = DATA ISTRING (OrderLine);

uom = DATA Uom (OrderLine);
nameUom 'Ед. изм.' (OrderLine l) = name(uom(l));
WHEN LOCAL CHANGED(item(OrderLine l)) DO uom(l) <- uom(item(l));

quantity 'Количество' = DATA NUMERIC[16,3] (OrderLine);
unitPrice 'Цена' = DATA NUMERIC[10,2] (OrderLine);

@defineTaxCalc(order, o, purchase);

WHEN LOCAL CHANGED (item(OrderLine ol)) DO {
    description(ol) <- OVERRIDE nameItemVendor(pricelistLineA(vendor(order(ol)), item(ol), dateTime(order(ol)))),
                                nameItem(ol); 
    unitPrice(ol) <- OVERRIDE unitPrice(pricelistLineA(vendor(order(ol)), item(ol), dateTime(order(ol)))), 
                              cost(item(ol));
    uom(ol) <- uom(item(ol));
    in(ol, Tax t) <- purchaseIn(item(ol),t); 
}

// Line properties
nameStatus 'Статус' (OrderLine l) = nameStatus(order(l)); 
dateTime 'Создан' (OrderLine l) = dateTime(order(l));
number 'Номер' (OrderLine l) = number(order(l));
nameLocation 'Место доставки' (OrderLine l) = nameLocation(order(l));
nameVendor 'Поставщик' (OrderLine l) = nameVendor(order(l));

edit (OrderLine l) + { edit(order(l)); } 

FORM order 'Заказ'
    OBJECTS o = Order PANEL
    PROPERTIES(o) dateTime, number, nameVendor,
                  scheduledDateTime, nameLocation, nameIncoterm,
                  nameRepresentative, vendorReference,
                  namePaymentTerms
    PROPERTIES(o) READONLY untaxedAmount, totalTax, totalAmount
                 
    OBJECTS l = OrderLine
    PROPERTIES(l) index, nameItem, description, nameUom, idBarCodeItem, idItem, quantity, unitPrice, subtotal, taxes ON CHANGE changeTax(l)
    PROPERTIES(l) NEW, DELETE
    FILTERS order(l) = o
     
    EDIT Order OBJECT o    
;      

DESIGN order {
    OBJECTS {
        NEW header {
            alignment = STRETCH;    
            type = CONTAINERH;
            NEW title {
                MOVE PROPERTY(dateTime(o));
                MOVE PROPERTY(number(o));
                MOVE PROPERTY(nameVendor(o)) { notNull = TRUE; } 
            }
            NEW relatedDoc {
                fill = 1;
                type = TABBED;
            }                                            
        }
        NEW details {
            fill = 7;
            type = TABBED;
            MOVE BOX(l) {
                PROPERTY(unitPrice(l)) { pattern = '#,##0.00'; }
                PROPERTY(subtotal(l))  { pattern = '#,##0.00'; }
            }
            NEW otherInformation {
                caption = 'Прочая информация';
                type = COLUMNS;
                columns = 2;
                NEW shippingInformation {
                    caption = 'Поставка';
                    MOVE PROPERTY(scheduledDateTime(o));
                    MOVE PROPERTY(nameLocation(o));
                    MOVE PROPERTY(nameIncoterm(o));
                }
                NEW purchaseInformation {
                    caption = 'Прочие';
                    MOVE PROPERTY(nameRepresentative(o));
                    MOVE PROPERTY(vendorReference(o));
                }
                NEW invoicing {
                    caption = 'Расчеты';
                    MOVE PROPERTY(namePaymentTerms(o));                         
                }
            }            
        }
        NEW footer {
            align = END;
            type = CONTAINERH; 
            NEW total {
                caption = 'Итого';
                type = CONTAINERH;
                MOVE PROPERTY(untaxedAmount(o)) { pattern = '#,##0.00'; }
                MOVE PROPERTY(totalTax(o)) { pattern = '#,##0.00'; }
                MOVE PROPERTY(totalAmount(o)) { pattern = '#,##0.00'; }                     
            }
        }
    }        
}

@defineBarCodeAdd(order, o, item);
@defineDocHistory(order, o, item, quantity);
@defineDocSearch(order, item, quantity, o, canBePurchased);

FORM orders 'Заказы'
    OBJECTS o = Order
    PROPERTIES(o) READONLY dateTime, number, nameVendor,
                           scheduledDateTime, nameLocation, nameIncoterm,
                           nameRepresentative, vendorReference,
                           namePaymentTerms,
                           untaxedAmount, totalTax, totalAmount     
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE 
;

DESIGN orders{
    PROPERTY(untaxedAmount(o)) { pattern = '#,##0.00'; }
    PROPERTY(totalTax(o)) { pattern = '#,##0.00'; }
    PROPERTY(totalAmount(o)) { pattern = '#,##0.00'; }
}

@defineStatusForm(order, o);

NAVIGATOR {
    purchase {
        NEW orders FIRST;    
    }
}

// price list
EXTEND FORM order
    FILTERGROUP  pricelist
        FILTER 'В прайс-листе' GROUP SUM 1 IF itm = item(PricelistLine line) AND 
            startDateTime(pricelist(line)) <= currentDateTime() AND endDateTime(pricelist(line)) >= currentDateTime();
;