MODULE PurchaseOrderType;

REQUIRE Numerator, PurchaseSettings;

NAMESPACE Purchase;

CLASS OrderType 'Тип заказа';
name 'Название' = DATA ISTRING[50] (OrderType) NONULL;

numerator 'Нумератор' = DATA Numerator (OrderType);
nameNumerator 'Нумератор' (OrderType o) = name(numerator(o));

taxIncluded 'Цена включает налоги' = DATA BOOLEAN (OrderType);

FORM orderType 'Тип заказа'
    OBJECTS o = OrderType PANEL 
    PROPERTIES(o) name, nameNumerator, taxIncluded
     
    EDIT OrderType OBJECT o;
;

FORM orderTypes 'Типы заказов'
    OBJECTS o = OrderType
    PROPERTIES(o) READONLY name
    
    LIST OrderType OBJECT o;
;

EXTEND FORM options
    OBJECTS ot = OrderType
    PROPERTIES(ot) READONLY name
    PROPERTIES(ot) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(ot);
    }
}

// default type
countOrderTypes 'Кол-во типов' () = GROUP SUM 1 IF r IS OrderType; 
firstOrderType = GROUP LAST OrderType r ORDER DESC r WHERE r IS OrderType;

defaultOrderType = firstOrderType() IF countOrderTypes() = 1;