MODULE PurchaseOrderBillReceipt;

REQUIRE PurchaseOrderBill, PurchaseOrderReceipt, BillReceipt;

NAMESPACE Invoicing;

EXTEND CLASS InvoicingPolicy {
    delivered 'Поступившее количество'
}

toBillShipped (Order o) = 
    GROUP SUM done(ReceiptLine sl) IF order(orderLine(sl)) = o AND active(receipt(sl)) AND NOT billLine(sl);
toBill(o) += toBillShipped(o) > 0;

createOrderBillLines (Order o, Bill b) + {
    IF invoicingPolicy(type(o)) = InvoicingPolicy.delivered THEN {
        location(b) <- location(o);

        FOR order(orderLine(ReceiptLine sl)) = o AND active(receipt(sl)) AND NOT billLine(sl) INLINE 
            NEW il = BillLine DO {
            bill(il) <- b;
            orderLine(il) <- orderLine(sl);
    
            item(il) <- product(sl);
            description(il) <- description(orderLine(sl));
            quantity(il) <- done(sl);
            price(il) <- price(orderLine(sl));
    
            billLine(sl) <- il; 
        }
    }                            
}

fill (ReceiptLine rl, BillLine il) + {
    orderLine(rl) <- orderLine(il);
}