MODULE PurchaseOrderBill;

REQUIRE PurchaseOrder, PurchaseOrderConfirmed,
        BillDone, BillCanceled;

NAMESPACE Purchase;

orderLine = DATA OrderLine (BillLine);
order = DATA Order (Bill);

billed 'Оформлено накладных' (OrderLine ol) = 
    GROUP SUM quantity(BillLine bl) IF orderLine(bl) = ol AND ready(bill(bl)) AND NOT canceled(bill(bl));
paid 'Оплачено' (OrderLine ol) = 
    GROUP SUM quantity(BillLine bl) IF orderLine(bl) = ol AND done(bill(bl)) AND NOT canceled(bill(bl));
toBill 'К оформлению' (OrderLine l) = max(quantity(l) (-) billed(l), 0);
 
toBill 'К оформлению' (Order o) = GROUP SUM toBill(OrderLine ol) IF order(ol) = o;

createBill 'Оформить накладную' (Order o) {
    APPLY;
    IF canceled() THEN RETURN;
               
    NEWSESSION {
        NEW nb = Bill {
            vendor(nb) <- vendor(o);
            order(nb) <- o;
            vendorReference(nb) <- vendorReference(o);
            paymentTerms(nb) <- paymentTerms(o);             
            sourceDocument(nb) <- number(o);              
            FOR order(OrderLine ol) = o AND toBill(ol) > 0 NEW bl = BillLine DO {
                bill(bl) <- nb;
                item(bl) <- item(ol);
                description(bl) <- description(ol);
                quantity(bl) <- toBill(ol);
                price(bl) <- price(ol);
                orderLine(bl) <- ol;
            }                            
            executeLocalEvents();
            FOR bill(BillLine bl) = nb DO {
                in(bl, Tax t) <- in(orderLine(bl), t);
            }                                
            SHOW bill OBJECTS b = nb DOCKED;
        }
    }
}

FORM orderBillShow 'Выставлено счетов'
    OBJECTS ol = OrderLine
    
    OBJECTS bl = BillLine
    PROPERTIES(bl) READONLY nameStatus, number, dateTime, quantity
    FILTERS orderLine(bl) = ol
;

EXTEND FORM order   
    PROPERTIES(o) SHOWIF (status(o) = OrderStatus.confirmed AND toBill(o) > 0) createBill
    PROPERTIES(l) SHOWIF confirmed(o) billed ON CHANGE { DIALOG orderBillShow OBJECTS ol = l; } BACKGROUND NOT billed(l) = quantity(l)
    PROPERTIES(l) SHOWIF confirmed(o) paid ON CHANGE { DIALOG orderBillShow OBJECTS ol = l; } BACKGROUND NOT paid(l) = quantity(l)

    OBJECTS bi = Bill
    PROPERTIES(bi) READONLY nameStatus BACKGROUND colorStatus(bi), number, dateTime    
    PROPERTIES(bi) EDIT GRID   
    FILTERS order(bi) = o       
;
 
DESIGN order {
    primaryActions {
        MOVE PROPERTY(createBill(o)) { fontStyle = 'bold'; };
    }
    relatedDoc  {
        MOVE BOX(bi) {
            GRID(bi) { headerHeight = 24; }
        }
        REMOVE TOOLBARBOX(bi);
    }
}