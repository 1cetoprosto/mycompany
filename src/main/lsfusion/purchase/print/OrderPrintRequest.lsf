MODULE OrderPrintRequest;

REQUIRE Order, OrderStatus, OrderSent, Partner;

NAMESPACE Purchase;

phoneVendor 'Телефон поставщика' (Order o) = phone(vendor(o));
fullAddressVendor 'Адрес поставщика' (Order o) = concatAddress(vendor(o),'\n'); 
fullAddressLocation 'Адрес места доставки' (Order o ) = concatAddress(location(o),'\n');
fullAddressCompany 'Адрес компании' (Order o) = concatAddress(partner(location(o)),'\n');
phoneCompany 'Телефон компании' (Order o) = phone(partner(location(o)));

FORM printRequest 'Запрос поставщику'
    OBJECTS o = Order PANEL 
    PROPERTIES(o) sentRequestDateTime, scheduledDate, number, nameLocation, fullAddressLocation, nameVendor, fullAddressVendor, phoneVendor, fullAddressCompany, phoneCompany
      
    OBJECTS l = OrderLine
    PROPERTIES(l) nameItem, description, scheduledDate, quantity
    FILTERS order(l) = o
;

printRequest 'Печать запрос' (Order o) {
    sentRequestDateTime(o) <- currentDateTime();
    sent(o) <- TRUE;
    PRINT printRequest OBJECTS o = o;   
    MESSAGE 'Запрос напечатан';    
}  

reprintRequest 'Печать запрос' (Order o) {
    sentRequestDateTime(o) <- currentDateTime();
    sent(o) <- TRUE;
    PRINT printRequest OBJECTS o = o;   
    MESSAGE 'Запрос напечатан';    
}  
 
EXTEND FORM order
     PROPERTIES (o) SHOWIF (status(o)=OrderStatus.draft)  printRequest BACKGROUND (CASE WHEN NOT sent(o) THEN nextAction())
     PROPERTIES (o) SHOWIF (status(o)=OrderStatus.requestSent)  reprintRequest
;

DESIGN order {
    statusPane {
        statusActions {
            MOVE PROPERTY (printRequest(o)) FIRST;             
            MOVE PROPERTY (reprintRequest(o));             
        }
    }
}

