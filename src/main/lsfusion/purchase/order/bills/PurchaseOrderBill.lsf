MODULE PurchaseOrderBill;

REQUIRE PurchaseOrder, BillPaymentTerms, BillCanceled, PurchaseOrderStatus, Time, PurchaseOrderStatusConfirm;

NAMESPACE Purchase;

orderLine = DATA OrderLine (BillLine);
order = DATA Order (Bill);

billed 'Оформлено по накладным' (OrderLine ol) = 
    GROUP SUM quantity(BillLine bl) IF orderLine(bl) = ol AND NOT canceled(bill(bl));
paid 'Оплачено' (OrderLine ol) = 
    GROUP SUM quantity(BillLine bl) IF orderLine(bl) = ol AND done(bill(bl)) AND NOT canceled(bill(bl));
toBill 'К оформлению' (OrderLine l) = max(quantity(l) (-) billed(l), 0);
 
toBill 'К оформлению' (Order o) = GROUP SUM toBill(OrderLine ol) IF order(ol) = o;

createBill 'Оформить накладную' (Order o) {
    APPLY;
    IF canceled() THEN RETURN;
               
    NEWSESSION {
        NEW nb = Bill {
            partner(nb) <- vendor(o);
            date(nb) <- scheduledDate(o);
            order(nb) <- o;
            vendorReference(nb) <- vendorReference(o);
            paymentTerms(nb) <- paymentterm(o);             
            sourceDocument(nb) <- number(o);              
            FOR order(OrderLine ol) = o AND toBill(ol) > 0 NEW bl = BillLine DO {
                bill(bl) <- nb;
                item(bl) <- item(ol);
                description(bl) <- description(ol);
                quantity(bl) <- toBill(ol);
                unitPrice(bl) <- unitPrice(ol);
                orderLine(bl) <- ol;
            }                            
            executeLocalEvents();
            FOR bill(BillLine bl) = nb DO {
                in(bl, Tax t) <- in(orderLine(bl), t);
            }                                
            SHOW bill OBJECTS b = nb DOCKED;
        }
    }
}

billDate 'Дата счета' (BillLine bl) = date(bill(bl));
billStatus 'Статус' (BillLine bl) = nameStatus(bill(bl));

FORM orderBillShow 'Выставлено счетов'
    OBJECTS ol = OrderLine
    
    OBJECTS bl = BillLine
    PROPERTIES(bl) billDate, billStatus, quantity
    FILTERS orderLine(bl) = ol
;

EXTEND FORM order   
    PROPERTIES(o) SHOWIF (confirmed(o) AND toBill(o) > 0) createBill
    PROPERTIES(l) SHOWIF confirmed(o) billed ON CHANGE { DIALOG orderBillShow OBJECTS ol = l; }
    PROPERTIES(l) SHOWIF confirmed(o) paid ON CHANGE { DIALOG orderBillShow OBJECTS ol = l; }

    OBJECTS bi = Bill
    PROPERTIES(bi) seriesNumber ON CHANGE { SHOW bill OBJECTS b = bi; } 
    PROPERTIES(bi) READONLY date, nameStatus    
    FILTERS order(bi) = o       
;
 
DESIGN order {
    statusActions {
        MOVE PROPERTY(createBill(o)) FIRST;
    }
    relatedDoc  {
        MOVE BOX(bi);
        REMOVE TOOLBARBOX(bi);
    }
}




