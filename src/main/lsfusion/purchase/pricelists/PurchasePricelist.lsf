MODULE PurchasePricelist;

REQUIRE Item, Partner, ItemPurchase, PartnerPurchase, PurchaseSettings, BarCode;

NAMESPACE Purchase; 

CLASS Pricelist 'Прайс-листы';

@defineNumber(pricelist, 'Прайс-листы', 'ПП');

startDateTime 'Начало действия' = DATA DATETIME (Pricelist);
WHEN LOCAL SET(Pricelist l IS Pricelist) AND NOT CHANGED(startDateTime(l)) DO startDateTime(l) <- currentDateTime(); 
endDateTime 'Окончание действия' = DATA DATETIME (Pricelist);

note 'Примечание' = DATA STRING[50] (Pricelist);

vendor 'Поставщик ' = DATA Partner (Pricelist);
nameVendor 'Поставщик '(Pricelist p) = name(vendor(p));

// lines
CLASS PricelistLine 'Номенклатура';

pricelist 'Прайс-лист' = DATA Pricelist (PricelistLine) NONULL DELETE;

index 'Индекс' = PARTITION SUM 1 ORDER PricelistLine l BY pricelist(l) IN id MATERIALIZED;

item 'Номенклатура' = DATA Item (PricelistLine);
nameItem 'Номенклатура' (PricelistLine prl) = name(item(prl));
nameUnitMeasure 'Единица измерения' (PricelistLine prl) = name(unitMeasure(item(prl))); 
idBarCodeItem 'Штрих-код' (PricelistLine prl) = idBarCode(item(prl)); 
idItem 'Код' (PricelistLine prl) = id(item(prl)); 

unitPrice 'Цена' = DATA NUMERIC[10,2] (PricelistLine);

nameItemVendor 'Наименование у поставщика' = DATA STRING[200] (PricelistLine);
codeItemVendor 'Код у поставщика' = DATA STRING[20] (PricelistLine);

unitMeasure 'Единицы поставщика' = DATA ItemMeasure (PricelistLine);

// Line properties
number 'Номер' (PricelistLine prl) = number(pricelist(prl));
startDateTime 'Начало действия' (PricelistLine prl) = startDateTime(pricelist(prl));
endDateTime 'Окончание действия' (PricelistLine prl) = endDateTime(pricelist(prl));
note 'Примечание' (PricelistLine prl) = note(pricelist(prl));

// value
pricelistLineA (Partner p, Item i, DATETIME d) =
    GROUP LAST PricelistLine prl IF item(prl) = i AND vendor(pricelist(prl)) = p AND 
               NOT startDateTime(pricelist(prl)) > d AND NOT endDateTime(pricelist(prl)) < d
          ORDER startDateTime(pricelist(prl)), pricelist(prl), prl;

FORM pricelist 'Прайс-лист'
    OBJECTS p = Pricelist PANEL
    PROPERTIES(p) number, startDateTime, endDateTime, nameVendor, note
    
    OBJECTS l = PricelistLine
    PROPERTIES(l) index READONLY, nameItem, nameUnitMeasure READONLY, nameItemVendor, codeItemVendor,
                  idBarCodeItem, idItem, unitPrice
    PROPERTIES(l) NEW, DELETE 
    FILTERS pricelist(l) = p
    
    EDIT Pricelist OBJECT p
;

DESIGN pricelist {
    OBJECTS {
        NEW header {
            MOVE PROPERTY(number(p));
            NEW period {
                type = CONTAINERH;
                MOVE PROPERTY(startDateTime(p)) { caption = 'Действует с'; }
                MOVE PROPERTY(endDateTime(p)) { caption = 'по '; }                
            }
            MOVE PROPERTY(nameVendor(p));
            MOVE PROPERTY(note(p));
        }
        NEW details {
            fill = 1;
            type = TABBED;
            MOVE BOX(l) {
                PROPERTY(unitPrice(l)) { pattern = '#,##0.00'; }                          
            }
        }
    }
}

FORM pricelists 'Прайс-листы'
    OBJECTS p = Pricelist
    PROPERTIES(p) READONLY number, startDateTime, endDateTime, nameVendor, note
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE 

    LIST Pricelist OBJECT p
;  

NAVIGATOR {
    purchase {
        NEW pricelists BEFORE settings;       
    }
}

copy 'Копировать' (Pricelist pl) {
    NEWSESSION {
        NEW p = Pricelist {
            startDateTime(p) <- startDateTime(pl);
            endDateTime(p) <- endDateTime(pl);
            note(p) <- note(pl);
            vendor(p) <- vendor(pl);
            FOR pricelist(PricelistLine l)  = pl DO NEW nl = PricelistLine {
                pricelist(nl) <- p;
                item(nl) <- item(l);
                unitPrice(nl) <- unitPrice(l);
                nameItemVendor(nl) <- nameItemVendor(l);
                codeItemVendor(nl) <- codeItemVendor(l);
                unitMeasure(nl) <- unitMeasure(l);
                
            }
            SHOW pricelist OBJECTS p = p;
        }
    }
}

EXTEND FORM pricelist
    PROPERTIES copy(p) DRAW l TOOLBAR 
;

