MODULE PurchaseOrderStatusConfirm;

REQUIRE PurchaseOrder, PurchaseOrderStatus, PurchaseOrderStatusDraft, PurchaseOrderStatusSent;

NAMESPACE Purchase;

EXTEND CLASS OrderStatus{
    orderConfirmed 'Заказ оформлен'
}

confirmedDateTime 'Подтвержден' = DATA DATETIME (Order);
confirmed 'Подтвержден' = DATA BOOLEAN (Order);

confirmOrder 'Подтвердить заказ' (Order o){
    confirmedDateTime(o) <- currentDateTime();
    confirmed(o) <- TRUE;
    MESSAGE 'Заказ подтвержден';    
}

confirmUnsentOrder 'Подтвердить заказ' (Order o){
    confirmedDateTime(o) <- currentDateTime();
    confirmed(o) <- TRUE;
    MESSAGE 'Заказ подтвержден';    
}

status(Order o) += WHEN confirmed(o) THEN OrderStatus.orderConfirmed;
colorStatus(Order o) += WHEN status(o) == OrderStatus.orderConfirmed THEN RGB(150,255,150);

EXTEND FORM order 
     PROPERTIES (o) SHOWIF (status(o)=OrderStatus.requestSent)  confirmOrder
     PROPERTIES (o) SHOWIF (status(o)=OrderStatus.draft)  confirmUnsentOrder
;

DESIGN order {
    statusPane {
        statusActions {
            MOVE PROPERTY(confirmUnsentOrder(o));
            MOVE PROPERTY(confirmOrder(o))  FIRST { fontStyle = 'bold'; };
        }
    }
}

EXTEND FORM orders
    PROPERTIES(o) READONLY SHOWIF showActionsTime() confirmedDateTime BEFORE seriesNumber(o);
;

EXTEND FORM orders
    PROPERTIES addOrder() DRAW o TOOLBAR
;

EXTEND FORM items
    OBJECTS pOrder = (po = Order, pl = OrderLine)
    PROPERTIES(po) date, seriesNumber, nameVendor, nameLocation
    PROPERTIES(pl) nameItem, quantity, unitPrice
    FILTERS order(pl) = po, item(pl) = i, confirmed(po)
;

DESIGN items {
    operations {
        NEW pOrders {
            caption = 'Заказы на закупку';
            MOVE BOX(pOrder);
        }
    }
}

EXTEND FORM partners
    OBJECTS pOrder = (po = Order, pl = OrderLine)
    PROPERTIES(po) date, seriesNumber, nameVendor, nameLocation
    PROPERTIES(pl) nameItem, quantity, unitPrice
    FILTERS order(pl) = po, vendor(po) = p, confirmed(po)
;

DESIGN partners {
    operations {
        NEW pOrders {
            caption = 'Заказы на закупку';
            MOVE BOX(pOrder);
        }
    }
}