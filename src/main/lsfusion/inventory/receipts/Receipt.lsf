MODULE Receipt;

REQUIRE System, Time,
        Inventory, Location, Partner, Product,
        ReceiptOperation;

NAMESPACE Inventory;

CLASS Receipt 'Поступление';

// Операция
operation 'Операция' = DATA ReceiptOperation (Receipt) NONULL;
nameOperation 'Операция' (Receipt r) = name(operation(r));

WHEN LOCAL SET(Receipt r IS Receipt) AND countOperations() = 1 DO operation(r) <- lastOperation(); 

scheduledDate 'Дата приемки' = DATA DATETIME (Receipt) NONULL;
scheduledDate (Receipt r) <- currentDateTime() WHEN SET(r IS Receipt);
receiptDate 'Дата проведения' = DATA DATETIME (Receipt);

// Номер
series 'Серия' = DATA STRING[2] (Receipt);
number 'Номер' = DATA STRING[28] (Receipt) NONULL;

seriesNumber 'Серия/Номер' (Receipt r) = CONCAT '/', series(r), number(r); 

WHEN SETCHANGED(operation(Receipt o)) AND NOT CHANGED(number(o)) AND NOT CHANGED(series(o)) DO {
    number(o) <- curStringValue(numerator(operation(o)));
    series(o) <- series(numerator(operation(o)));
    incrementValueSession(numerator(operation(o)));
}

// Места хранения
location 'Место хранения' = DATA Location (Receipt) NONULL INDEXED;
nameLocation 'Место хранения' (Receipt r) = name(location(r));
WHEN LOCAL CHANGED(operation(Receipt r)) DO location(r) <- defaultLocation(operation(r));

partner 'Партнер' = DATA Partner (Receipt);
namePartner 'Партнер' (Receipt r) = name(partner(r));
notes 'Примечания' = DATA ISTRING[50] (Receipt);

CLASS ReceiptLine 'Строка поступления';

receipt 'Документ' = DATA Receipt (ReceiptLine) NONULL DELETE INDEXED;

product 'Продукт' = DATA Product (ReceiptLine) NONULL INDEXED;
nameProduct 'Продукт' (ReceiptLine l) = name(product(l));

initialDemand 'Исходное количество' = DATA NUMERIC[16,3] (ReceiptLine) NONULL;

CONSTRAINT initialDemand(ReceiptLine l) < 0 OR initialDemand(l) > maxQuantity(operation(receipt(l))) 
    AND maxQuantity(operation(receipt(l))) MESSAGE 'Исходное количество должно быть от 0 до максимально разрешенного';

// при редактировании строки открываем редактирование документа
edit (ReceiptLine l) + { edit(receipt(l)); } 

index 'Индекс' = PARTITION SUM 1 ORDER ReceiptLine l BY receipt(l);

FORM receipt 'Поступление'
    OBJECTS r = Receipt PANEL
    PROPERTIES(r) scheduledDate, receiptDate READONLY, series, number, nameOperation, nameLocation, namePartner, notes
   
    EDIT Receipt OBJECT r
;

FORM receipts 'Поступления'
    OBJECTS r = Receipt
    PROPERTIES(r) READONLY scheduledDate, receiptDate, series, number, nameOperation, nameLocation, namePartner, notes
    PROPERTIES(r) NEWSESSION NEW, EDIT, DELETE
;

DESIGN receipt {
    OBJECTS {        
        NEW pane {
            fill = 1;                         
            NEW pane1 {
                type = CONTAINERV;
                NEW box1 {
                    type = CONTAINERH; 
                    alignment = STRETCH;                                        
                    MOVE PROPERTY(scheduledDate(r));
                    MOVE PROPERTY(receiptDate(r));
                    MOVE PROPERTY(series(r));
                    MOVE PROPERTY(number(r));
                }
                NEW box2 {
                    type = CONTAINERH;
                    MOVE PROPERTY(nameOperation(r)) {notNull = TRUE;}
                    MOVE PROPERTY(nameLocation(r)) {notNull = TRUE;}
                    MOVE PROPERTY(namePartner(r));
                }
                NEW box3 {
                    type = CONTAINERH;
                    alignment = STRETCH; 
                    MOVE PROPERTY(notes(r));
                }
            }
        }        
    }
}

NAVIGATOR {
    operations {
        NEW receipts;
    }
}

copyReceipt 'Копировать' (Receipt r)  { 
    NEWSESSION {
        NEW nr = Receipt {
            FOR receipt(ReceiptLine l) = r INLINE NEW nl = ReceiptLine DO {
                receipt(nl) <- nr;
                product(nl) <- product(l);
                initialDemand(nl) <- initialDemand(l);
            }
            SHOW receipt OBJECTS r = nr DOCKED;
        }
    }
}

EXTEND FORM receipts
    PROPERTIES(r) copyReceipt TOOLBAR 
;