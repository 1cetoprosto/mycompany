MODULE ReceiptType;

REQUIRE Numerator, Location;

NAMESPACE Inventory;

CLASS ReceiptType 'Тип поступления';
name 'Название' = DATA ISTRING[50] (ReceiptType) NONULL;

numerator = DATA Numerator (ReceiptType);
nameNumerator 'Нумератор' (ReceiptType o) = name(numerator(o));

defaultLocation = DATA Location (ReceiptType);
nameDefaultLocation 'Место хранения по умолчанию' (ReceiptType o) = name(defaultLocation(o));

maxQuantity 'Максимальное кол-во товара' = DATA NUMERIC[16,3] (ReceiptType);

FORM receiptType 'Тип поступления'
    OBJECTS o = ReceiptType PANEL 
    PROPERTIES(o) name, nameNumerator, nameDefaultLocation, maxQuantity
     
    EDIT ReceiptType OBJECT o;
;

FORM receiptTypes 'Типы поступления'
    OBJECTS o = ReceiptType
    PROPERTIES(o) READONLY name, nameNumerator, nameDefaultLocation, maxQuantity
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    LIST ReceiptType OBJECT o;
;

NAVIGATOR {
    settings {
        NEW receiptTypes;
    }
}

// default type
countReceiptTypes 'Кол-во типов' () = GROUP SUM 1 IF r IS ReceiptType; 
firstReceiptType = GROUP LAST ReceiptType r ORDER DESC r WHERE r IS ReceiptType;

defaultReceiptType = firstReceiptType() IF countReceiptTypes() = 1;

// initial data
loadInitialData () + {
    NEWSESSION {
        NEW r = ReceiptType {
            name(r) <- 'Поступление от поставщика';

            NEW n = Numerator {
                name(n) <- 'Поступления от поставщика';
                series(n) <- 'ПП';
                minValue(n) <- 1;
                maxValue(n) <- 999999;
                stringLength(n) <- 6;
                numerator(r) <- n;
            }
        }
        APPLY;
    }
}