MODULE ReceiptType;

REQUIRE Numerator, Location;

NAMESPACE Inventory;

CLASS ReceiptType 'Тип поступления';

name 'Название' = DATA ISTRING[50] (ReceiptType) NONULL;
putAway 'Размещение' = DATA BOOLEAN (ReceiptType);

numerator 'Нумератор' = DATA Numerator (ReceiptType);
nameNumerator 'Нумератор' (ReceiptType o) = name(numerator(o));

loadInitialData () + {
    NEWSESSION {
        NEW r = ReceiptType {
            name(r) <- 'Стандартное поступление';
            putAway(r) <- TRUE;

            NEW n = Numerator {
                name(n) <- 'Поступлений';
                series(n) <- 'RC';
                minValue(n) <- 1;
                maxValue(n) <- 999999;
                stringLength(n) <- 6;
                numerator(r) <- n;
            }
        }
        APPLY;
    }
}

defaultLocation 'Место хранения по умолчанию' = DATA Location (ReceiptType);
nameDefaultLocation 'Место хранения по умолчанию' (ReceiptType o) = name(defaultLocation(o));
maxQuantity 'Максимальное кол-во товара' = DATA NUMERIC[16,3] (ReceiptType);

countTypes 'Кол-во типов' () = GROUP SUM 1 IF r IS ReceiptType; 
lastType 'Последний тип' = GROUP LAST r AS ReceiptType ORDER r;

FORM receiptType 'Тип поступления'
    OBJECTS o = ReceiptType PANEL 
    PROPERTIES(o) name, nameNumerator, nameDefaultLocation, maxQuantity, putAway
     
    EDIT ReceiptType OBJECT o;
;

FORM receiptTypes 'Типы поступления'
    OBJECTS o = ReceiptType
    PROPERTIES(o) READONLY name, nameNumerator, nameDefaultLocation, maxQuantity, putAway
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    LIST ReceiptType OBJECT o;
;

NAVIGATOR {
    settings {
        NEW receiptTypes;
    }
}