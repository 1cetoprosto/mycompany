MODULE ReceiptImport;

REQUIRE ReceiptDone, Import, InvLedger, CostLedger, ItemPurchase;

NAMESPACE Inventory;

receiptExport 'Экспорт остатков' () {
    LOCAL f = EXCELFILE ();
    
    IF NOT (GROUP SUM 1 IF onHand(Location l, Product p) AND l IS Location AND p IS Product) THEN
        EXPORT XLSX HEADER FROM 'Код места хранения' = '123', 'Код продукта' = '123', 
                                    'Кол-во' = '1' TO f;
        ELSE EXPORT XLSX HEADER FROM 'Код места хранения' = id(Location l), 'Код продукта' = id(Product p), 
                                    'Кол-во' = onHand(l, p), 'Себестоимость' =  costSum(l,p) WHERE onHand(l, p) TO f;   
    open(f());
}

receiptImport 'Импорт остатков' () {
    LOCAL idLocation = STRING[50] (INTEGER);
    LOCAL idProduct = ISTRING[50] (INTEGER);
    LOCAL quantity = NUMERIC[16,3] (INTEGER);
    LOCAL cost = NUMERIC[16,3] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO idLocation = A, idProduct = B, quantity = C, cost = D;
            
            FOR [GROUP SUM 1 BY idLocation(INTEGER i)](num) AND NOT location[STRING](num) DO {
                MESSAGE 'Отсутствуют необходимые объекты. Импорт отменен';
                RETURN;
            }
            
            FOR [GROUP SUM 1 BY idProduct(INTEGER i)](num) AND NOT item[STRING](num) DO {
                MESSAGE 'Отсутствуют необходимые объекты. Импорт отменен';
                RETURN;
            }
            
            IF NOT (GROUP SUM 1 IF ReceiptType t AND t IS ReceiptType) THEN {
                MESSAGE 'Отсутствуют типы поступлений в базе данных. Импорт отменен';
                RETURN;
            }
            
            FOR [GROUP SUM 1 BY idLocation(INTEGER i)](num) AND NOT receipt('INIT_' + num) DO NEW nr = Receipt {
                number(nr) <- 'INIT_' + num;
            }
            
            FOR INTEGER i = [GROUP MIN INTEGER ii BY idLocation(ii)](num) AND number(Receipt r) = 'INIT_' + num DO {
                scheduledDate(r) <- currentDateTime();
                location(r) <- location(idLocation(i));
                type(r) <- GROUP LAST ReceiptType t ORDER name(t);
                done(r) <- TRUE;
            }
            
            FOR idProduct(INTEGER i) DO NEW l = ReceiptLine {
                product(l) <- item(idProduct(i));
                initialDemand(l) <- quantity(i);
                done(l) <- quantity(i);
                receipt(l) <- receipt(idLocation(i));
                cost(product(l)) <- NUMERIC[10,2](cost(i)/quantity(i));
            }
                        
            APPLY;
            MESSAGE 'Импорт завершен';
        }
    }
}

EXTEND FORM import 
    PROPERTIES receiptImport(), receiptExport()
;

DESIGN import {
    OBJECTS {
        NEW receipt {
            type = CONTAINERH;
            caption = 'Остатки';
            MOVE PROPERTY(receiptImport());
            MOVE PROPERTY(receiptExport());
        }
    }
}


