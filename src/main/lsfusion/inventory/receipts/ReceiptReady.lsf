MODULE ReceiptReady;

REQUIRE Inventory, ReceiptStatus;

NAMESPACE Inventory;

// К приемке
done 'Принятое кол-во' = DATA NUMERIC[16,3] (ReceiptLine);

ready 'К приемке' = DATA BOOLEAN (Receipt);

markAsToDo 'В работу' (Receipt r) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        ready(r) <- TRUE;
        APPLY;
    }
}

EXTEND CLASS ReceiptStatus {
    ready 'К приемке'
}

EXTEND FORM receipt
    PROPERTIES(r) markAsToDo SHOWIF status(r) = ReceiptStatus.draft, ready READONLY 

    PROPERTIES(l) done READONLYIF NOT status(r) = ReceiptStatus.ready
;

EXTEND FORM receipts    
    EXTEND FILTERGROUP status
        FILTER 'К приемке' status(r) = ReceiptStatus.ready 'F8'    
;

status(Receipt r) += WHEN ready(r) THEN ReceiptStatus.ready;
colorStatus(Receipt r) += WHEN status(r) == ReceiptStatus.ready THEN RGB(252,247,149);

DESIGN receipt {
    statusActions {
        MOVE PROPERTY(markAsToDo(r));                
    }
    status {
        MOVE PROPERTY(ready(r));                               
    }
}

CONSTRAINT done(ReceiptLine l) < 0 OR done(l) > initialDemand(l) 
    MESSAGE 'Отгруженое количество должно быть от 0 до исходного';