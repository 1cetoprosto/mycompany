MODULE ReceiptReady;

REQUIRE Receipt, ResLedger;

NAMESPACE Inventory;

// К приемке
ready 'К приемке' = DATA BOOLEAN (Receipt);

markAsToDo 'В работу' (Receipt r) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        ready(r) <- TRUE;
        APPLY;
    }
}

countLines (Receipt r) = GROUP SUM 1 IF receipt(ReceiptLine l) = r;

EXTEND CLASS ReceiptStatus {
    ready 'К приемке'
}

EXTEND FORM receipt
    PROPERTIES(r) markAsToDo SHOWIF status(r) = ReceiptStatus.draft AND countLines(r) AND NOT immediate(r), ready READONLY 
;

EXTEND FORM receipts    
    EXTEND FILTERGROUP status
        FILTER 'К приемке' status(r) = ReceiptStatus.ready 'F8'    
;

status(Receipt r) += WHEN ready(r) THEN ReceiptStatus.ready;
colorStatus(Receipt r) += WHEN status(r) == ReceiptStatus.ready THEN RGB(252,247,149);

DESIGN receipt {
    primaryActions {
        MOVE PROPERTY(markAsToDo(r)) { fontStyle = 'bold'; }
    }       
    status {
        MOVE PROPERTY(ready(r));                               
    }
}
