MODULE ReceiptPutAway;

REQUIRE ReceiptDone, Input;

NAMESPACE Inventory;

CLASS ReceiptPutAway 'Размещение';

receiptLine 'Строка поступления' = DATA ReceiptLine (ReceiptPutAway) NONULL DELETE;

location 'Место хранения' = DATA Location (ReceiptPutAway);
nameLocation 'Место хранения' (ReceiptPutAway p) = name(location(p));

CONSTRAINT location(ReceiptPutAway p) AND NOT level(location(p), location(receipt(receiptLine(p)))) 
                    CHECKED BY location[ReceiptPutAway]
                    MESSAGE 'Место хранения должно быть дочерним от места хранения из документа потупления';

quantity 'Кол-во' = DATA NUMERIC[16,3] (ReceiptPutAway); 

put 'Размещено' (ReceiptLine l) = GROUP SUM quantity(ReceiptPutAway p) IF receiptLine(p) = l;

CONSTRAINT put(ReceiptLine l) > done(l) MESSAGE 'Размещенное количество не может быть больше принятого';

EXTEND CLASS ReceiptStatus {
    putAway 'Размещен'
}

putAway 'Размещен' = DATA BOOLEAN (Receipt);

markAsPutAway 'Разместить' (Receipt r) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        putAway(r) <- TRUE;
        APPLY;
    }
}

status(Receipt r) += WHEN putAway(r) THEN ReceiptStatus.putAway;

EXTEND FORM receipt
    PROPERTIES (r) markAsPutAway SHOWIF status(r) = ReceiptStatus.done, putAway READONLY
;

DESIGN receipt {
    statusActions {
        primary {
            MOVE PROPERTY (markAsPutAway(r));
        }
    }
    status {
        MOVE PROPERTY (putAway(r));
    }
}

EXTEND FORM receipt  
    PROPERTIES(l) READONLY put SHOWIF putAway(type(r)) AND ready(r)
  
    OBJECTS p = ReceiptPutAway
    PROPERTIES(p) nameLocation, quantity
    PROPERTIES(p) READONLYIF (NOT ready(r)) OR putAway(r) DELETE
    FILTERS receiptLine(p) = l
;

FORM putAwayLoc 'Места хранения'
    OBJECTS loc = Location
    
    OBJECTS l = Location
    PROPERTIES(l) READONLY name, nameParent, canonicalName
    FILTERS level(l,loc)
;

DESIGN putAwayLoc {
    PROPERTY(formOk()) { changeKey = 'ENTER'; }
}

newPutAway 'Добавить размещение' (ReceiptLine ln) {
    DIALOG putAwayLoc OBJECTS l INPUT, loc = location(receipt(ln)) DO {
        DIALOG dialogQuantity OBJECTS q = done(ln) (-) put(ln) INPUT DO {
            NEW p = ReceiptPutAway {
                receiptLine(p) <- ln;
                location(p) <- l;
                quantity(p) <- q;
                SEEK receipt.p = p;
            } 
        }
    }
} CHANGEKEY 'ADD';

EXTEND FORM receipt
    PROPERTIES READONLYIF (NOT ready(r)) OR putAway(r) newPutAway(l) DRAW p TOOLBAR SHOWIF putAway(type(r)) AND ready(r)
;

DESIGN receipt {
    OBJECTS {
        lines {
            NEW putaway {
                MOVE BOX(p) {showIf = (putAway(type(r)) AND ready(r));}
            }
        }
    }
}

CLASS ReceiptPutAwayLedger : InvLedger;
putAwayLedger = AGGR ReceiptPutAwayLedger WHERE quantity(ReceiptPutAway line);

done(ReceiptPutAwayLedger l) += done(receipt(receiptLine(line(l))));
dateTime(ReceiptPutAwayLedger l) += executionDate(receipt(receiptLine(line(l))));
toLocation(ReceiptPutAwayLedger l) += location(line(l));

product(ReceiptPutAwayLedger l) += product(receiptLine(line(l)));
quantity(ReceiptPutAwayLedger l) += quantity(line(l));

description(ReceiptPutAwayLedger l) += 'Поступление' IF l IS ReceiptPutAwayLedger;
partner(ReceiptPutAwayLedger l) += partner(receipt(receiptLine(line(l))));

series(ReceiptPutAwayLedger l) += series(receipt(receiptLine(line(l))));
number(ReceiptPutAwayLedger l) += number(receipt(receiptLine(line(l))));