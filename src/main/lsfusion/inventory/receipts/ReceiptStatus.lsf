MODULE ReceiptStatus;

REQUIRE Inventory, Receipt;

NAMESPACE Inventory;

// Статусы

CLASS Status 'Статус прихода' {
    draft 'Черновик'
}

name 'Имя' (Status o) = staticCaption(o) IF o IS Status CHARWIDTH 15;

FORM dialogStatuses 'Статус прихода'
    OBJECTS o = Status
    PROPERTIES(o) READONLY name
    
    LIST Status OBJECT o
;

status 'Статус' = ABSTRACT CASE Status (Receipt);
nameStatus 'Статус' (Receipt r) = name(status(r));

EXTEND FORM receipt
    PROPERTIES(r) nameStatus
;

EXTEND FORM receipts    
    FILTERGROUP status
        FILTER 'Все статусы' status(r) 'F10' DEFAULT 
        FILTER 'Черновик' status(r) = Status.draft 'F9'
;

EXTEND FORM receipts
    PROPERTIES(r) nameStatus
;

status(Receipt r) += WHEN r IS Receipt THEN Status.draft;

DESIGN receipt {
    OBJECTS {        
        NEW statusPane FIRST {
            caption = 'Статусы';
            type = CONTAINERH;
            alignment = STRETCH;                          
            NEW statusActions {
                type = CONTAINERH;                    
                flex = 1;                
            }
            NEW status {
                type = CONTAINERH;                
                MOVE PROPERTY(nameStatus(r)); 
            }
        }                
    }
}