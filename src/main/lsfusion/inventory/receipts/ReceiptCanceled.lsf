MODULE ReceiptCanceled;

REQUIRE ReceiptDone;

NAMESPACE Inventory;
    
EXTEND CLASS ReceiptStatus {
    canceled 'Отменен'
}

canceled 'Отменен' = DATA BOOLEAN (Receipt);
active 'Активен' (Receipt r) = done(r) AND NOT canceled(r);

cancel 'Отменить' (Receipt r) {
    APPLY; 
    IF canceled() THEN RETURN;
           
    NEWSESSION {
        canceled(r) <- TRUE;
        APPLY;
    }
}

status(Receipt r) += WHEN canceled(r) THEN ReceiptStatus.canceled;
colorStatus(Receipt r) += WHEN status(r) == ReceiptStatus.canceled THEN RGB(181,195,247);

EXTEND FORM receipt
    PROPERTIES(r) cancel SHOWIF NOT (status(r) = ReceiptStatus.draft OR status(r) = ReceiptStatus.canceled), canceled 
;

EXTEND FORM receipts    
    EXTEND FILTERGROUP status
        FILTER 'Отменен' status(r) = ReceiptStatus.canceled 'F5'    
;

DESIGN receipt {
    secondaryActions {
        MOVE PROPERTY(cancel(r));
    }             
    status {
        MOVE PROPERTY(canceled(r));                               
    }    
}

