MODULE ReceiptDone;

REQUIRE ReceiptReady;

NAMESPACE Inventory;

// Принят
done 'Принят' = DATA BOOLEAN (Receipt);

executionDate 'Дата поступления' = DATA DATETIME (Receipt);
WHEN SET(done(Receipt r)) AND NOT CHANGED(executionDate(r)) DO executionDate(r) <- currentDateTime(); 
executionDate 'Дата поступления' (ReceiptLine l) = executionDate(receipt(l));

done 'Принятое количество' = DATA NUMERIC[16,3] (ReceiptLine);

CONSTRAINT done(ReceiptLine l) < 0 OR done(l) > initialDemand(l) 
    MESSAGE 'Принятое количество должно быть от 0 до исходного';

CONSTRAINT immediateTransfer(Receipt r) AND (NOT done(ReceiptLine l) AND receipt(l) = r) MESSAGE 'Необходимо указать принятые кол-ва';

validate 'Принят' (Receipt r) {        
    APPLY; // сохраняем изменения
    IF canceled() THEN RETURN; // если сработало ограничение, то выходим
        
    NEWSESSION {
        IF NOT immediateTransfer(r) THEN {
            IF GROUP SUM done(ReceiptLine l) IF receipt(l) = r THEN {
                IF GROUP SUM 1 IF receipt(ReceiptLine l) = r AND NOT done(l) = initialDemand(l) THEN {
                    ASK 'Принято не все количество. Создать дополнительную приемку ?' DO {
                        NEW rn = Receipt {
                            type(rn) <- type(r);
                            partner(rn) <- partner(r);
                            location(rn) <- location(r);
                            scheduledDate(rn) <- scheduledDate(r);
                            notes(rn) <- notes(r);
                            FOR receipt(ReceiptLine l) = r AND (q = initialDemand(l) (-) done(l)) DO NEW ln = ReceiptLine {
                                receipt(ln) <- rn;
                                product(ln) <- product(l);
                                initialDemand(ln) <- q;
                            }
                        }
                        initialDemand(ReceiptLine l) <- done(l) WHERE receipt(l) = r;
                        DELETE ReceiptLine l WHERE receipt(l) = r AND NOT done(l);
                    }
                }
            } ELSE {
                ASK 'Не заполены принятые количества. Проставить ожидаемыми ?' DO {
                    done(ReceiptLine l) <- initialDemand(l) WHERE receipt(l) = r;
                } ELSE RETURN;
            }
        } ELSE ready(r) <- TRUE;
        
        done(r) <- TRUE;
        APPLY;
    }
}

EXTEND CLASS ReceiptStatus {
    done 'Принят'
}

readonlyHeader (Receipt r) += WHEN status(r) = ReceiptStatus.done THEN TRUE;

EXTEND FORM receipt
    PROPERTIES(r) validate SHOWIF status(r) = ReceiptStatus.ready OR (immediateTransfer(r) AND NOT done(r) AND countLines(r)), done READONLY
    PROPERTIES(r) executionDate READONLY AFTER scheduledDate(r) SHOWIF done(r)
    
    PROPERTIES(l) done READONLYIF NOT (status(r) = ReceiptStatus.ready OR (immediateTransfer(r) AND status(r) = ReceiptStatus.draft)) BACKGROUND 
                       IF status(r) = ReceiptStatus.ready THEN RGB(198,230,247)
;
DESIGN receipt {
    PROPERTY(validate(r)) { background = RGB(219,160,232);}//#DBA0E8
}

EXTEND FORM receipts 
    PROPERTIES(r) READONLY executionDate AFTER scheduledDate(r)

    EXTEND FILTERGROUP status
        FILTER 'Принят' status(r) = ReceiptStatus.done 'F7'
;

status(Receipt r) += WHEN done(r) THEN ReceiptStatus.done;
colorStatus(Receipt r) += WHEN status(r) == ReceiptStatus.done THEN RGB(255,255,255);



DESIGN receipt {                               
    statusActions { 
        primary {
            MOVE PROPERTY(validate(r)) { fontStyle = 'bold'; }
        }                 
    }
    status {
        MOVE PROPERTY(done(r));                              
    } 
    box1 {
        MOVE PROPERTY(executionDate(r));
    }                  
}

EXTEND FORM partner
    OBJECTS r = Receipt
    PROPERTIES(r) READONLY nameStatus, scheduledDate, executionDate, series, number, nameType, nameLocation, namePartner, notes
    FILTERS partner(r) = p
;

DESIGN partner {
    tabs {
        NEW receipts {
            caption = 'Поступления';
            MOVE BOX(r);
        }
    }
}
