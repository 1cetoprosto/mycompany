MODULE InvLedger;

REQUIRE InventorySettings, Category, Product, Location, Partner, Time;

NAMESPACE Inventory;

CLASS ABSTRACT InvLedger 'Регистр изменения остатка';

done 'Проведен' = ABSTRACT BOOLEAN (InvLedger);
dateTime 'Время' = ABSTRACT DATETIME (InvLedger) MATERIALIZED;

fromLocation 'Место хранения (откуда)' = ABSTRACT Location (InvLedger) MATERIALIZED;
nameFromLocation 'Место хранения (откуда)' (InvLedger l) = name(fromLocation(l));

toLocation 'Место хранения (куда)' = ABSTRACT Location (InvLedger) MATERIALIZED;
nameToLocation 'Место хранения (куда)' (InvLedger l) = name(toLocation(l));

product 'Товар' = ABSTRACT Product (InvLedger) MATERIALIZED;
nameProduct 'Товар' (InvLedger l) = name(product(l));

series 'Серия' = ABSTRACT STRING[2] (InvLedger);
number 'Номер' = ABSTRACT STRING[28] (InvLedger);

seriesNumber 'Серия/Номер' (InvLedger l) = CONCAT '/', series(l), number(l);

INDEX product(InvLedger l);
INDEX fromLocation(InvLedger l), product(l);
INDEX toLocation(InvLedger l), product(l);

quantity 'Кол-во' = ABSTRACT NUMERIC[16,3] (InvLedger) MATERIALIZED;
quantityStr 'Кол-во' = ABSTRACT STRING (InvLedger) MATERIALIZED;

partner 'Партнер' = ABSTRACT Partner(InvLedger) MATERIALIZED;
namePartner 'Партнер' (InvLedger l) = name(partner(l));
description 'Описание' = ABSTRACT ISTRING[100] (InvLedger);
  
inQuantity 'Кол-во поступившее' (Location ll, Product p) = GROUP SUM quantity(InvLedger l) 
                                                    IF done(l) AND toLocation(l) = ll AND product(l) = p MATERIALIZED;
                                                    
inQuantity 'Кол-во поступившее'(Location ll, Product p, DATETIME dt) = inQuantity(ll, p) (-)
    GROUP SUM quantity(InvLedger l)  IF done(l) AND toLocation(l) = ll AND product(l) = p AND dateTime(l) > dt;
                                   
outQuantity 'Кол-во отправленное' (Location ll, Product p) = GROUP SUM quantity(InvLedger l) 
                                                    IF done(l) AND fromLocation(l) = ll AND product(l) = p MATERIALIZED;

outQuantity 'Кол-во отправленное' (Location ll, Product p, DATETIME dt) = outQuantity(ll, p) (-)
    GROUP SUM quantity(InvLedger l) IF done(l) AND  fromLocation(l) = ll AND product(l) = p AND dateTime(l) > dt;

onHand 'Текущий остаток' (Location ll, Product p) = inQuantity(ll, p) (-) outQuantity(ll, p) MATERIALIZED;

onHand 'Текущий остаток' (Location ll, Product p, DATETIME dt) = 
                                                            inQuantity(ll, p, dt) (-) outQuantity(ll, p, dt) IF dt;

onHandRec 'Текущий остаток (с учетом вложенности)' (Location l, Product p) = 
    GROUP SUM onHand(Location child, p) IF level(child, l) MATERIALIZED;

positiveOnHand 'Только положительное кол-во товаров' = DATA BOOLEAN (Location) CHARWIDTH 15;
            
recPositiveOnHand 'Только положительное кол-во товаров' (Location child) = 
    GROUP LAST positiveOnHand(Location parent) ORDER DESC level(child, parent) 
    WHERE positiveOnHand(parent) MATERIALIZED CHARWIDTH 15;
                                                      
CONSTRAINT SET(onHand(Location ll, Product p) < 0) AND recPositiveOnHand(ll) 
            MESSAGE 'Текущий остаток не может быть отрицательным';
            
EXTEND FORM location
    PROPERTIES(l) positiveOnHand
;

EXTEND FORM locations
    PROPERTIES(l) READONLY recPositiveOnHand
;

dateReceived 'Дата последнего прихода' (Location l, Product p) = 
    GROUP LAST dateTime(InvLedger il) ORDER dateTime(il) WHERE toLocation(il) = l AND product(il) = p;
prevDateReceived (Location l, Product p) = PREV(dateReceived(l, p));
    
dateShipped 'Дата последней отгрузки' (Location l, Product p) = 
    GROUP LAST dateTime(InvLedger il) ORDER dateTime(il) WHERE fromLocation(il) = l AND product(il) = p;
    
EXTEND FORM items
    OBJECTS inv = InvLedger
    PROPERTIES(inv) READONLY dateTime, nameProduct, quantity, nameFromLocation, nameToLocation, description
    FILTERS product(inv) = i
;

DESIGN items {
    operations {
        NEW invledger {
            caption = 'Движения';
            MOVE BOX(inv);
        }
    }
}
