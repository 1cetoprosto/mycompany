MODULE ResLedger;

REQUIRE InvLedger;

NAMESPACE Inventory;

CLASS ResLedger;

dateTime 'Время' = ABSTRACT DATETIME (ResLedger) MATERIALIZED;

location 'Место хранения (откуда)' = ABSTRACT Location (ResLedger) MATERIALIZED;
nameLocation 'Место хранения (откуда)' (ResLedger r) = name(location(r));

product 'Товар' = ABSTRACT Product (ResLedger) MATERIALIZED;
nameProduct 'Товар' (ResLedger r) = name(product(r));

series 'Серия' = ABSTRACT STRING[2] (ResLedger);
number 'Номер' = ABSTRACT STRING[28] (ResLedger);

seriesNumber 'Серия/Номер' (ResLedger l) = CONCAT '/', series(l), number(l) CHARWIDTH 10;

INDEX product(ResLedger l);
INDEX location(ResLedger l), product(l);

reserved 'Зарезервированое кол-во' = ABSTRACT NUMERIC[16,3] (ResLedger) MATERIALIZED;

partner 'Партнер' = ABSTRACT Partner (ResLedger) MATERIALIZED;
namePartner 'Партнер' (ResLedger l) = name(partner(l));
description 'Описание' = ABSTRACT ISTRING[100] (ResLedger);

reserved 'Зарезервировано' (Location l, Product p) = 
    GROUP SUM reserved(ResLedger sl) IF location(sl) = l 
                                        AND product(sl) = p MATERIALIZED; 

reservedRec 'Зарезервировано (с учетом вложенности)' (Location l, Product p) = 
    GROUP SUM reserved(Location child, p) IF level(child, l) MATERIALIZED;
                                        
available 'Доступный остаток' (Location ll, Product p) = onHand(ll,p) (-) reserved(ll,p);
prevAvailable 'Доступный остаток (без учета изменений)' (Location ll, Product p) = PREV(available(ll, p)); 

availableRec 'Доступный остаток (с учетом вложенности)' (Location ll, Product p) = onHandRec(ll,p) (-) reservedRec(ll,p);
prevAvailableRec 'Доступный остаток (с учетом вложенности без учета изменений)' (Location ll, Product p) = PREV(availableRec(ll, p)); 


CONSTRAINT SET(available (Location ll, Product p) < 0) AND recPositiveOnHand(ll) 
            MESSAGE 'Доступный остаток не может быть отрицательным';
            
EXTEND FORM items
    OBJECTS res = ResLedger
    PROPERTIES(res) READONLY dateTime, nameProduct, reserved, nameLocation, description
    FILTERS product(res) = i
;

DESIGN items {
    operations {
        NEW resledger {
            caption = 'Резервирование';
            MOVE BOX(res);
        }
    }
}