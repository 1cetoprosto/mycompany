MODULE InventoryReport;

REQUIRE InvLedger, ResLedger;// ShipmentReady;

NAMESPACE Inventory;

selectedDateTime 'Выбрать дату' = DATA LOCAL DATETIME () PREREAD;
onHandDateTime 'Остаток на дату' (Location l, Product p) = onHand(l, p, selectedDateTime());

FORM inventoryReport 'Текущие остатки'
    OBJECTS invL = InvLedger PANEL 
    PROPERTIES selectedDateTime()
    
    TREE locations lt = Location PARENT parent
    PROPERTIES READONLY name(lt)
    ORDER name(lt)
    
    TREE categories c = Category PARENT parent
    PROPERTIES READONLY name(c)
    ORDER name(c)
    
    OBJECTS lp = (l = Location, p = Product)
    PROPERTIES READONLY 'Склад' = canonicalName(l), 'Товар' = name(p) 
    PROPERTIES(l,p) reserved, available, onHand SHOWIF NOT selectedDateTime()
    PROPERTIES onHandDateTime(l, p) SHOWIF selectedDateTime()
    FILTERS level(l, lt),
            level(category(p), c),
            IF selectedDateTime() THEN onHandDateTime(l,p) ELSE onHand(l, p) 
            
    OBJECTS il = InvLedger
    PROPERTIES(il) READONLY dateTime, series, number, description, namePartner, nameFromLocation, nameToLocation, 
                            quantity, EDIT
    FILTERS done(il),
            product(il) = p,
            fromLocation(il) = l OR toLocation(il) = l,
            dateTime(il) <= selectedDateTime() OR NOT selectedDateTime()
;

DESIGN inventoryReport {
   OBJECTS {
       NEW pane {
           type = SPLITH;
           fill = 1;
           NEW pane1 {
               fill = 1;
               type = SPLITV;
               MOVE BOX(TREE locations){caption = 'Места хранения';};
               MOVE BOX(TREE categories){caption = 'Категории';};
           }
           NEW pane2 {
               fill = 2;
               type = SPLITV;
               MOVE BOX(lp);
               MOVE BOX(il);
           }               
       }
   }
}

NAVIGATOR {
    reporting {
        NEW inventoryReport;
    }
}
