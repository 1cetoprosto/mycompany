MODULE Transfer;

REQUIRE Inventory, Location, Product, InvLedger;

NAMESPACE Inventory;

CLASS Transfer 'Перемещение со склада на склад';
done 'Проведен' = DATA BOOLEAN (Transfer) CHARWIDTH 10;
dateTime 'Дата/время' = DATA DATETIME (Transfer);

fromLocation 'Место хранения (откуда)' = DATA Location (Transfer);
nameFromLocation 'Место хранения (откуда)' (Transfer t) = name(fromLocation(t));
toLocation 'Место хранения (куда)' = DATA Location (Transfer);
nameToLocation 'Место хранения (куда)' (Transfer t) = name(toLocation(t));

CLASS TransferLine 'Строка отгрузки со склада';
transfer 'Поступление' = DATA Transfer (TransferLine) NONULL DELETE;
product 'Товар' = DATA Product (TransferLine);
nameProduct 'Продукт' (TransferLine l) = name(product(l));
quantity 'Кол-во' = DATA INTEGER (TransferLine);

EXTEND CLASS TransferLine : InvLedger;

dateTime(TransferLine l) += dateTime(transfer(l));

fromLocation(TransferLine l) += fromLocation(transfer(l));
toLocation(TransferLine l) += toLocation(transfer(l));

product(TransferLine l) += product(l);
quantity(TransferLine l) += quantity(l);

description(TransferLine l) += 'Перемещение' IF l IS TransferLine;

FORM transfer 'Перемещение'
    OBJECTS t = Transfer PANEL
    PROPERTIES(t) dateTime, nameFromLocation, nameToLocation, done
  
    OBJECTS l = TransferLine
    PROPERTIES(l) nameProduct, quantity, NEW, DELETE
    FILTERS transfer(l) = t
 
    EDIT Transfer OBJECT t
;
      
FORM transfers 'Перемещения'
    OBJECTS t = Transfer
    PROPERTIES(t) READONLY dateTime, nameFromLocation, nameToLocation, done
    PROPERTIES(t) NEWSESSION NEW, EDIT, DELETE
;

NAVIGATOR {
    inventory {
        NEW transfers;
    }
}