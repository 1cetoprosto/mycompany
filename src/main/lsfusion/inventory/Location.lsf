MODULE Location;

REQUIRE Inventory, Partner;

NAMESPACE Inventory;

CLASS Location 'Место хранения';
id 'Код' = DATA STRING[50] (Location) IN id;
location (STRING[50] id) = GROUP AGGR Location l BY id(l);

name 'Название' = DATA ISTRING[30] (Location) IN id;
parent 'Родитель' = DATA Location (Location);
idParent 'Код родителя' (Location l) = id(parent(l));
nameParent 'Родительское место хранения' (Location l) = name(parent(l));
owner 'Владелец' = DATA Partner (Location); 
ownerName 'Владелец' (Location l)= name(owner(l)); 
@addressesCalc(location,l);

level 'Уровень' (Location child, Location parent) =
   RECURSION 1l IF child IS Location AND parent == child
        STEP 2l IF parent == parent($parent) MATERIALIZED;

hierarchyAddress (Location l) =  GROUP LAST address(Location parent) ORDER DESC level(l,parent);
hierarchyCity (Location l) =  GROUP LAST city(Location parent) ORDER DESC level(l,parent);
hierarchyState  (Location l) =  GROUP LAST state(Location parent) ORDER DESC level(l,parent);
hierarchyZip  (Location l) =  GROUP LAST zip(Location parent) ORDER DESC level(l,parent);
  
concatAddress (Location l, STRING sep) = (OVERRIDE hierarchyAddress(l)+sep,'') + 
            (OVERRIDE hierarchyCity(l)+sep,'') + (OVERRIDE hierarchyState(l)+sep,'') + (OVERRIDE hierarchyZip(l),'');
  
fullAddress 'Адрес' (Location l) = concatAddress(l,', ');    
        
canonicalName 'Каноническое имя' (Location l) =
   GROUP CONCAT name(Location parent), ' / ' ORDER DESC level(l, parent) CHARWIDTH 50 IN id;
   
FORM location 'Место хранения'
    OBJECTS l = Location PANEL
    PROPERTIES(l) id, name, nameParent, ownerName 
     
    EDIT Location OBJECT l  
;

FORM locations 'Места хранения'
    OBJECTS l = Location
    PROPERTIES(l) READONLY id, name, nameParent, canonicalName, ownerName
    PROPERTIES(l) NEWSESSION NEW, EDIT, DELETE
    
    LIST Location OBJECT l
;

@addressesForm(location,l);


newLocation 'Место хранения' (Location ll) {
    NEWSESSION {
        NEW loc = Location {
            parent(loc) <- ll;
            
            SHOW location OBJECTS l = loc;  
        }
    }
} IMAGE 'add.png';

NAVIGATOR {
    settings {
        NEW locations;
    }
}
