MODULE InventoryValuation;

REQUIRE CostLedger;

NAMESPACE Inventory;

FORM inventoryValuation 'Себестоимость остатков'
    OBJECTS lp = (l = Location, p = Product)
    PROPERTIES READONLY 'Склад' = canonicalName(l), 'Товар' = name(p), costQuantity(l, p), costPrice(l, p), costSum(l, p)
    PROPERTIES recalculateCost() DRAW lp TOOLBAR
    FILTERS costQuantity(l, p)
        
    OBJECTS ol = OutCostLedger
    PROPERTIES(ol) READONLY dateTime, nameLocation, nameProduct, quantity, costed, price, sum
    PROPERTIES(ol) EDIT
    FILTERS location(ol) = l, product(ol) = p
    
    OBJECTS il = InCostLedger
    PROPERTIES(il) READONLY dateTime, nameLocation, nameProduct, quantity, costed, price, sum
    PROPERTIES(il) EDIT
    PROPERTIES READONLY quantity(ol, il)
    FILTERS location(il) = l, product(il) = p
    
    OBJECTS locProd = (location = Location, product = Product)
    PROPERTIES READONLY 'Склад' = canonicalName(location), 'Товар' = name(product), 
        level1(product), level2(product), level3(product), level4(product), 'Группа полная' = canonicalNameCategory(product),
        costQuantity(location, product), costPrice(location, product), costSum(location, product)
    FILTERS costQuantity(location, product)
    
    FILTERGROUP filterIn
        FILTER 'По расходу' quantity(ol, il)
;

DESIGN inventoryValuation {
    OBJECTS {
        NEW header {
            fill = 1;
            type = TABBED;
            NEW valuation {
                caption = 'Себестоимость остатков';
                MOVE BOX (lp);
                NEW details {
                    fill = 1;
                    type = SPLITH;
                    MOVE BOX(ol);
                    MOVE BOX(il);
                }
            }
            NEW report {
                caption = 'Отчет';
                MOVE BOX (locProd);
            }
        }
    }
}

NAVIGATOR {
    reporting {
        NEW inventoryValuation;
    }
}
