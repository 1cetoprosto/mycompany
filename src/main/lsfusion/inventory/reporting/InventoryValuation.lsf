MODULE InventoryValuation;

REQUIRE CostLedger;

NAMESPACE Inventory;

FORM inventoryValuation 'Себестоимость остатков'
    OBJECTS lp = (l = Location, p = Product)
    PROPERTIES READONLY 'Склад' = canonicalName(l), 'Товар' = name(p), costQuantity(l, p), costPrice(l, p), costSum(l, p)
    PROPERTIES recalculateCost() DRAW lp TOOLBAR
    FILTERS costQuantity(l, p)
        
    OBJECTS ol = OutCostLedger
    PROPERTIES(ol) READONLY dateTime, nameLocation, nameProduct, quantity, costed, price, sum
    FILTERS location(ol) = l, product(ol) = p
    
    OBJECTS il = InCostLedger
    PROPERTIES(il) READONLY dateTime, nameLocation, nameProduct, quantity, costed, price, sum
    PROPERTIES READONLY quantity(ol, il)
    FILTERS location(il) = l, product(il) = p
    
    FILTERGROUP filterIn
        FILTER 'По расходу' quantity(ol, il) DEFAULT
;

DESIGN inventoryValuation {
    OBJECTS {
        NEW details {
            fill = 1;
            type = SPLITH;
            MOVE BOX(ol);
            MOVE BOX(il);
        }
    }
}

NAVIGATOR {
    reporting {
        NEW inventoryValuation;
    }
}
