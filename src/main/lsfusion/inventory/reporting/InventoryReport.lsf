MODULE InventoryReport;

REQUIRE InvLedger, ResLedger, ItemImage, ItemAttribute;

NAMESPACE Inventory;

selectedDateTime 'Выбрать дату' = DATA LOCAL DATETIME () PREREAD;
singleLocation 'По одному складу' = DATA LOCAL BOOLEAN () PREREAD;
onHandDateTime 'Остаток на дату' (Location l, Product p) = onHand(l, p, selectedDateTime());
                                
FORM inventoryReport 'Текущие остатки'
    OBJECTS invL = InvLedger PANEL 
    PROPERTIES selectedDateTime(), singleLocation()
    
    TREE locations lt = Location PARENT parent(lt)
    PROPERTIES READONLY name(lt)
    ORDER name(lt)
    
    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    ORDER name(c)
    
    OBJECTS lp = (l = Location, p = Product)
    PROPERTIES READONLY 'Склад' = canonicalName(l) SHOWIF NOT singleLocation(), 'Товар' = name(p)
    PROPERTIES(l,p) READONLY reserved, available, onHand SHOWIF NOT selectedDateTime(), onHandRec SHOWIF NOT selectedDateTime()
    PROPERTIES(l,p) READONLY onHandDateTime SHOWIF selectedDateTime(), dateReceived, dateShipped
    FILTERS IF singleLocation() THEN l = lt ELSE isChild(l, lt),
            level(category(p), c),
            IF selectedDateTime() THEN onHandDateTime(l, p) ELSE onHand(l, p)
                
    OBJECTS il = InvLedger
    PROPERTIES(il) READONLY dateTime, series, number, description, namePartner, nameFromLocation, nameToLocation, 
                            quantity
    FILTERS done(il),
            product(il) = p,
            fromLocation(il) = l OR toLocation(il) = l,
            dateTime(il) <= selectedDateTime() OR NOT selectedDateTime()
            
    OBJECTS a = ItemAttribute
    OBJECTS locProd = (location = Location, product = Product)
    PROPERTIES READONLY 'Склад' = canonicalName(location), 'Товар' = name(product), value(product, a) COLUMNS (a) HEADER name(a),
                    level1(product), level2(product), level3(product), level4(product), 'Группа полная' = canonicalNameCategory(product)
    PROPERTIES(location,product) reserved, available, onHandDateTime, dateReceived, dateShipped
    FILTERS IF selectedDateTime() THEN onHandDateTime(location, product) ELSE onHand(location, product)
    
    OBJECTS pr = Product PANEL 
    PROPERTIES(pr) READONLY image
    FILTERS pr = p
;

DESIGN inventoryReport {
    OBJECTS {
        NEW header {
            fill = 1;
            NEW params {
                type = CONTAINERH;
                MOVE PROPERTY (selectedDateTime());
                MOVE PROPERTY (singleLocation());
            }
            NEW invReport {
                fill = 1;
                type = TABBED;
                NEW pane {
                    caption = 'Текущие остатки';
                    type = SPLITH;
                    fill = 1;
                    NEW pane1 {
                        fill = 1;
                        type = SPLITV;
                        MOVE BOX(TREE locations){caption = 'Места хранения';};
                        MOVE BOX(TREE categories){caption = 'Категории';};
                    }
                    NEW pane2 {
                        fill = 2;
                        type = SPLITV;
                        MOVE BOX(lp);
                        NEW tabs {
                            type = TABBED;
                            NEW ledger {
                                caption = 'Регистр изменения остатка';
                                MOVE BOX(il);
                            }
                            NEW image {
                                caption = 'Изображение';
                                MOVE PROPERTY(image(pr)) { caption = ''; fill = 1; } 
                            }
                        }
                    }               
                }
                NEW report {
                    fill = 1;
                    caption = 'Отчет';
                    MOVE BOX (locProd);
                }
            }
        }
    }
}

NAVIGATOR {
    reporting {
        NEW inventoryReport;
    }
}
