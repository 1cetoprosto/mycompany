MODULE Scrap;

REQUIRE InventorySettings, Product, Location, InvLedger;

NAMESPACE Inventory;

CLASS Scrap 'Списание';

done 'Проведен' = DATA BOOLEAN (Scrap) CHARWIDTH 10;
date 'Дата' = DATA DATETIME (Scrap) NONULL;

locationFrom 'Место хранения' = DATA Location (Scrap);
nameLocationFrom 'Место хранения' (Scrap s) = name(locationFrom(s));

CLASS ScrapLine 'Строка списания';

scrap 'Списание' = DATA Scrap (ScrapLine) NONULL DELETE;

product 'Продукт' = DATA Product(ScrapLine);
nameProduct 'Продукт' (ScrapLine l) = name(product(l));

quantity 'Кол-во' = DATA NUMERIC[16,3] (ScrapLine);

readonly = ABSTRACT CASE BOOLEAN (Scrap);

FORM scrap 'Списание'
    OBJECTS s = Scrap PANEL 
    PROPERTIES(s) READONLYIF readonly(s) date, nameLocationFrom
    
    OBJECTS l = ScrapLine
    PROPERTIES(l) READONLYIF readonly(s) nameProduct, quantity, NEW, DELETE
    FILTERS scrap(l) = s
    
    EDIT Scrap OBJECT s
;

FORM scraps 'Списания'
    OBJECTS s = Scrap 
    PROPERTIES(s)  READONLY date, nameLocationFrom
    PROPERTIES (s) NEWSESSION NEW, EDIT, DELETE READONLYIF readonly(s)
    
    LIST Scrap OBJECT s
;

NAVIGATOR {
    operations {
        NEW scraps;
    }
}

copyScrap 'Копировать' (Scrap s) {
    NEWSESSION {
        NEW ns = Scrap {
            date(ns) <- date(s);
            locationFrom(ns) <- locationFrom(s);
            FOR scrap(ScrapLine l) = s INLINE NEW nl = ScrapLine DO {
                scrap(nl) <- ns;
                product(nl) <- product(l);
                quantity(nl) <- quantity(l);
            }
            SHOW scrap OBJECTS s = ns DOCKED;
        }
    }
}

EXTEND FORM scrap
    PROPERTIES(s) copyScrap
;

DESIGN scrap {
    OBJECTS {
        NEW statusActions {
            alignment = STRETCH;
            type = CONTAINERH;
            NEW actions {
                flex = 1;
                type = CONTAINERH;
                MOVE PROPERTY(copyScrap(s));
            }
        }
        NEW details {
            fill = 1;
            MOVE BOX(s);
            MOVE BOX(l);
        }
    }
}


EXTEND CLASS ScrapLine  : InvLedger;

done(ScrapLine l) += done(scrap(l));
dateTime(ScrapLine l) += date(scrap(l));
fromLocation(ScrapLine l) += locationFrom(scrap(l));

product(ScrapLine l) += product(l);
quantity(ScrapLine l) += quantity(l);
description(ScrapLine l) += 'Списание' IF l IS ScrapLine;

