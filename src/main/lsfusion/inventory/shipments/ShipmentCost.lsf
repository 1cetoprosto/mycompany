MODULE ShipmentCost;

REQUIRE ShipmentCanceled, CostLedger, CostLocation;

NAMESPACE Inventory;

// расходная проводка
CLASS ShipmentOutCostLedger : OutCostLedger;
outCostLedger = AGGR ShipmentOutCostLedger WHERE done(ShipmentLine line) AND active(shipment(line))
                                             AND NOT costLocation(location(shipment(line))) = costLocation(toLocation(shipment(line)));

dateTime(ShipmentOutCostLedger l) += executionDate(shipment(line(l)));
location(ShipmentOutCostLedger l) += costLocation(location(shipment(line(l))));

product(ShipmentOutCostLedger l) += product(line(l));
quantity(ShipmentOutCostLedger l) += done(line(l));

edit(ShipmentOutCostLedger l) + { edit(line(l)); }

// приходная проводка
CLASS ShipmentInCostLedger : InCostLedger;
inCostLedger = AGGR ShipmentInCostLedger WHERE done(ShipmentLine line) AND active(shipment(line)) 
                            AND toLocation(shipment(line))
                            AND NOT costLocation(location(shipment(line))) = costLocation(toLocation(shipment(line))); 

dateTime(ShipmentInCostLedger l) += executionDate(shipment(line(l)));
number(ShipmentInCostLedger l) += number(shipment(line(l)));

location(ShipmentInCostLedger l) += costLocation(toLocation(shipment(line(l))));

product(ShipmentInCostLedger l) += product(line(l));
quantity(ShipmentInCostLedger l) += done(line(l));

calcSum (ShipmentInCostLedger l) += sum(outCostLedger(line(l)));

edit(ShipmentInCostLedger l) + { edit(line(l)); }
