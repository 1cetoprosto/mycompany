MODULE ShipmentCanceled;

REQUIRE ShipmentDone;

NAMESPACE Inventory;
    
EXTEND CLASS ShipmentStatus {
    canceled 'Отменен'
}

canceled 'Отменен' = DATA BOOLEAN (Shipment);

cancel 'Отменить' (Shipment s) {
    APPLY; 
       IF canceled() THEN RETURN;
           
       NEWSESSION {
           canceled(s) <- TRUE;
           APPLY;
       }
}

status(Shipment s) += WHEN canceled(s) THEN ShipmentStatus.canceled;
colorStatus(Shipment s) += WHEN status(s) == ShipmentStatus.canceled THEN RGB(181,195,247);

readonlyHeader (Shipment s) += WHEN status(s) = ShipmentStatus.canceled THEN TRUE;

EXTEND FORM shipment
    PROPERTIES(s) cancel SHOWIF NOT (status(s) = ShipmentStatus.draft OR status(s) = ShipmentStatus.canceled), canceled 
;

EXTEND FORM shipments    
    EXTEND FILTERGROUP status
        FILTER 'Отменен' status(s) = ShipmentStatus.canceled 'F5'    
;

DESIGN shipment {
    secondaryActions {
        MOVE PROPERTY(cancel(s));
    }             
    status {
        MOVE PROPERTY(canceled(s));                               
    }    
}
