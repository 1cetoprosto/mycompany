MODULE ShipmentWaiting;

REQUIRE Shipment, InvLedger;

NAMESPACE Inventory;

waiting 'В ожидании' = DATA BOOLEAN (Shipment);

markAsToDo 'В работу' (Shipment s) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        waiting(s) <- TRUE;
        APPLY;
    }
}

EXTEND CLASS ShipmentStatus {
    waiting 'В ожидании'
}

EXTEND FORM shipment
    PROPERTIES(s) markAsToDo SHOWIF status(s) = ShipmentStatus.draft AND NOT immediateTransfer(s), waiting 
;

EXTEND FORM shipments    
    EXTEND FILTERGROUP status
        FILTER 'В ожидании' status(s) = ShipmentStatus.waiting 'F9'    
;

status(Shipment s) += WHEN waiting(s) THEN ShipmentStatus.waiting;
colorStatus(Shipment s) += WHEN status(s) == ShipmentStatus.waiting THEN RGB(250,150,157);

DESIGN shipment {
    primaryActions {
        MOVE PROPERTY(markAsToDo(s)) { fontStyle = 'bold'; }
    }                
    status {
        MOVE PROPERTY(waiting(s));                               
    }
}