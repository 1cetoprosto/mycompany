MODULE ShipmentWaiting;

REQUIRE Inventory, ShipmentStatus, InvLedger;

NAMESPACE Inventory;

done 'Отгруженое кол-во' = DATA NUMERIC[16,3] (ShipmentLine);

waiting 'В ожидании' = DATA BOOLEAN (Shipment);

markAsToDo 'В работу' (Shipment s) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        waiting(s) <- TRUE;
        APPLY;
    }
}

EXTEND CLASS ShipmentStatus {
    waiting 'В ожидании'
}

EXTEND FORM shipment
    PROPERTIES(s) markAsToDo SHOWIF status(s) = ShipmentStatus.draft, waiting READONLY 
;

EXTEND FORM shipments    
    EXTEND FILTERGROUP status
        FILTER 'В ожидании' status(s) = ShipmentStatus.waiting 'F9'    
;

status(Shipment s) += WHEN waiting(s) THEN ShipmentStatus.waiting;
colorStatus(Shipment s) += WHEN status(s) == ShipmentStatus.waiting THEN RGB(250,150,157);
readonly (ShipmentLine l) += WHEN NOT 
                (status(shipment(l)) = ShipmentStatus.waiting  OR status(shipment(l)) = ShipmentStatus.draft) THEN TRUE;

DESIGN shipment {
    statusActions {
        primary {
            MOVE PROPERTY(markAsToDo(s));
        }                
    }
    status {
        MOVE PROPERTY(waiting(s));                               
    }
}