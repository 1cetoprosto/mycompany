MODULE ShipmentReceipt;

REQUIRE ShipmentDone, ReceiptDone;

NAMESPACE Inventory;

isReturn 'Возврат' = DATA BOOLEAN (Receipt);
referenceShipment 'Ссылка на отгрузку' = DATA ShipmentLine (ReceiptLine);
referenceNumber 'Ссылка на отгрузку' (ReceiptLine l) = number(shipment(referenceShipment(l)));

CONSTRAINT referenceShipment(ReceiptLine l) AND NOT (customer(referenceShipment(l)) = vendor(l) AND 
    product(referenceShipment(l)) = product(l))
    CHECKED BY referenceShipment
    MESSAGE 'Контрагент и товар отгрузки должны совпадать с контрагентом и товаром возврата';
    
WHEN LOCAL (SETCHANGED(product(ReceiptLine l)) OR SETCHANGED(vendor(l))) AND isReturn(receipt(l)) AND product(l) AND vendor(l) DO {
    referenceShipment(l) <- GROUP LAST ShipmentLine sl IF customer(sl) = vendor(l) AND product(sl) = product(l) ORDER executionDate(shipment(sl));
}

WHEN LOCAL SETCHANGED(referenceShipment(ReceiptLine l)) DO {
    initialDemand(l) <- done(referenceShipment(l));
}

return 'Возврат' (Shipment s) {    
    NEWSESSION {
        NEW nr = Receipt {
            isReturn(nr) <- TRUE;
            scheduledDate(nr) <- currentDateTime();
            location(nr) <- location(s);
            vendor(nr) <- customer(s);
                FOR shipment(ShipmentLine l) = s INLINE NEW nl = ReceiptLine DO {
                    referenceShipment(nl) <- l;
                    receipt(nl) <- nr;
                    product(nl) <- product(l);
                    initialDemand(nl) <- done(l);
                }
                SHOW receipt OBJECTS r = nr DOCKED;
        }
    }
}

EXTEND FORM shipment
    PROPERTIES(s) return SHOWIF status(s) = ShipmentStatus.done AND NOT isTransfer(type(s))
;

DESIGN shipment {
    secondaryActions {
        MOVE PROPERTY(return(s));
    }             
}

shipmentNumber 'Отгрузка' (ShipmentLine l) = number(shipment(l));

FORM listShipmentLines
    OBJECTS p = Product, pa = Partner PANEL 
    
    OBJECTS l = ShipmentLine
    PROPERTIES(l) READONLY shipmentNumber, index, nameProduct, done, executionDate
    FILTERS product(l) = p, customer(l) = pa
;

showList (ReceiptLine line) {
    DIALOG listShipmentLines OBJECTS p = product(line), pa = vendor(line), l INPUT DO {
        referenceShipment(line) <- l;
    }
}

EXTEND FORM receipt
    PROPERTIES(r) isReturn
    PROPERTIES(l) referenceNumber SHOWIF isReturn(r) READONLYIF readonly(l) ON CHANGE showList(l)
;

DESIGN receipt {
    headerRight {
        MOVE PROPERTY(isReturn(r));
    }
}
