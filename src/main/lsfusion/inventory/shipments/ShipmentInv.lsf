MODULE ShipmentInv;

REQUIRE ShipmentDone, InvLedger;

NAMESPACE Inventory;

EXTEND CLASS ShipmentLine : InvLedger;

done(ShipmentLine l) += done(shipment(l));
dateTime(ShipmentLine l) += executionDate(shipment(l));
fromLocation(ShipmentLine l) += location(shipment(l));
toLocation(ShipmentLine l) += toLocation(shipment(l));

product(ShipmentLine l) += product(l);
quantity(ShipmentLine l) += done(l) (-) picked(l);

description(ShipmentLine l) += 'Отгрузка' IF l IS ShipmentLine;
partner(ShipmentLine l) += partner(shipment(l));

series(ShipmentLine l) += series(shipment(l));
number(ShipmentLine l) += number(shipment(l));

CLASS ShipmentPickingInv : InvLedger;

pickLedger = AGGR ShipmentPickingInv WHERE done(ShipmentLine l, Location loc) AND level(loc, location(shipment(l)))
                                     AND status(shipment(l)) = ShipmentStatus.done;

done(ShipmentPickingInv p) += done(shipment(l(p)));
dateTime(ShipmentPickingInv p) += executionDate(shipment(l(p)));
fromLocation(ShipmentPickingInv p) += loc(p);
toLocation(ShipmentPickingInv p) += toLocation(shipment(l(p)));

product(ShipmentPickingInv p) += product(l(p));
quantity(ShipmentPickingInv p) += done(l(p), loc(p));

description(ShipmentPickingInv p) += 'Отгрузка' IF p IS ShipmentPickingInv;
partner(ShipmentPickingInv p) += partner(shipment(l(p)));

series(ShipmentPickingInv p) += series(shipment(l(p)));
number(ShipmentPickingInv p) += number(shipment(l(p)));
