MODULE ShipmentType;

REQUIRE InventorySettings, Numerator, Location;

NAMESPACE Inventory;

CLASS ShipmentType 'Тип отгрузки';

name 'Название' = DATA ISTRING[50] (ShipmentType) NONULL;
isTransfer 'Трансфер?' = DATA BOOLEAN (ShipmentType) CHARWIDTH 10;
picking 'Отбор' = DATA BOOLEAN (ShipmentType);

numerator 'Нумератор' = DATA Numerator (ShipmentType);
nameNumerator 'Нумератор' (ShipmentType o) = name(numerator(o));

loadInitialData () + {
    NEWSESSION {
        NEW s = ShipmentType {
            name(s) <- 'Стандартная отгрузка';

            NEW n = Numerator {
                name(n) <- 'Отгрузок';
                series(n) <- 'SH';
                minValue(n) <- 1;
                maxValue(n) <- 999999;
                stringLength(n) <- 6;
                numerator(s) <- n;
            }
        }
        
        NEW s = ShipmentType {
            name(s) <- 'Стандартный трансфер';
            isTransfer(s) <- TRUE;

            NEW n = Numerator {
                name(n) <- 'Трансферов';
                series(n) <- 'SТ';
                minValue(n) <- 1;
                maxValue(n) <- 999999;
                stringLength(n) <- 6;
                numerator(s) <- n;
            }
        }
        APPLY;
    }
}

defaultLocation = DATA Location (ShipmentType);
nameDefaultLocationFrom 'Место хранения по умолчанию (откуда)' (ShipmentType o) = name(defaultLocation(o));

defaultToLocation = DATA Location (ShipmentType);
nameDefaultToLocation 'Место хранения по умолчанию (куда)' (ShipmentType o) = name(defaultToLocation(o));

CONSTRAINT defaultToLocation(ShipmentType s) = defaultLocation(s)
           MESSAGE 'Места хранения откуда и куда не могут совпадать';

maxQuantity 'Максимальное кол-во товара' = DATA NUMERIC[16,3] (ShipmentType);

countShipmentTypes 'Кол-во типов' () = GROUP SUM 1 IF r IS ShipmentType; 
firstShipmentType = GROUP LAST ShipmentType r ORDER DESC r WHERE r IS ShipmentType;

defaultShipmentType = firstShipmentType() IF countShipmentTypes() = 1;

FORM shipmentType 'Тип отгрузки'
    OBJECTS o = ShipmentType PANEL 
    PROPERTIES(o) name, isTransfer, nameNumerator, nameDefaultToLocation SHOWIF isTransfer(o), 
                nameDefaultLocationFrom, maxQuantity, picking
     
    EDIT ShipmentType OBJECT o;
;

FORM shipmentTypes 'Типы отгрузки'
    OBJECTS o = ShipmentType
    PROPERTIES(o) READONLY name, isTransfer, nameNumerator, nameDefaultToLocation, 
                nameDefaultLocationFrom, maxQuantity, picking
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    LIST ShipmentType OBJECT o;
;

NAVIGATOR {
    settings {
        NEW shipmentTypes;
    }
}