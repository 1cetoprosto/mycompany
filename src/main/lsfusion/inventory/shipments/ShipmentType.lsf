MODULE ShipmentType;

REQUIRE InventorySettings, Numerator, Location;

NAMESPACE Inventory;

CLASS ShipmentType 'Тип отгрузки';

name 'Название' = DATA ISTRING[50] (ShipmentType);
isTransfer 'Трансфер?' = DATA BOOLEAN (ShipmentType) CHARWIDTH 10;
picking 'Отбор' = DATA BOOLEAN (ShipmentType);

numerator 'Нумератор' = DATA Numerator (ShipmentType);
nameNumerator 'Нумератор' (ShipmentType o) = name(numerator(o));

MasterData.loadDefaultData[] () + {
    NEWSESSION {
        NEW s = ShipmentType {
            name(s) <- 'Стандартная отгрузка';

            NEW n = Numerator {
                name(n) <- 'Отгрузок';
                series(n) <- 'SH';
                minValue(n) <- 1;
                maxValue(n) <- 999999;
                stringLength(n) <- 6;
                numerator(s) <- n;
            }
        }
        
        NEW s = ShipmentType {
            name(s) <- 'Стандартный трансфер';
            isTransfer(s) <- TRUE;

            NEW n = Numerator {
                name(n) <- 'Трансферов';
                series(n) <- 'SH';
                minValue(n) <- 1;
                maxValue(n) <- 999999;
                stringLength(n) <- 6;
                numerator(s) <- n;
            }
        }
        APPLY;
    }
}

defaultLocationTo 'Место хранения по умолчанию (куда)' = DATA Location(ShipmentType);
nameDefaultLocationTo 'Место хранения по умолчанию (куда)' (ShipmentType o) = name(defaultLocationTo(o));
defaultLocationFrom 'Место хранения по умолчанию (откуда)' = DATA Location(ShipmentType);
nameDefaultLocationFrom 'Место хранения по умолчанию (откуда)' (ShipmentType o) = name(defaultLocationFrom(o));
maxQuantity 'Максимальное кол-во товара' = DATA NUMERIC[16,3] (ShipmentType);

countTypesShipment 'Кол-во типов' () = GROUP SUM 1 IF r IS ShipmentType; 
lastTypeShipment 'Последний тип' = GROUP LAST r AS ShipmentType ORDER r;

CONSTRAINT defaultLocationTo(ShipmentType s) == defaultLocationFrom(s)
                MESSAGE 'Места хранения откуда и куда не могут совпадать';

FORM shipmentType 'Тип отгрузки'
    OBJECTS o = ShipmentType PANEL 
    PROPERTIES(o) name, isTransfer, nameNumerator, nameDefaultLocationTo SHOWIF isTransfer(o), 
                nameDefaultLocationFrom, maxQuantity, picking
     
    EDIT ShipmentType OBJECT o;
;

FORM shipmentTypes 'Типы отгрузки'
    OBJECTS o = ShipmentType
    PROPERTIES(o) READONLY name, isTransfer, nameNumerator, nameDefaultLocationTo, 
                nameDefaultLocationFrom, maxQuantity, picking
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    LIST ShipmentType OBJECT o;
;

NAVIGATOR {
    settings {
        NEW shipmentTypes;
    }
}