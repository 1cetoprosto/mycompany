MODULE ShipmentStatus;

REQUIRE Shipment;

NAMESPACE Inventory;

CLASS ShipmentStatus 'Статус прихода' {
    draft 'Черновик'
}

name 'Имя' (ShipmentStatus o) = staticCaption(o) IF o IS ShipmentStatus CHARWIDTH 15;

status 'Статус' = ABSTRACT CASE ShipmentStatus (Shipment);
nameStatus 'Статус' (Shipment s) = name(status(s));
nameStatus 'Статус' (ShipmentLine l) = nameStatus(shipment(l));
colorStatus 'Цвет' = ABSTRACT CASE COLOR (Shipment);

EXTEND FORM shipment
    PROPERTIES(s) nameStatus BACKGROUND colorStatus(s)
;

EXTEND FORM shipments    
    FILTERGROUP status
        FILTER 'Черновик' status(s) = ShipmentStatus.draft 'F10'
;

EXTEND FORM shipments
    PROPERTIES(s) nameStatus BEFORE scheduledDate(s) BACKGROUND colorStatus(s)
;

status(Shipment s) += WHEN s IS Shipment THEN ShipmentStatus.draft;
readonly (Shipment s) += WHEN NOT status(s) = ShipmentStatus.draft THEN TRUE;
colorStatus(Shipment s) += WHEN status(s) == ShipmentStatus.draft THEN RGB(187,242,210);

DESIGN shipment {
    OBJECTS {        
        statusPane {
            NEW status AFTER statusActions{
                type = CONTAINERH;                
                MOVE PROPERTY(nameStatus(s)); 
            }
        }            
    }
}
