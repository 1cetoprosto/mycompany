MODULE ShipmentStatus;

REQUIRE Inventory, Shipment;

NAMESPACE Inventory;

CLASS ShipmentStatus 'Статус прихода' {
    draft 'Черновик'
}

name 'Имя' (ShipmentStatus o) = staticCaption(o) IF o IS ShipmentStatus CHARWIDTH 15;

status 'Статус' = ABSTRACT CASE ShipmentStatus (Shipment);
nameStatus 'Статус' (Shipment s) = name(status(s));
colorStatus 'Цвет' = ABSTRACT CASE COLOR (Shipment);

EXTEND FORM shipment
    PROPERTIES(s) nameStatus BACKGROUND colorStatus(s)
;

EXTEND FORM shipments    
    FILTERGROUP status
        FILTER 'Черновик' status(s) = ShipmentStatus.draft 'F10'
;

EXTEND FORM shipments
    PROPERTIES(s) nameStatus BEFORE scheduledDate(s) BACKGROUND colorStatus(s)
;

status(Shipment s) += WHEN s IS Shipment THEN ShipmentStatus.draft;
readonly (Shipment s) += WHEN NOT status(s) = ShipmentStatus.draft THEN TRUE;
colorStatus(Shipment s) += WHEN status(s) == ShipmentStatus.draft THEN RGB(187,242,210);
readonly (ShipmentLine l) += WHEN NOT status(shipment(l)) = ShipmentStatus.draft THEN TRUE;

DESIGN shipment {
    OBJECTS {        
        NEW statusPane FIRST {
            caption = 'Статусы';
            type = CONTAINERH;
            alignment = STRETCH;                          
            NEW statusActions {
                type = CONTAINERH;                    
                flex = 1;                
            }
            NEW status {
                type = CONTAINERH;                
                MOVE PROPERTY(nameStatus(s)); 
            }
        }                
    }
}
