MODULE ShipmentStatus;

REQUIRE Inventory, Shipment;

NAMESPACE Inventory;

CLASS ShipmentStatus 'Статус прихода' {
    draft 'Черновик'
}

name 'Имя' (ShipmentStatus o) = staticCaption(o) IF o IS ShipmentStatus CHARWIDTH 15;

status 'Статус' = ABSTRACT CASE ShipmentStatus (Shipment);
nameStatus 'Статус' (Shipment s) = name(status(s));

EXTEND FORM shipment
    PROPERTIES(s) nameStatus
;

EXTEND FORM shipments    
    FILTERGROUP status
        FILTER 'Черновик' status(s) = ShipmentStatus.draft 'F10'
;

EXTEND FORM shipments
    PROPERTIES(s) nameStatus
;

status(Shipment s) += WHEN s IS Shipment THEN ShipmentStatus.draft;

readonly (ShipmentLine l) += WHEN NOT status(shipment(l)) = ShipmentStatus.draft THEN TRUE;

DESIGN shipment {
    OBJECTS {        
        NEW statusPane FIRST {
            caption = 'Статусы';
            type = CONTAINERH;
            alignment = STRETCH;                          
            NEW statusActions {
                type = CONTAINERH;                    
                flex = 1;                
            }
            NEW status {
                type = CONTAINERH;                
                MOVE PROPERTY(nameStatus(s)); 
            }
        }                
    }
}
