MODULE ShipmentDone;

REQUIRE Inventory, ShipmentReady;

NAMESPACE Inventory;

done 'Отгружен' = DATA BOOLEAN (Shipment);

validate 'Отгружен' (Shipment s) {
    APPLY; // сохраняем изменения
    IF canceled() THEN RETURN; // если сработало ограничение, то выходим
    
    NEWSESSION {
        IF NOT GROUP SUM done(ShipmentLine l) IF shipment(l) = s THEN
            ASK 'Не заполены отгруженые количества. Проставить ожидаемыми?' DO {
                done(ShipmentLine l) <- reserved(l) WHERE shipment(l) = s;
            } ELSE RETURN;
        
        IF GROUP SUM 1 IF shipment(ShipmentLine l) = s AND NOT done(l) = initialDemand(l) THEN {
            ASK 'Отгружено не все количество. Создать дополнительную отгрузку?' DO {
                NEW rn = Shipment {
                    partner(rn) <- partner(s);
                    location(rn) <- location(s);
                    scheduledDate(rn) <- scheduledDate(s);
                    FOR shipment(ShipmentLine l) = s AND (q = initialDemand(l) (-) done(l)) DO NEW ln = ShipmentLine {
                        shipment(ln) <- rn;
                        product(ln) <- product(l);
                        initialDemand(ln) <- q;
                    }
                }
                initialDemand(ShipmentLine l) <- done(l) WHERE shipment(l) = s;
                DELETE ShipmentLine l WHERE shipment(l) = s AND NOT done(l);
            }
        }
        done(s) <- TRUE;
        executionDate(s) <- currentDateTime();
        APPLY;
    }
}

EXTEND CLASS ShipmentStatus {
    done 'Отгружен'
}

EXTEND FORM shipment
    PROPERTIES(s) validate SHOWIF ready(s), done READONLY 
    PROPERTIES(s) executionDate READONLY AFTER scheduledDate(s) SHOWIF done(s)
;

EXTEND FORM shipments 
    EXTEND FILTERGROUP status
        FILTER 'Отгружен' status(s) = ShipmentStatus.done 'F7'
;

status(Shipment s) += WHEN done(s) THEN ShipmentStatus.done;

DESIGN shipment {                               
    statusActions {                  
        MOVE PROPERTY(validate(s));
    }
    status {
        MOVE PROPERTY(done(s));                              
    } 
    box1 {
        MOVE PROPERTY(executionDate(s));
    }                  
}

// Проводим по регистру изменения остатков
EXTEND CLASS ShipmentLine : InvLedger;

done(ShipmentLine l) += done(shipment(l));
dateTime(ShipmentLine l) += executionDate(shipment(l));
fromLocation(ShipmentLine l) += location(shipment(l));
toLocation(ShipmentLine l) += toLocation(shipment(l));

product(ShipmentLine l) += product(l);
quantity(ShipmentLine l) += done(l);

description(ShipmentLine l) += 'Отгрузка' IF l IS ShipmentLine;
partner(ShipmentLine l) += partner(shipment(l));

series(ShipmentLine l) += series(shipment(l));
number(ShipmentLine l) += number(shipment(l));

CLASS ShipmentResLedger : ResLedger;
ResLedger = AGGR ShipmentResLedger WHERE reserved(ShipmentLine l) AND status(shipment(l)) = ShipmentStatus.ready;

done(ShipmentResLedger r) += done(shipment(l(r)));
dateTime(ShipmentResLedger r) += executionDate(shipment(l(r)));

location(ResLedger r) += location(shipment(l(r)));
//toLocation(ResLedger r) += toLocation(shipment(l(r)));

product(ResLedger r) += product(l(r));
quantity(ResLedger r) += done(l(r));
reserved(ResLedger r) += reserved(l(r));

description(ResLedger r) += 'Отгрузка' IF l(r) IS ShipmentLine;
partner(ResLedger r) += partner(shipment(l(r)));

series(ResLedger r) += series(shipment(l(r)));
number(ResLedger r) += number(shipment(l(r)));
