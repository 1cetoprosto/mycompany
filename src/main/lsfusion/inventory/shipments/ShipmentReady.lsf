MODULE ShipmentReady;

REQUIRE Inventory, Utils, ShipmentPicking;

NAMESPACE Inventory;

EXTEND CLASS ShipmentStatus {
    ready 'К отгрузке'
}

ready 'К отгрузке' = DATA BOOLEAN (Shipment);

checkAvailability 'Зарезервировать' (Shipment s) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        FOR shipment(ShipmentLine l) = s DO {
            IF picked(l) < initialDemand(l) OR NOT picked(l) THEN {
                reserved(l) <- reserved(l) (+) min(initialDemand(l) (-) reserved(l) (-) picked(l), available(location(s),product(l)));
            }
        }
        IF GROUP SUM 1 IF (reserved(ShipmentLine l) OR picked(l)) AND shipment(l) = s THEN{
            ready(s) <- TRUE;
            APPLY;
        }
    }
}

undo 'Снять резервирование' (Shipment s) {
    NEWSESSION {
        FOR shipment(ShipmentLine l) = s DO {
            reserved(l) <- NULL;
            FOR (reserved(l, Location loc) IF level(loc, location(shipment(l)))) DO {
                reserved(l, loc) <- NULL;
            }     
        }
        ready(s) <- NULL;
        APPLY;
    }
}

countLinesDemand (Shipment s) = GROUP SUM 1 IF shipment(ShipmentLine sl) = s AND NOT initialDemand(sl) = reserved(sl) (+) picked(sl);
editPick (ShipmentLine l) += WHEN NOT (status(shipment(l)) == ShipmentStatus.waiting 
    OR (status(shipment(l)) = ShipmentStatus.ready AND countLinesDemand(shipment(l)))) THEN TRUE;

EXTEND FORM shipment
    PROPERTIES(s) checkAvailability SHOWIF (status(s) = ShipmentStatus.waiting 
                                    OR (status(s) = ShipmentStatus.ready AND countLinesDemand(s))),
                  undo SHOWIF status(s) = ShipmentStatus.ready, ready READONLY 
       
    PROPERTIES(l) reserved READONLYIF NOT(status(s) = ShipmentStatus.waiting 
                           OR (status(s) = ShipmentStatus.ready AND countLinesDemand(s))), 
                           picked READONLY SHOWIF picking(type(s)) AND waiting(s)
    FILTERS shipment(l) = s
;

EXTEND FORM shipments    
    EXTEND FILTERGROUP status
        FILTER 'К отгрузке' status(s) = ShipmentStatus.ready 'F8'    
;

status(Shipment s) += WHEN ready(s) THEN ShipmentStatus.ready;
colorStatus(Shipment s) += WHEN status(s) == ShipmentStatus.ready THEN RGB(252,247,149);

DESIGN shipment {
    statusActions {
        primary {
            MOVE PROPERTY(checkAvailability(s));
            MOVE PROPERTY(undo(s));
        }              
    }
    status {
        MOVE PROPERTY(ready(s));                               
    }    
}
