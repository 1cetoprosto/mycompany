MODULE ShipmentReady;

REQUIRE Inventory, Utils, ShipmentWaiting, ResLedger;

NAMESPACE Inventory;

EXTEND CLASS ShipmentStatus {
    ready 'К отгрузке'
}

ready 'К отгрузке' = DATA BOOLEAN (Shipment);

reserved 'Зарезервировано' = DATA NUMERIC[16,3] (ShipmentLine);

available 'Доступный остаток' (ShipmentLine l) = prevAvailableRec(location(shipment(l)), product(l)) (+) PREV(reserved(l)); 

extraCheckAvailability ABSTRACT (Shipment);
checkAvailability 'Зарезервировать' (Shipment s) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        reserved(ShipmentLine l) <- min(initialDemand(l), available(l)) WHERE shipment(l) = s;
        extraCheckAvailability(s);
        IF GROUP SUM 1 IF (reserved(ShipmentLine l)) AND shipment(l) = s THEN {
            ready(s) <- TRUE;
            APPLY;
        }
    }
}

extraUnreserve ABSTRACT (Shipment); 
unreserve 'Снять резервирование' (Shipment s) {
    NEWSESSION {
        reserved(ShipmentLine l) <- NULL WHERE shipment(l) = s;
        extraUnreserve(s); 
        ready(s) <- NULL;
        APPLY;
    }
}

countLinesDemand (Shipment s) = GROUP SUM 1 IF shipment(ShipmentLine sl) = s AND NOT initialDemand(sl) = reserved(sl);

EXTEND FORM shipment
    PROPERTIES(s) checkAvailability SHOWIF (status(s) = ShipmentStatus.waiting 
                                    OR (status(s) = ShipmentStatus.ready AND countLinesDemand(s))),
                  unreserve SHOWIF status(s) = ShipmentStatus.ready, ready READONLY 
       
    PROPERTIES(l) READONLY available, reserved
    
    FILTERS shipment(l) = s
;

EXTEND FORM shipments    
    EXTEND FILTERGROUP status
        FILTER 'К отгрузке' status(s) = ShipmentStatus.ready 'F8'    
;

status(Shipment s) += WHEN ready(s) THEN ShipmentStatus.ready;
colorStatus(Shipment s) += WHEN status(s) == ShipmentStatus.ready THEN RGB(252,247,149);
readonly (ShipmentLine l) += WHEN ready(shipment(l)) THEN TRUE;

DESIGN shipment {
    statusActions {
        primary {
            MOVE PROPERTY(checkAvailability(s));
            MOVE PROPERTY(unreserve(s));
        }              
    }
    status {
        MOVE PROPERTY(ready(s));                               
    }    
}
