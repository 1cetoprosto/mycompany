MODULE ShipmentReady;

REQUIRE Inventory, ShipmentWaiting;

NAMESPACE Inventory;

EXTEND CLASS ShipmentStatus {
    ready 'К отгрузке'
}

reserved 'Зарезервировано' = DATA NUMERIC[16,3] (ShipmentLine);

reserved 'Зарезервировано' (Location l, Product p) = 
    GROUP SUM reserved(ShipmentLine sl) IF location(shipment(sl)) = l 
                                        AND product(sl) = p 
                                        AND status(shipment(sl)) = ShipmentStatus.ready;
                                        
available 'Доступный остаток' (Location ll, Product p) = onHand(ll,p) (-) reserved(ll,p);

ready 'К отгрузке' = DATA BOOLEAN (Shipment);

checkAvailability 'Зарезервировать' (Shipment s) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        FOR shipment(ShipmentLine l) = s DO {
            reserved(l) <- reserved(l) (+)
                           IF (initialDemand(l) (-) reserved(l)) >= available(location(s),product(l)) 
                                THEN available(location(s),product(l))
                                ELSE initialDemand(l) (-) reserved(l);
        }
        
        IF GROUP SUM 1 IF reserved(ShipmentLine l) AND shipment(l) = s THEN{
            ready(s) <- TRUE;
            APPLY;
        }
    }
}

EXTEND FORM shipment
    PROPERTIES(s) checkAvailability SHOWIF status(s) = ShipmentStatus.waiting OR status(s) = ShipmentStatus.ready, ready
    
    PROPERTIES(l) reserved, done READONLYIF NOT ready(s)
;

EXTEND FORM shipments    
    EXTEND FILTERGROUP status
        FILTER 'К отгрузке' status(s) = ShipmentStatus.ready 'F8'    
;

status(Shipment s) += WHEN ready(s) THEN ShipmentStatus.ready;

DESIGN shipment {
    statusActions {
        MOVE PROPERTY(checkAvailability(s));                
    }
    status {
        MOVE PROPERTY(ready(s));                               
    }
}