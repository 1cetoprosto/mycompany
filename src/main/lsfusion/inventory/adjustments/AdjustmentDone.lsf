MODULE AdjustmentDone;

REQUIRE AdjustmentInProgress, Utils;

NAMESPACE Inventory;

done 'Завершена' = DATA BOOLEAN (Adjustment);

EXTEND CLASS AdjustmentStatus {
    done 'Завершена'
}

status(Adjustment a) += WHEN done(a) THEN AdjustmentStatus.done;

difference (AdjustmentLine l) = theoreticalQuantity(l) - realQuantity(l);

CLASS InvAdjustment;

CLASS InAdjustment : InvAdjustment;
inAdjustment = AGGR InAdjustment WHERE difference(AdjustmentLine line) < 0 AND done(adjustment(line));

CLASS OutAdjustment : InvAdjustment;
outAdjustment = AGGR OutAdjustment WHERE difference(AdjustmentLine line) > 0 AND done(adjustment(line));

line (InvAdjustment a) = MULTI line[InAdjustment](a), line[OutAdjustment](a) MATERIALIZED INDEXED;
adjustment (InvAdjustment a) = adjustment(line(a)) MATERIALIZED INDEXED;

location(InvAdjustment a) = location(line(a));

product (InvAdjustment a) = product(line(a));

EXTEND CLASS InvAdjustment : InvLedger;
done(InvAdjustment a) += done(adjustment(a));
dateTime(InvAdjustment a) += dateTime(adjustment(a));
fromLocation(InvAdjustment a) += location(a);

product(InvAdjustment a) += product(a);
quantity(InvAdjustment a) += difference(line(a));
description(InvAdjustment a) += 'Инвентаризация' IF a IS InvAdjustment;

edit (InvAdjustment a) + { edit(adjustment(a)); } 

validate 'Завершить' (Adjustment a) {
    APPLY; // сохраняем изменения
    IF canceled() THEN RETURN;
    NEWSESSION {
        done(a) <- TRUE;
        APPLY;
    }    
}

showTab(Adjustment a) = status(a) = AdjustmentStatus.done;

EXTEND FORM adjustment
    PROPERTIES(a) validate SHOWIF status(a) = AdjustmentStatus.inProgress, done READONLY 
    
    OBJECTS i = InvAdjustment
    PROPERTIES(i) nameProduct, nameFromLocation, quantity
    FILTERS adjustment(i) = a //OR adjustment(line[OutAdjustment](i)) = a
;

DESIGN adjustment {                               
    statusActions {                  
        MOVE PROPERTY(validate(a)) { fontStyle = 'bold'; }
    }
    status {
        MOVE PROPERTY(done(a));                              
    }  
    body {
        NEW tab2 {
            showIf = showTab(a);
            caption = 'Изменения';
            MOVE BOX(i);
        }
    }                
}
