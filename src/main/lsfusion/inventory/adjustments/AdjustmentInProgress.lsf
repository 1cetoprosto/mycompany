MODULE AdjustmentInProgress;

REQUIRE AdjustmentStatus, AdjustmentSearch;

NAMESPACE Inventory;

inProgress 'В процессе' = DATA BOOLEAN (Adjustment);

markAsToDo 'Начать' (Adjustment a) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        inProgress(a) <- TRUE;
        APPLY;
    }
}

EXTEND CLASS AdjustmentStatus {
     inProgress 'В процессе'
}

status(Adjustment a) += WHEN inProgress(a) THEN AdjustmentStatus.inProgress;
colorStatus(Adjustment a) += WHEN status(a) == AdjustmentStatus.inProgress THEN RGB(252,247,149);

readonly (AdjustmentLine l) += WHEN NOT status(adjustment(l)) = AdjustmentStatus.inProgress THEN TRUE;
showSearch (Adjustment a) += WHEN status(a) = AdjustmentStatus.inProgress AND type(a) = AdjustmentType.manual THEN TRUE;

EXTEND FORM adjustment
    PROPERTIES(a) markAsToDo SHOWIF status(a) = AdjustmentStatus.draft, inProgress READONLY
    
    FILTERGROUP adjustment
        FILTER 'Расхождения' NOT theoreticalQuantity(l) = realQuantity(l) 'F10'
        FILTER 'Излишки'  theoreticalQuantity(l) < realQuantity(l) 'F9'
        FILTER 'Недостачи'  theoreticalQuantity(l) > realQuantity(l) 'F8'
;

DESIGN adjustment {
    statusActions {
        MOVE PROPERTY(markAsToDo(a)) { fontStyle = 'bold'; }               
    }
    status {
        MOVE PROPERTY(inProgress(a));                               
    }
}

