MODULE SalesPriceType;

REQUIRE SalesNavigator, PartnerSale, Partner; 

NAMESPACE Sales;

CLASS PriceType 'Виды цены';

name 'Наименование' = DATA ISTRING[100](PriceType);
default 'по умолчаню' = DATA BOOLEAN (PriceType); 

FORM pricelistType 'Вид цены'
    OBJECTS pt = PriceType
    PROPERTIES(pt) name, default
    EDIT PriceType OBJECT pt
;

DESIGN pricelistType {
    NEW pane {
        fill = 1;
        MOVE BOX(pt);
    }
}

FORM pricelistTypes 'Виды цен'
    OBJECTS pt = PriceType
    PROPERTIES(pt) name, default
    PROPERTIES(pt) NEW, DELETE 
//    PROPERTIES(pt) NEWSESSION NEW, EDIT, DELETE 
;   

FORM pricelistTypeList 'Вид цены'
    OBJECTS p = PriceType
    PROPERTIES(p) READONLY name

    LIST PriceType OBJECT p
;

//добавляем для контрагента определение типа цен
priceType 'Тип цен для покупателя' =  DATA PriceType (Partner);
namePriceType 'Тип цен для покупателя' (Partner p) =  name(priceType(p));
//если не указан тип цен при добавлении контрагента, ставим дефолтный
WHEN SET (Partner p IS Partner) DO {
    IF isCustomer(p) AND NOT priceType(p) THEN priceType(p) <- GROUP LAST (PriceType pt) IF default(pt) ORDER pt;
} 

EXTEND FORM partner 
    PROPERTIES(p) namePriceType 
;

DESIGN partner {
    tabs {
        customer {
            NEW pricelist {
                showIf=isCustomer(p);
                MOVE PROPERTY (namePriceType(p));
            }
        }
    }
}

NAVIGATOR {
    sales{
        NEW pricelistTypes;    
    }
}
