MODULE SalesPriceType;

REQUIRE SalesNavigator, PartnerSale, Partner; 

NAMESPACE Sales;

CLASS PriceType 'Виды цены';

name 'Наименование' = DATA ISTRING[100](PriceType);
default 'по умолчаню' = DATA BOOLEAN (PriceType);

FORM pricelistType 'Вид цены'
    OBJECTS pt = PriceType PANEL 
    PROPERTIES(pt) name, default
    EDIT PriceType OBJECT pt
;

DESIGN pricelistType {
    OBJECTS {
        NEW header {
            type = CONTAINERV;        
            caption='Виды цен';
            fill = 1;
            MOVE PROPERTY(name(pt));
            NEW pane{
                type = CONTAINERV;
                MOVE PROPERTY(default(pt)) {caption='по умолчанию для новых покупателей';};
            }
        }
    }
}

FORM pricelistTypes 'Виды цен'
    OBJECTS pt = PriceType
    PROPERTIES(pt) READONLY name, default
    PROPERTIES(pt) NEWSESSION NEW, EDIT, DELETE 
;   

FORM pricelistTypeList 'Вид цены'
    OBJECTS pt = PriceType
    PROPERTIES(pt) READONLY name

    LIST PriceType OBJECT pt
;

//добавляем для контрагента определение типа цен
priceType 'Тип цен для покупателя' =  DATA PriceType (Partner);
namePriceType 'Тип цен для покупателя' (Partner p) =  name(priceType(p));
//если не указан тип цен при добавлении контрагента, ставим дефолтный
WHEN SET (Partner p IS Partner) DO {
    IF isCustomer(p) AND NOT priceType(p) THEN priceType(p) <- GROUP LAST (PriceType pt) IF default(pt) ORDER pt;
} 

EXTEND FORM partner 
    PROPERTIES(p) namePriceType 
;

DESIGN partner {
    tabs {
        customer {
            NEW pricelist {
                showIf=isCustomer(p);
                MOVE PROPERTY (namePriceType(p));
            }
        }
    }
}

NAVIGATOR {
    sales{
        NEW pricelistTypes;    
    }
}
