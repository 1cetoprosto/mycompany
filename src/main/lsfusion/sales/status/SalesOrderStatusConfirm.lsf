MODULE SalesOrderStatusConfirm;

REQUIRE SalesOrderStatus, SalesOrderStatusDraft, SalesOrderStatusSent;

NAMESPACE Sales;

EXTEND CLASS OrderStatus{
    orderConfirmed 'Заказ оформлен'
}

confirmedDateTime 'Подтвержден' = DATA DATETIME (Order);
confirmed 'Подтвержден' = DATA BOOLEAN (Order);

confirmOrder 'Подтвердить заказ' (Order o){
    confirmedDateTime(o) <- currentDateTime();
    confirmed(o) <- TRUE;
    MESSAGE 'Заказ подтвержден';    
}

confirmOrderBeforeSend 'Подтвердить заказ' (Order o) { confirmOrder(o); }
confirmOrderAfterSend 'Подтвердить заказ' (Order o) { confirmOrder(o); }

status(Order o) += WHEN confirmed(o) THEN OrderStatus.orderConfirmed;
colorStatus(Order o) += WHEN status(o) == OrderStatus.orderConfirmed THEN RGB(150,255,150);

EXTEND FORM order 
     PROPERTIES (o) SHOWIF (status(o)=OrderStatus.draft)  confirmOrderBeforeSend
     PROPERTIES (o) SHOWIF (status(o)=OrderStatus.offerSent)  confirmOrderAfterSend
     PROPERTIES (o) confirmedDateTime
;

DESIGN order {
    statusPane {
        statusActions {
            MOVE PROPERTY(confirmOrderBeforeSend(o));
            MOVE PROPERTY(confirmOrderAfterSend(o)) FIRST { fontStyle = 'bold'; };
        }
    }
    title {
        MOVE PROPERTY(confirmedDateTime(o));
    }
}

EXTEND FORM orders
    PROPERTIES(o) READONLY SHOWIF showActionsTime() confirmedDateTime BEFORE seriesNumber(o);
;