MODULE SalesPricelist;

REQUIRE Item, Partner, ItemSale, PartnerSale, TaxItem, SalesSettings, SalesOrder, SalesPriceType, CostLedger;

NAMESPACE Sales; 

CLASS Pricelist 'Прайс-листы';

@defineSeriesNumber(pricelist, 'Прайс-листы', 'ПЛ');

startDate 'Начало действия' = DATA DATETIME (Pricelist);
WHEN LOCAL SET(Pricelist l IS Pricelist) AND NOT CHANGED(startDate(l)) DO startDate(l) <- currentDateTime(); 
endDate 'Окончание действия' = DATA DATETIME (Pricelist);

note 'Примечания' = DATA STRING[50] (Pricelist);

// price types
editIn 'Изменять цены' = DATA BOOLEAN (Pricelist, PriceType);

priceListTypes 'Виды цен' (Pricelist pricelist) = 
    GROUP CONCAT name(PriceType pricelistType) IF editIn(pricelist, pricelistType) , ', '
          ORDER pricelistType CHARWIDTH 10 MATERIALIZED;

FORM chooseTypes 'Выбор видов цен'
    OBJECTS pr = Pricelist PANEL 
    
    OBJECTS t = PriceType
    PROPERTIES(t) READONLY name 
    PROPERTIES(pr, t) editIn[Pricelist,PriceType]
;

// lines
CLASS PricelistLine 'Номенклатура';

pricelist 'Прайс-лист' = DATA Pricelist (PricelistLine) NONULL DELETE;

index 'Индекс' = PARTITION SUM 1 ORDER PricelistLine l BY pricelist(l) IN id MATERIALIZED;

item 'Номенклатура' = DATA Item (PricelistLine);
nameItem 'Номенклатура' (PricelistLine prl) = name(item(prl));
nameUnitMeasure 'Единица измерения' (PricelistLine prl) = name(unitMeasure(item(prl))); 

costPrice 'Себестоимость' = DATA NUMERIC[32,16] (PricelistLine);
WHEN LOCAL SETCHANGED(item(PricelistLine l)) DO costPrice(l) <- OVERRIDE costPrice(item(l)), cost(item(l));

markup 'Надбавка' = DATA NUMERIC[10,2] (PricelistLine, PriceType);
price 'Цена' = DATA NUMERIC[10,2] (PricelistLine, PriceType);

CONSTRAINT DROPPED(PriceType t IS PriceType) AND PREV(price(PricelistLine l, t))
    MESSAGE 'Запрещено удалять виды цен, по которым есть прайсы';

// events
noTaxPrice (PricelistLine l, PriceType t) = 
    price(l, t) / (OVERRIDE 100.0 / (100.0 + salesTax(item(l))) IF taxIncluded(t), 1.0);

WHEN LOCAL (SETCHANGED(costPrice(PricelistLine l)) OR SETCHANGED(price(l, PriceType t))) AND NOT CHANGED(markup(l, t)) DO
    markup(l, t) <- round2((noTaxPrice(l, t) * 100.0 / costPrice(l)) - 100.0);

calcPrice(PricelistLine l, PriceType t) = 
    NUMERIC[10,2](costPrice(l) * 
                  (markup(l, t) + 100.0) / 100.0 * 
                  (OVERRIDE (100.0 + salesTax(item(l))) / 100.0 IF taxIncluded(t), 1.0));

changeMarkup (PricelistLine l, PriceType t) {
    INPUT val = markup(l, t) CHANGE DO {
        price(l, t) <- calcPrice(l, t);
    }
}

// Line properties
seriesNumber 'Номер' (PricelistLine prl) = seriesNumber(pricelist(prl));
startDate 'Начало действия' (PricelistLine prl) = startDate(pricelist(prl));
endDate 'Окончание действия' (PricelistLine prl) = endDate(pricelist(prl));
note 'Примечание' (PricelistLine prl) = note(pricelist(prl));

editIn (PricelistLine pl, PriceType priceType) = editIn(pricelist(pl), priceType);

// show columns
showCurrentPrice 'Действующие цены' = DATA BOOLEAN (Pricelist);
showMarkup 'Надбавки' = DATA BOOLEAN (Pricelist);
showCost 'Себестоимость' = DATA BOOLEAN (Pricelist);

currentPriceColor (PricelistLine l, PriceType t) = IF currentPrice(l, t) < price(l, t) THEN RGB(187,242,210) 
    ELSE IF currentPrice(l, t) > price(l, t) THEN RGB(250,150,157);

FORM pricelist 'Прайс-лист'
    OBJECTS p = Pricelist PANEL
    PROPERTIES(p) series, number, startDate, endDate, note,
                  priceListTypes ON CHANGE { SHOW chooseTypes OBJECTS pr = p; }, 
                  showCost, showMarkup, showCurrentPrice

    OBJECTS t = PriceType GRID
    FILTERS editIn(p, t) 
   
    OBJECTS l = PricelistLine
    PROPERTIES(l) index READONLY, nameItem, nameUnitMeasure READONLY, costPrice SHOWIF showCost(p) READONLY
    PROPERTIES    markup(l, t) COLUMNS 'p' (t) HEADER name(t) + ' (надбавка)' SHOWIF showMarkup(p) ON CHANGE changeMarkup(l, t)
    PROPERTIES    price(l, t) COLUMNS 'p' (t) HEADER name(t) 
    PROPERTIES    currentPrice(l, t) COLUMNS 'p' (t) HEADER name(t) + ' (текущая)' SHOWIF showCurrentPrice(p) BACKGROUND currentPriceColor(l, t)
    PROPERTIES(l) NEW, DELETE 
    FILTERS pricelist(l) = p 

    EDIT Pricelist OBJECT p
;

DESIGN pricelist {
    OBJECTS {
        NEW header {
            type = CONTAINERH;
            NEW params {
                NEW seriesNumber {
                    type = CONTAINERH;
                    MOVE PROPERTY(series(p));
                    MOVE PROPERTY(number(p));
                }
                NEW period {
                    type = CONTAINERH;
                    MOVE PROPERTY(startDate(p)) { caption = 'Действует с '; }
                    MOVE PROPERTY(endDate(p)) { caption = 'по '; }
                }
                MOVE PROPERTY(priceListTypes(p)); 
                MOVE PROPERTY(note(p)); 
            }
            NEW show {
                caption = 'Показывать';
                MOVE PROPERTY(showCost(p));
                MOVE PROPERTY(showMarkup(p));
                MOVE PROPERTY(showCurrentPrice(p));     
            }     
        }
        NEW details {
            fill = 1;
            type = TABBED;
            MOVE BOX(l) {
                PROPERTY(markup(l, t)) { background = RGB(212,242,252); }
                PROPERTY(price(l, t)) { 
                    pattern = '#,##0.00'; 
                    background = RGB(198,230,247); 
                }
            }
        }
    }
}

FORM pricelists 'Прайс-листы'
    OBJECTS p = Pricelist
    PROPERTIES(p) READONLY seriesNumber, note, startDate, endDate
    PROPERTIES(p) NEWSESSION NEW, EDIT ,DELETE
    
    LIST Pricelist OBJECT p
;

NAVIGATOR {
    sales {
        NEW pricelists AFTER orders;    
    }
}

// copy
copy 'Копировать' (Pricelist pl) {
    NEWSESSION {
        NEW p = Pricelist {
            startDate(p) <- startDate(pl);
            endDate(p) <- endDate(pl);
            note(p) <- note(pl);
            showCost(p) <- showCost(pl);
            showMarkup(p) <- showMarkup(pl);
            showCurrentPrice(p) <- showCurrentPrice(pl);
            editIn(p, PriceType t) <- editIn(pl, t);
            FOR pricelist(PricelistLine l)  = pl DO NEW nl = PricelistLine {
                pricelist(nl) <- p;
                item(nl) <- item(l);
                price(nl, PriceType t) <- price(l, t);
            }
            SHOW pricelist OBJECTS p = p;
        }
    }
}

EXTEND FORM pricelist
    PROPERTIES(p) copy DRAW l TOOLBAR 
;

DESIGN pricelist {
    NEW statusPane FIRST {
        caption = 'Статусы';
        type = CONTAINERH;
        alignment = STRETCH;                          
        NEW statusActions {
            type = CONTAINERH;                    
            flex = 1; 
            MOVE PROPERTY(copy(p));          
        }
        NEW status AFTER statusActions {
            type = CONTAINERH;
        }
    }
}

