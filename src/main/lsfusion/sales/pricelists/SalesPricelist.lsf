MODULE SalesPricelist;

REQUIRE Item, Partner, ItemSale, PartnerSale, TaxItem, SalesSettings, SalesOrder, SalesPriceType, CostLedger;

NAMESPACE Sales; 

// http://documentation.luxsoft.by/x/RwBZB

CLASS Pricelist 'Прайс-листы';

@defineSeriesNumber(pricelist, 'Прайс-листы', 'ПЛ');

note 'Примечания' = DATA STRING[50] (Pricelist);

startDate 'Начало действия' = DATA DATETIME (Pricelist);

endDate 'Окончание действия' = DATA DATETIME (Pricelist);

editIn 'Изменять цены' = DATA BOOLEAN (Pricelist, PriceType);

priceListTypes 'Варианты цен' (Pricelist pricelist) = GROUP
                                               CONCAT name(PriceType pricelistType) IF editIn(pricelist, pricelistType) , ', '
                                               ORDER pricelistType CHARWIDTH 10 MATERIALIZED;

CLASS PricelistLine 'Номенклатура';

pricelist 'Прайс-лист' = DATA Pricelist (PricelistLine) NONULL DELETE;
notePricelist 'Прайс-лист' (PricelistLine prl)= note(pricelist(prl));

item 'Номенклатура' = DATA Item (PricelistLine);
nameItem 'Номенклатура' (PricelistLine prl) = name(item(prl));
nameUnitMeasure 'Единицы' (PricelistLine prl) = name(unitMeasure(item(prl))); 

editIn (PricelistLine pl, PriceType priceType) = editIn(pricelist(pl), priceType);

price 'Цена' = DATA NUMERIC[10,2] (PricelistLine, PriceType);

pricelistLine (PriceType pt, Item i, DATETIME d) = 
    GROUP LAST PricelistLine prl IF item(prl) = i AND price(prl, pt) AND NOT startDate(pricelist(prl)) >= d AND NOT endDate(pricelist(prl)) < d
          ORDER startDate(pricelist(prl)), prl;

price 'Действующая цена' (PriceType pt, Item i, DATETIME d) = price(pricelistLine(pt, i, d), pt);

currentPrice (PricelistLine l, PriceType t) = price(t, item(l), startDate(pricelist(l)));

showCurrentPrice 'Действующие цены' = DATA BOOLEAN (Pricelist);
showMarkup 'Надбавки' = DATA BOOLEAN (Pricelist);
showCost 'Себестоимость' = DATA BOOLEAN (Pricelist);

costPrice 'Текущая себестоимость' (PricelistLine pl) = costPrice(item(pl));

taxes (PricelistLine l, PriceType pt) = (GROUP SUM value(Tax t) IF salesIn(item(l),t)) * price(l, pt) / 100;
markup 'Надбавка' (PricelistLine l, PriceType t) = IF price(l, t) THEN round2((price(l, t) (+) taxes(l, t) (-) costPrice(l)) * 100 / costPrice(l));

changeSalesTax (PricelistLine l) {
    changeSalesTax(item(l));
}

FORM chooseTypes 'Выбор видов цен'
    OBJECTS pr = Pricelist PANEL 
    
    OBJECTS t = PriceType
    PROPERTIES(t) READONLY name 
    PROPERTIES(pr, t) editIn[Pricelist,PriceType]
;

chooseTypes (Pricelist pr) {
    SHOW chooseTypes OBJECTS pr = pr;
}

changeMarkup (PricelistLine l, PriceType t) {
    INPUT val = NUMERIC[10,2] DO {
        price(l, t) <- round2((val * costPrice(l) / 100 (+) costPrice(l)) / (1 (+) (GROUP SUM value(Tax tax) IF salesIn(item(l),tax)) / 100));
    }
}

FORM pricelist 'Прайс-листы'
    OBJECTS pr = Pricelist PANEL
    PROPERTIES(pr) note, seriesNumber, startDate, endDate, priceListTypes ON CHANGE chooseTypes(pr), showCurrentPrice,
                showMarkup, showCost

    OBJECTS tc = PriceType GRID
    FILTERS editIn(pr, tc) 
   
    OBJECTS prl = PricelistLine
    PROPERTIES(prl) nameItem, nameUnitMeasure READONLY, costPrice SHOWIF showCost(pr)
    PROPERTIES(prl) NEW,DELETE 
    FILTERS pricelist(prl) = pr 
    PROPERTIES markup(prl, tc) COLUMNS 'p' (tc) HEADER 'Надбавка ' + name(tc) SHOWIF showMarkup(pr) ON CHANGE changeMarkup(prl, tc)
    PROPERTIES(prl, tc) price COLUMNS (tc) HEADER name(tc) 
    PROPERTIES currentPrice(prl, tc) COLUMNS 'p' (tc) HEADER name(tc) + ' (текущая)' SHOWIF showCurrentPrice(pr)

    EDIT Pricelist OBJECT pr
;

DESIGN pricelist {
    OBJECTS {
        NEW header {
            NEW col1 {
                align = START;
                type = CONTAINERH;
                NEW left {
                    NEW rek {
                        type = CONTAINERH;
                        MOVE PROPERTY(startDate(pr)) { caption = 'Период действия с '; }
                        MOVE PROPERTY(endDate(pr)) { caption = 'по '; }
                        MOVE PROPERTY(seriesNumber(pr)) { marginLeft = 10; }
                    }
                    MOVE PROPERTY(priceListTypes(pr)); 
                    }
                NEW show {
                    caption = 'Показывать';
                    type = CONTAINERH;
                    MOVE PROPERTY(showCurrentPrice(pr));     
                    MOVE PROPERTY(showMarkup(pr));
                    MOVE PROPERTY(showCost(pr));
                }     
            }
            MOVE PROPERTY(note(pr)); 
        }
        NEW details {
            fill = 1;
            type = TABBED;
            NEW container1 {
                caption = 'Спецификация';
                fill = 1;
                MOVE BOX(prl) {
                    PROPERTY(price(prl, tc)) { pattern='#,##0.00'; background = RGB(198,230,247); }
                    MOVE PROPERTY (currentPrice(prl, tc));          
                };           
            }
        }
        REMOVE BOX(tc);         
        MOVE TOOLBARBOX;          
    }
}

FORM pricelistsList 'Прайс-листы'
    OBJECTS pr = Pricelist
    PROPERTIES(pr) READONLY seriesNumber, note, startDate, endDate
    LIST Pricelist OBJECT pr
;

FORM pricelists 'Прайс-листы'
    OBJECTS pr = Pricelist
    PROPERTIES(pr) READONLY seriesNumber, note, startDate, endDate
    PROPERTIES(pr) NEW NEWSESSION ,EDIT NEWSESSION ,DELETE NEWSESSION 
;

NAVIGATOR {
    sales{
        NEW pricelists AFTER orders;    
    }
}

copyPricelist 'Копировать прайслист' (Pricelist pl) {
    NEWSESSION {
        NEW p = Pricelist {
            startDate(p) <- startDate(pl);
            endDate(p) <- endDate(pl);
            note(p) <- note(pl);
            showCurrentPrice(p) <- showCurrentPrice(pl);
            showMarkup(p) <- showMarkup(pl);
            FOR editIn(pl, PriceType t) DO {
                editIn(p, t) <- editIn(pl, t);
            }
            FOR pricelist(PricelistLine l)  = pl DO {
                NEW nl = PricelistLine {
                    pricelist(nl) <- p;
                    item(nl) <- item(l);
                    FOR price(l, PriceType t) DO {
                        price(nl, t) <- price(l, t);
                    }
                }
            }
            SHOW pricelist OBJECTS pr = p;
        }
    }
}

EXTEND FORM pricelist
    PROPERTIES(pr) copyPricelist DRAW prl TOOLBAR 
;

