MODULE SalesPriceType;

REQUIRE SalesSettings, PartnerSale, Partner, SalesOrder; 

NAMESPACE Sales;

CLASS PriceType 'Виды цены';

name 'Наименование' = DATA ISTRING[100](PriceType) NONULL;
taxIncluded 'С учетом налогов' = DATA BOOLEAN (PriceType);  

FORM pricelistType 'Вид цены'
    OBJECTS pt = PriceType PANEL 
    PROPERTIES(pt) name, taxIncluded

    EDIT PriceType OBJECT pt
;

FORM pricelistTypes 'Виды цен'
    OBJECTS pt = PriceType
    PROPERTIES(pt) READONLY name, taxIncluded
    PROPERTIES(pt) NEWSESSION NEW, EDIT, DELETE 
;   

FORM pricelistTypeList 'Вид цены'
    OBJECTS pt = PriceType
    PROPERTIES(pt) READONLY name

    LIST PriceType OBJECT pt
;

NAVIGATOR {
    settings {
        NEW pricelistTypes;
    }
}

defaultPriceType = DATA PriceType ();
nameDefaultPriceType 'Тип цены по умолчанию' () = name(defaultPriceType());

EXTEND FORM options PROPERTIES nameDefaultPriceType();

priceType = DATA PriceType (Partner);
namePriceType 'Тип цен' (Partner p) =  name(priceType(p));

WHEN LOCAL SET (Partner p IS Partner) AND NOT priceType(p) DO 
    priceType(p) <- defaultPriceType();

EXTEND FORM partner 
    PROPERTIES(p) namePriceType 
;

DESIGN partner {
    customerOptions {
        MOVE PROPERTY (namePriceType(p));
    }
}

//добавляем в заказ свойство типа цен от партнера
priceType 'Тип цен' =  DATA PriceType (Order);
namePriceType 'Тип цен' (Order o) =  name(priceType(o));
 
WHEN LOCAL CHANGED(customer(Order o)) DO 
    priceType(o) <- priceType(customer(o));

EXTEND FORM order 
    PROPERTIES(o) namePriceType
;

DESIGN order {
    params {
        MOVE PROPERTY(namePriceType(o));   
    }
}
