MODULE SalesPriceType;

REQUIRE SalesSettings, PartnerSales, Partner; 

NAMESPACE Sales;

CLASS PriceType 'Виды цены';

name 'Наименование' = DATA ISTRING[100] (PriceType) NONULL CHARWIDTH 10;
taxIncluded 'С учетом налогов' = DATA BOOLEAN (PriceType);  

FORM pricelistType 'Вид цены'
    OBJECTS pt = PriceType PANEL 
    PROPERTIES(pt) name, taxIncluded

    EDIT PriceType OBJECT pt
;

FORM pricelistTypes 'Виды цен'
    OBJECTS pt = PriceType
    PROPERTIES(pt) READONLY name, taxIncluded
    PROPERTIES(pt) NEWSESSION NEW, EDIT, DELETE 
;   

FORM pricelistTypeList 'Вид цены'
    OBJECTS pt = PriceType
    PROPERTIES(pt) READONLY name

    LIST PriceType OBJECT pt
;

NAVIGATOR {
    settings {
        NEW pricelistTypes;
    }
}

defaultPriceType = DATA PriceType ();
nameDefaultPriceType 'Вид цены по умолчанию' () = name(defaultPriceType());

EXTEND FORM options PROPERTIES nameDefaultPriceType();

priceType = DATA PriceType (Partner);
namePriceType 'Вид цен' (Partner p) =  name(priceType(p));

WHEN LOCAL SET (Partner p IS Partner) AND NOT priceType(p) DO 
    priceType(p) <- defaultPriceType();

EXTEND FORM partner 
    PROPERTIES(p) namePriceType 
;

DESIGN partner {
    customerOptions {
        MOVE PROPERTY (namePriceType(p));
    }
}