MODULE SalesPricelistPrint;

REQUIRE SalesPricelistValue;

NAMESPACE Sales;

CLASS PrintPricelistType 'Тип прайс-листа';

name 'Наименование' = DATA STRING[50] (PrintPricelistType);

set 'Вкл' = DATA BOOLEAN (PrintPricelistType, Category);
set 'Вкл' = DATA BOOLEAN (PrintPricelistType, PriceType);

FORM printPricelistType 'Тип прайс-листа'
    OBJECTS t = PrintPricelistType PANEL
    PROPERTIES(t) name
    
    TREE categories cat = Category PARENT parent(cat) 
    PROPERTIES name(cat) READONLY, set(t, cat) BACKGROUND (GROUP LAST set(t, Category cc) ORDER level(cc, cat))
    ORDER name(cat)
    
    OBJECTS pt = PriceType
    PROPERTIES name(pt) READONLY, set(t, pt)
    
    EDIT PrintPricelistType OBJECT t
;

FORM printPricelistTypes 'Типы прайс-листа'
    OBJECTS t = PrintPricelistType
    PROPERTIES READONLY name(t)
    PROPERTIES(t) NEWSESSION NEW, EDIT, DELETE
    
    LIST PrintPricelistType OBJECT t
;

DESIGN printPricelistType {
    OBJECTS {
        MOVE PROPERTY(name(t));
        NEW details {
            fill = 1;
            type = CONTAINERH;
            MOVE BOX(TREE categories);
            MOVE BOX(pt);
        }
    }
}

NAVIGATOR {
    sales {
        NEW printPricelistTypes AFTER pricelists;    
    }
}

EXTEND FORM pricelists
    OBJECTS pp = PrintPricelistType
    
    PROPERTIES READONLY name(pp)
    PROPERTIES(pp) NEWSESSION NEW, EDIT, DELETE
;

DESIGN pricelists {
    OBJECTS {
        fill = 1;
        type = TABBED;
        NEW lists {
            caption = 'Прайс-листы';
            MOVE BOX(p);
        }
        NEW print {
            caption = 'Печать';
            MOVE BOX(pp);
        }
    }
}

FORM printPricelist 'Текущие цены'
    OBJECTS dt = DATETIME PANEL
    
    OBJECTS pt = PrintPricelistType PANEL

    OBJECTS t = PriceType
    FILTERS set(pt, t)
    
    OBJECTS c = Category
    FILTERS set(pt, c)
    
    OBJECTS i = Item
    PROPERTIES nameCategory(i), name(i), nameUnitMeasure(i), price = priceB(t, i, dt) COLUMNS (t) HEADER name(t)
    FILTERS (GROUP SUM 1 IF priceB(PriceType type, i, dt) AND set(pt, type)), category(i) = c 
;

printPriceList 'Сформировать' (PrintPricelistType pt) {
    PRINT printPricelist OBJECTS dt = currentDateTime(), pt = pt;
}

printPriceListXls 'Сформировать (xls)' (PrintPricelistType pt) {
    PRINT printPricelist OBJECTS dt = currentDateTime(), pt = pt XLS TO file;
    open(file());
}

EXTEND FORM pricelists
    PROPERTIES(pp) printPriceList, printPriceListXls
;

DESIGN pricelists {
    print {
        NEW actions FIRST {
            type = CONTAINERH;
            MOVE PROPERTY(printPriceList(pp));
            MOVE PROPERTY(printPriceListXls(pp));  
        }
    }
}

