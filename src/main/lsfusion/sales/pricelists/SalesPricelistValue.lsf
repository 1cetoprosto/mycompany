MODULE SalesPricelistValue;

REQUIRE SalesPriceListDone;

NAMESPACE Sales;

// prices
pricelistLineB (PriceType t, Item i, DATETIME d) = 
    GROUP LAST PricelistLine prl IF item(prl) = i AND price(prl, t) AND done(pricelist(prl)) AND 
               NOT startDateTime(pricelist(prl)) >= d AND NOT endDateTime(pricelist(prl)) < d
          ORDER startDateTime(pricelist(prl)), prl;

priceB (PriceType t, Item i, DATETIME d) = price(pricelistLineB(t, i, d), t);

pricelistLineA (PriceType t, Item i, DATETIME d) = 
    GROUP LAST PricelistLine prl IF item(prl) = i AND price(prl, t) AND done(pricelist(prl)) AND
               NOT startDateTime(pricelist(prl)) > d AND NOT endDateTime(pricelist(prl)) < d
          ORDER startDateTime(pricelist(prl)), prl;

priceA (PriceType t, Item i, DATETIME d) = price(pricelistLineA(t, i, d), t);

noTaxPriceA (PriceType t, Item i, DATETIME d) = 
    NUMERIC[10,2](priceA(t, i, d) / (OVERRIDE (100.0 + salesTax(i)) / 100.0 IF taxIncluded(t), 1.0));

// prices for lines
currentPrice (PricelistLine l, PriceType t) = priceB(t, item(l), startDateTime(pricelist(l)));

currentPriceColor (PricelistLine l, PriceType t) = 
    IF currentPrice(l, t) < price(l, t) 
        THEN RGB(187,242,210) 
        ELSE IF currentPrice(l, t) > price(l, t) THEN RGB(250,150,157);

EXTEND FORM pricelist
    PROPERTIES    currentPrice(l, t) COLUMNS 'p' (t) HEADER name(t) + ' (текущая)' 
                        SHOWIF showCurrentPrice(p) AFTER price(l, t) BACKGROUND currentPriceColor(l, t)
;