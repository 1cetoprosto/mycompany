MODULE SalesPricelistSearch;

REQUIRE SalesOrderStatus, Doc, SalesPriceListStatus;

NAMESPACE Sales;

// Подбор товаров
detail= GROUP SUM 1 BY item(PricelistLine pl), pricelist(pl);

in 'Вкл.' (Item item, Pricelist pr) = TRUE IF detail(item, pr);

changeIn(Item item, Pricelist pricelist)  { 
    INPUT b = BOOLEAN DO
        IF detail(item, pricelist) THEN {
            IF NOT b THEN
                DELETE PricelistLine pl WHERE item == item(pl) AND pricelist == pricelist(pl);
        } ELSE
            IF b THEN 
                NEW d = PricelistLine {
                   pricelist(d) <- pricelist;
                   item(d) <- item;
                }
}

taxes (Item i, PriceType pt, DATETIME dt) = salesTaxes(i) * price(pt, i, dt) / 100;
markup 'Текущая надбавка' (Item i, PriceType t, DATETIME  dt) = IF price(t, i, dt) THEN 
                IF taxIncluded(t) THEN round2((price(t, i, dt) / (costPrice(i) * (1 (+) salesTaxes(i) / 100))) * 100 - 100)
                ELSE round2((price(t, i, dt) / costPrice(i)) * 100 - 100);

EXTEND FORM pricelist
    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    ORDER name(c)
    
    OBJECTS dt = DATETIME 
    FILTERS dt = startDate(pr)
    
    OBJECTS item = Item
    PROPERTIES READONLY name(item),
                        costPrice(item) SHOWIF showCost(pr),
                        markup(item, tc, dt) COLUMNS 'p' (tc) HEADER name(tc) + ', %' SHOWIF showMarkup(pr),
                        price(tc, item, dt) COLUMNS 'p' (tc) HEADER name(tc)
    FILTERS level(category(item), c), canBeSold(item)  
;

DESIGN pricelist {
    details {
        NEW search {
            //showIf = NOT readonly(pr);
            caption = 'Подбор';
            type = SPLITH;
            fill = 1;
            MOVE BOX(TREE categories);
            MOVE BOX(item) { 
                fill = 2;
            }
            MOVE PROPERTY(costPrice(item));
            MOVE PROPERTY(markup(item, tc, dt));
        }       
    }
}

selectAll 'Подобрать все' (Pricelist pr, Category c) {
    FOR [FILTER pricelist.item](Item i) AND NOT in(i, pr) DO NEW l = PricelistLine {
        pricelist(l) <- pr;
        item(l) <- i;
    }
}

EXTEND FORM pricelist
    PROPERTIES in(item, pr) ON CHANGE changeIn(item, pr) BEFORE name(item), selectAll(pr, c) DRAW item TOOLBAR 
;
