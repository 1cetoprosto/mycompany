MODULE SalesPricelistSearch;

REQUIRE SalesOrderStatus, Doc, SalesPriceListStatus;

NAMESPACE Sales;

// Подбор товаров
detail= GROUP SUM 1 BY item(PricelistLine pl), pricelist(pl);

in 'Вкл.' (Item item, Pricelist pr) = TRUE IF detail(item, pr);

changeIn(Item item, Pricelist pricelist)  { 
    INPUT b = BOOLEAN DO
        IF detail(item, pricelist) THEN {
            IF NOT b THEN
                DELETE PricelistLine pl WHERE item == item(pl) AND pricelist == pricelist(pl);
        } ELSE
            IF b THEN 
                NEW d = PricelistLine {
                   pricelist(d) <- pricelist;
                   item(d) <- item;
                }
}

taxes (Item i, PriceType pt, DATETIME dt) = salesTax(i) * priceB(pt, i, dt) / 100;
markup 'Текущая надбавка' (Item i, PriceType t, DATETIME  dt) = IF priceB(t, i, dt) THEN 
                IF taxIncluded(t) THEN round2((priceB(t, i, dt) / (costPrice(i) * (1 (+) salesTax(i) / 100))) * 100 - 100)
                ELSE round2((priceB(t, i, dt) / costPrice(i)) * 100 - 100);

EXTEND FORM pricelist
    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    ORDER name(c)
    
    OBJECTS dt = DATETIME 
    FILTERS dt = startDate(p)
    
    OBJECTS item = Item
    PROPERTIES READONLY name(item),
                        costPrice(item) SHOWIF showCost(p),
                        markup(item, t, dt) COLUMNS 'p' (t) HEADER name(t) + ', %' SHOWIF showMarkup(p),
                        priceB(t, item, dt) COLUMNS 'p' (t) HEADER name(t)
    FILTERS level(category(item), c), canBeSold(item)  
;

DESIGN pricelist {
    details {
        NEW search {
            //showIf = NOT readonly(pr);
            caption = 'Подбор';
            type = SPLITH;
            fill = 1;
            MOVE BOX(TREE categories);
            MOVE BOX(item) { 
                fill = 2;
            }
            MOVE PROPERTY(costPrice(item));
            MOVE PROPERTY(markup(item, t, dt));
        }       
    }
}

selectAll 'Подобрать все' (Pricelist pr, Category c) {
    FOR [FILTER pricelist.item](Item i) AND NOT in(i, pr) DO NEW l = PricelistLine {
        pricelist(l) <- pr;
        item(l) <- i;
    }
}

EXTEND FORM pricelist
    PROPERTIES in(item, p) ON CHANGE changeIn(item, p) BEFORE name(item), selectAll(p, c) DRAW item TOOLBAR 
;
