MODULE SalesPriceListStatus;

REQUIRE SalesPricelist;

NAMESPACE Sales;

CLASS PricelistStatus 'Статус прайс-листа'{
    draft 'Черновик',
    done 'Проведен'
}

name 'Имя' (PricelistStatus s) = staticCaption(s) IF s IS PricelistStatus CHARWIDTH 15;

status 'Статус' = ABSTRACT CASE PricelistStatus (Pricelist);
nameStatus 'Статус' (Pricelist p) = name(status(p));

colorStatus 'Цвет' = ABSTRACT CASE COLOR (Pricelist);

status(Pricelist p) += WHEN p IS Pricelist THEN PricelistStatus.draft;
colorStatus(Pricelist p) += WHEN status(p) = PricelistStatus.draft THEN RGB(187,242,210);

EXTEND FORM pricelist
    PROPERTIES(p) READONLY nameStatus BACKGROUND colorStatus(p)             
;

EXTEND FORM pricelists
    PROPERTIES(p) READONLY nameStatus BACKGROUND colorStatus(p)
;

DESIGN pricelist {
    status {
        MOVE PROPERTY(nameStatus(p));
    }     
}