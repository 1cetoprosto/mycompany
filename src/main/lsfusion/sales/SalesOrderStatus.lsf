MODULE SalesOrderStatus;

REQUIRE SalesOrder, SalesSetting;

NAMESPACE Sales;

CLASS OrderStatus 'Статус заказа'{
    draft 'Черновик предложения'
}

name 'Имя' (OrderStatus s) = staticCaption(s) IF s IS OrderStatus CHARWIDTH 15;

status 'Статус' = ABSTRACT CASE OrderStatus (Order);
nameStatus 'Статус' (Order o) = name(status(o));
colorStatus 'Цвет' = ABSTRACT CASE COLOR (Order);

readonly (OrderLine l) += WHEN NOT status(order(l)) = OrderStatus.draft THEN TRUE;
readonly (Order o) += WHEN NOT status(o) = OrderStatus.draft THEN TRUE;

status(Order o) += WHEN o IS Order THEN OrderStatus.draft;
colorStatus(Order o) += WHEN status(o) == OrderStatus.draft THEN RGB(255,255,255);

EXTEND FORM order
    PROPERTIES(o) nameStatus BACKGROUND colorStatus(o)
;

EXTEND FORM orders
    PROPERTIES(o) READONLY nameStatus BEFORE seriesNumber(o) BACKGROUND colorStatus(o)  
;

DESIGN order {
    NEW statusPane FIRST {
        caption = 'Статусы';
        type = CONTAINERH;
        alignment = STRETCH;                          
        NEW statusActions {
            type = CONTAINERH;                    
            flex = 1;            
        }
        NEW status {
            type = CONTAINERH;                
            MOVE PROPERTY(nameStatus(o)); 
        }
    }
}                