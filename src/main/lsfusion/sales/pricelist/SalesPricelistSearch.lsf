MODULE SalesPricelistSearch;

REQUIRE SalesOrderStatus, Doc, SalesPricelist;

NAMESPACE Sales;

// Подбор товаров
detail= GROUP SUM 1 BY item(PricelistLine pl), pricelist(pl);

in 'Вкл.' (Item item, Pricelist pr) = TRUE IF detail(item, pr);

changeIn(Item item, Pricelist pricelist)  { 
    INPUT b = BOOLEAN DO
        IF detail(item, pricelist) THEN {
            IF NOT b THEN
                DELETE PricelistLine pl WHERE item == item(pl) AND pricelist == pricelist(pl);
        } ELSE
            IF b THEN 
                NEW d = PricelistLine {
                   pricelist(d) <- pricelist;
                   item(d) <- item;
                }
}

EXTEND FORM pricelist
    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    ORDER name(c)

    OBJECTS item=Item
    PROPERTIES in(item, pr) ON CHANGE changeIn(item, pr)
    PROPERTIES READONLY dataName(item) 
    FILTERS level(category(item), c), canBeSold(item)  
;

DESIGN pricelist {
    details {
        NEW search {
            caption = 'Подбор';
            type = SPLITH;
            fill = 1;
            MOVE BOX(TREE categories);
            MOVE BOX(item) { 
                fill = 2;
            }
        }       
    }
}
