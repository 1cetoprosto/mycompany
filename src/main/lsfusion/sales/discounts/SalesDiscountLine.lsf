MODULE SalesDiscountLine;

REQUIRE SalesDiscount, SalesLedgerPartner, SalesPricelistValue;

NAMESPACE Sales;

CLASS ABSTRACT SalesDiscountLine;

dateTime = ABSTRACT DATETIME (SalesDiscountLine);
customer = ABSTRACT Partner (SalesDiscountLine);
item = ABSTRACT Item (SalesDiscountLine);

quantity = ABSTRACT NUMERIC[16,3] (SalesDiscountLine);
price = ABSTRACT NUMERIC[10,2] (SalesDiscountLine);
totalAmount = ABSTRACT NUMERIC[14,2] (SalesDiscountLine);

matches (Discount d, SalesDiscountLine l) =
    set(d, item(l)) AND
    NOT startDateTime(d) > dateTime(l) AND
    NOT endDateTime(d) < dateTime(l) AND
    NOT fromQuantity(d) > quantity(l) AND
    NOT fromAmount(d) > totalAmount(l) AND
    NOT fromAccumulated(d) > prevSoldAmount(customer(l)) AND
    NOT fromAccumulatedPreviousMonth(d) > prevSoldAmountPreviousMonth(customer(l)) COMPLEX;

discountPrice 'Цена со скидкой' (Discount d, SalesDiscountLine l) =
    IF priceType(d) THEN priceA(priceType(d), item(l), dateTime(l)) ELSE NUMERIC[10,2](price(l) * (100.0 - discount(d)) / 100.0); 
    
minSalesDiscountAuto (SalesDiscountLine l) =
    GROUP LAST Discount d ORDER DESC discountPrice(d, l) IF matches(d, l) AND NOT manual(d), d; 

// manual select

FORM selectDiscount 'Выбор скидки'
    OBJECTS l = SalesDiscountLine PANEL
    
    OBJECTS d = Discount
    PROPERTIES(d) READONLY name, manual, discount, namePriceType
    PROPERTIES    READONLY discountPrice(d, l)
    FILTERS matches(d, l)
;

// meta
META defineDocSalesDiscountLine (doc, caption, NS)
    EXTEND CLASS ###doc##Line : SalesDiscountLine;
    
    dateTime(###doc##Line l) += NS.dateTime(l);
    customer(###doc##Line l) += NS.customer(l);
    item(###doc##Line l) += NS.item(l);
    
    quantity(###doc##Line l) += NS.quantity(l);
    price(###doc##Line l) += NS.price(l);
    
    fullAmount (###doc o) = GROUP SUM NUMERIC[14,2](quantity(###doc##Line l) * price(l)) IF doc(l) = o;
    totalAmount(###doc##Line l) += fullAmount(doc(l));
    
    recalcDiscountAuto###doc 'Автоматически рассчитывать скидки в '##caption = DATA BOOLEAN () PREREAD;
    EXTEND FORM options PROPERTIES() recalcDiscountAuto###doc;
    
    WHEN LOCAL (CHANGED(dateTime(###doc##Line l)) OR CHANGED(quantity(l)) OR CHANGED(fullAmount(doc(l))) OR 
                CHANGED(customer(l)) OR CHANGED(item(l)) OR CHANGED(price(l))) AND recalcDiscountAuto###doc() DO
        salesDiscount(l) <- minSalesDiscountAuto(l);
END