MODULE SalesOrderPrint;

REQUIRE SalesOrder;

NAMESPACE Sales;

phoneVendor 'Телефон поставщика' (Order o) = phone(customer(o));
fullAddressVendor 'Адрес поставщика' (Order o) = linesAddress(customer(o)); 
fullAddressLocation 'Адрес места доставки' (Order o ) = linesAddress(location(o));
fullAddressCompany 'Адрес компании' (Order o) = csvAddress(owner(location(o)));
nameCompany 'Компания' (Order o) = name(owner(location(o)));
phoneCompany 'Телефон компании' (Order o) = phone(owner(location(o)));

FORM printOrder 'Заказ поставщику'   
    OBJECTS o = Order PANEL 
    PROPERTIES(o) scheduledDate, number, nameLocation, fullAddressLocation, nameCustomer, fullAddressVendor, phoneVendor, nameCompany, fullAddressCompany, phoneCompany
        
    OBJECTS l = OrderLine
    PROPERTIES(l) nameItem, description, quantity, taxes, subtotal, unitPrice, unitMeasure
    FILTERS order(l) = o
; 

print 'Печать' (Order o) {
    PRINT printOrder OBJECTS o = o;
}  

EXTEND FORM order
     PROPERTIES(o) print
;

DESIGN order {
    secondaryActions {
        MOVE PROPERTY(print(o));             
    }
}