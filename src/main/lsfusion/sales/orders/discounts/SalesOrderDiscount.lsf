MODULE SalesOrderDiscount;

REQUIRE SalesOrder;

NAMESPACE Sales;

discount 'Скидка, %' = DATA NUMERIC[5,2] (OrderLine);

discountPrice 'Цена со скидкой' = DATA NUMERIC[10,2] (OrderLine);
resultPrice (OrderLine l) += discountPrice(l);

WHEN LOCAL (CHANGED(price(OrderLine l)) OR CHANGED(discount(l))) AND NOT CHANGED(discountPrice(l)) DO 
    discountPrice(l) <- NUMERIC[10,2] (price(l) * (100.0 - discount(l)) / 100.0);

recalcDiscount (OrderLine l) { 
    discount(l) <- NUMERIC[5,2](min(100.0 - (discountPrice(l) * 100.0 / price(l)), 100.0));
}
changeDiscountPrice (OrderLine l) {
    INPUT p = discountPrice(l) CHANGE DO {
        recalcDiscount(l);
    } 
}

EXTEND FORM order
    PROPERTIES(l) AFTER price(l) discount, discountPrice ON CHANGE changeDiscountPrice(l)
;