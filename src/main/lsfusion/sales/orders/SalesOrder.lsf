MODULE SalesOrder;

REQUIRE Time, MetaNumerator, Individual, Company,
        MetaTax, TaxItem, BarCode,
        SalesSettings, PartnerSale, ItemSale,
        Incoterms, PaymentTerms, SalesPricelistValue, Doc;  

NAMESPACE Sales;

CLASS Order 'Заказ';
readonly = ABSTRACT CASE BOOLEAN (Order);

dateTime 'Дата' = DATA DATETIME (Order);
WHEN LOCAL SET(Order o IS Order) AND NOT CHANGED(dateTime(o)) DO dateTime(o) <- currentDateTime();

@defineSeriesNumber(order, 'Заказы на продажу', 'ЗР');

customer 'Заказчик' = DATA Partner (Order) NONULL;
nameCustomer 'Заказчик' (Order o) = name(customer(o));

CONSTRAINT customer(Order o) AND NOT isCustomer(customer(o))
                CHECKED BY isCustomer
                MESSAGE 'Партнер должен быть покупателем';

// Quotation
validity 'Срок действия' = DATA DATETIME (Order);

// Invoicing information
invoiceDate 'Дата счета' = DATA DATETIME (Order);

paymentTerms 'Условие оплаты' = DATA PaymentTerm (Order);
namePaymentTerms 'Условие оплаты' (Order o)= name(paymentTerms(o));

addressCustomer 'Адрес получателя' (Order o) = concatAddress(customer(o), ', ');

// Shipping information
scheduledDate 'Срок поставки' = DATA DATETIME (Order);
WHEN LOCAL SET(Order o IS Order) AND NOT CHANGED(scheduledDate(o)) DO scheduledDate(o) <- currentDateTime();     

deliveryAddress 'Адрес доставки' = DATA STRING (Order);
WHEN LOCAL CHANGED(customer(Order o)) DO deliveryAddress(o) <- addressCustomer(o);

location 'Место отгрузки' = DATA Location (Order);
nameLocation 'Место отгрузки' (Order o) = name(location(o));

incoterm 'Условие поставки' = DATA Incoterm (Order);
nameIncoterm 'Условие поставки' (Order o)= name(incoterm(o));

// Sales information
representative 'Наш представитель' = DATA Individual (Order);
nameRepresentative 'Наш представитель' (Order o)= name(representative(o));

CONSTRAINT representative(Order o) AND NOT type(legalEntity(representative(o))) = PartnerType.company 
    CHECKED BY representative MESSAGE 'Должен быть наш работник';

customerReference 'Входящий номер заказчика' = DATA STRING[30] (Order);

// Lines
CLASS OrderLine 'Спецификация';
order =  DATA Order (OrderLine) NONULL DELETE;

index 'Индекс' = PARTITION SUM 1 ORDER OrderLine l BY order(l) IN id MATERIALIZED;

item = DATA Item (OrderLine) NONULL;
nameItem 'Номенклатура' (OrderLine l) = name(item(l));
idBarCodeItem 'Штрих код' (OrderLine l) = idBarCode(item(l));
idItem 'Код' (OrderLine l) = id(item(l));

CONSTRAINT item(OrderLine l) AND NOT canBeSold(item(l))
            CHECKED BY item[OrderLine] 
            MESSAGE 'Номенклатура не предназначена для продажи';

description 'Описание' = DATA ISTRING (OrderLine) CHARWIDTH 20;

unitMeasure 'Ед. изм.' = DATA ItemMeasure (OrderLine);
nameUnitMeasure 'Ед. изм.' (OrderLine l) = name(unitMeasure(l));

quantity 'Количество' = DATA NUMERIC[16,3] (OrderLine);

unitPrice 'Цена' = DATA NUMERIC[10,2] (OrderLine);
cost 'Себестоимость' = DATA NUMERIC[10,2] (OrderLine);
discount 'Скидка %' =  DATA NUMERIC[4,2] (OrderLine);

@defineTaxCalc(order, o, sales);

WHEN LOCAL CHANGED(item(OrderLine ol)) DO {
    description(ol) <- nameItem(ol);
    unitMeasure(ol) <- unitMeasure(item(ol));
    in(ol, Tax t) <- salesIn(item(ol), t); 
}

// price types
priceType 'Тип цен' =  DATA PriceType (Order);
namePriceType 'Тип цен' (Order o) =  name(priceType(o));
 
WHEN LOCAL CHANGED(customer(Order o)) DO priceType(o) <- priceType(customer(o));

WHEN LOCAL (SETCHANGED(item(OrderLine ol)) OR SETCHANGED(priceType(order(ol)))) AND NOT CHANGED(unitPrice(ol)) DO {
    unitPrice(ol) <- OVERRIDE noTaxPriceA(priceType(order(ol)), item(ol), dateTime(order(ol))), salesPrice(item(ol)); 
}

// Line properties
dateTime 'Дата' (OrderLine l) = dateTime(order(l));
seriesNumber 'Номер' (OrderLine l) = seriesNumber(order(l));
nameLocation 'Место доставки' (OrderLine l) = nameLocation(order(l));
nameCustomer 'Заказчик' (OrderLine l) = nameCustomer(order(l));

FORM order 'Заказ'
    OBJECTS o = Order PANEL
    PROPERTIES(o) dateTime READONLY, series, number, nameCustomer, namePriceType,
                  scheduledDate, deliveryAddress, nameLocation, nameIncoterm, 
                  nameRepresentative, customerReference,
                  validity,
                  invoiceDate, namePaymentTerms, addressCustomer
    PROPERTIES(o) READONLY untaxedAmount, totalTax, totalAmount
                 
    OBJECTS l = OrderLine
    PROPERTIES(l) index, nameItem, nameUnitMeasure, idBarCodeItem, idItem, description, quantity, unitPrice, cost, discount, subtotal, taxes ON CHANGE changeTax(l)
    PROPERTIES(l) NEW, DELETE
    FILTERS order(l) = o
    
    EDIT Order OBJECT o    
;      

DESIGN order {
    OBJECTS {
        NEW header {
            alignment = STRETCH;   
            type = CONTAINERH;
            NEW title {
                MOVE PROPERTY(dateTime(o));
                NEW seriesNumber { 
                    type = CONTAINERH; 
                    MOVE PROPERTY(series(o)); 
                    MOVE PROPERTY(number(o));
                }
                MOVE PROPERTY(nameCustomer(o)) { notNull = TRUE; } 
                MOVE PROPERTY(namePriceType(o)); 
            }    
            NEW relatedDoc {
                type = TABBED;
                fill = 1;
            }                                 
        }
        NEW details {
            fill = 7;
            type = TABBED;
            MOVE BOX(l) {
                PROPERTY(unitPrice(l)) { pattern = '#,##0.00'; }
                PROPERTY(subtotal(l))  { pattern = '#,##0.00'; }
            }
            NEW otherInformation {
                caption = 'Прочая информация';
                type = COLUMNS;
                columns = 2;
                NEW quotation {
                    caption = 'Запрос предложений';
                    MOVE PROPERTY(validity(o));
                }
                NEW invoicing {
                    caption = 'Расчеты';
                    MOVE PROPERTY(invoiceDate(o));
                    MOVE PROPERTY(namePaymentTerms(o));                         
                    MOVE PROPERTY(addressCustomer(o));
                }
                NEW shippingInformation {
                    caption = 'Поставка';
                    MOVE PROPERTY(scheduledDate(o));
                    MOVE PROPERTY(deliveryAddress(o));
                    MOVE PROPERTY(nameLocation(o));               
                    MOVE PROPERTY(nameIncoterm(o));
                }
                NEW salesInformation {
                    caption = 'Прочие';
                    MOVE PROPERTY(nameRepresentative(o));                                       
                    MOVE PROPERTY(customerReference(o));               
                }
            }            
        }
        NEW footer {
            align = END;
            type = CONTAINERH; 
            NEW total {
                caption = 'Итого';
                type = CONTAINERH;
                MOVE PROPERTY(untaxedAmount(o)) { pattern = '#,##0.00'; }
                MOVE PROPERTY(totalTax(o)) { pattern = '#,##0.00'; }
                MOVE PROPERTY(totalAmount(o)) { pattern = '#,##0.00'; }                     
            }
        }
    }            
}

FORM orders 'Заказы'
     OBJECTS o = Order
     PROPERTIES(o) READONLY seriesNumber, nameCustomer,
                            scheduledDate, deliveryAddress, nameLocation, nameIncoterm, 
                            invoiceDate, namePaymentTerms, addressCustomer,
                            nameRepresentative, customerReference,
                            untaxedAmount, totalTax, totalAmount
                            
     PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE 
;

DESIGN orders {
    PROPERTY(untaxedAmount(o)) { pattern = '#,##0.00'; }
    PROPERTY(totalTax(o)) { pattern = '#,##0.00'; }
    PROPERTY(totalAmount(o)) { pattern = '#,##0.00'; }
}

NAVIGATOR {
    sales {
        NEW orders FIRST;    
    }
}

@defineDocSearch(order, item, quantity, o, canBeSold);
@defineStatus(order, 'заказа', o);
@defineDocHistory(order, o, item, quantity);

nameStatus 'Статус' (OrderLine l) = nameStatus(order(l)); 

EXTEND FORM order
    FILTERGROUP  pricelist
        FILTER 'В прайс-листе' GROUP SUM 1 IF itm = item(PricelistLine line) AND 
            startDate(pricelist(line)) <= currentDateTime() AND endDate(pricelist(line)) >= currentDateTime();
;