MODULE SalesOrderInvoiceShipment;

REQUIRE SalesOrderInvoice, SalesOrderShipment, InvoiceShipment;

NAMESPACE Invoicing;

EXTEND CLASS InvoicingPolicy {
    delivered 'Отгруженное количество'
}

toInvoiceShipped (Order o) = 
    GROUP SUM done(ShipmentLine sl) IF order(orderLine(sl)) = o AND active(shipment(sl)) AND NOT invoiceLine(sl);
toInvoice(o) += toInvoiceShipped(o) > 0;

createOrderInvoiceLines (Order o, Invoice i) + {
    IF invoicingPolicy() = InvoicingPolicy.delivered THEN {
        location(i) <- location(o);
        
        FOR order(orderLine(ShipmentLine sl)) = o AND active(shipment(sl)) AND NOT invoiceLine(sl) INLINE 
            NEW il = InvoiceLine DO {
            invoice(il) <- i;
            orderLine(il) <- orderLine(sl);
    
            item(il) <- product(sl);
            description(il) <- description(orderLine(sl));
            quantity(il) <- done(sl);
            price(il) <- price(orderLine(sl));
    
            invoiceLine(sl) <- il; 
        }
    }                            
}