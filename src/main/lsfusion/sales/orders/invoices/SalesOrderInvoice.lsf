MODULE SalesOrderInvoice;

REQUIRE SalesOrderConfirmed, 
        InvoiceDone, InvoiceCanceled;

NAMESPACE Sales;

// invoicing policy
CLASS InvoicingPolicy 'Политика оформления накладных' {
    ordered 'Заказанное количество'
}
name 'Имя' (InvoicingPolicy p) = staticCaption(p) IF p IS InvoicingPolicy CHARWIDTH 20;

invoicingPolicy = DATA InvoicingPolicy ();
nameInvoicingPolicy 'Политика оформления накладных' () = name(invoicingPolicy()); 
EXTEND FORM options PROPERTIES() nameInvoicingPolicy;

// lines
orderLine = DATA OrderLine (InvoiceLine);

in (Order o, Invoice i) = GROUP SUM 1 IF order(orderLine(InvoiceLine l)) = o AND invoice(l) = i MATERIALIZED; 

countOrders 'Кол-во заказов' (Invoice i) = GROUP SUM 1 IF in(Order o, i) MATERIALIZED;
descriptionOrders 'Заказы' (Invoice i) = GROUP CONCAT description(Order o) IF in(o, i), ','; 

// source
sourceDocument (Invoice i) += WHEN countOrders(i) THEN descriptionOrders(i);
openSourceDocument (Invoice i) + { FOR in(Order o, i) DO edit(o); }

invoiced 'Оформлено накладных' (OrderLine ol) = 
    GROUP SUM quantity(InvoiceLine bl) IF orderLine(bl) = ol AND ready(invoice(bl)) AND NOT canceled(invoice(bl));
paid 'Оплачено' (OrderLine ol) = 
    GROUP SUM quantity(InvoiceLine bl) IF orderLine(bl) = ol AND done(invoice(bl)) AND NOT canceled(invoice(bl));
toInvoice 'К оформлению' (OrderLine l) = max(quantity(l) (-) invoiced(l), 0);

toInvoice 'К оформлению' (Order o) = 
    GROUP SUM toInvoice(OrderLine ol) IF order(ol) = o;

createOrderInvoiceLines ABSTRACT LIST (Order, Invoice);

createInvoice 'Оформить накладную' (Order o) {
    APPLY;
    IF canceled() THEN RETURN;
               
    NEWSESSION {
        NEW ni = Invoice {
            ready(ni) <- TRUE;    

            customer(ni) <- customer(o);
            customerReference(ni) <- customerReference(o); 
            paymentTerms(ni) <- paymentTerms(o);
            
            FOR order(OrderLine ol) = o AND toInvoice(ol) > 0 AND 
                (invoicingPolicy() = InvoicingPolicy.ordered OR NOT invoicingPolicy() OR NOT item(ol) IS Product) 
                NEW il = InvoiceLine DO {
                invoice(il) <- ni;
                item(il) <- item(ol);
                description(il) <- description(ol);
                quantity(il) <- toInvoice(ol);
                price(il) <- price(ol);
                orderLine(il) <- ol;
            }                            
            createOrderInvoiceLines(o, ni);
            executeLocalEvents();
            FOR invoice(InvoiceLine il) = ni DO {
                in(il, Tax t) <- in(orderLine(il), t);
            }                                
            SHOW invoice OBJECTS i = ni DOCKED;
        }
    }
}

FORM orderInvoiceShow 'Оформлено накладными'
    OBJECTS ol = OrderLine
    
    OBJECTS il = InvoiceLine
    PROPERTIES(il) READONLY nameStatus, number, dateTime, quantity    
    FILTERS orderLine(il) = ol
;

EXTEND FORM order   
    PROPERTIES(o) SHOWIF (status(o) = OrderStatus.confirmed AND toInvoice(o) > 0) createInvoice
    PROPERTIES(l) SHOWIF confirmed(o) invoiced ON CHANGE { DIALOG orderInvoiceShow OBJECTS ol = l; } BACKGROUND NOT invoiced(l) = quantity(l)
    PROPERTIES(l) SHOWIF confirmed(o) paid ON CHANGE { DIALOG orderInvoiceShow OBJECTS ol = l; } BACKGROUND NOT paid(l) = quantity(l)
    
    OBJECTS in = Invoice
    PROPERTIES(in) READONLY nameStatus BACKGROUND colorStatus(in), number, dateTime    
    PROPERTIES(in) NEWSESSION EDIT GRID   
    FILTERS in(o, in)   
;
 
DESIGN order {
    primaryActions {
        MOVE PROPERTY(createInvoice(o)) FIRST { fontStyle = 'bold'; };
    }
    relatedDoc  {
        MOVE BOX(in) {
            GRID(in) { headerHeight = 24; }
        }
        REMOVE TOOLBARBOX(in);
    }
}