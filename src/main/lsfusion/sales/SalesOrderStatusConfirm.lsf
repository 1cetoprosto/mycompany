MODULE SalesOrderStatusConfirm;

REQUIRE SalesOrderStatus, SalesOrderStatusDraft, SalesOrderStatusSent;

NAMESPACE Sales;

EXTEND CLASS OrderStatus{
    orderConfirmed 'Заказ оформлен'
}

confirmedDateTime 'Подтвержден' = DATA DATETIME (Order);
confirmed 'Подтвержден' = DATA BOOLEAN (Order);

confirmOrder 'Подтвердить заказ' (Order o){
    confirmedDateTime(o) <- currentDateTime();
    confirmed(o) <- TRUE;
    MESSAGE 'Заказ подтвержден';    
}

status(Order o) += WHEN confirmed(o) THEN OrderStatus.orderConfirmed;
colorStatus(Order o) += WHEN status(o) == OrderStatus.orderConfirmed THEN RGB(150,255,150);

EXTEND FORM order 
     PROPERTIES (o) SHOWIF (status(o)=OrderStatus.draft OR status(o)=OrderStatus.offerSent)  confirmOrder BACKGROUND (CASE WHEN NOT confirmed(o) AND sent(o) THEN nextAction())
     PROPERTIES (o) confirmedDateTime
;

DESIGN order {
    statusPane {
        statusActions {
            MOVE PROPERTY(confirmOrder(o));
        }
    }
    title {
        MOVE PROPERTY(confirmedDateTime(o));
    }
}

EXTEND FORM orders
    PROPERTIES(o) READONLY SHOWIF showActionsTime() confirmedDateTime BEFORE seriesNumber(o);
;