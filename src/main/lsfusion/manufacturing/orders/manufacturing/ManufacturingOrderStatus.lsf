MODULE ManufacturingOrderStatus;

REQUIRE ManufacturingOrder, Doc;

NAMESPACE Manufacturing;

@defineStatus(manufacturingOrder, 'заказа на производство', o);

CLASS MaterialsAvailabilityStatus 'Доступность материалов' {
    available 'доступны',
    waiting 'в ожидании'
}

name 'Имя' (MaterialsAvailabilityStatus a) = staticCaption(a) IF a IS MaterialsAvailabilityStatus CHARWIDTH 15;

countConsumedLines (ManufacturingOrder m) = GROUP SUM 1 IF manufacturingOrder(ConsumedLine l) = m;

materialsAvailabilityStatus 'Доступность материалов' (ManufacturingOrder m) = IF 
    countConsumedLines(m) = GROUP SUM 1 IF manufacturingOrder(ConsumedLine l) = m AND toConsume(l) = reserved(l) 
    THEN MaterialsAvailabilityStatus.available
    ELSE MaterialsAvailabilityStatus.waiting;
nameMaterialsAvailabilityStatus 'Доступность материалов' (ManufacturingOrder m) = name(materialsAvailabilityStatus(m));
colorMaterialsAvailabilityStatus 'Цвет' (ManufacturingOrder m) = 
    IF materialsAvailabilityStatus(m) = MaterialsAvailabilityStatus.available THEN RGB(187,242,210)
    ELSE RGB(250,150,157);

EXTEND FORM manufacturingOrders
    PROPERTIES(o) READONLY nameMaterialsAvailabilityStatus BACKGROUND colorMaterialsAvailabilityStatus(o)
;

DESIGN manufacturingOrder {
    secondary {
        MOVE PROPERTY(copyManufacturingOrder(o)); 
    }
}

