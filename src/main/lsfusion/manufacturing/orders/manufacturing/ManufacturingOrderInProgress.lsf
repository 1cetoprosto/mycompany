MODULE ManufacturingOrderInProgress;

REQUIRE ManufacturingOrderReady, Input;

NAMESPACE Manufacturing;

EXTEND CLASS ManufacturingOrderStatus {
    inProgress 'В процессе'
}
inProgress 'В процессе' = DATA BOOLEAN (ManufacturingOrder);

status(ManufacturingOrder m) += WHEN inProgress(m) THEN ManufacturingOrderStatus.inProgress;
colorStatus(ManufacturingOrder m) += WHEN status(m) = ManufacturingOrderStatus.inProgress THEN RGB(252,247,149);

produced 'Произведено' = DATA NUMERIC[16,3] (FinishedLine);
produced 'Произведено' (ManufacturingOrder o) = GROUP SUM produced(FinishedLine l) IF manufacturingOrder(l) = o AND item(l) = item(o) MATERIALIZED;

consumed 'Израсходовано' = DATA NUMERIC[16,3] (ConsumedLine);

// produce
setProduced (ManufacturingOrder o, NUMERIC[16,3] q) {
    produced(FinishedLine l) <- NUMERIC[16,3](toProduce(l) * q / toProduce(o)); 
    consumed(ConsumedLine l) <- NUMERIC[16,3](toConsume(l) * q / toProduce(o)); 
}

changeProduced (ManufacturingOrder o) {
    INPUT q = NUMERIC[16,3] DO
        setProduced(o, q);
}

produce 'Произвести' (ManufacturingOrder m) {
    APPLY;
    IF canceled() THEN RETURN;
        
    NEWSESSION {
        DIALOG dialogQuantity OBJECTS q = toProduce(m) INPUT DO {
            setProduced(m, q);
            inProgress(m) <- TRUE;
            APPLY;
        }
    }
}

EXTEND FORM manufacturingOrder
    PROPERTIES(o) produce SHOWIF (status(o) = ManufacturingOrderStatus.ready OR status(o) = ManufacturingOrderStatus.inProgress), 
                  inProgress 
                  
    PROPERTIES SHOWIF inProgress(o) produced(o) ON CHANGE changeProduced(o),
                                    produced(l),
                                    consumed(c) 
;

DESIGN manufacturingOrder {
    primaryActions {
        MOVE PROPERTY(produce(o)) { fontStyle = 'bold'; }
    }
    status {
        MOVE PROPERTY(inProgress(o));
    }
    quantity {
        MOVE PROPERTY(produced(o));
    }
}

EXTEND FORM manufacturingOrders
    PROPERTIES(o) produced AFTER toProduce(o)
;