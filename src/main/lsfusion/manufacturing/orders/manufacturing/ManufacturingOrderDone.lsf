MODULE ManufacturingOrderDone;

REQUIRE ManufacturingOrderInProgress;

NAMESPACE Manufacturing;

EXTEND CLASS ManufacturingOrderStatus {
    done 'Завершен'
}

done 'Завершен' = DATA BOOLEAN (ManufacturingOrder);

produce 'Произвести' (ManufacturingOrder m) {
    APPLY;
    IF canceled() THEN RETURN;
        
    NEWSESSION {
        FOR manufacturingOrder(ConsumedLine l) = m DO {
            consumed(l) <- reserved(l);
        }
        
        FOR bom(ByproductLine bl) = bom(m) DO NEW l = FinishedLine {
            manufacturingOrder(l) <- m;
            product(l) <- product(bl);
            unitMeasure(l) <- unitMeasure(bl);
            done(l) <- NUMERIC[16,3](quantity(bl) * quantity(m));
        }
        
        NEW l = FinishedLine {
             manufacturingOrder(l) <- m;
             product(l) <- product(m);
             unitMeasure(l) <- unitMeasure(m);
             done(l) <- quantity(m);
        } 
        done(m) <- TRUE;
        manufacturingDate(m) <- currentDateTime();
        APPLY;
    }
}

status(ManufacturingOrder m) += WHEN done(m) THEN ManufacturingOrderStatus.done;

EXTEND FORM manufacturingOrder
    PROPERTIES(o) produce SHOWIF status(o) = ManufacturingOrderStatus.inprogress, done READONLY 
;

DESIGN manufacturingOrder {
    primary {
        MOVE PROPERTY(produce(o)) { fontStyle = 'bold'; }
    }
    status {
        MOVE PROPERTY(done(o));
    }
}
