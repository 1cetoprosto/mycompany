MODULE ManufacturingOrderCost;

REQUIRE ManufacturingOrderCanceled, CostLedger, CostLocation;

NAMESPACE Manufacturing;

// расходная проводка
CLASS ConsumedOutCostLedger : OutCostLedger;
outCostLedger = AGGR ConsumedOutCostLedger WHERE consumed(ConsumedLine line) AND active(manufacturingOrder(line)) AND item(line) IS Product;

dateTime(ConsumedOutCostLedger l) += manufacturingDate(manufacturingOrder(line(l)));
number(ConsumedOutCostLedger l) += number(manufacturingOrder(line(l)));

location(ConsumedOutCostLedger l) += costLocation(materialsLocation(manufacturingOrder(line(l))));

product(ConsumedOutCostLedger l) += item(line(l)) AS Product;
quantity(ConsumedOutCostLedger l) += consumed(line(l));

edit(ConsumedOutCostLedger l) + { edit(line(l)); }

// приходная проводка
CLASS ConsumedInCostLedger : InCostLedger;
inCostLedger = AGGR ConsumedInCostLedger WHERE done(FinishedLine line) AND active(manufacturingOrder(line)) AND item(line) IS Product;

dateTime(ConsumedInCostLedger l) += manufacturingDate(manufacturingOrder(line(l)));
number(ConsumedInCostLedger l) += number(manufacturingOrder(line(l)));

location(ConsumedInCostLedger l) += costLocation(productsLocation(manufacturingOrder(line(l))));

product(ConsumedInCostLedger l) += item(line(l)) AS Product;
quantity(ConsumedInCostLedger l) += done(line(l));

cost (ManufacturingOrder o) = GROUP SUM sum(ConsumedOutCostLedger l) IF manufacturingOrder(line(l)) = o;

calcSum (ConsumedInCostLedger l) += cost(manufacturingOrder(line(l))) IF item(line(l)) = item(manufacturingOrder(line(l)));

edit(ConsumedInCostLedger l) + { edit(line(l)); }
