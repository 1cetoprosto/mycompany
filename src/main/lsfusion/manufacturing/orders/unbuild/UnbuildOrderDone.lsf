MODULE UnbuildOrderDone;

REQUIRE UnbuildOrder;

NAMESPACE Manufacturing;

EXTEND CLASS UnbuildOrderStatus {
    done 'Завершен'
}

done 'Завершен' = DATA BOOLEAN (UnbuildOrder);

unbuild 'Разобрать' (UnbuildOrder o) {
    APPLY;
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        IF manufactureComponents(bom(o)) THEN {
            FOR finalBom(bom(o), ComponentLine cl, TEXT path) AND NOT manufactureComponents(bom(cl)) DO NEW l = UnbuildOrderLine {
                unbuildOrder(l) <- o;
                dateTime(l) <- currentDateTime();
                item(l) <- item(cl);
                unitMeasure(l) <- unitMeasure(cl);
                quantity(l) <- NUMERIC[16,3](quantity(bom(o), cl, path) * quantity(o));
                costRatio(l) <- NUMERIC[16,3](costRatio(cl));
            } 
        }
        ELSE
            FOR bom(ComponentLine cl) = bom(o) DO NEW l = UnbuildOrderLine {
                unbuildOrder(l) <- o;
                dateTime(l) <- currentDateTime();
                item(l) <- item(cl);
                unitMeasure(l) <- unitMeasure(cl);
                quantity(l) <- NUMERIC[16,3](quantity(cl) * quantity(o));
                costRatio(l) <- NUMERIC[16,3](costRatio(cl));
            }

        done(o) <- TRUE;
        APPLY;
    }
}

status(UnbuildOrder o) += WHEN done(o) THEN UnbuildOrderStatus.done;

EXTEND FORM unbuildOrder
    PROPERTIES(o) unbuild SHOWIF status(o) = UnbuildOrderStatus.draft, done READONLY 
;

DESIGN unbuildOrder {
    primaryActions {
        MOVE PROPERTY(unbuild(o)) { fontStyle = 'bold'; }
    }
    status {
        MOVE PROPERTY(done(o));
    }
}
