MODULE UnbuildOrderInv;

REQUIRE UnbuildOrderDone, InvLedger;

NAMESPACE Manufacturing;

CLASS UnbuildLedger : InvLedger;
uLedger = AGGR UnbuildLedger WHERE quantity(UnbuildOrderLine line) AND item(line) IS Product;

active(UnbuildLedger l) += done(unbuildOrder(line(l)));
dateTime(UnbuildLedger l) += date(line(l));
toLocation(UnbuildLedger l) += locationTo(unbuildOrder(line(l)));

product(UnbuildLedger l) += item(line(l)) AS Product;
quantity(UnbuildLedger l) += quantity(line(l));
description(UnbuildLedger l) += 'Разбор' IF l IS UnbuildLedger;

number(UnbuildLedger l) += number(unbuildOrder(line(l)));

edit (UnbuildLedger l) + { edit(unbuildOrder(line(l))); }

CLASS UnbuildOrderLedger : InvLedger;
unbuildLedger = AGGR UnbuildOrderLedger WHERE done(UnbuildOrder line) AND product(line) IS Product;

active(UnbuildOrderLedger l) += done[UnbuildOrder](line(l));
dateTime(UnbuildOrderLedger l) += currentDateTime();
fromLocation(UnbuildOrderLedger l) += locationFrom(line(l));

product(UnbuildOrderLedger l) += product(line(l)) AS Product;
quantity(UnbuildOrderLedger l) += quantity(line(l));
description(UnbuildOrderLedger l) += 'Разбор' IF l IS UnbuildOrderLedger;

number(UnbuildOrderLedger l) += number(line(l));

edit (UnbuildOrderLedger l) + { edit(line(l)); }
