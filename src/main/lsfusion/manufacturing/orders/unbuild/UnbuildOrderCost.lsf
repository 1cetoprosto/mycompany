MODULE UnbuildOrderCost;

REQUIRE UnbuildOrderDone, CostLedger, CostLocation;

NAMESPACE Manufacturing;

// расходная проводка
CLASS UnbuildOutCostLedger : OutCostLedger;
outCostLedger = AGGR UnbuildOutCostLedger WHERE quantity(unbuildOrder(UnbuildOrderLine line)) 
                                                AND done(unbuildOrder(line)) AND item(unbuildOrder(line)) IS Product;

dateTime(UnbuildOutCostLedger l) += date(line(l));
location(UnbuildOutCostLedger l) += costLocation(locationFrom(unbuildOrder(line(l))));

product(UnbuildOutCostLedger l) += item(unbuildOrder(line(l))) AS Product;
quantity(UnbuildOutCostLedger l) += quantity(unbuildOrder(line(l)));

edit(UnbuildOutCostLedger l) + { edit(line(l)); }

// приходная проводка
CLASS UnbuildInCostLedger : InCostLedger;
inCostLedger = AGGR UnbuildInCostLedger WHERE quantity(UnbuildOrderLine line) 
                                                AND done(unbuildOrder(line)) AND item(line) IS Product;

dateTime(UnbuildInCostLedger l) += date(line(l));
location(UnbuildInCostLedger l) += costLocation(locationTo(unbuildOrder(line(l))));

product(UnbuildInCostLedger l) += item(line(l)) AS Product;
quantity(UnbuildInCostLedger l) += quantity(line(l));

sum (UnbuildInCostLedger l) += NUMERIC[16,3](cost(item(unbuildOrder(line(l)))) * costRatio(line(l)));

edit(UnbuildInCostLedger l) + { edit(line(l)); }
