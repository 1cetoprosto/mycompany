MODULE BoM;

REQUIRE Product, Company, Operation, ManufacturingSettings, MetaNumerator, BarCode;

NAMESPACE Manufacturing;

CLASS Bom 'Спецификация';

unbuild 'Для разбора' = DATA BOOLEAN (Bom) CHARWIDTH 10;
archived 'Неактивный' = DATA BOOLEAN (Bom) CHARWIDTH 10;
active 'Активный' (Bom b) = NOT archived(b) CHARWIDTH 10;

item 'Номенклатура' = DATA Item (Bom) NONULL DELETE;
nameItem 'Номенклатура' (Bom b) = name(item(b));
quantity 'Кол-во' = DATA NUMERIC[16,3] (Bom) NONULL;
unitMeasure 'Ед.изм.' = DATA ItemMeasure (Bom);
nameUnitMeasure 'Ед.изм.' (Bom b) = name(unitMeasure(b));

@defineSeriesNumber(bom, 'Спецификации', 'СП');

bom (STRING[50] id) = GROUP AGGR Bom b BY seriesNumber(b);

company 'Компания' = DATA Partner (Bom);
nameCompany 'Компания' (Bom b) = name(company(b));

CONSTRAINT company(Bom b) AND NOT company(b) IS Company
    CHECKED BY company[Bom]
    MESSAGE 'Нужно выбрать компанию';

sequence 'Последовательность' = DATA INTEGER (Bom) CHARWIDTH 30;

operation 'Операция' = DATA Operation (Bom);
nameOperation 'Операция' (Bom b) = name(operation(b));

CLASS ComponentLine 'Строка компонентов';

bom 'Спецификация' = DATA Bom (ComponentLine) NONULL DELETE;
index 'Индекс' = PARTITION SUM 1 ORDER ComponentLine l BY bom(l) IN id MATERIALIZED;

item 'Номенклатура' = DATA Item (ComponentLine);
nameItem 'Номенклатура' (ComponentLine l) = name(item(l));
quantity 'Кол-во' = DATA NUMERIC[16,3] (ComponentLine);
unitMeasure 'Ед.изм.' = DATA ItemMeasure (ComponentLine);
nameUnitMeasure 'Ед.изм.' (ComponentLine l) = name(unitMeasure(l));
idBarCode 'Штрих код' (ComponentLine l) = idBarCode(item(l));
id 'Код' (ComponentLine l) = id(item(l));
costRatio 'Доля стоимости' (ComponentLine l) = quantity(l) * cost(item(bom(l))) / 
                ((GROUP SUM quantity(ComponentLine ll) IF bom(ll) = bom(l)) * 100);

CLASS ByproductLine 'Строка производных номенклатур';

bom 'Спецификация' = DATA Bom (ByproductLine) NONULL DELETE;
index 'Индекс' = PARTITION SUM 1 ORDER ByproductLine l BY bom(l) IN id MATERIALIZED;

item 'Номенклатура' = DATA Item (ByproductLine);
nameItem 'Номенклатура' (ByproductLine l) = name(item(l));
quantity 'Кол-во' = DATA NUMERIC[16,3] (ByproductLine);
unitMeasure 'Ед.изм.' = DATA ItemMeasure (ByproductLine);
idBarCode 'Штрих код' (ByproductLine l) = idBarCode(item(l));
id 'Код' (ByproductLine l) = id(item(l));
nameUnitMeasure 'Ед.изм.' (ByproductLine l) = name(unitMeasure(l));

WHEN LOCAL SETCHANGED (item(Bom b)) DO {
    unitMeasure(b) <- unitMeasure(item(b));
} 

WHEN LOCAL SETCHANGED (item(ComponentLine c)) DO {
    unitMeasure(c) <- unitMeasure(item(c));
}

WHEN LOCAL SETCHANGED (item(ByproductLine l)) DO {
    unitMeasure(l) <- unitMeasure(item(l));
}

FORM bom 'Спецификация'
    OBJECTS b = Bom PANEL 
    PROPERTIES(b) nameItem, quantity, nameUnitMeasure, series, number, nameCompany, archived, unbuild
    PROPERTIES(b) sequence, nameOperation
    
    OBJECTS c = ComponentLine
    PROPERTIES(c) index, nameItem, nameUnitMeasure, idBarCode, id, quantity, costRatio SHOWIF unbuild(b), NEW, DELETE 
    FILTERS bom(c) = b
    
    OBJECTS l = ByproductLine
    PROPERTIES(l) index, nameItem, nameUnitMeasure, idBarCode, id, quantity, NEW, DELETE
    FILTERS bom(l) = b
    
    EDIT Bom OBJECT b
;

DESIGN bom {
    OBJECTS {
        NEW header {
            alignment = STRETCH;
            type = CONTAINERH;
            NEW leftHeader {
                caption = '';
                MOVE PROPERTY(nameItem(b)) { notNull = TRUE; }
                NEW booleans {
                    type = CONTAINERH;
                    MOVE PROPERTY(archived(b));
                    MOVE PROPERTY(unbuild(b));
                }
                NEW item {
                    type = CONTAINERH;
                    MOVE PROPERTY(quantity(b));
                    MOVE PROPERTY(nameUnitMeasure(b));
                }
            }
            NEW rightHeader {
                caption = '';
                MOVE PROPERTY(series(b));
                MOVE PROPERTY(number(b));
                MOVE PROPERTY(nameCompany(b));
            }            
        }
        NEW details {
            fill = 1;
            type = TABBED;
            NEW components {
                fill = 1;
                caption = 'Компоненты';
                MOVE BOX(c);
            }
            NEW misc {
                fill = 1;
                caption = 'Разное';
                type = CONTAINERH;
                NEW left {
                    MOVE PROPERTY(sequence(b));
                }
                NEW right {
                    MOVE PROPERTY(nameOperation(b));
                }
            }
            NEW byproducts {
                fill = 1;
                caption = 'Производные номенклатуры';
                MOVE BOX(l);
            }
        }
    }
}

FORM boms 'Спецификации'
    OBJECTS b = Bom
    PROPERTIES(b) READONLY nameItem, quantity, nameUnitMeasure, seriesNumber, nameCompany, archived
    PROPERTIES(b) NEWSESSION NEW, EDIT, DELETE 
    
    LIST Bom OBJECT b
;

NAVIGATOR {
    manufacturing {
        NEW boms;
    }
}