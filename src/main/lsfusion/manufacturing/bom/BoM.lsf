MODULE BoM;

REQUIRE Product, BoMType, Company, ManufacturingReadiness, Operation, ManufacturingNavigator;

NAMESPACE Manufacturing;

CLASS Bom 'Технологическая карта';

archived 'Неактивный' = DATA BOOLEAN (Bom) CHARWIDTH 10;
active 'Активный' (Bom b) = NOT archived(b) CHARWIDTH 10;

product 'Номенклатура' = DATA Product (Bom) NONULL DELETE;
nameProduct 'Номенклатура' (Bom b) = name(product(b));
quantity 'Кол-во' = DATA NUMERIC[16,3] (Bom);
unitMeasure 'Ед.изм.' = DATA ItemMeasure (Bom);
nameUnitMeasure 'Ед.изм.' (Bom b) = name(unitMeasure(b));

reference 'Код' = DATA STRING[50] (Bom) NONULL IN id;
bom (STRING[50] reference) = GROUP AGGR Bom b BY reference(b);

bomType 'Тип' = DATA BomType (Bom);
nameType 'Тип' (Bom b) = name(bomType(b));

company 'Компания' = DATA Partner (Bom);
nameCompany 'Компания' (Bom b) = name(company(b));

CONSTRAINT company(Bom b) AND NOT company(b) IS Company
    CHECKED BY company[Bom]
    MESSAGE 'Нужно выбрать компанию';

sequence 'Последовательность' = DATA INTEGER (Bom) CHARWIDTH 30;
manufacturingReadiness 'Начать производство' = DATA ManufacturingReadiness (Bom);
nameReadiness 'Начать производство' (Bom b) = name(manufacturingReadiness(b));
operation 'Операция' = DATA Operation (Bom);
nameOperation 'Операция' (Bom b) = name(operation(b));

CLASS ComponentLine 'Строка компонентов';

bom 'Технологическая карта' = DATA Bom (ComponentLine) NONULL DELETE;
product 'Номенклатура' = DATA Product (ComponentLine);
nameProduct 'Номенклатура' (ComponentLine l) = name(product(l));
quantity 'Кол-во' = DATA NUMERIC[16,3] (ComponentLine);
unitMeasure 'Ед.изм.' = DATA ItemMeasure (ComponentLine);
nameUnitMeasure 'Ед.изм.' (ComponentLine l) = name(unitMeasure(l));

CLASS ByproductLine 'Строка производных номенклатур';

bom 'Технологическая карта' = DATA Bom (ByproductLine) NONULL DELETE;
product 'Номенклатура' = DATA Product (ByproductLine);
nameProduct 'Номенклатура' (ByproductLine l) = name(product(l));
quantity 'Кол-во' = DATA NUMERIC[16,3] (ByproductLine);
unitMeasure 'Ед.изм.' = DATA ItemMeasure (ByproductLine);
nameUnitMeasure 'Ед.изм.' (ByproductLine l) = name(unitMeasure(l));

WHEN LOCAL SETCHANGED (product(Bom b)) DO {
    unitMeasure(b) <- unitMeasure(product(b));
} 

WHEN LOCAL SETCHANGED (product(ComponentLine c)) DO {
    unitMeasure(c) <- unitMeasure(product(c));
}

WHEN LOCAL SETCHANGED (product(ByproductLine l)) DO {
    unitMeasure(l) <- unitMeasure(product(l));
}

FORM bom 'Технологическая карта'
    OBJECTS b = Bom PANEL 
    PROPERTIES(b) nameProduct, quantity, nameUnitMeasure, reference, nameType, nameCompany, archived
    PROPERTIES(b) sequence, nameReadiness, nameOperation
    
    OBJECTS c = ComponentLine
    PROPERTIES(c) nameProduct, quantity, nameUnitMeasure, NEW, DELETE 
    FILTERS bom(c) = b
    
    OBJECTS l = ByproductLine
    PROPERTIES (l) nameProduct, quantity, nameUnitMeasure, NEW, DELETE
    FILTERS bom(l) = b
    
    EDIT Bom OBJECT b
;

DESIGN bom {
    OBJECTS {
        NEW header {
            alignment = STRETCH;
            type = CONTAINERH;
            NEW leftHeader {
                caption = '';
                MOVE PROPERTY(nameProduct(b)) { notNull = TRUE; }
                MOVE PROPERTY(nameType(b));
                MOVE PROPERTY(archived(b));
                NEW item {
                    type = CONTAINERH;
                    MOVE PROPERTY(quantity(b));
                    MOVE PROPERTY(nameUnitMeasure(b));
                }
            }
            NEW rightHeader {
                caption = '';
                MOVE PROPERTY(reference(b)) { notNull = TRUE; }
                MOVE PROPERTY(nameType(b));
                MOVE PROPERTY(nameCompany(b));
            }            
        }
        NEW details {
            fill = 1;
            type = TABBED;
            NEW components {
                fill = 1;
                caption = 'Компоненты';
                MOVE BOX(c);
            }
            NEW misc {
                fill = 1;
                caption = 'Разное';
                type = CONTAINERH;
                NEW left {
                    MOVE PROPERTY(sequence(b));
                }
                NEW rigth {
                    MOVE PROPERTY(nameReadiness(b));
                    MOVE PROPERTY(nameOperation(b));
                }
            }
            NEW byproducts {
                fill = 1;
                caption = 'Производные номенклатуры';
                MOVE BOX(l);
            }
        }
    }
}

FORM boms 'Технологические карты'
    OBJECTS b = Bom
    PROPERTIES(b) READONLY nameProduct, quantity, nameUnitMeasure, reference, nameType, nameCompany, archived
    PROPERTIES(b) NEWSESSION NEW, EDIT, DELETE 
    
    LIST Bom OBJECT b
;

NAVIGATOR {
    manufacturing {
        NEW boms;
    }
}