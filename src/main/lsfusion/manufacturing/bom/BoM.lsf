MODULE BoM;

REQUIRE Product, Company, Operation, ManufacturingSettings;

NAMESPACE Manufacturing;

CLASS Bom 'Технологическая карта';

unbuild 'Для разбора' = DATA BOOLEAN (Bom) CHARWIDTH 10;
archived 'Неактивный' = DATA BOOLEAN (Bom) CHARWIDTH 10;
active 'Активный' (Bom b) = NOT archived(b) CHARWIDTH 10;

item 'Номенклатура' = DATA Item (Bom) NONULL DELETE;
nameItem 'Номенклатура' (Bom b) = name(item(b));
quantity 'Кол-во' = DATA NUMERIC[16,3] (Bom) NONULL;
unitMeasure 'Ед.изм.' = DATA ItemMeasure (Bom);
nameUnitMeasure 'Ед.изм.' (Bom b) = name(unitMeasure(b));

reference 'Код' = DATA STRING[50] (Bom) IN id;
bom (STRING[50] reference) = GROUP AGGR Bom b BY reference(b);

numeratorBom 'Нумератор заказов' = DATA Numerator();
nameNumeratorBom 'Нумератор заказов' = name(numeratorBom());

loadInitialData () + {
    NEWSESSION {
        NEW n = Numerator {
            name(n) <- 'Технологических карт';
            series(n) <- 'BM';
            minValue(n) <- 1;
            maxValue(n) <- 999999;
            stringLength(n) <- 6;
            numeratorBom() <- n;
        }
        APPLY;
    }
}

EXTEND FORM options
    PROPERTIES nameNumeratorBom()
;

DESIGN options {
    OBJECTS {
        MOVE PROPERTY(nameNumeratorBom());
    }
}

WHEN SETCHANGED(Bom m IS Bom  AND numeratorBom() AND NOT reference(m)) DO {
    reference(m) <- STRING[50](CONCAT '/', nameItem(m), series(numeratorBom()), curStringValue(numeratorBom()));
    incrementValueSession(numeratorBom());   
}

company 'Компания' = DATA Partner (Bom);
nameCompany 'Компания' (Bom b) = name(company(b));

CONSTRAINT company(Bom b) AND NOT company(b) IS Company
    CHECKED BY company[Bom]
    MESSAGE 'Нужно выбрать компанию';

sequence 'Последовательность' = DATA INTEGER (Bom) CHARWIDTH 30;

operation 'Операция' = DATA Operation (Bom);
nameOperation 'Операция' (Bom b) = name(operation(b));

CLASS ComponentLine 'Строка компонентов';

bom 'Технологическая карта' = DATA Bom (ComponentLine) NONULL DELETE;
item 'Номенклатура' = DATA Item (ComponentLine);
nameItem 'Номенклатура' (ComponentLine l) = name(item(l));
quantity 'Кол-во' = DATA NUMERIC[16,3] (ComponentLine);
unitMeasure 'Ед.изм.' = DATA ItemMeasure (ComponentLine);
nameUnitMeasure 'Ед.изм.' (ComponentLine l) = name(unitMeasure(l));
costRatio 'Доля стоимости' (ComponentLine l) = quantity(l) * cost(item(bom(l))) / 
                ((GROUP SUM quantity(ComponentLine ll) IF bom(ll) = bom(l)) * 100);

CLASS ByproductLine 'Строка производных номенклатур';

bom 'Технологическая карта' = DATA Bom (ByproductLine) NONULL DELETE;
item 'Номенклатура' = DATA Item (ByproductLine);
nameItem 'Номенклатура' (ByproductLine l) = name(item(l));
quantity 'Кол-во' = DATA NUMERIC[16,3] (ByproductLine);
unitMeasure 'Ед.изм.' = DATA ItemMeasure (ByproductLine);
nameUnitMeasure 'Ед.изм.' (ByproductLine l) = name(unitMeasure(l));

WHEN LOCAL SETCHANGED (item(Bom b)) DO {
    unitMeasure(b) <- unitMeasure(item(b));
} 

WHEN LOCAL SETCHANGED (item(ComponentLine c)) DO {
    unitMeasure(c) <- unitMeasure(item(c));
}

WHEN LOCAL SETCHANGED (item(ByproductLine l)) DO {
    unitMeasure(l) <- unitMeasure(item(l));
}

FORM bom 'Технологическая карта'
    OBJECTS b = Bom PANEL 
    PROPERTIES(b) nameItem, quantity, nameUnitMeasure, reference, nameCompany, archived, unbuild
    PROPERTIES(b) sequence, nameOperation
    
    OBJECTS c = ComponentLine
    PROPERTIES(c) nameItem, quantity, nameUnitMeasure, costRatio SHOWIF unbuild(b), NEW, DELETE 
    FILTERS bom(c) = b
    
    OBJECTS l = ByproductLine
    PROPERTIES(l) nameItem, quantity, nameUnitMeasure, NEW, DELETE
    FILTERS bom(l) = b
    
    EDIT Bom OBJECT b
;

DESIGN bom {
    OBJECTS {
        NEW header {
            alignment = STRETCH;
            type = CONTAINERH;
            NEW leftHeader {
                caption = '';
                MOVE PROPERTY(nameItem(b)) { notNull = TRUE; }
                NEW booleans {
                    type = CONTAINERH;
                    MOVE PROPERTY(archived(b));
                    MOVE PROPERTY(unbuild(b));
                }
                NEW item {
                    type = CONTAINERH;
                    MOVE PROPERTY(quantity(b));
                    MOVE PROPERTY(nameUnitMeasure(b));
                }
            }
            NEW rightHeader {
                caption = '';
                MOVE PROPERTY(reference(b));
                MOVE PROPERTY(nameCompany(b));
            }            
        }
        NEW details {
            fill = 1;
            type = TABBED;
            NEW components {
                fill = 1;
                caption = 'Компоненты';
                MOVE BOX(c);
            }
            NEW misc {
                fill = 1;
                caption = 'Разное';
                type = CONTAINERH;
                NEW left {
                    MOVE PROPERTY(sequence(b));
                }
                NEW right {
                    MOVE PROPERTY(nameOperation(b));
                }
            }
            NEW byproducts {
                fill = 1;
                caption = 'Производные номенклатуры';
                MOVE BOX(l);
            }
        }
    }
}

FORM boms 'Технологические карты'
    OBJECTS b = Bom
    PROPERTIES(b) READONLY nameItem, quantity, nameUnitMeasure, reference, nameCompany, archived
    PROPERTIES(b) NEWSESSION NEW, EDIT, DELETE 
    
    LIST Bom OBJECT b
;

NAVIGATOR {
    manufacturing {
        NEW boms;
    }
}