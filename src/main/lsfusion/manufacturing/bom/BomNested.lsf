MODULE BomNested;

REQUIRE BoM;

NAMESPACE Manufacturing;

selectedReference 'Спецификация' = DATA Bom (ComponentLine);

nestedBom (ComponentLine c) = OVERRIDE selectedReference(c), GROUP LAST Bom bb IF item(c) = item(bb) ORDER bb;
        
textIndex (ComponentLine c) = lpad(TEXT(index(c)), 4, '0');

level 'Уровень' (Bom b, ComponentLine c, TEXT path) =
    RECURSION 1l IF bom(c) = b AND path = textIndex(c)
         STEP 2l IF bom(c) = nestedBom($c) AND path = ($path + '/' + textIndex(c)); //MATERIALIZED

quantity 'Уровень' (Bom b, ComponentLine c, TEXT path, NUMERIC[20,11] q) =
    RECURSION NUMERIC[20,11](1.0) IF q = quantity(c) AND bom(c) = b AND path = lpad(TEXT(index(c)), 4, '0')
         STEP NUMERIC[20,11](1.0) IF q = $q * quantity(c) / quantity(bom(c)) AND bom(c) = nestedBom($c) AND path = ($path + '/' + lpad(TEXT(index(c)), 4, '0')) CYCLES NO; //MATERIALIZED

quantity (Bom b, ComponentLine c, TEXT path) = NUMERIC[16,3]
    (GROUP SUM quantity(b, c, path, NUMERIC[20,11] q) * q); 
         
manufactureComponents 'Производить компоненты' = DATA BOOLEAN (Bom);

EXTEND FORM bom
    PROPERTIES(b) manufactureComponents
;

DESIGN bom {
    item {
        MOVE PROPERTY(manufactureComponents(b));
    }
}

finalBom (Bom b, ComponentLine c, TEXT path) =
    RECURSION 1l IF bom(c) = b AND path = textIndex(c) 
         STEP 2l IF bom(c) = nestedBom($c) AND path = ($path + '/' + textIndex(c)) AND manufactureComponents(bom($c));
